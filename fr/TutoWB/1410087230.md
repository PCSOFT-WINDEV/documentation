
## Annexe 4. Tour d'horizon des variables disponibles


<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Ce que vous allez apprendre dans cette leçon
<a name="que_vous_allez_apprendre_dans_cette_lecon_ELTTEXTE000124"></a>


- Les types Date, Heure et Durée.

- Le type Tableau.

- Le type Structure. 





|   |   |
| --- | --- |
| <br>![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=dur%E9e.png)<br> | <br>Durée estimée : 1 h |

| [Leçon précédente](../TutoWB/1410087228.md) | [Sommaire](../TutoWB/1410087510.md) | [Leçon suivante](../TutoWB/1410087231.md) |
| --- | --- | --- |







<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Présentation
<a name="presentation_ELTTEXTE000181"></a>
En dehors des types simples (booléen, entier, réel, etc.), le WLangage propose plusieurs types de variables qui permettent de prendre en compte beaucoup de spécificités. 

Nous allons passer rapidement en revue ces différents types de variables avec pour chacun un petit exemple pratique. Nous vous conseillons de consulter l'aide en ligne du type proposé pour plus de détails. 



- Ouvrez si nécessaire le projet "WLangage" que vous avez créé dans la première leçon de cette partie (consultez le paragraphe [Un projet pour découvrir le WLangage](../TutoWB/1410087226.md) de la leçon Annexe 2).




<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Variables de type Date, Heure et Durée
<a name="variables_type_date_heure_duree_ELTTEXTE000228"></a>
Le WLangage propose différents types de variables permettant de gérer les dates, les heures et les durées.


### Le type Date
<a name="type_date_ELTPARAGRAPHE000028"></a>Pour gérer les dates, le WLangage propose le type **Date**. De nombreuses fonctions et propriétés permettent de manipuler les dates.  

- Pour tester la manipulation des dates, nous allons tester un petit exemple de code : 

	1. Supprimez le code saisi dans l'événement "Initialisation" du projet (Ctrl + A pour tout sélectionner, et Suppr pour supprimer).

	2. Saisissez le code suivant : 
			
		```wl
		MaDate est une Date
		```
Cette ligne de code permet de déclarer une variable de type Date. Par défaut, la variable est initialisée avec la date du jour. 

	3. Maintenant, voyons comment affecter une date. 
		```wl
		MaDate = Hier()
		MaDate = "20210929"
		```
Ce code présente les différentes manières d'initialiser une variable de type Date. L'initialisation peut être effectuée : 

		- via une fonction WLangage. Dans cet exemple, la fonction [Hier](../WDLang1/1000023786.md) est utilisée. Le WLangage propose de nombreuses fonctions permettant de connaître le premier ou dernier jour du mois, de l'année, ... 

		- via une chaîne de caractères contenant la date au format "AAAAMMJJ". 




4. Les paramètres de la date peuvent également être modifiés à l'aide de propriétés WLangage. Saisissez le code suivant : 
			
		```wl
		// Modification des paramètres de la date
		MaDate.Année = 1985
		MaDate.Mois = 10
		MaDate.Jour = 26
		```


5. La fonction [&lt;Date&gt;.VersChaîne](../WDLang1/1000023952.md) permet de choisir le format d'affichage d'une date. Par exemple, saisissez le code suivant : 
			
		```wl
		Trace(MaDate.VersChaîne())
		Trace(MaDate.VersChaîne("Jjjj JJ Mmmm AAAA"))
		Trace(MaDate.VersJourEnLettre())
		```


6. Testons immédiatement ce code : cliquez sur ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=ICO_GO_Projet_WB_GAF.jpg) parmi les boutons d'accès rapide.  

7. Le volet "Trace du débogueur" contient : 
			
		```txt
		26/10/1985
		Samedi 26 Octobre 1985
		samedi
		```


	- l'affichage en clair de la date.

	- l'affichage en clair de la date selon un masque.

	- la récupération du jour en lettres correspondant à la date.





### Le type Heure
<a name="type_heure_ELTPARAGRAPHE000033"></a>

- De la même manière, il est possible de manipuler des heures grâce au type **Heure**. 

	1. Supprimez le code saisi dans l'événement "Initialisation" du projet (Ctrl + A pour tout sélectionner, et Suppr pour supprimer).

	2. Saisissez le code suivant : 
			
		```wl
		// Déclaration d'une heure
		MonHeure est une Heure // Par défaut, l'heure est initialisée avec l'heure actuelle
		
		// Affectation d'une heure
		// - via une fonction WLangage
		MonHeure = Maintenant()
		// - fixée dans le code (par exemple 15h25)
		MonHeure = "1525"
		
		// Modification des paramètres de l'heure
		MonHeure.Heure = 12
		MonHeure.Minute = 0
		MonHeure.Seconde = 0
		
		// Affichage en clair de l'heure
		Trace(MonHeure.VersChaîne())
		// Affichage en clair de l'heure selon un masque
		Trace(MonHeure.VersChaîne("HH:mm:SS"))
		
		// Comparaison de 2 heures
		SI MonHeure < Maintenant() ALORS
			Trace("L'heure mémorisée est antérieure à l'heure actuelle")
		SINON
			Trace("L'heure mémorisée est postérieure à l'heure actuelle")
		FIN
		```
Ce code reprend toutes les possibilités que nous avons vu pour les dates, et les applique aux heures. 

	3. Testons immédiatement ce code : cliquez sur ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=ICO_GO_Projet_WB_GAF.jpg) parmi les boutons d'accès rapide.  

	4. Le volet "Trace du débogueur" affiche les différentes informations souhaitées. 
			
		```txt
		12:00:00:00
		12:00:00
		L'heure mémorisée est antérieure à l'heure actuelle
		```


		- l'affichage en clair de l'heure.

		- l'affichage en clair de l'heure selon un masque.








### Le type Durée
<a name="type_duree_ELTPARAGRAPHE000038"></a>

- Pour les durées, le même type de code peut être saisi. 

	1. Supprimez le code saisi dans l'événement "Initialisation" du projet (Ctrl + A pour tout sélectionner, et Suppr pour supprimer).

	2. Saisissez le code suivant : 
			
		```wl
		// Déclaration d'une durée
		MaDurée est une Durée
		
		// Affectation d'une durée
		// - avec une valeur "en clair"
		MaDurée = 2 min 8 s
		// ou 
		MaDurée = 128 s
		// - en fournissant la durée en secondes, en minutes, ...
		MaDurée.EnSecondes = 128
		
		// Affichage en clair de la durée
		Trace(MaDurée.VersChaîne("MM:SS"))
		```


	3. Testons immédiatement ce code : cliquez sur ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=ICO_GO_Projet_WB_GAF.jpg) parmi les boutons d'accès rapide.  

	4. Le volet "Trace du débogueur" affiche les différentes informations souhaitées. 
			
		```txt
		02:08
		```








Pour plus de détails sur les variables de type Date, Heure et Durée, consultez les pages d'aide : 

- [Date](../Motscles/1514066.md),

- [Heure](../Motscles/1514068.md),

- [Durée](../Motscles/1514069.md).




<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Les variables de type Tableau
<a name="les_variables_type_tableau_ELTTEXTE000409"></a>
Les tableaux sont un type de variable très utilisé.

Un tableau est un type structuré qui permet de regrouper une série d'éléments de même type. Chaque élément du tableau est accessible directement par son numéro d'ordre (indice).

Des fonctions WLangage spécifiques permettent de manipuler les tableaux et leurs éléments.
<a name="NOTE3_2"></a>


### Déclaration
<a name="declaration_ELTPARAGRAPHE000052"></a>La déclaration d'une variable de type tableau se fait de la façon suivante : 

```txt
<Nom du tableau> est un tableau de <Type des éléments du tableau>
```


Par exemple :

```wl
MonTabChaîne est un tableau de chaînes
MonTabEntier est un tableau d'entiers
```

<a name="NOTE3_3"></a>


### Remplissage d'un tableau et accès aux éléments
<a name="remplissage_tableau_acces_aux_elements_ELTPARAGRAPHE000059"></a>Lors de sa déclaration, le tableau est vide. L'ajout d'éléments est réalisé par la fonction [&lt;Tableau&gt;.Ajoute](../WDLang1/1000024299.md) grâce à la syntaxe suivante : 

```txt
<Nom du tableau>.Ajoute(<Valeur de l'élément>)
```


Pour accéder aux éléments d'un tableau, il suffit d'utiliser la syntaxe suivante :

```txt
<Nom du tableau> [<Indice de l'élément>]
```
|   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=avertissement.png) | Important | Les indices des éléments du tableau commencent à 1. |







- Pour tester l'ajout d'éléments dans un tableau, nous allons tester un petit exemple de code : 

	1. Supprimez le code saisi dans l'événement "Initialisation" du projet (Ctrl + A pour tout sélectionner, et Suppr pour supprimer).

	2. Saisissez le code suivant : 
			
		```wl
		// Création d'un tableau de chaînes
		MonTableau est un tableau de chaînes
		
		// Ajout d'éléments
		MonTableau.Ajoute("WINDEV")
		MonTableau.Ajoute("WEBDEV")
		MonTableau.Ajoute("WINDEV Mobile")
		
		// Affichage du contenu du troisième élément
		Trace("Valeur de l'élément 3 : [%MonTableau[3]%]")
		```


	3. Testons immédiatement ce code : cliquez sur ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=ICO_GO_Projet_WB_GAF.jpg) parmi les boutons d'accès rapide.  

	4. Affichez si nécessaire le volet "Trace du débogueur" pour voir le résultat : sous le volet "Accueil", dans le groupe "Environnement", déroulez "Volets" et sélectionnez "Volets" puis "Trace du débogueur". 

	5. Le volet "Trace du débogueur" contient les informations suivantes : 
			
		```txt
		Valeur de l'élément 3 : WINDEV Mobile
		```






|   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=astuce.png) | Astuce | **Initialisation rapide d'un tableau** <br><br>Pour initialiser un tableau, il est également possible d'utiliser la syntaxe suivante : <br><br><pre><code>// Déclare un tableau<br>TableauJour est un tableau de chaînes<br>// Initialisation avec <br>// les noms des jours de la semaine<br>TableauJour = ["Lundi", "Mardi", "Mercredi", "Jeudi", <br>"Vendredi", "Samedi", "Dimanche"]</code></pre><br> |




<a name="NOTE3_4"></a>


### Parcours d'un tableau
<a name="parcours_tableau_ELTPARAGRAPHE000066"></a>Pour parcourir les éléments d'un tableau, le WLangage propose deux méthodes : 

- Une boucle classique de type POUR .. A. 

- Une boucle de type POUR TOUT.




- Dans le cas d'une boucle classique de type POUR .. A .., il est nécessaire de donner la valeur de l'indice de départ (1) et de l'indice d'arrivée. 

	1. Par exemple, ajoutez le code suivant dans l'événement "Initialisation" du projet : 
			
		```wl
		POUR Indice = 1 À MonTableau.Occurrence
			Trace("Valeur de l'élément [%Indice%] : [%MonTableau[Indice]%]")
		FIN
		```

			Dans ce code, l'indice de départ est 1, l'indice d'arrivée est donné par la propriété [Occurrence](../Proprietes/2510097.md) du tableau. 

	2. Testons immédiatement ce code : cliquez sur ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=ICO_GO_Projet_WB_GAF.jpg) parmi les boutons d'accès rapide.  

	3. Le volet "Trace du débogueur" contient les informations suivantes : 
			
		```txt
		Valeur de l'élément 1 : WINDEV
		Valeur de l'élément 2 : WEBDEV
		Valeur de l'élément 3 : WINDEV Mobile
		```








- Le WLangage propose un type de boucle beaucoup plus simple et tout aussi performant : la boucle POUR TOUT. Nous l'avons déjà vue sur les chaînes. Ce type de boucle s'applique également sur les tableaux. Dans ce cas, pas besoin d'indiquer l'indice de départ ou d'arrivée.  

	1. Par exemple, remplacez le code de la boucle POUR par le code suivant dans l'événement "Initialisation" du projet : 
			
		```wl
		POUR TOUT ELEMENT UneChaîne, Indice DE MonTableau
			Trace("Valeur de l'élément [%Indice%] : [%UneChaîne%]")
		FIN
		```
Dans ce cas, il suffit d'indiquer le nom de la variable qui correspond à l'élément du tableau et celle qui correspond à l'indice. 

	2. Testons immédiatement ce code : cliquez sur ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=ICO_GO_Projet_WB_GAF.jpg) parmi les boutons d'accès rapide.  

	3. Le volet "Trace du débogueur" contient les informations suivantes : 
			
		```txt
		Valeur de l'élément 1 : WINDEV
		Valeur de l'élément 2 : WEBDEV
		Valeur de l'élément 3 : WINDEV Mobile
		```








Pour plus de détails sur les variables de type Tableau, consultez la page d'aide [Tableau](../Motscles/1514030.md).
<a name="NOTE3_6"></a>


### Le tableau associatif
<a name="tableau_associatif_ELTPARAGRAPHE000073"></a>Un tableau associatif est un type "avancé" de tableau : il permet de regrouper une série d'éléments du même type. Chaque élément du tableau est indexé sur n'importe quel type d'information (et non plus simplement sur un indice numérique, comme dans les autres types de tableau).

Les Tableaux associatifs (avec ou sans doublons) sont manipulables en WLangage.  



- Vous voulez tester ? Rien de plus simple. 

	1. Supprimez le code saisi dans l'événement "Initialisation" du projet (Ctrl + A pour tout sélectionner, et Suppr pour supprimer).

	2. Saisissez le code suivant : 
			
		```wl
		// Création d'un tableau associatif de dates
		MonTableau est un tableau associatif de Dates
		
		// Ajout d'éléments
		MonTableau["Marc"] = "19820201"
		MonTableau["Anne"] = "19840604"
		
		// Affichage du contenu de l'élément ayant pour clé "Marc"
		Trace("Valeur de l'élément ayant pour clé ""Marc"" : " + MonTableau["Marc"])
		
		// Parcours du tableau
		POUR TOUT ELEMENT Valeur, Clé, Indice DE MonTableau
			Trace("Valeur de l'élément [%Indice%] qui a pour clé [%Clé%] : [%Valeur%]")
		FIN
		```


	3. Testons immédiatement ce code : cliquez sur ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=ICO_GO_Projet_WB_GAF.jpg) parmi les boutons d'accès rapide.  

	4. Le volet "Trace du débogueur" contient les informations suivantes : 
			
		```txt
		Valeur de l'élément ayant pour clé "Marc" : 19820201
		Valeur de l'élément 1 qui a pour clé Marc : 19820201
		Valeur de l'élément 2 qui a pour clé Anne : 19840604
		```








Pour plus de détails sur les variables de type Tableau associatif, consultez la page d'aide [Tableau associatif](../Motscles/1514058.md).

<a name="NOTE7"></a>
<a name="NOTE7_1"></a>


## Variables de type Structure
<a name="variables_type_structure_ELTTEXTE000667"></a>


### Le type Structure
<a name="type_structure_ELTPARAGRAPHE000082"></a>Une structure est un type de données personnalisé. Une structure regroupe des variables de types différents.

- Pour tester la manipulation des structures, nous allons tester un petit exemple de code : 

	1. Supprimez le code saisi dans l'événement "Initialisation" du projet (Ctrl + A pour tout sélectionner, et Suppr pour supprimer).

	2. Saisissez le code suivant : 
			
		```wl
		NomComplet est une Structure
			Nom est une chaîne
			Prénom est une chaîne
		FIN
		```
Ce code permet de déclarer une structure composée de deux variables, Nom et Prénom. Les variables composant une structure sont appelées "Membre". Pour manipuler les membres, il faut déclarer une variable du type de la structure. 

	3. Nous allons maintenant : 

		- Déclarer une variable de type structure. Ajoutez le code WLangage suivant : 
						
			```wl
			Contact est un NomComplet
			```


		- Affecter les membres de la structure. Ajoutez le code WLangage suivant : 
						
			```wl
			Contact.Nom = "POLO"
			Contact.Prénom = "MARCO"
			```





4. Pour lire le contenu d'un membre, il suffit d'utiliser la syntaxe :
			
		```txt
		<Nom de la variable>.<Nom du membre>
		```
Dans notre exemple, saisissez le code suivant : 
			
		```wl
		LeNom est une chaîne
		LeNom = Contact.Nom
		// Affiche le contenu de la variable LeNom
		Trace(LeNom)
		// Affiche directement Contact.Nom (sans passer par une variable)
		Trace(Contact.Nom)
		```


5. Testons immédiatement ce code : cliquez sur ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=ICO_GO_Projet_WB_GAF.jpg) parmi les boutons d'accès rapide.  

6. Le volet "Trace du débogueur" affiche : 
			
		```txt
		POLO
		POLO
		```





Comme pour les autres types de variables, il est également possible de manipuler des tableaux de structures. 

- Pour tester la manipulation des tableaux de structures, nous allons compléter note exemple : 

	1. A la suite du code déjà saisi, saisissez le code suivant : 
			
		```wl
		// Déclaration d'un tableau de structures
		MesContacts est un tableau de NomComplet
		Ajoute(MesContacts, Contact)
		Trace(MesContacts[1].Nom, MesContacts[1].Prénom)
		```
Dans ce code, nous créons un tableau contenant la structure NomComplet, nous lui ajoutons le contact que nous avons créé précédemment puis nous affichons le nom et le prénom de ce contact. 

	2. Testons immédiatement ce code : cliquez sur ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=ICO_GO_Projet_WB_GAF.jpg) parmi les boutons d'accès rapide.  

	3. Le volet "Trace du débogueur" affiche maintenant : 
			
		```txt
		POLO
		POLO
		POLO MARCO
		```








Pour plus de détails sur les variables de type Structure et Tableau de structures, consultez [Structure](../Motscles/1514008.md).

<a name="NOTE8"></a>
<a name="NOTE8_1"></a>


## Conclusion
<a name="conclusion_ELTTEXTE000765"></a>
Dans cette leçon, vous avez découvert une partie des différents types de variables que propose le WLangage : 

- le type Chaîne, 

- les types Date, Heure et Durée, 

- le type Tableau,

- le type Structure. 




Dans la prochaine leçon nous verrons comment manipuler certaines de ces variables grâce aux instructions conditionnelles disponibles en WLangage.



| [Leçon précédente](../TutoWB/1410087228.md) | [Sommaire](../TutoWB/1410087510.md) | [Leçon suivante](../TutoWB/1410087231.md) |
| --- | --- | --- |






