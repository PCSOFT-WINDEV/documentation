
## Gestion avancée des instances de classes
			

<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Présentation
<a name="presentation_ELTTEXTE000185"></a>
La gestion avancée des instances permet de choisir le mode de prise de référence. Ce choix impacte directement la libération des objets de classe : selon la prise de référence choisie, le "Destructeur" ne sera pas appelé au même moment. 

Il existe deux modes de prise de référence : 

- la référence faible. 

- la référence forte.




**Rappel** : La prise de référence permet de référencer la même zone de données que l'élément source. Par exemple, deux variables pointent sur le même objet de classe. Pour prendre référence sur un objet, il suffit d'utiliser l'opérateur **<-**.

**Important** : Dans tous les cas, il est possible de forcer l'appel au destructeur grâce au mot-clé "Libérer".



<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Références faibles et références fortes
<a name="references_faibles_references_fortes_ELTTEXTE000209"></a>
Deux types de prise de référence sont disponibles : 

- **La référence forte** (par défaut) : Dans ce cas, l'objet de classe sera libéré uniquement lorsque toutes les références de l'objet auront été libérées.

- **La référence faible** : Dans ce cas, les objets seront libérés automatiquement selon leur portée (même si une référence globale a été prise sur ces objets). La référence faible doit être indiquée lors de l'instanciation dynamique de l'objet prenant référence.
	![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/fr/UNIVERSALAPP.png) La gestion des références faibles n'est pas disponible.




**Pour indiquer une référence faible**, utilisez la syntaxe suivante :

```txt
Reference est un objet Classe1 dynamique, faible
```
ou bien :

```txt
Reference est un objet Classe1 dynamique <faible>
```


**Remarque** : Il est possible de forcer la prise de référence faible pour tous les objets avec la syntaxe suivante :

```wl
ModeExecution(ForceDestructeurObjetNonDynamique)
```


**Attention** : Ce fonctionnement est global à toute l'application et risque de perturber le fonctionnement des composants.





<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Exemples de références
<a name="exemples_references_ELTTEXTE000233"></a>


### Référence forte
<a name="reference_forte_ELTPARAGRAPHE000074"></a>A la fin de la procédure, l'objet "MonObjet" n'est <u><u><u><u>pas</u></u></u></u> libéré (le destructeur n'est <u><u><u><u>pas</u></u></u></u> appelé). L'objet "MonObjet" sera libéré lorsque "ReferenceGlobale" sera libéré. "MonObjet" sera donc libéré à la fermeture de la fenêtre.


```wl
// Déclarations globales de la fenêtre : instanciation dynamique d'un objet Classe1
ReferenceGlobale est un objet Classe1 dynamique
```



```wl
// Procédure locale de la fenêtre
PROCEDURE ProcédureLocaleFenêtre()

// Instanciation d'un objet Classe1
MonObjet est un objet Classe1

// Prise de référence forte sur l'objet MonObjet.
ReferenceGlobale <- MonObjet
```

<a name="NOTE3_2"></a>




### Référence faible
<a name="reference_faible_ELTPARAGRAPHE000094"></a>A la fin de la procédure, l'objet "MonObjet" est libéré (le destructeur est appelé) et "ReferenceGlobale" vaut NULL.


```wl
// Déclarations globales de la fenêtre : instanciation dynamique d'un objet Classe1
ReferenceGlobale est un objet Classe1 dynamique, faible
```



```wl
// Procédure locale de la fenêtre
PROCEDURE ProcédureLocaleFenêtre()

// Instanciation d'un objet Classe1
MonObjet est un objet Classe1

// Prise de référence faible sur l'objet MonObjet.
ReferenceGlobale <- MonObjet
```


<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Destructeur forcé
<a name="destructeur_force_ELTTEXTE000263"></a>
Il est possible de forcer l'appel du destructeur (libérer l'objet) même si une référence forte a été prise sur cet objet.

Pour forcer l'appel au destructeur, utilisez la syntaxe suivante : 

```wl
// Le destructeur sera appelé à la fin de la portée de la variable 
// (même si une référence forte a été prise sur cet objet)
MonObjet est un objet Classe1, force destructeur
```


ou bien :

```wl
// Le destructeur sera appelé à la fin de la portée de la variable 
// (même si une référence forte a été prise sur cet objet)
MonObjet est un objet Classe1 <force destructeur>
```



![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/fr/UNIVERSALAPP.png) Cette fonctionnalité n'est pas disponible.




### Equivalence
<a name="equivalence_ELTPARAGRAPHE000122"></a>Les codes suivants (exemple 1 et exemple 2) sont équivalents. Dans les deux cas, l'objet sera libéré à la fin de la procédure.

**Exemple 1** :


```wl
// Déclarations globales de la fenêtre 
// Instanciation dynamique d'un objet Classe1
ReferenceGlobale est un objet Classe1 dynamique, faible
```



```wl
// Procédure locale de la fenêtre
PROCEDURE ProcédureLocaleFenêtre()

// Instanciation d'un objet Classe1
MonObjet est un objet Classe1

// Prise de référence faible sur l'objet MonObjet.
ReferenceGlobale <- MonObjet
```


**Exemple 2** : 


```wl
// Déclarations globales de la fenêtre
// Instanciation dynamique d'un objet Classe1
ReferenceGlobale est un objet Classe1 dynamique
```



```wl
// Procédure locale de la fenêtre
PROCEDURE ProcédureLocaleFenêtre()

// Instanciation d'un objet Classe1
MonObjet est un objet Classe1, force destructeur

// Prise de référence faible sur l'objet MonObjet.
ReferenceGlobale <- MonObjet
```







