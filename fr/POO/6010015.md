
## Eléments d'une classe : Enumérer et manipuler par programmation
			

<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Présentation
<a name="presentation_ELTTEXTE000214"></a>
Le WLangage propose plusieurs types de variable pour permettre l'énumération et la manipulation dynamique des éléments d'une classe. Un développeur maîtrisant la Programmation Orientée Objet (POO) peut ainsi créer des méthodes entièrement paramétrées qui s'adaptent aux éléments contenus dans une [classe](../POO/6010006.md).

Il est possible d'énumérer :

- les [méthodes](../POO/6010006.md) (ou procédures) d'une classe.

- les [propriétés](../POO/6010006.md) d'une classe.

- les [membres](../POO/6010006.md) (ou variables) d'une classe.




Lors de cette énumération, il est par exemple possible de récupérer : 

- le nom de l'élément.

- le type de l'élément.




Par exemple :

- il est possible de récupérer le nom d'une méthode pour l'appeler en utilisant la fonction [ExécuteTraitement](../WDLang1/3013031.md).

- il est possible de récupérer le nom et le type d'une variable pour connaître ou affecter sa valeur en utilisant les [opérateurs d'indirection](../Motscles/1512005.md) { }.










## Comment le faire ?
<a name="comment_faire_ELTTEXTE000238"></a>
Les différentes étapes pour énumérer et manipuler un objet sont les suivantes : 

1. Récupération de la définition de l'objet à manipuler grâce à la fonction [RécupèreDéfinition](../WDLang1/1000019333.md). 
	Exemple : 
	
	```wl
	O est un cWord    // Instanciation de la classe CWord
	Def est une Définition // Déclaration d'une variable de type Définition
	
	Def = RécupèreDéfinition(O)  // Récupération de la définition de l'objet O dans la variable Def
	
	Trace(Def.Nom)  // Affichage du nom de l'objet 
	Trace(Def.FamilleDéfinition)
	Trace(Def.Type) // Affichage du type de l'objet
	```


2. Récupération de la liste des méthodes. Une boucle de type "POUR TOUT" permet de lire la propriété **Procédure** de la variable de type Définition. Cette propriété correspond à un tableau contenant toutes les procédures contenues dans l'objet décrit. 
	Exemple : 
	
	```wl
	// Déclaration de la variable qui contiendra la description d'une procédure
	Proc est une Description de procédure  
	
	// Liste des procédures (ou méthodes) de l'objet Def
	POUR TOUT Proc de Def.Procédure
		Trace (Proc.Nom)  // Affiche le nom de la procédure trouvée
	FIN
	```

	D'autres propriétés sont accessibles en utilisant la propriété **NomPropriété** sur l'objet. Pour plus de détails, consultez  [Description de procédure](../WDLang1/1000019317.md).

3. Récupération de la liste des propriétés. Une boucle de type POUR TOUT permet de lire la propriété **Propriété**. Cette propriété correspond à un tableau contenant toutes les propriétés contenues dans l'objet décrit.
	Exemple : 
	
	```wl
	// Déclaration d'une variable qui contiendra la description d'une propriété
	Prop est une Description de Propriété  
	
	// Liste des propriétés de l'objet Def
	POUR TOUT Prop de Def.Propriété  
		Trace (Prop.Nom)  // Affiche le nom de la propriété trouvée
	FIN
	```

	D'autres propriétés sont accessibles en utilisant la propriété **NomPropriété** sur l'objet. Pour plus de détails, consultez [Description de Propriété](../WDLang1/1000019324.md).
	Il est également possible d'utiliser la fonction [PropriétéExiste](../WDLang1/1000020472.md) pour savoir si une propriété spécifique existe bien dans une classe. 

4. Récupération de la liste des variables. Une boucle de type POUR TOUT permet de lire la propriété **Variable**. Cette propriété correspond à un tableau contenant toutes les variables de l'objet décrit.
	Exemple :
	
	```wl
	// Déclaration de la variable qui contiendra la description d'une variable
	Prop est une Description de Variable  
	
	// Liste des variables de l'objet Def
	POUR TOUT Var de Def.Variable  
		// Affiche le nom et le type de la Variable trouvée
		Trace (Var.Nom, Var.Déclaration.Type, Var.Définition.Nom)
	FIN
	```

	D'autres propriétés sont accessibles en utilisant la propriété **NomPropriété** sur l'objet. Pour plus de détails, consultez [Description de Variable](../WDLang1/1000019560.md).










## Manipulations grâce à ExécuteTraitement et aux opérateurs d'indirection
<a name="manipulations_grace_executetraitement_aux_operateurs_indirection_ELTTEXTE000262"></a>


### Utilisation de la fonction ExécuteTraitement
<a name="utilisation_fonction_executetraitement_ELTPARAGRAPHE000112"></a>La fonction [ExécuteTraitement](../WDLang1/3013031.md) permet d'appeler le code d'un événement associé à un objet.

Par exemple, le code suivant provoque l'appel du code de clic du bouton Valider :


```wl
ExécuteTraitement(BoutonValider, trtClic)
```


Pour exécuter une méthode de classe, il suffit d'utiliser une variable de type "Description de procédure" avec la fonction [ExécuteTraitement](../WDLang1/3013031.md).

Par exemple :


```wl
c est un BalloonTip
p est une Description de procédure
Def est une Définition

Def = RécupèreDéfinition(c)
// Récupère la première procédure de l'objet BalloonTip
p = Def.Procédure[1]  

// Exécute la procédure représentée par p de l'objet c
ExécuteTraitement(c, p)
```



### Utilisation des opérateurs d'indirection { ... }
<a name="utilisation_des_operateurs_indirection_ELTPARAGRAPHE000133"></a>Les opérateurs { } d'indirection permettent de manipuler le contenu d'une variable par un adressage indirect : le nom de l'objet à adresser est contenu dans une variable. 

La syntaxe est la suivante : 

```txt
{Variable_contenant_nom_Objet, TypeVariable}
```


où TypeVariable est une constante du langage commençant par indXXX.

Exemple:



```wl
// Remplit le champ "SAI_VILLE" avec la valeur "Montpellier"
NomChamp est une chaîne
NomChamp = "SAI_VILLE"
{NomChamp, IndChamp} = "Montpellier"
```


Pour manipuler un membre d'une méthode de classe, il suffit de passer dans les { } : 

- l'instance de la classe

- une variable de type Description de Variable ou le nom du membre. 




Par exemple:



```wl
c est un cWord
var est une Description de Variable
Def est une Définition

Def = RécupèreDéfinition(c)
// Récupère la 2ème variable de la classe : version de Word
var = Def.Variable[2] 

Trace(var.Nom) // Affiche le nom de la variable
Trace("Version de Word: ",{c, var})  // Affiche la version de Word
```








## Exemple
<a name="exemple_ELTTEXTE000292"></a>
L'exemple de code ci-dessous permet d'énumérer toutes les procédures, propriétés et variables contenus dans une classe. Dans cet exemple, la classe **cWord** est issue de l'exemple "WD Pilotage de Word" (fourni dans les exemples de WINDEV).



```wl
o est un cWord
def est une Définition
def = RécupèreDéfinition(o)

Trace(def.Nom)
Trace(def.FamilleDéfinition)
Trace(defp.Type)
	
Proc est une Description de procédure
Prop est une Description de Propriété
Var est une Description de Variable
	
Trace("--------------- Procédures --------------------")
POUR TOUT Proc DE def.Procédure
	Trace(Proc.Global, Proc.Hérité, Proc.Nom, Proc.Virtuel)		
FIN
	
Trace("--------------- Propriétés --------------------")
POUR TOUT Prop DE def.Propriété
	Trace(Prop.Déclaration, Prop.Définition, Prop.Ecriture, ...
		Prop.Global, Prop.Hérité, Prop.Lecture, Prop.Nom, Prop.Virtuel)
FIN
	
Trace("--------------- Variables --------------------")
POUR TOUT Var DE def.Variable
	Trace(Var.Nom, Var.Définition.Type, Var.Définition.Nom)
	SELON Var.Définition.Type
		CAS 1, 4, 8, 19
			// Affichage de la valeur de la variable par indirection
			Trace({o,Var})  
		CAS 34 // tableau
			Trace("Dimension: ", {o,Var.Nom}..Occurrence)
		CAS 36 // structure
			v est une Description de Variable
			Trace("Structure " + Var.Définition.Nom)
			Trace("........................................................")
			POUR TOUT v DE Var.Définition.Variable
				Trace(v.Nom, v.Définition.Type, v.Définition.Nom)
			FIN
			Trace("........................................................")
		AUTRE CAS
				
	FIN
	Trace("======================================================================")
FIN
```









