


## Manipuler un champ Planning par programmation (syntaxe préfixée)
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Présentation
<a name="presentation_ELTTEXTE000121"></a>
Un champ Planning peut être : 

- manipulé par programmation. 

- ![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png) [lié à un fichier de données](../WDChamp/1000019543.md).

- ![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png) [lié à une variable](../WDChamp/1000019543.md).




Pour manipuler un champ Planning par programmation, WINDEV et WEBDEV proposent les [fonctions PlanningXXX](../WDLang1/1000022917.md). 

Cette page d'aide détaille comment manipuler un champ Planning par programmation. L'exemple utilisé en illustration permet de mémoriser les rendez-vous dans une base HFSQL. 




<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Manipuler le champ Planning par programmation
<a name="manipuler_champ_planning_par_programmation_ELTTEXTE000184"></a>


### Ajouter une ressource
<a name="ajouter_une_ressource_ELTPARAGRAPHE000207"></a>L'ajout d'une ressource dans un champ Planning est réalisé grâce à la fonction [&lt;Planning&gt;.AjouteRessource](../WDLang1/1000022905.md). Cette ressource peut correspondre à une personne, une salle, ...

Exemple : 

```wl
// Ajoute des ressources dans un champ Planning 
PLN_Planning1.AjouteRessource("Florence" + gValeurMémorisée("FP"))
PLN_Planning1.AjouteRessource("Emma" + gValeurMémorisée("EM"))
PLN_Planning1.AjouteRessource("Frédéric" + gValeurMémorisée("FR"))
```

Dans cet exemple, la fonction [gValeurMémorisée](../WDLang1/1410088363.md) permet de proposer une ressource affichée (par exemple "Florence") et une ressource manipulée par programmation (par exemple "FP"). 
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png) Remarque : La fonction [&lt;Planning&gt;.AjouteRessource](../WDLang1/1000022905.md) permet d'insérer une image dans le titre de la ressource.


### Ajouter un rendez-vous
<a name="ajouter_rendezvous_ELTPARAGRAPHE000243"></a>L'ajout d'un rendez-vous dans un champ Planning est réalisé grâce à la fonction [&lt;Planning&gt;.AjouteRendezVous](../WDLang1/1000022904.md). Cette fonction accepte deux syntaxes : 

- syntaxe permettant de préciser les caractéristiques du rendez-vous : titre, description, ...
	Exemple : 
	
	```wl
	// Ajoute un rendez-vous d'1 heure pour ce soir 
	MaRessource est une chaîne
	MonTitre est une chaîne
	DébutRDV est un DateHeure
	
	MaRessource = "Françoise"
	MonTitre = "Réunion commerciale"
	DébutRDV = DateSys() + "17000000"
	
	PLN_Planning.AjouteRendezVous(MaRessource, MonTitre, DébutRDV)
	```


- syntaxe manipulant une variable de type [RendezVous](../WDLang1/1000019244.md).
	Exemple : 
	
	```wl
	// Déclare une variable de type RendezVous 
	MonRendezVous est un RendezVous
	
	// Remplit le rendez-vous
	MonRendezVous.Titre = "Réunion commerciale"
	MonRendezVous.Contenu = "Réunion pour définir les objectifs de la semaine"
	MonRendezVous.DateDébut = "201003220845"
	MonRendezVous.DateFin = "201003221230"
	MonRendezVous.Catégorie = "Commerciale"
	MonRendezVous.ID = 1
	
	// Ajoute le rendez-vous au champ
	PLN_Planning.AjouteRendezVous(MonRendezVous)
	```




La propriété **CouleurFond** de la variable [RendezVous](../WDLang1/1000019244.md) permet de définir une couleur d'affichage pour un rendez-vous. Si aucune couleur de fond n'est définie, le champ Planning utilisera automatiquement la couleur associée à la catégorie du rendez-vous.


### Remplir un champ Planning avec les données présentes dans un fichier de données HFSQL
<a name="remplir_champ_planning_avec_les_donnees_presentes_dans_fichier_donnees_hfsql_ELTPARAGRAPHE000288"></a>Les enregistrements sont stockés dans un fichier HFSQL. Le remplissage initial du champ Planning peut être réalisé par un parcours du fichier de données via la syntaxe POUR TOUT et par l'ajout de chaque rendez-vous via la fonction [&lt;Planning&gt;.AjouteRendezVous](../WDLang1/1000022904.md).

```wl
// Variable de type RendezVous 
MonRendezVous est un RendezVous

// Parcours des rendez-vous mémorisés en base
POUR TOUT RDV 
	// Remplit les informations de la variable
	MonRendezVous.Titre = RDV.Titre
	MonRendezVous.Contenu = RDV.Contenu
	MonRendezVous.DateDébut = RDV.DateDébut
	MonRendezVous.DateFin = RDV.DateFin
	MonRendezVous.Catégorie = RDV.Catégorie
	MonRendezVous.ID = RDV.IDRDV
	
	// Ajoute le rendez-vous au champ Planning
	PLN_MonPlanning.RendezVous(MonRendezVous)
FIN
```



### Récupérer une liste de rendez-vous
<a name="recuperer_une_liste_rendezvous_ELTPARAGRAPHE000309"></a>La fonction [&lt;Planning&gt;.ListeRendezVous](../WDLang1/1000022909.md) permet de récupérer : 

- la liste de tous les rendez-vous du champ Planning. 
	Par exemple : 
	
	```wl
	// Tableau contenant une liste de Rendezvous
	tabListeRendezVous est un tableau de RendezVous
	
	// Liste des rendez-vous
	tabListeRendezVous = PLN_MonPlanning.ListeRendezVous()
	```


- la liste des rendez-vous d'une ressource compris entre deux dates. 
	Par exemple : 
	
	```wl
	// Liste des rendez-vous de Janvier 2020
	tabListeRendezVous est un tableau de RendezVous
	
	// Liste des rendez-vous
	tabListeRendezVous = PLN_MonPlanning.ListeRendezVous(MaRessource, ...
				"20200101", "20200131")
	```


- le rendez-vous actuellement sélectionné ou survolé. 
	Par exemple : 
	
	```wl
	// Rendez-vous sélectionné
	tabListeRendezVous est un tableau de RendezVous
	
	tabListeRendezVous = PLN_MonPlanning.ListeRendezVous(plnRdvSélectionné)
	```






### Afficher le planning à partir d'une ressource spécifique ou d'une date spécifique
<a name="afficher_planning_partir_une_ressource_specifique_une_date_specifique_ELTPARAGRAPHE000357"></a>Pour afficher le planning à partir : 

- d'une ressource spécifique, utilisez la fonction [&lt;Planning&gt;.PositionRessource](../WDLang1/1000022921.md). 
	Par exemple : 
	
	```wl
	PLN_SansNom1.AjouteRendezVous("Salle ABC", "RDV 1", ...
			DateSys() + "14000", DateSys() + "16000")
	PLN_SansNom1.PositionRessource("Salle ABC")
	```


- d'une date spécifique, utilisez la fonction [&lt;Planning&gt;.PositionDateHeure](../WDLang1/1000022920.md). 
	Par exemple : 
	
	```wl
	// Positionne le planning sur la date du jour
	PLN_Planning.PositionDateHeure(DateDuJour())
	```






### Supprimer un rendez-vous
<a name="supprimer_rendezvous_ELTPARAGRAPHE000398"></a>La fonction [&lt;Planning&gt;.SupprimeRendezVous](../WDLang1/1000022912.md) permet de supprimer : 

- le rendez-vous sélectionné dans le champ. 

- un rendez-vous précis.



```wl
// Supprime le premier rendez-vous
PLN_MonPlanning.SupprimeRendezVous(1)
```



### Supprimer une ressource
<a name="supprimer_une_ressource_ELTPARAGRAPHE000423"></a>La fonction [&lt;Planning&gt;.SupprimeRessource](../WDLang1/1000022913.md) permet de supprimer une ressource du champ Planning.

```wl
// Supprime une des ressources
ResSup est un booléen
ResSup = PLN_Planning1.SupprimeRessource("Flo") 
SI ResSup = Vrai ALORS
	Info("Ressource supprimée")
FIN
```

La fonction [&lt;Planning&gt;.SupprimeTout](../WDLang1/1000022914.md) permet de supprimer tous les rendez-vous présents dans le champ Planning ainsi que toutes les ressources du champ Planning.


### Modifier l'affichage du champ
<a name="modifier_affichage_champ_ELTPARAGRAPHE000451"></a>L'affichage courant d'un champ Planning peut être modifié via les fonctions suivantes : 

- [&lt;Planning&gt;.AffichePériodePrécédente](../WDLang1/1000022901.md) et [&lt;Planning&gt;.AffichePériodeSuivante](../WDLang1/1000022902.md) : Ces deux fonctions permettent de modifier la période d'affichage (identique à l'utilisation du bouton spin présent en haut du champ Planning). 

- [&lt;Planning&gt;.ChangeMode](../WDLang1/1000022906.md) : Cette fonction permet de modifier le mode d'affichage du champ : jour, semaine, mois.  

- [&lt;Planning&gt;.HeureAffichée](../WDLang1/1000022907.md) : Cette fonction permet de définir la plage horaire affichée.




<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Manipuler un rendez-vous par programmation
<a name="manipuler_rendezvous_par_programmation_ELTTEXTE000477"></a>
Il est possible de manipuler un rendez-vous directement par programmation : 

- soit en utilisant l'indice du rendez-vous à modifier. 

- soit en pointant par référence directement sur le rendez-vous à modifier. 




**1. Utilisation de l'indice**

Lors de chaque ajout d'un rendez-vous dans un planning, un indice est retourné par la fonction [&lt;Planning&gt;.AjouteRendezVous](../WDLang1/1000022904.md). Cet indice représente le rendez-vous ajouté. Cet indice peut être utilisé pour manipuler directement le rendez-vous. 

Exemple :

```wl
NumRDV est un entier
NumRDV = 5

// Changement du titre affiché sur le rendez-vous numéro 5
PLN_SALLE[NumRDV].Titre = "Salle bleue"
```


**2. Utilisation d'une référence**

Pour manipuler un rendez-vous, il suffit d'utiliser l'opérateur **&lt;-** pour associer le rendez-vous du champ Planning et la variable de type [RendezVous](../WDLang1/1000019244.md). Une modification effectuée sur la variable sera automatiquement reportée dans le champ. 

Exemple : 

```wl
R est un RendezVous
R <- PLN_Salle[NumRDV]
R.Titre = "Nouveau titre"
```


<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Utilisation du menu contextuel (FAA)
<a name="utilisation_menu_contextuel_faa_ELTTEXTE000530"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/fr/WINDOWS.png) Le champ Planning est associé par défaut à un menu contextuel (FAA). Via le menu contextuel du champ Planning, il est possible de : 

- modifier le mode d'affichage du champ,

- ajouter, modifier ou supprimer un rendez-vous. 




Pour sauvegarder les opérations réalisées, il est nécessaire d'utiliser les événements du champ Planning. 

Dans l'événement concerné, il suffit de récupérer le rendez-vous en cours d'utilisation et de faire le traitement correspondant. 

**Exemple** : Pour mémoriser dans un fichier de données RDV, un rendez-vous que l'utilisateur ajoute via le menu contextuel, il suffit de saisir dans l'événement "Entrée en saisie d'un rendez-vous" :

```wl
PROCEDURE Edition(rdvEdité est un RendezVous)

// Mémorisation des données
RDV.Titre = rdvEdité.Titre
RDV.DateDébut = rdvEdité.DateDébut
RDV.DateFin = rdvEdité.DateFin
...
HAjoute(RDV)
```


Le même type de code peut être mis en place pour les différents événements du champ Planning. En effet, pour chaque événement du champ qui manipule un rendez-vous, une procédure a été déclarée automatiquement par le champ Planning. 

Ces procédures reçoivent en paramètre une variable de type [RendezVous](../WDLang1/1000019244.md) concernée par le traitement. 

<a name="NOTE5"></a>
<a name="NOTE5_1"></a>


## Utilisation avancée des événements associés au champ Planning
<a name="utilisation_avancee_des_evenements_associes_champ_planning_ELTTEXTE000575"></a>


### Utilisation avancée des événements avec procédure
<a name="utilisation_avancee_des_evenements_avec_procedure_ELTPARAGRAPHE000598"></a>Il est possible de proposer à l'utilisateur de définir plus précisément les caractéristiques de son rendez-vous lors d'un ajout ou d'une modification. Il suffit pour cela de créer une fenêtre ou une page avec les informations à remplir. 

Dans le code, il suffit d'ouvrir la fenêtre ou la page par exemple dans l'événement "Entrée en saisie d'un rendez-vous". Pour bloquer la saisie directe, l'événement doit alors renvoyer <u><u><u><u>Faux</u></u></u></u>. 
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/fr/WINDOWS.png) Ce principe peut être appliqué à tous les événements appelés par le menu contextuel du champ Planning. Exemple : Ouverture d'une fenêtre de saisie du rendez-vous.  

```wl
PROCEDURE Edition(rdvEdité est un RendezVous)

// Ouvre la fenêtre de saisie d'un rendez-vous 
// avec le rendez-vous sélectionné (en mode Création ou Modification)
FEN_SaisieRDV_HFSQL.Ouvre(rdvEdité)

// Renvoie FAUX pour bloquer la saisie directe dans le champ Planning
RENVOYER Faux
```


<a name="NOTE6"></a>
<a name="NOTE6_1"></a>


## Gestion des jours fériés
<a name="gestion_des_jours_feries_ELTTEXTE000619"></a>
La gestion des jours fériés est réalisée par programmation. Le WLangage met à votre disposition plusieurs fonctions débutant par JourFériéXXX.

Pour définir les jours fériés affichés dans les champs Agenda, Planning et Calendrier, il faut utiliser la fonction [JourFériéAjoute](../WDLang1/1000017304.md). Cette fonction permet de définir la liste des jours fériés à utiliser. Cette fonction permet de personnaliser les jours fériés en fonction du pays et de la réglementation locale. Cette fonction doit être utilisée au début de l'application car elle a un effet global sur l'application.

Les jours fériés seront colorés en vert dans le planning. 

Exemple :

```wl
// Suppression de tous les jours fériés
JourFériéSupprimeTout()
// Initialisation des 11 jours fériés communs aux départements français et DOM/TOM
JourFériéAjoute("0101")		// 1er Janvier
JourFériéAjoute(jfLundiDePâques)	// Lundi de Pâques
JourFériéAjoute("0501")		// 1er Mai
JourFériéAjoute("0508")		// 8 Mai
JourFériéAjoute(jfJeudiDeLAscension)	// Jeudi de l'Ascension
JourFériéAjoute(jfLundiDePentecôte)	// Lundi de Pentecôte
JourFériéAjoute("0714")		// 14 Juillet
JourFériéAjoute("0815")		// 15 Août (Assomption)
JourFériéAjoute("1101")		// Toussaint
JourFériéAjoute("1111")		// 11 Novembre
JourFériéAjoute("1225")		// Noël

// Ajout de 2 jours fériés supplémentaires pour la Moselle et l'Alsace
JourFériéAjoute("1226" + RC + jfVendrediSaint)
```


<a name="NOTE7"></a>
<a name="NOTE7_1"></a>


## Propriétés spécifiques aux champs Planning
<a name="proprietes_specifiques_aux_champs_planning_ELTTEXTE000657"></a>
Les propriétés suivantes sont spécifiques à la gestion d'un champ Planning par programmation.



|   |   |
| --- | --- |
| [DateDébut](../Proprietes/1000017426.md) | La propriété **DateDébut** permet de connaître et de modifier la date de début de la période sélectionnée :<br><br>- dans un champ Calendrier. <br><br>- dans un champ Agenda.<br><br>- dans un champ Planning.<br><br><br> |
| [DateFin](../Proprietes/1000017427.md) | La propriété **DateFin** permet de connaître et de modifier la date de fin de période sélectionnée : <br><br>- dans un champ Calendrier,<br><br>- dans un champ Agenda.<br><br>- dans un champ Planning.<br><br><br><br><br> |
| [DéplacementRDV](../Proprietes/1000019993.md) | La propriété **DéplacementRDV** permet de savoir et d'indiquer si l'utilisateur peut déplacer un rendez-vous dans un champ Planning ou dans un champ Agenda. |
| [GranularitéDéplacement](../Proprietes/1000019642.md) | La propriété **GranularitéDéplacement** permet de connaître et de modifier la taille de la grille pour le déplacement :  <br><br>- des rendez-vous d'un champ Agenda. <br><br>- des rendez-vous d'un champ Planning.<br><br>- des événements d'un champ TimeLine.<br><br>- des tâches d'une colonne de type Diagramme de Gantt.<br><br><br> |
| [GranularitéDurée](../Proprietes/1000019643.md) | La propriété **GranularitéDurée** permet de connaître et de modifier la taille de la grille utilisée pour le redimensionnement :<br><br>- des rendez-vous d'un champ Agenda. <br><br>- des rendez-vous d'un champ Planning.<br><br>- des événements d'un champ TimeLine.<br><br>- des tâches d'une colonne de type Diagramme de Gantt.<br><br><br> |
| [GranularitéRendezVous](../Proprietes/1000019100.md) | La propriété **GranularitéRendezVous** permet de connaître et de modifier la précision de la grille utilisée par le champ Agenda ou le champ Planning pour définir les rendez-vous. <br>Propriété conservée par compatibilité. |
| [HauteurJour](../Proprietes/1000020106.md) | La propriété **HauteurJour** permet de connaître et de modifier la hauteur des jours dans un champ Planning avec les jours en ligne et les ressources en colonne. |
| [HauteurRessource](../Proprietes/1000020109.md) | La propriété **HauteurRessource** permet de connaître et de modifier la hauteur des ressources dans un champ Planning avec les ressources en ligne. |
| [HauteurRuptureJour](../Proprietes/1000020105.md) | La propriété **HauteurRuptureJour** permet de connaître et de modifier la hauteur des ruptures entre les jours dans un champ Planning avec les jours en ligne et les ressources en colonne. |
| [HeureOuvrableDébut](../Proprietes/1000018958.md) | La propriété **HeureOuvrableDébut** permet de connaître et de modifier l'heure de début des heures ouvrables utilisées : <br><br>- par un champ Agenda.<br><br>- par un champ Planning.<br><br>- par une colonne de type Diagramme de Gantt (dans un champ Table ou Table hiérarchique). <br><br><br> |
| [HeureOuvrableFin](../Proprietes/1000019056.md) | La propriété **HeureOuvrableFin** permet de connaître et de modifier l'heure de fin des heures ouvrables utilisées :<br><br>- par un champ Agenda.<br><br>- par un champ Planning.<br><br>- par une colonne de type Diagramme de Gantt (dans un champ Table ou Table hiérarchique).<br><br><br> |
| [LargeurJour](../Proprietes/1000020108.md) | La propriété **LargeurJour** permet de connaître et de modifier la largeur des jours : <br><br>- dans un champ Planning avec les jours en colonne et les ressources en ligne.<br><br>- dans une colonne de type Diagramme de Gantt.<br><br><br> |
| [LargeurRessource](../Proprietes/1000020110.md) | La propriété **LargeurRessource** permet de connaître et de modifier la largeur des ressources dans un champ Planning avec les ressources en colonne. |
| [MasqueTitreDate](../Proprietes/1000019463.md) | La propriété **MasqueTitreDate** permet de connaître ou de modifier le masque utilisé pour le titre des colonnes des jours dans un champ Agenda ou dans un champ Planning. |
| [NbJourAffiché](../Proprietes/1000019005.md) | La propriété **NbJourAffiché** permet de : <br><br>- connaître et de modifier le nombre de jours affichés dans un champ Agenda ou dans un champ Planning. <br><br>- connaître le nombre de jours affichés dans un champ Diagramme de Gantt présent dans un état.<br><br><br> |
| [Num1erJourDeLaSemaine](../Proprietes/1000019004.md) | La propriété **Num1erJourDeLaSemaine** permet de connaître et de modifier le 1er jour de la semaine affiché dans :<br><br>- un champ Calendrier.<br><br>- un champ Agenda.<br><br>- un champ Planning. <br><br>- un champ de saisie au format Date avec Calendrier.<br><br><br> |
| [Ressource](../Proprietes/1000019308.md) | La propriété **Ressource** permet de : <br><br>- connaître les ressources visibles dans un champ Planning. <br><br>- connaître la ressource du champ Planning correspondant à l'indice spécifié. <br><br><br> |
| [RessourceSélectionnée](../Proprietes/1000019445.md) | La propriété **RessourceSélectionnée** renvoie le nom de la ressource correspondant à la sélection de l'utilisateur dans un champ Planning. |
| [SaisieDirecteRDV](../Proprietes/1000019995.md) | La propriété **SaisieDirecteRDV** permet de savoir et d'indiquer si l'utilisateur peut modifier directement le titre d'un rendez-vous dans un champ Planning ou dans un champ Agenda. |
| [SélectionPériode](../Proprietes/1000019996.md) | La propriété **SélectionPériode** permet de savoir et d'indiquer si l'utilisateur peut sélectionner une période dans un champ Planning ou dans un champ Agenda. |





Pour connaître la liste complète des propriétés du WLangage pouvant être utilisées avec un champ de type Planning, consultez [Propriétés associées au champ Planning](../WDChamp/1000019502.md).


