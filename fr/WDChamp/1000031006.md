


## Manipuler un champ Tableau de bord par programmation (syntaxe préfixée)
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Présentation
<a name="presentation_ELTTEXTE000091"></a>
Pour manipuler un champ Tableau de bord par programmation, WINDEV, WEBDEV et WINDEV Mobile proposent les [fonctions de gestion des Tableaux de bord](../WDLang1/1000022537.md). 

Cette page d'aide détaille comment manipuler un champ Tableau de bord par programmation. 

![Linux](https://doc.pcsoft.fr/ext/images/fr/LX.png) Remarque : Le champ Tableau de bord est disponible en Linux uniquement dans les sites WEBDEV Linux. Le champ Tableau de bord n'est pas disponible dans les applications WINDEV pour Linux.



<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Manipuler le champ Tableau de bord par programmation
<a name="manipuler_champ_tableau_bord_par_programmation_ELTTEXTE000137"></a>


### Ajouter un Widget
<a name="ajouter_widget_ELTPARAGRAPHE000160"></a>L'ajout des Widgets dans un champ Tableau de bord par programmation est réalisé via la fonction [&lt;Tableau de bord&gt;.AjouteWidget](../WDLang1/1000022502.md). Par défaut, ce Widget est invisible. Il est possible de l'afficher en utilisant la propriété [Visible](../Proprietes/2510138.md). Si le Widget n'est pas rendu directement visible, l'utilisateur pourra le rendre visible via le menu contextuel du mode édition.

Exemples : 

- Ajout d'un Widget simple (sans paramètres) : 
	
	```wl
	// Ajoute un Widget simple
	nIndice = TDB_TableauDeBord.AjouteWidget(FI_Widget_AlerteStock, "Alerte stock")
	```


- Ajout d'un Widget avec paramètres : 
	
	```wl
	nIndice est un entier
	
	// Ajoute un widget qui attend des paramètres
	nIndice = TDB_TableauDeBord.AjouteWidget(FI_Widget_Horloge, ...
		"Horloge " + COMBO_AjoutWidget[COMBO_AjoutWidget].ValeurAffichée, ...
		COMBO_AjoutWidget[COMBO_AjoutWidget].ValeurAffichée)
	
	// Affiche ce widget à la demande
	SI OuiNon(Oui, "Souhaitez-vous afficher ce widget ?") = Oui ALORS
		TDB_TableauDeBord[nIndice].Visible = Vrai
	SINON
	 	ToastAffiche("Widget ajouté au tableau de bord en ""caché"". "+ ...
		"Passez en mode édition pour l'ajouter", toastCourt, cvMilieu, chCentre)
	FIN
	```



**Cas particulier : ajouter un Widget dans la configuration initiale**
Par défaut, le champ Tableau de bord affiche les Widgets tels qu'ils ont été définis sous l'éditeur de fenêtres ou de pages. 

Pour ajouter des Widgets par programmation au chargement du champ Tableau de bord, il suffit d'utiliser : 

- la fonction [&lt;Tableau de bord&gt;.AjouteWidget](../WDLang1/1000022502.md) pour définir le Widget à ajouter. 

- la fonction [&lt;Tableau de bord&gt;.ConfigurationInitiale](../WDLang1/1000022525.md) pour paramétrer la position du Widget dans la configuration initiale. 




Attention : Dans ce cas, ces fonctions doivent obligatoirement être utilisées dans l'événement "Initialisation" du champ Tableau de bord. 

Exemples : 

- Ajout d'un Widget simple (sans paramètres)
	
	```wl
	// --- Initialisation du champ TDB_TableauDeBord
	// - alerte de stock
	// Ajoute le widget
	nIndice = TDB_TableauDeBord.AjouteWidget(FI_Widget_AlerteStock, "Alerte stock")
	// Configure le widget (calé à gauche)
	TDB_TableauDeBord.ConfigurationInitiale(nIndice, 5, 1)
	```


- Ajout d'un widget avec paramètres
	
	```wl
	// --- Initialisation du champ TDB_TableauDeBord
	
	dDate est une Date
	nIndice est un entier
	nX, nY sont des entiers
	
	// Ajoute des informations dans la configuration initiale :
	// - chiffres clés des 3 derniers mois
	nX = 1 ; nY = 3
	POUR i = 1 A 3
		// Ajoute le widget
		nIndice = TDB_TableauDeBord.AjouteWidget(FI_Widget_ChiffreClé, ...
			ChaîneConstruit("Chiffres clés de %1", ...
			dDate.VersChaîne("Mmm AAAA")), dDate)
		// Configure le widget (calé à gauche)
		TDB_TableauDeBord.ConfigurationInitiale(nIndice, nX, nY)
		// Le prochain sera plus bas
		nY++
		dDate.Mois--
	FIN
	```



Remarque : Pour rafraîchir un Widget avec paramètres, il est conseillé de :  

- Ajouter un événement optionnel de rafraîchissement du Widget dans les événements de la fenêtre ou de la page interne. 

- Saisir le code nécessaire au rafraîchissement du Widget. Ce code peut par exemple permettre de mettre à jour les éléments affichés dans la fenêtre ou la page interne. 

- Rafraîchir le Widget avec la fonction [&lt;Tableau de bord&gt;.Affiche](../WDLang1/1000022500.md).



<a name="NOTE2_2"></a>


### Exécuter une procédure définie pour une fenêtre ou une page interne (utilisée en Widget)
<a name="executer_une_procedure_definie_pour_une_fenetre_une_page_interne_utilisee_widget_ELTPARAGRAPHE000248"></a>Pour exécuter une procédure définie dans la fenêtre ou la page interne utilisée en Widget, il est possible d'utiliser les syntaxes suivantes : 

- Syntaxe utilisant le nom de la fenêtre ou de la page interne : 
	
	```txt
	NomFenêtreInterne.NomProcédure(Param1, Param2, ...)
	```

	Par exemple : 
	
	```wl
	FI_Widget_ChiffreClé.NouvelleDate(SAI_Date)
	```


- Syntaxe utilisant une variable de type Champ (à utiliser notamment si la même fenêtre ou page interne est utilisée plusieurs fois dans le champ Tableau de bord) : 
	
	```txt
	NomVariable est un Champ <- NomTableauDeBord[IndiceWidget]
	NomVariable.NomProcédure(Param1, Param2, ...)
	```

	Par exemple : 
	
	```wl
	X est un Champ <-TDB_TableauDeBord[4]
	X.NouvelleDate(SAI_Date)
	```





<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Manipuler un Widget par programmation
<a name="manipuler_widget_par_programmation_ELTTEXTE000268"></a>
Pour manipuler un Widget par programmation, il est possible d'utiliser une des syntaxes suivantes : 

- Syntaxe 1 : Utilisation directe du champ Tableau de bord :  
	
	```txt
	NomTableauDeBord[IndiceWidget]
	```

	Par exemple : 
	
	```wl
	Trace(TDB_MonTableauDeBord[1].Libellé)
	```


- Syntaxe 2 : Utilisation d'une variable de type Champ  : 
	
	```txt
	NomVariable est un Champ <- NomTableauDeBord[IndiceWidget]
	```

	Par exemple : 
	
	```wl
	X est un Champ <-TDB_TableauDeBord[4]
	Trace(X.Libellé)
	```





Les propriétés pouvant être utilisées sur les Widgets sont identiques à celles pouvant être utilisées sur les fenêtres ou pages internes. Pour plus de détails, consultez [Propriétés associées aux fenêtres internes](../WDChamp/1013256.md) et [Propriétés associées aux pages internes](../WDChamp/1013282.md).

<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Réaliser un Drag and Drop d'un Widget sur un champ Tableau de bord
<a name="realiser_drag_and_drop_widget_sur_champ_tableau_bord_ELTTEXTE000317"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png) WINDEV permet de réaliser un Drag and Drop programmé d'un champ Image, Liste, Zone répétée (ou autre) vers un champ Tableau de bord. 

Les différentes étapes sont les suivantes : 

1. **Déclarer que le champ Tableau de bord est cible du Drag and Drop**.
	Cette déclaration est effectuée dans le code d'initialisation du champ grâce à la propriété [DndCible](../Proprietes/2510016.md). 
	Exemple : 
	
	```wl
	// -- Initialisation de TDB_TableauDeBord
	// Autorise le Drag and drop
	MoiMême.DndCible = dndAuto
	```


2. **Déclarer que le champ Tableau de bord est source du Drag and Drop**.
	Dans le code d'initialisation du champ source, déclarez que le champ est source du Drag and Drop avec la propriété [DndSource](../Proprietes/2510014.md). 
	Exemple : 
	
	```wl
	// L'image est source de Drag and Drop
	MoiMême.DndSource = dndProgrammé
	```


3. **Définir la procédure pour le début de glisser** (par exemple dans le code d'initialisation du champ Source).
	Exemple : 
	
	```wl
	// Définit une procédure pour le "début de glisser"
	// Cette procédure utilise la fonction DnDDonneElementTdb 
	// pour définir le widget à dropper
	DnDEvénement(onDndDebut, MoiMême, dndDébutGlisser)
	```


4. **Saisir le code de la procédure**. 
	Exemple : 
	
	```wl
	PROCEDURE onDndDebut()
	
	// Selon le champ source, il faut définir le widget à dropper
	SELON _DND.ChampSource
		// Indique que le drop sur le champ Tableau de bord 
		// doit ajouter le widget "Horloge" 
		// en utilisant la fenêtre interne "FI_Widget_Horloge"
		CAS IMG_Widget_Calendrier.Nom
			DnDDonneElémentTDB(FI_Widget_Calendrier, "Horloge")
			
		AUTRES CAS
			Erreur("Seule l'image IMG_Widget_Calendrier" + ...
				"est autorisée pour le Drag and Drop")
	FIN
	```





<a name="NOTE7"></a>
<a name="NOTE7_1"></a>


## Propriétés spécifiques aux champs Tableau de bord
<a name="proprietes_specifiques_aux_champs_tableau_bord_ELTTEXTE000378"></a>
Les propriétés suivantes sont spécifiques à la gestion d'un champ Tableau de bord par programmation.



|   |   |
| --- | --- |
| [CompacteOption](../Proprietes/1000020608.md) | La propriété **CompacteOption** permet de :<br><br>- savoir si les options d'un champ Interrupteur, Sélecteur ou Tableau de bord sont en mode compacté.<br><br>- passer les options d'un champ Interrupteur, Sélecteur ou Tableau de bord en mode compacté.<br><br><br> |
| [HauteurElément](../Proprietes/1000020068.md) | La propriété **HauteurElément** permet de :<br><br>- Connaître ou modifier la hauteur des éléments d'un champ Organigramme. <br><br>- Connaître ou modifier la hauteur des cellules d'un champ Tableau de bord.<br><br><br> |
| [LargeurElément](../Proprietes/1000020066.md) | La propriété **LargeurElément** permet de :<br><br>- Connaître ou modifier la largeur des éléments d'un champ Organigramme.<br><br>- Connaître ou modifier la largeur des cellules d'un champ Tableau de bord.<br><br><br> |
| [MargeHauteur](../Proprietes/1000021029.md) | La propriété **MargeHauteur** permet de : <br><br>- Connaître la marge verticale entre les Widgets présents dans un champ Tableau de bord. <br><br>- Modifier la marge verticale entre les Widgets présents dans un champ Tableau de bord.<br><br><br> |
| [MargeLargeur](../Proprietes/1000021028.md) | La propriété **MargeLargeur** permet de : <br><br>- Connaître la marge horizontale entre les Widgets présents dans un champ Tableau de bord. <br><br>- Modifier la marge horizontale entre les Widgets présents dans un champ Tableau de bord.<br><br><br> |





Pour connaître la liste complète des propriétés du WLangage pouvant être utilisées avec un champ de type Tableau de bord, consultez [Propriétés associées au champ Tableau de bord](../WDChamp/1000031003.md).


