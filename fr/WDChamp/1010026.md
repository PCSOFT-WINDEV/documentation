


## Fenêtre paramétrée
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Présentation
<a name="presentation_ELTTEXTE000294"></a>
Une fenêtre peut :

- Recevoir des paramètres à son ouverture.

- Renvoyer des valeurs à sa fermeture.




Cette fenêtre se comporte alors comme une procédure renvoyant ou non des valeurs.

Ce type de fenêtres peut être utile dans les cas suivants par exemple :

- fenêtres "calendrier" (fixer une date par défaut à l'ouverture et renvoyer une date sélectionnée à la fermeture),

- fenêtres de recherche,

- fenêtres de login renvoyant le mot de passe saisi, ...




Cette page d'aide présente :

- [Le passage de paramètres à une fenêtre,](#NOTE2)

- [Le renvoi d'une valeur lors de la fermeture d'une fenêtre,](#NOTE3)

- [Le test d'une fenêtre paramétrée.](#NOTE4)




<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Passer des paramètres à une fenêtre lors de son ouverture
<a name="passer_des_parametres_une_fenetre_lors_son_ouverture_ELTTEXTE000318"></a>


### Passage de paramètres : cas général
<a name="passage_parametres_cas_general_ELTPARAGRAPHE000038"></a>Pour passer des paramètres à une fenêtre lors de son ouverture :

1. Déclarez une procédure dans l'événement "Déclarations globales" de la fenêtre. Le nom de cette procédure doit être identique au nom de la fenêtre. Les paramètres de cette procédure correspondent aux paramètres à passer à la fenêtre.
	**Attention** : La déclaration de la procédure (mot-clé PROCEDURE) doit correspondre à la première ligne de l'événement "Déclarations globales".
	Par exemple, la fenêtre FEN_Calendrier permet de gérer la sélection d'une date dans un calendrier. Cette fenêtre attend un paramètre indiquant la date à sélectionner.
	
	```wl
	// -- Déclarations globales de FEN_Calendrier 
	
	PROCEDURE FEN_Calendrier(sDateSel)
	//  sDateSel : date à sélectionner
	```

	Ce paramètre est ensuite manipulable depuis n'importe quel événement de la fenêtre (code d'un bouton, procédures locales, etc.).
	Par exemple, dans l'événement "Initialisation" de la fenêtre FEN_Calendrier :
	
	```wl
	// -- Initialisation de FEN_Calendrier
	
	// Le champ SAI_ChampDate est un champ de saisie 
	// Il contient la valeur du paramètre sDateSel
	SAI_ChampDate = sDateSel
	```


2. Passez le paramètre attendu par la fenêtre lors de son ouverture (fonctions [Ouvre](../WDLang1/3038035.md), [OuvreFenêtreMobile](../WDLang1/1000021018.md), [OuvreFille](../WDLang1/3038021.md), [OuvreSoeur](../WDLang1/3038045.md), [MDIOuvre](../WDLang1/3052005.md)). Par exemple, le bouton BTN_Calendrier permet d'ouvrir la fenêtre FEN_Calendrier. Lors de l'ouverture de cette fenêtre, la date du jour est passée en paramètre.
	
	```wl
	// -- Code de clic du bouton BTN_Calendrier
	
	// Ouverture de la fenêtre FEN_Calendrier
	// Passage de la date du jour en paramètre
	Ouvre(FEN_Calendrier, DateDuJour())
	```






### Paramètres passés par valeur
<a name="parametres_passes_par_valeur_ELTPARAGRAPHE000075"></a>Si les paramètres passés à une fenêtre sont modifiés dans cette fenêtre, ces modifications seront prises en compte uniquement dans cette fenêtre. La valeur de ces paramètres n'est pas modifiée dans le traitement appelant.

Par exemple :

1. La variable *MaDate* est déclarée dans le code du bouton BTN_Calendrier. Cette variable contient la date du jour (par exemple : MaDate = DateDuJour()).

2. Cette variable est passée en paramètre à la fenêtre FEN_Calendrier. Le paramètre *sDateSel* contient la valeur de la variable *MaDate*.

3. La valeur du paramètre *sDateSel* est modifiée dans la fenêtre FEN_Calendrier (par exemple : sDateSel = "20020701").

4. La valeur de la variable *MaDate* n'est pas modifiée.



<a name="NOTE5_2"></a>


### Donner une valeur par défaut aux paramètres dans la déclaration
<a name="donner_une_valeur_par_defaut_aux_parametres_dans_declaration_ELTPARAGRAPHE000089"></a>Il est possible de donner une valeur par défaut aux paramètres dans la déclaration des paramètres. Dans ce cas, le paramètre devient optionnel et s'il n'est pas précisé, la valeur par défaut sera utilisée. 

Par exemple, pour donner une valeur par défaut dans l'exemple précédent, saisissez le code suivant dans l'événement "Déclarations globales" de la fenêtre FEN_Calendrier :

```wl
// -- Déclarations globales de FEN_Calendrier
PROCEDURE FEN_Calendrier(sDateSel = "20030101")
//  sDateSel : date à sélectionner
```



### Paramètres nommés
<a name="parametres_nommes_ELTPARAGRAPHE000099"></a>Pour passer des paramètres à une fenêtre lors de son ouverture, il est également possible d'utiliser des paramètres nommés :

1. Déclarez la procédure toujours de la même façon. Exemple : 
	
	```wl
	PROCEDURE FEN_Voyage(Identifiant, Disponibilité)
	```


2. Passez le paramètre attendu par la fenêtre lors de son ouverture (fonctions [Ouvre](../WDLang1/3038035.md), [OuvreFenêtreMobile](../WDLang1/1000021018.md), [OuvreFille](../WDLang1/3038021.md), [OuvreSoeur](../WDLang1/3038045.md), [MDIOuvre](../WDLang1/3052005.md)) en utilisant pour chaque paramètre la syntaxe : 
	```txt
	<Nom du paramètre> : Valeur
	```

	Par exemple : 
	```wl
	Ouvre(FEN_Voyage(<Identifiant>:123456, <Disponiblité>:Vrai))
	```



Remarque : L'utilisation des paramètres nommés permet par exemple de changer l'ordre des paramètres attendus. Par exemple, il est possible d'écrire : 
```wl
Ouvre(FEN_Voyage(<Disponiblité>:Vrai, <Identifiant>:123456))
```

<a name="NOTE5_3"></a>


### Portées des paramètres
<a name="portees_des_parametres_ELTPARAGRAPHE000132"></a>Les paramètres passés à une fenêtre sont globaux à tous les événements de cette fenêtre (initialisation, code d'un bouton, code d'une procédure locale, ...).

<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Renvoyer une valeur lors de la fermeture d'une fenêtre
<a name="renvoyer_une_valeur_lors_fermeture_une_fenetre_ELTTEXTE000366"></a>
Pour renvoyer une valeur lors de la fermeture d'une fenêtre : 

1. Utilisez la propriété [ValeurRenvoyée](../Proprietes/2510134.md) dans les traitements faisant appel à la fonction [Ferme](../WDLang1/3038018.md).
	Par exemple, dans la fenêtre FEN_Calendrier, le bouton BTN_OK et le bouton BTN_ANNULER ferment la fenêtre après avoir initialisé la valeur à renvoyer :
	
	```wl
	// -- Clic sur bouton BTN_OK (fenêtre FEN_Calendrier)
	
	// L'utilisateur a cliqué sur OK, 
	// la date saisie dans le champ SAI_ChampDate est renvoyée 
	MaFenêtre.ValeurRenvoyée = SAI_ChampDate
	// Fermeture de la fenêtre
	Ferme()
	```

	
	```wl
	// -- Clic sur bouton BTN_ANNULER (fenêtre FEN_Calendrier)
	
	// L'utilisateur a cliqué sur BTN_ANNULER,
	// une chaîne vide est renvoyée
	MaFenêtre.ValeurRenvoyée = ""
	// Fermeture de la fenêtre
	Ferme()
	```

	Il est également possible d'utiliser la propriété [ValeurRenvoyée](../Proprietes/2510134.md) dans l'événement "Fermeture" de la fenêtre :
	
	```wl
	// -- Code de fermeture de la fenêtre FEN_Calendrier
	
	// La date est saisie dans le champ SAI_ChampDate
	SI SAI_ChampDate = "" ALORS
		// Aucune date saisie
		MaFenêtre.ValeurRenvoyée = ""
	SINON
		// Une date est saisie
		MaFenêtre.ValeurRenvoyée = SAI_ChampDate
	FIN
	```


2. Récupérez dans une variable la valeur renvoyée par la fenêtre lors de sa fermeture. Cette variable contient le résultat de la fonction [Ouvre](../WDLang1/3038035.md).
	Par exemple, le bouton BTN_Calendrier permet d'ouvrir la fenêtre FEN_Calendrier. Lors de l'ouverture de cette fenêtre (fonction [Ouvre](../WDLang1/3038035.md)), la date du jour est passée en paramètre. La valeur renvoyée par la fenêtre lors de sa fermeture correspond au résultat renvoyé par la fonction [Ouvre](../WDLang1/3038035.md). Ce résultat est récupéré dans la variable DateRésultat :
	
	```wl
	// -- Clic sur bouton BTN_Calendrier
	// Ouverture de la fenêtre FEN_Calendrier
	// DateRésultat est une variable chaîne contenant 
	// la valeur renvoyée par la fenêtre FEN_Calendrier
	DateRésultat = Ouvre(FEN_Calendrier, DateDuJour())
	
	// Analyse du résultat renvoyé par la fenêtre
	SI DateRésultat = "" ALORS
		Info("Aucune date n'a été sélectionnée.")
	SINON
		Info("Date sélectionnée : " + DateVersChaîne(DateRésultat, maskDateSystème))
	FIN
	```




L'exécution du code du bouton BTN_Calendrier est stoppé jusqu'à la fermeture de la fenêtre FEN_Calendrier.
![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/fr/UNIVERSALAPP.png) **Cas particulier : Valeur renvoyée par une fenêtre fille dans une application WINDEV Mobile**
Dans les applications mobiles, l'ouverture des fenêtres est effectuée par la fonction [OuvreFenêtreMobile](../WDLang1/1000021018.md) (ou la fonction [OuvreFille](../WDLang1/3038021.md)) et non la fonction [Ouvre](../WDLang1/3038035.md). 

Il est possible de connaître la valeur renvoyée par une fenêtre fille dans l'événement "Fermeture d'une fenêtre fille" de la fenêtre ouvrant la fenêtre fille (celle qui a utilisé la fonction [OuvreFenêtreMobile](../WDLang1/1000021018.md) ou la fonction [OuvreFille](../WDLang1/3038021.md)).

Par exemple :

- Dans le code de fermeture de la fenêtre fille, la fonction [Ferme](../WDLang1/3038018.md) est utilisée pour renvoyer un paramètre : 
	
	```wl
	Ferme("", MonParamètre)
	```


- Dans l'événement "Fermeture d'une fenêtre fille" de la fenêtre appelante, le nom de la fenêtre fille ainsi que la valeur renvoyée peuvent être récupérés en utilisant le mot-clé [MaFenêtreFille](../Motscles/1511021.md) : 
	
	```wl
	// Fermeture d'une fenêtre fille
	NomFenêtreFille = MaFenêtreFille.Nom 
	ValeurFenêtreFille = MaFenêtreFille.ValeurRenvoyée
	```





<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Tester une fenêtre paramétrée
<a name="tester_une_fenetre_parametree_ELTTEXTE000396"></a>
Pour tester une fenêtre paramétrée : 

1. Ouvrez la fenêtre paramétrée sous l'éditeur de fenêtres : sous le volet "Accueil", dans le groupe "Général", cliquez sur "Ouvrir".

2. Cliquez sur ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=Ico_Go_Fenetre_WD_bl.gif) parmi les boutons d'accès rapide (ou appuyez sur la touche F9). La fenêtre suivante apparaît :<br>![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=fen%EAtre_param%E9tr%E9e%20-%20HC%20N%B0001.gif)


3. Spécifiez la valeur du ou des paramètres à utiliser pour le test de la fenêtre. Pour utiliser la valeur par défaut du ou des paramètres, saisissez le caractère "\*".

4. Validez. La fenêtre s'affiche selon la ou les valeurs spécifiées.





