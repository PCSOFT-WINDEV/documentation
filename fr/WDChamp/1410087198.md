


## Manipuler un champ Traitement de texte par programmation (syntaxe préfixée)
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Présentation
<a name="presentation_ELTTEXTE000087"></a>
Un champ Traitement de texte peut être manipulé par programmation. 

Le WLangage propose pour manipuler le champ Traitement de texte : 

- un ensemble de fonctions qui permettent de réaliser toutes les opérations courantes (programmation directe, à l'aide des fonctions WLangage de type DocXXX. Ces fonctions Pour plus de détails, consultez [Fonctions de gestion du champ Traitement de texte](../WDLang1/1000024544.md). 

- une programmation avancée à l'aide des différents types avancés du WLangage permettant de manipuler directement le contenu du fichier docx affiché par le champ Traitement de texte. Pour plus de détails, consultez [Types associés au champ Traitement de texte](../WDLang1/1000022736.md). 




Cette page d'aide détaille comment manipuler un champ Traitement de texte par programmation.




## Manipuler le champ Traitement de texte par programmation
<a name="manipuler_champ_traitement_texte_par_programmation_ELTTEXTE000131"></a>


### Ouverture et création d'un document docx dans un champ Traitement de texte
<a name="ouverture_creation_document_docx_dans_champ_traitement_texte_ELTPARAGRAPHE000154"></a>Pour ouvrir un document docx existant par programmation dans un champ Traitement de texte, il est possible de : 

- affecter le champ Traitement de texte directement avec le fichier docx. Par exemple :
	
	```wl
	// Sélectionne un fichier .docx dans le répertoire en cours
	sNomFichier est une chaîne
	sNomFichier = fSélecteur("", "présentation.docx", ...
		"Sélection de fichier DocX", ...
		"Fichier Docx (*.docx)" + TAB + "*.docx", "*.docx", fselOuvre)
	
	SI sNomFichier ~= "" ALORS
		// Aucun fichier sélectionné
		RETOUR
	FIN
	
	// Chargement du fichier docx
	TT_MonDocument = sNomFichier
	```


- utiliser la fonction WLangage [&lt;Traitement de texte&gt;.Ouvre](../WDLang1/1000024269.md).
	 Par exemple :
	
	```wl
	TT_MonDocument.Ouvre("C:\Users\test\Documents\fichier.docx")
	
	SI ErreurDétectée() ALORS
		Erreur(ErreurInfo())
	RETOUR
	```


- utiliser une variable de type [Document](../WDLang1/1000022461.md) et l'affecter au champ Traitement de texte.
	Par exemple :
	
	```wl
	MonDocument est un Document
	MonDocument = "C:\temp\presentation.docx"
	TT_MonDocument = MonDocument
	```

	ou bien : 
	
	```wl
	MonDocument est un Document
	MonDocument = DocOuvre("C:\Users\test\Documents\fichier.docx")
	SI ErreurDétectée() ALORS
		Erreur(ErreurInfo())
	SINON
		TT_MonDocument = MonDocument
	FIN
	```



Attention : seuls les fichiers au format docx peuvent être manipulés par le champ Traitement de texte. 

**Astuce** : Pour afficher un document vide dans un champ Traitement de texte, il suffit d'affecter le champ Traitement de texte avec une chaîne vide. 
Exemple : 

```wl
// Création d'un document vierge
TT_MonDocument = ""
```



### Enregistrement d'un document docx
<a name="enregistrement_document_docx_ELTPARAGRAPHE000203"></a>Pour enregistrer un document docx, il suffit d'utiliser la fonction [&lt;Traitement de texte&gt;.Sauve](../WDLang1/1000024393.md). 

Exemple : 

```wl
// On choisit le répertoire et le nom de sauvegarde 
sNomFichier est une chaîne = fSélecteur(ComplèteRep(fRepExe()), "présentation.docx", ...
	"Sélection de fichiers DocX","Fichier Docx (*.docx)" + TAB + "*.docx" , "*.docx", fselCrée)

// On sauvegarde le fichier
TT_SansNom1.Sauve(sNomFichier)

SI ErreurDétectée ALORS
	Erreur(ChaîneConstruit("Le fichier %1 n'a pas pu être enregistré.", sNomFichier), ErreurInfo())
	RETOUR
FIN
```
Remarques : 

- Le contenu du champ Traitement de texte peut également être : 

	- imprimé (fonction [iImprimeDoc](../WDLang5/1000022390.md)).

	- enregistré au format PDF (fonction [&lt;Traitement de texte&gt;.VersPDF](../WDLang1/1000024402.md)).




- La fonction [&lt;Traitement de texte&gt;.VersTexte](../WDLang1/1000024403.md) permet de récupérer le texte du document. 

- La fonction [&lt;Traitement de texte&gt;.VersImage](../WDLang1/1000024401.md) permet de convertir une page du document en une image.





### Effectuer une recherche dans un document et une modification sur le résultat de la recherche
<a name="effectuer_une_recherche_dans_document_une_modification_sur_resultat_recherche_ELTPARAGRAPHE000259"></a>Pour effectuer une recherche dans un document Traitement de texte, il suffit d'utiliser la fonction [&lt;Traitement de texte&gt;.Recherche](../WDLang1/1000024271.md). 

Exemple : 

```wl
// Recherche du mot "BEAUGRAND" dans le texte 
// Sélectionne uniquement le premier trouvé pour changer sa couleur
tabFragments est tableau de docFragments = TT_SansNom1.Recherche("BEAUGRAND")

SI tabFragments.Occurrence >= 1 ALORS
	// Positionnement du curseur au début du premier mot trouvé
	TT_SansNom1.Curseur = tabFragments[1].PositionDébut
	// Calcul de la longueur de la sélection
	TT_SansNom1.LongueurSélection = ...
				tabFragments[1].PositionFin - tabFragments[1].PositionDébut+1
	// Modification de la couleur du texte
	tabFragments[1].MiseEnForme.CouleurTexte = RougePastel
	SORTIR
FIN
```


Dans ce code, la variable [docFragment](../WDLang1/1000022482.md) correspond à la portion de texte contenant le mot recherché, avec sa position dans le texte complet. Il est ensuite possible de manipuler le fragment trouvé.


### Manipuler la sélection
<a name="manipuler_selection_ELTPARAGRAPHE000297"></a>Pour manipuler la sélection actuellement réalisée dans un champ Traitement de texte, il est possible de récupérer cette sélection dans une variable de type [docFragment](../WDLang1/1000022482.md). 

Le code suivant permet par exemple de changer la couleur du texte sélectionné. 
Exemple : 

```wl
// Changement de la couleur de la sélection
// Récupération de la sélection actuelle
MaSélection est un docFragment(TT_MonDocument, TT_MonDocument.Curseur, ...
		TT_MonDocument.LongueurSélection)
// Changement de la couleur
MaSélection.MiseEnForme.CouleurTexte = RougePastel
```


Le type de variable [docFragment](../WDLang1/1000022482.md) possède un constructeur spécifique permettant de récupérer facilement la sélection.


### Ajouter du texte dans un document
<a name="ajouter_texte_dans_document_ELTPARAGRAPHE000323"></a>**Pour ajouter du texte dans un document existant**, il existe plusieurs méthodes : 

- Insérer un fragment à la position désirée. Exemple : 
	
	```wl
	// Insère un texte à la fin du document 
	frag2 est un docFragment(TT_MonDoc, -1 , 0)
	frag2.Texte += "Texte de fin"
	```


- Insérer directement un texte à partir d'une position donnée avec la fonction [&lt;Traitement de texte&gt;.Insère](../WDLang1/1000024267.md). 
	
	```wl
	TT_Document.Insère(TT_Document.Curseur, "Mon texte inséré")
	```


- Ajouter un texte à la fin du document avec la fonction [&lt;Traitement de texte&gt;.Ajoute](../WDLang1/1000024032.md). 




**Pour ajouter du texte dans un document vide**, il est possible de : 

- utiliser l'affectation directe du champ : 
	
	```txt
	<Champ Traitement de texte> = "Texte à insérer"
	```
Exemple : 
	
	```wl
	TT_Mondocument = "Je suis la première phrase de ce document."
	```


- ajouter le texte avec la fonction [&lt;Traitement de texte&gt;.Ajoute](../WDLang1/1000024032.md). 

- insérer un fragment à la première position. Exemple : 
	
	```wl
	// sTexteOriginal contient le texte à insérer 
	frag est un docFragment(TT_MonDoc, TT_MonDoc.Curseur, 0)
	frag.Texte += sTexteOriginal
	```


- insérer directement un texte à partir de la position 1 avec la fonction [&lt;Traitement de texte&gt;.Insère](../WDLang1/1000024267.md). 
	
	```wl
	TT_Document.Insère(1, "Mon texte inséré")
	```






### Ajouter un saut de page dans un document
<a name="ajouter_saut_page_dans_document_ELTPARAGRAPHE000387"></a>Pour ajouter par programmation un saut de page dans un document, il suffit d'utiliser le code suivant : 

```wl
// Ajoute un saut de page 
TT_SansNom1.Ajoute(Caract(12))
```
Il est également possible d'utiliser les constantes suivantes pour ajouter le type de saut voulu dans un document ou un fragment : 

- *docSautDeColonne* : Ajoute un saut de colonne dans une section multicolonne. Si la section n'est pas multicolonne, un saut de page est ajouté. 

- *docSautDeLigne* : Ajoute un saut de ligne. 

- *docSautDePage* : Ajoute un saut de page. 

- *docSautDeParagraphe* : Ajoute un saut de paragraphe. 




Exemple : 

```wl
// Ajoute un saut de page 
TT_SansNom1.Ajoute(docSautDePage + "Texte sur la page suivante")
```



### Gestion des signets
<a name="gestion_des_signets_ELTPARAGRAPHE000424"></a>Il est possible de gérer les signets dans un document de type Traitement de texte. Quelques exemples : 

- Ajouter un signet : Ajoute un signet ayant pour nom MonSignet avec le texte sélectionné.
	
	```wl
	doc est un document
	doc <- TT_MonDocument
	fragSélection est un docFragment = TT_MonDocument.Sélection.Fragment
	doc.Signet["MonSignet"] = fragSélection
	```


- Supprimer un signet : Supprime le signet qui porte le nom Signet1 : 
	
	```wl
	doc est un document
	doc <- TT_MonDocument
	Supprime(doc.Signet, "Signet1")
	```


- Lister les signets : Liste les signets d'un document : 
	
	```wl
	doc est un document 
	doc <- TT_MonDocument
	POUR TOUT uUnSignet,NomSignet de doc.Signet
		Trace(NomSignet) // Nom du signet
		Trace(uUnSignet.Texte) // Texte du signet
	FIN
	```


- Insérer un texte à la fin d'un signet : 
	
	```wl
	doc est un Document <- TT_MonDocument.Valeur
	// Recherche de la position du signet 
	fragmentSignet est un docFragment = doc.Signet["Signet 1"]	
	SI fragmentSignet <> Null ALORS
		// Insertion du texte à la fin du signet
		soit nPositionInsertion = fragmentSignet.PositionFin
		// Insertion à proprement dite
		TT_MonDocument.Insère(nPositionInsertion, "Texte à insérer à la position du signet")
	SINON
		Erreur("Signet 'Signet 1' non trouvé dans le document")
	FIN
	```






### Enchaîner des états et des documents de type Traitement de texte
<a name="enchainer_des_etats_des_documents_type_traitement_texte_ELTPARAGRAPHE000454"></a>De la même manière qu'il est possible d'enchaîner des états, il est également possible d'enchaîner des états et des documents de type Traitement de texte. 

Pour cela, il suffit d'utiliser la fonction [iEnchaînementAjouteDoc](../WDLang5/1000023089.md) en plus des fonctions classiques de création des enchaînements d'états. Par exemple : 

```wl
MonDocument est un Document = "c:\temp\conditiongenerales.docx"

// Impression dans le visualisateur de rapports
iDestination(iVisualisateur)
// Ajout des états dans l'enchaînement
iEnchaînementAjoute(ETAT_Etat1)
iEnchaînementAjoute(ETAT_Etat2, 3)
// Ajout des conditions générales sous forme d'un document
iEnchaînementAjouteDoc(MonDocument)
iEnchaînementImprime()
```
Pour plus de détails, consultez [Enchaîner des états](../WDChamp/9000128.md).


### Propriétés associées au champ Traitement de Texte
<a name="proprietes_associees_champ_traitement_texte_ELTPARAGRAPHE000476"></a>Les propriétés suivantes sont spécifiques au champ Traitement de texte. Elles permettent essentiellement de manipuler les caractéristiques du champ : 



|   |   |
| --- | --- |
| [AffichageCaractèresContrôle](../Proprietes/1000022634.md) | La propriété **AffichageCaractèresContrôle** permet de : <br><br>- Savoir si l'affichage des caractères de contrôles est activé dans un champ Traitement de texte. <br><br>- Activer ou désactiver l'affichage des caractères de contrôle dans un champ Traitement de texte.<br><br><br> |
| [AffichageSignets](../Proprietes/1000023694.md) | La propriété **AffichageSignets** permet : <br><br>- de connaître le mode d'affichage des signets dans un champ Traitement de texte. <br><br>- d'activer ou de désactiver l'affichage des signets dans un champ Traitement de texte.<br><br><br> |
| [BarreOutilsVisible](../Proprietes/1000021333.md) | La propriété **BarreOutilsVisible** permet : <br><br>- de savoir si la barre d'outils ou le ruban est affichée dans un champ.  <br><br>- d'afficher ou non la barre d'outils ou le ruban dans un champ.<br><br><br> |
| [CheminFichier](../Proprietes/1000023104.md) | La propriété **CheminFichier** permet de connaître : <br><br>- le nom du fichier xlsx associé à un champ Tableur.<br><br>- le nom du fichier associé à un champ Editeur d'images. <br><br>- le nom du fichier PDF associée à un champ Lecteur PDF. <br><br>- le nom du fichier DOCX associé à un champ Traitement de texte. <br><br>- le nom du fichier wddiag associé à un champ Editeur de diagrammes. <br><br><br> |
| [CorrectionAutomatique](../Proprietes/1410088993.md) | La propriété **CorrectionAutomatique** permet de définir un dictionnaire des corrections automatiques à appliquer dans un champ Traitement de texte pendant la saisie. |
| [CouleurCaractèresContrôle](../Proprietes/1000024632.md) | La propriété **CouleurCaractèresContrôle** permet de : <br><br>- connaître la couleur des caractères de contrôle dans un champ Traitement de texte. <br><br>- modifier la couleur des caractères de contrôle dans un champ Traitement de texte.<br><br><br> |
| [EditionHTML](../Proprietes/1000023415.md) | La propriété **EditionHTML** permet de : <br><br>- savoir si un champ Traitement de texte est affiché en mode optimisé pour l'édition HTML, <br><br>- modifier un champ Traitement de texte pour l'afficher ou non en mode optimisé pour l'édition HTML.<br><br><br> |
| [LienAutomatique](../Proprietes/2513017.md) | La propriété **LienAutomatique** permet de : <br><br>- savoir si le mode de détection automatique des liens est activé, <br><br>- activer ou non le mode de détection automatique des liens.<br><br><br> |
| [LongueurSélection](../Proprietes/1000022522.md) | La propriété **LongueurSélection** permet de connaître et de modifier la longueur de la sélection effectuée dans un champ Traitement de texte. |
| [ModeAffichage](../Proprietes/1000023378.md) | La propriété **ModeAffichage** permet de connaître et de modifier : <br><br>- le mode d'affichage d'un champ Traitement de texte, <br><br>- le mode d'affichage d'un champ Lecteur PDF<br><br>- le mode d'affichage d'un champ Editeur HTML, <br><br>- le mode d'affichage d'un champ Kanban.<br><br><br> |
| [NombrePage](../Proprietes/2510084.md) | La propriété **NombrePage** permet de connaître : <br><br>- le nombre de pages présentes dans un fichier image de type "multi-pages". Cette image est affichée soit dans un champ Image, soit dans le fond d'un champ Graphe.<br><br>- le nombre de pages présentes dans un fichier PDF affiché dans un champ Image.<br><br>- le nombre de pages présentes dans un fichier PDF affiché dans un champ Lecteur PDF.<br><br>- le nombre de pages présentes dans un fichier DOCX affiché dans un champ Traitement de texte. <br><br><br> |
| [NombrePageAccessible](../Proprietes/1000023477.md) | La propriété **NombrePageAccessible** permet de connaître le nombre de pages actuellement chargées dans le champ Lecteur PDF ou dans le champ Traitement de texte. |
| [NuméroPageAffichée](../Proprietes/1000023479.md) | La propriété **NuméroPageAffichée** permet de connaître et de modifier le numéro de la page actuellement affichée dans le champ Lecteur PDF ou dans le champ Traitement de texte. |
| [RègleVisible](../Proprietes/1000023076.md) | La propriété **RègleVisible** permet de : <br><br>- Savoir si les règles sont visibles ou invisibles dans un champ Traitement de texte. <br><br>- Rendre les règles visibles ou invisibles dans un champ Traitement de texte.<br><br><br> |
| [Sélection](../Proprietes/1000023033.md) | La propriété **Sélection** permet d'obtenir les caractéristiques de la sélection (ou du curseur) : <br><br>- dans un champ Traitement de texte.<br>	Remarque : Cette sélection est située dans la partie en cours d'édition dans le champ (corps, entête ou pied de page). <br><br>- dans un champ Tableur.<br><br>- dans un champ Editeur HTML. <br><br>- dans un champ Editeur de diagrammes.<br><br><br> |





Pour connaître la liste complète des propriétés du WLangage pouvant être utilisées avec un champ de type Traitement de texte, consultez [Propriétés associées au champ Traitement de texte](../WDChamp/1410087022.md).


