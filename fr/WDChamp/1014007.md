
## Faire référence à un élément dans un événement / traitement WLangage
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Qu'est-ce qu'un élément ?
<a name="questce_quun_element_ELTTEXTE000268"></a>
Le tableau suivant présente les différents éléments d'une application :

| Élément | Élément de 2° niveau | Élément de 3° niveau |
| --- | --- | --- |
| Fenêtre | <br><br>- Champs d'une fenêtre<br><br>- Menus d'une fenêtre<br><br>- Cases d'une barre de message<br><br><br> | <br><br>- Champs appartenant à un superchamp ou à une barre d'outils<br><br>- Options d'un menu<br><br>- Colonnes d'une table<br><br><br> |
| Page<br>Frameset | <br><br>- Champs d'une page<br><br>- Menus d'une page<br><br><br> | Colonnes d'une table |
| Etat | <br><br>- Champs d'un état<br><br>- Blocs d'un état<br><br><br> | Colonnes d'une table |
| Requête | Rubriques d'une requête |   |
| Fichier de données | Rubriques d'un fichier de données |   |





Les options de menu et les rubriques d'un fichier de données font l'objet d'une manipulation particulière. Pour plus de détails, consultez les paragraphes [Cas particulier : Rubriques](#NOTE3_2) et [Cas particulier : Options de menu](#NOTE4_2).

**Remarque** : Pour simplifier l'écriture des traitements WLangage, WINDEV, WINDEV Mobile et WEBDEV permettent de récupérer facilement le nom d'un élément dans une fenêtre de code. Pour plus de détails, consultez le paragraphe [Récupérer le nom d'un élément dans le code](#NOTE5_1).



<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Manipulation d'un élément
<a name="manipulation_element_ELTTEXTE000292"></a>
Pour manipuler un élément (page, fenêtre, état, requête ou fichier de données) dans un événement ou traitement WLangage, il suffit d'utiliser le nom de cet élément. Ce nom peut être utilisé :

- soit dans une fonction WLangage.

- soit avec une propriété WLangage.

- soit en affectation directe.




Par exemple :


```wl
// Initialisation et exécution de la requête REQ_ListeClient
HExécuteRequête(REQ_ListeClient)

// Modification du nombre d'exemplaires de l'état ETAT_Client
ETAT_Client.NbCopie =10

// Récupération du titre de la fenêtre FEN_FenRecherche
ResTitre est une chaîne = FEN_FenRecherche

// Récupération du titre de la page PAGE_PageRecherche
ResTitre est une chaîne = PAGE_PageRecherche
```




<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Manipulation d'un élément de 2° niveau
<a name="manipulation_element_2_niveau_ELTTEXTE000316"></a>
Pour manipuler un élément de 2° niveau (champ ou menu d'une fenêtre, champ ou menu d'une page, champ ou bloc d'un état, ...) dans un événement ou traitement WLangage, il suffit :

- soit d'utiliser directement le nom de cet élément de 2° niveau. Cette syntaxe est valable uniquement si cet élément de 2° niveau est manipulé depuis la fenêtre (ou l'état) auquel il appartient.

- soit d'utiliser la notation suivante : "&lt;NomElément&gt;.&lt;NomElément2°Niveau&gt;".




Un élément de 2° niveau peut être manipulé :

- soit dans une fonction WLangage.

- soit avec une propriété WLangage.

- soit en affectation directe.




Par exemple :


```wl
// Déroule la liste du champ Combo COMBO_VilleFrance 
// de la fenêtre FEN_Client
ComboOuvre(COMBO_VilleFrance)
// équivalent à : ComboOuvre(FEN_Client.COMBO_VilleFrance)

// Modification de la couleur de fond du champ ChTotalHT
// dans l'état ETAT_Commande
ChTotalHT.CouleurFond = BleuClair
// équivalent à ETAT_Commande.ChTotalHT.CouleurFond = BleuClair

// Initialisation du champ de saisie SAI_Ville de la 
// fenêtre FEN_Client depuis une autre fenêtre
FEN_Client.SAI_Ville = "Montpellier"

// Rafraîchissement de la zone répétée ZR_ZoneProduit
// à partir du début de la zone répétée
ZoneRépétéeAffiche(ZR_ZoneProduit, taDébut)
// équivalent à : ZoneRépétéeAffiche(PAGE_PageClient.ZR_ZoneProduit, taDébut)
```



<a name="NOTE3_2"></a>


### Cas particuliers : Rubriques de fichier de données ou de requête
<a name="cas_particuliers_rubriques_fichier_donnees_requete_ELTPARAGRAPHE000109"></a>Pour manipuler par programmation une rubrique d'un fichier de données ou d'une requête, il suffit :

- soit d'utiliser directement le nom de la rubrique. Cette syntaxe est valable uniquement si la rubrique est manipulée à l'aide d'une fonction ou d'une propriété dans laquelle le nom du fichier de données ou de la requête à laquelle la rubrique appartient est spécifiée.

- soit d'utiliser la syntaxe suivante : "&lt;NomFichier/NomRequête&gt;.&lt;NomRubrique&gt;".




Par exemple :


```wl
// Affectation dans la rubrique CP du fichier de données Client
// de la valeur saisie dans le champ SAI_CodePostal
CLIENT.CP = SAI_CodePostal

// Avance jusqu'au 2000ième enregistrement
// du fichier CLIENT selon la rubrique NOMCLI
HLitPremier(CLIENT, NOMCLI)
HAvance(CLIENT, NOMCLI, 1999)
```




<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Manipulation d'un élément de 3° niveau
<a name="manipulation_element_3_niveau_ELTTEXTE000346"></a>
Pour manipuler un élément de 3° niveau (champ appartenant à un superchamp, champ appartenant à une barre d'outils, colonne de table, ...) dans un événement ou traitement WLangage, il suffit :

- soit d'utiliser directement le nom de cet élément de 3° niveau. Cette syntaxe est valable uniquement si les conditions suivantes sont réunies :

	- cet élément de 3° niveau est manipulé depuis l'élément de 2° niveau auquel il appartient (superchamp, barre d'outils ou table).

	- aucun autre élément de 3° niveau utilise le même nom.




- soit d'utiliser la notation suivante si l'élément de 3° niveau est manipulé depuis la fenêtre (ou l'état) auquel il appartient : "&lt;NomElément2°Niveau&gt;.&lt;NomElément3°Niveau&gt;".

- soit d'utiliser la notation suivante si l'élément de 3° niveau est manipulé depuis une autre fenêtre (ou un autre état) : "&lt;NomElément&gt;.&lt;NomElément2°Niveau&gt;.&lt;NomElément3°Niveau&gt;".




Un élément de 3° niveau peut être manipulé :

- soit dans une fonction WLangage.

- soit avec une propriété WLangage.

- soit en affectation directe.




Par exemple :


```wl
// Modification de la largeur de la colonne COL_NomClient
// Aucune autre colonne COL_NomClient n'est présente dans la fenêtre
COL_NomClient.Largeur += 10

// Modification du nombre de caractères maximum du champ SAI_Police
// appartenant au superchamp SC_SchStyle depuis la fenêtre du superchamp
SC_SchStyle.SAI_Police.Taille += 10

// Modification de la hauteur du champ SAI_Police appartenant à la
// barre d'outils BAR_Style depuis une autre fenêtre
FEN_Client.BAR_Style.SAI_Police.Hauteur += 10
```



<a name="NOTE4_2"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/fr/WINDOWS.png)![Code Utilisateur (MCU)](https://doc.pcsoft.fr/ext/images/fr/MCU.png) 

### Cas particuliers : Options de menus
<a name="cas_particuliers_options_menus_ELTPARAGRAPHE000151"></a>Pour manipuler par programmation une option de menu, il suffit :

- soit d'utiliser directement le nom de l'option. Cette syntaxe est valable uniquement si l'option de menu est manipulée depuis le menu ou depuis la fenêtre associée au menu.

- soit d'utiliser une des syntaxes suivantes : "&lt;NomFenêtre&gt;.&lt;NomOption&gt;" ou "&lt;NomFenêtre&gt;.&lt;NomMenu&gt;.&lt;NomOption&gt;".




Par exemple :


```wl
// Changement de la couleur de la police de l'option OPT_FicheClient
// du menu MENU_Client depuis la fenêtre de ce menu
OPT_FicheClient.Couleur = BleuFoncé

// Changement de la couleur de la police de l'option OPT_FicheClient
// du menu MENU_Client depuis une autre fenêtre
FEN_Client.MENU_Client.OPT_FicheClient.Couleur = BleuFoncé
// équivalent à : FEN_Client.OPT_FicheClient.Couleur = BleuFoncé
```


<a name="NOTE5"></a>
<a name="NOTE5_1"></a>


## Récupérer le nom d'un élément dans le code
<a name="recuperer_nom_element_dans_code_ELTTEXTE000376"></a>


### Présentation
<a name="presentation_ELTPARAGRAPHE000167"></a>Pour récupérer le nom d'un élément (peu importe son niveau) dans le code :

- utilisez la complétion automatique. 
	La complétion automatique permet une saisie assistée du code WLangage. Pour plus de détails, consultez [Complétion automatique](../Editeurs/2013001.md).

- réalisez des "Drag and Drop" entre l'élément à utiliser et la fenêtre de code.
	Ces "Drag and Drop" peuvent être effectués :

	- entre le [volet "Explorateur de Projet"](../Editeurs/2027029.md) et la fenêtre de code.

	- entre le [volet "Explorateur de Projet"](../Editeurs/2027029.md) et le [volet "Visualisation contextuelle du code"](../Editeurs/2027033.md).

	- entre l'élément et la fenêtre de code.

	- entre l'élément et le [volet "Visualisation contextuelle du code"](../Editeurs/2027033.md).








<a name="NOTE5_2"></a>


### Récupérer le nom d'un élément grâce au "Drag and Drop"
<a name="recuperer_nom_element_grace_drag_and_drop_ELTPARAGRAPHE000202"></a>Pour récupérer le nom d'un élément grâce au "Drag and Drop" :

1. Sélectionnez l'élément dont le nom doit être récupéré :

	- soit dans le [volet "Explorateur de Projet"](../Editeurs/2027029.md).

	- soit depuis un éditeur (éditeur de fenêtres pour récupérer le nom d'un champ par exemple).




2. Maintenez le bouton gauche de la souris enfoncé et déplacez l'élément :

	- soit vers la fenêtre de code.

	- soit vers le [volet "Visualisation contextuelle du code"](../Editeurs/2027033.md).




3. Relâchez le bouton de la souris à la position où le nom de l'élément doit être utilisé. Le nom de l'élément droppé apparaît.







