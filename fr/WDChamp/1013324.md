


## Enrouler/Dérouler un champ Arbre par programmation (syntaxe préfixée)
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Enrouler/Dérouler un arbre par programmation
<a name="enroulerderouler_arbre_par_programmation_ELTTEXTE000065"></a>


- Pour enrouler un arbre par programmation, il suffit d'utiliser la fonction [&lt;Arbre&gt;.Enroule](../WDLang1/1000023758.md).

- Pour dérouler un arbre par programmation, il suffit d'utiliser la fonction [&lt;Arbre&gt;.ListeFils](../WDLang1/1000023762.md) combinée avec une procédure locale spécifique.


![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png) Remarque : Pour optimiser le remplissage d'un champ Arbre, il est possible d'utiliser la propriété [AjouteFilsDifféré](../Proprietes/1000020927.md) sur les lignes du champ Arbre. Cette propriété permet de définir la procédure à exécuter pour le remplissage du niveau à dérouler. La recherche et le remplissage des fils ne sont effectués que lorsque l'utilisateur demande à les voir.

<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Exemple
<a name="exemple_ELTTEXTE000111"></a>
Les différents codes suivants permettent :

- d'initialiser un champ Arbre. 

- de dérouler une partie du champ Arbre.

- d'enrouler le champ Arbre.




1. Initialiser le champ Arbre par programmation :
	
	```wl
	ARBRE_RecetteTV.Ajoute("Recette" + TAB + "Dessert", "GATEAU-.BMP", "GATEAU-.BMP")
	ARBRE_RecetteTV.Ajoute("Recette" + TAB + "Dessert" + TAB + "Glace")
	ARBRE_RecetteTV.Ajoute("Recette" + TAB + "Dessert" + TAB + "Glace" + ...
			TAB + "Vanille", "SUIVAN5-.BMP")
	ARBRE_RecetteTV.Ajoute("Recette" + TAB + "Dessert" + TAB + "Glace" + ...
			TAB + "Fraise", "SUIVAN5-.BMP")
	ARBRE_RecetteTV.Ajoute("Recette" + TAB + "Dessert" + TAB + "Glace" + ...
			TAB + "Chocolat", "SUIVAN5-.BMP")
	ARBRE_RecetteTV.Ajoute("Recette" + TAB + "Dessert" + TAB + "Coupe")
	ARBRE_RecetteTV.Ajoute("Recette" + TAB + "Dessert" + TAB + "Coupe" + ...
			TAB + "Pêche Melba", "SUIVAN5-.BMP")
	ARBRE_RecetteTV.Ajoute("Recette" + TAB + "Dessert" + TAB + "Coupe" + ...
			TAB + "Poire belle hélène", "SUIVAN5-.BMP")
	ARBRE_RecetteTV.Ajoute("Recette" + TAB + "Dessert" + TAB + "Coupe" + ...
			TAB + "Colonel", "SUIVAN5-.BMP")
	ARBRE_RecetteTV.Ajoute("Recette" + TAB + "Dessert" + TAB + "Tarte aux pommes")
	ARBRE_RecetteTV.Ajoute("Recette" + TAB + "Dessert" + TAB + "Tarte aux pommes" + ...
			TAB + "à la crème fraîche", "SUIVAN5-.BMP")
	ARBRE_RecetteTV.Ajoute("Recette" + TAB + "Dessert" + TAB + "Tarte aux pommes" + ...
			TAB + "au calvados", "SUIVAN5-.BMP")
	ARBRE_RecetteTV.Ajoute("Recette" + TAB + "Dessert" + TAB + "Crème brûlée", "SUIVAN5-.BMP")
	ARBRE_RecetteTV.Ajoute("Recette" + TAB + "Dessert" + TAB + "Vacherin", "SUIVAN5-.BMP")
	```


2. Dérouler tous les éléments : La procédure "DérouleTout" est appelée pour chaque "fils" trouvé. Cette procédure déroule tous les noeuds "fils" enroulés du noeud "Dessert".
	
	```wl
	// Liste les "fils" du noeud "Desserts" dans l'arbre "ARBRE_RecetteTV"
	// La procédure "DérouleTout" est appelée
	// pour chaque "fils" trouvé du noeud "Dessert"
	Res = ARBRE_RecetteTV.ListeFils("Recette" + TAB + "Dessert", "DérouleTout")
	```

	```wl
	// Procédure locale "DérouleTout"
	// Cette procédure déroule tous les noeuds "fils" enroulés du noeud "Desserts"
	PROCEDURE DérouleTout(ARBRE_RecetteTV, CheminFils, FilsTrouvé, Niveau, Pointeur)
	// L'élément trouvé est-il une feuille ?
	SI ARBRE_RecetteTV.TypeElément(CheminFils + FilsTrouvé) = aFeuille ALORS
		RETOUR  // Retour à la fonction ArbreListeFils
	SINON
		// L'élément trouvé est-il enroulé ?
		SI ARBRE_RecetteTV.Etat(CheminFils + FilsTrouvé) = aEnroule ALORS
			ARBRE_RecetteTV.Déroule(CheminFils + FilsTrouvé)
		FIN
	FIN
	```


3. Enrouler le noeud "Dessert".
	
	```wl
	// Enroule le noeud "Dessert" dans l'arbre "ARBRE_RecetteTV"
	Res = ARBRE_RecetteTV.Enroule("Recette" + TAB + "Dessert")
	```






