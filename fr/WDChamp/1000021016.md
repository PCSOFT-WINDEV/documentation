
## Graphe composite
			

<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Présentation
<a name="presentation_ELTTEXTE000196"></a>
Un graphe composite est un graphe affichant plusieurs types de graphes. 

Cette page d'aide présente : 

- [Les principales caractéristiques d'un graphe composite](#NOTE2_1). 

- [Comment créer un graphe composite sous l'éditeur](#NOTE3_1). 

- [Comment créer un graphe composite par programmation](#NOTE4_1). 




Remarque : un graphe composite n'est pas un graphe de type "Composite" mais c'est un graphe contenant plusieurs séries correspondant chacune à un type de graphe spécifique.  



<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Caractéristiques d'un graphe de type Composite
<a name="caracteristiques_graphe_type_composite_ELTTEXTE000220"></a>

![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=Graphe_Composite%20-%20HC%20N%B0001.gif&type=thumb)
Dans ce graphe : 

**1. Graphe de type Aire**
Graphe de type Aire constituant le premier graphe du graphe composite. Pour réaliser ce graphe, la première série du graphe a été définie de type Aire (sous l'éditeur, onglet "Séries") ou par programmation via la fonction [grTypeSérie](../WDLang3/1000022262.md). 

**2. Graphe de type Histogramme**
Graphe de type Histogramme constituant le second graphe du graphe composite. 

**3. Moyenne**
Moyenne affichée. Dans un graphe composite, il est possible d'ajouter une courbe de tendance : 

- soit par la fonction [grTypeSérie](../WDLang3/1000022262.md). 

- soit sous l'éditeur, en ajoutant une série de type "Courbe de tendance". 




**4. Légende**
Les caractéristiques de la légende sont saisies dans l'onglet "Détail" de la description du champ (bouton "Editer la légende du graphe").

**5. Titre du graphe**. 
Les caractéristiques du titre du graphe sont saisies dans l'onglet "Détail" de la description du champ (bouton "Editer le titre du graphe"). Il est possible de définir le titre du graphe, sa position et son style.

**6. Titre des axes**.

<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Créer un graphe composite sous l'éditeur
<a name="creer_graphe_composite_sous_editeur_ELTTEXTE000244"></a>
Pour créer un graphe composite sous l'éditeur : 

1. Créez un nouveau champ de type graphe. Définissez le type de ce graphe. 

2. Dans les onglets "Général" et "Détail" de la fenêtre de description du champ Graphe, définissez les caractéristiques générales de votre graphe. 

3. Dans l'onglet "Séries" de la fenêtre de description du champ Graphe, ajoutez les séries nécessaires, avec leur source de données. 

4. Pour chaque série définie, spécifiez dans la partie basse de l'écran : 

	- le type de graphe associé à la série : Courbe, Aire, Histogramme, ...

	- la couleur de la série, l'opacité, le type de points, ...




5. Selon le type de graphe associé à la série, définissez les propriétés spécifiques à la série. 








<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Créer un graphe composite par programmation
<a name="creer_graphe_composite_par_programmation_ELTTEXTE000268"></a>
Pour créer un graphe composite par programmation : 

1. Créez un graphe (fonction [grCrée](../WDLang3/3042005.md)).

2. Paramétrez les options globales de ce graphe (fonctions [grLissage](../WDLang3/3042033.md), [grLégende](../WDLang3/3042047.md), ...).

3. Pour chaque série : 

	- Ajoutez les données de la série (fonction [grAjouteDonnée](../WDLang3/3042023.md)).

	- Paramétrez les options de la série (fonctions [grEtiquetteSérie](../WDLang3/3042024.md), [grCouleurSérie](../WDLang3/3042013.md), [grTypeTraitSérie](../WDLang3/1000022318.md), ...).

	- Définissez le type de la série via la fonction [grTypeSérie](../WDLang3/1000022262.md).




4. Dessinez le graphe (fonction [grDessine](../WDLang3/3042026.md)). 




Remarque : La fonction [grTypeSérie](../WDLang3/1000022262.md) permet également de définir des courbes automatiques par rapport à une série, pour afficher une moyenne ou une régression linéaire.

Exemple de code : 


```wl
nAnnéeEnCours est un entier = AnnéeEnCours()
nAnnéeN1 est un entier = nAnnéeEnCours - 1
nAnnéeN2 est un entier = nAnnéeEnCours - 2

// Création du graphe
grCrée(GRF_GrapheComposite, grAire)

// Couleur de bordure claire
grCouleur(GRF_GrapheComposite, grCouleurBordure, Blanc)

// Affiche le quadrillage
grQuadrillage(GRF_GrapheComposite, Vrai, grAbscisse)

// Etiquettes des abscisses
POUR i = 1 A 12
	grEtiquetteCatégorie(GRF_GrapheComposite, i, Gauche(NuméroDeMoisVersLettre(i), 1) + ".")
FIN

// La légende sera affichée à droite
grLégende(GRF_GrapheComposite, grADroite)
// Les couleurs ne doivent pas être dégradées
grDégradé(GRF_GrapheComposite, Faux)

// Série 1 : objective (aire)
// - type de la série
grTypeSérie(GRF_GrapheComposite, 1, grAire)
// - épaisseur du trait
grEpaisseurTrait(GRF_GrapheComposite, 1, 2)
// - type de trait
grTypeTraitSérie(GRF_GrapheComposite, 1, TraitTiret)
// - opacité
grOpacitéSérie(GRF_GrapheComposite, 1, 50)
// - étiquette
grEtiquetteSérie(GRF_GrapheComposite, 1, "Objectif de CA")
// - couleur
grCouleurSérie(GRF_GrapheComposite, 1, HTMLVersRVB("#64B5F6"))
// - ajout des données
grAjouteDonnée(GRF_GrapheComposite, 1, 1, 170)
grAjouteDonnée(GRF_GrapheComposite, 1, 2, 240)
grAjouteDonnée(GRF_GrapheComposite, 1, 3, 120)
grAjouteDonnée(GRF_GrapheComposite, 1, 4, 100)
grAjouteDonnée(GRF_GrapheComposite, 1, 5, 150)
grAjouteDonnée(GRF_GrapheComposite, 1, 6, 180)
grAjouteDonnée(GRF_GrapheComposite, 1, 7, 200)
grAjouteDonnée(GRF_GrapheComposite, 1, 8, 270)
grAjouteDonnée(GRF_GrapheComposite, 1, 9, 270)
grAjouteDonnée(GRF_GrapheComposite, 1, 10, 205)
grAjouteDonnée(GRF_GrapheComposite, 1, 11, 180)
grAjouteDonnée(GRF_GrapheComposite, 1, 12, 190)


// Série 2 et 3 : historique A et A-1 (histogramme groupé)
// - type de la série
grTypeSérie(GRF_GrapheComposite, 2, grHisto)
grTypeSérie(GRF_GrapheComposite, 3, grHisto)
// - étiquette
grEtiquetteSérie(GRF_GrapheComposite, 2, "CA Année " + nAnnéeEnCours)
grEtiquetteSérie(GRF_GrapheComposite, 3, "CA Année " + nAnnéeN1)
// - couleur
grCouleurSérie(GRF_GrapheComposite, 2, HTMLVersRVB("#FFF176"))
grCouleurSérie(GRF_GrapheComposite, 3, HTMLVersRVB("#AED581"))
// - ajout des données (série 2)
grAjouteDonnée(GRF_GrapheComposite, 2, 1, 110)
grAjouteDonnée(GRF_GrapheComposite, 2, 2, 230)
grAjouteDonnée(GRF_GrapheComposite, 2, 3, 80)
grAjouteDonnée(GRF_GrapheComposite, 2, 4, 72)
grAjouteDonnée(GRF_GrapheComposite, 2, 5, 50)
grAjouteDonnée(GRF_GrapheComposite, 2, 6, 120)
grAjouteDonnée(GRF_GrapheComposite, 2, 7, 170)
grAjouteDonnée(GRF_GrapheComposite, 2, 8, 230)
grAjouteDonnée(GRF_GrapheComposite, 2, 9, 125)
grAjouteDonnée(GRF_GrapheComposite, 2, 10, 215)

// - ajout des données (série 3)
grAjouteDonnée(GRF_GrapheComposite, 3, 1, 150)
grAjouteDonnée(GRF_GrapheComposite, 3, 2, 210)
grAjouteDonnée(GRF_GrapheComposite, 3, 3, 50)
grAjouteDonnée(GRF_GrapheComposite, 3, 4, 62)
grAjouteDonnée(GRF_GrapheComposite, 3, 5, 40)
grAjouteDonnée(GRF_GrapheComposite, 3, 6, 160)
grAjouteDonnée(GRF_GrapheComposite, 3, 7, 180)
grAjouteDonnée(GRF_GrapheComposite, 3, 8, 220)
grAjouteDonnée(GRF_GrapheComposite, 3, 9, 230)
grAjouteDonnée(GRF_GrapheComposite, 3, 10, 205)
grAjouteDonnée(GRF_GrapheComposite, 3, 11, 150)
grAjouteDonnée(GRF_GrapheComposite, 3, 12, 160)


// Série 4 : écart (histogramme)
// - type de la série
grTypeSérie(GRF_GrapheComposite, 4, grHisto)
// - étiquette
grEtiquetteSérie(GRF_GrapheComposite, 4, ChaîneConstruit("Ecart %1-%2", nAnnéeEnCours, nAnnéeN1))
// - couleur
grCouleurSérie(GRF_GrapheComposite, 4, HTMLVersRVB("#E57373"))
// - ajout des données
grAjouteDonnée(GRF_GrapheComposite, 4, 1, -40)
grAjouteDonnée(GRF_GrapheComposite, 4, 2, 20)
grAjouteDonnée(GRF_GrapheComposite, 4, 3, 30)
grAjouteDonnée(GRF_GrapheComposite, 4, 4, 10)
grAjouteDonnée(GRF_GrapheComposite, 4, 5, 10)
grAjouteDonnée(GRF_GrapheComposite, 4, 6, -40)
grAjouteDonnée(GRF_GrapheComposite, 4, 7, -10)
grAjouteDonnée(GRF_GrapheComposite, 4, 8, 10)
grAjouteDonnée(GRF_GrapheComposite, 4, 9, 20)
grAjouteDonnée(GRF_GrapheComposite, 4, 10, 10)


// Affiche une moyenne pour la courbe de CA
// - type de la série
grTypeSérie(GRF_GrapheComposite, 5, grDroiteMoyenne, 2)
// - étiquette
grEtiquetteSérie(GRF_GrapheComposite, 5, "Moyenne CA " + nAnnéeEnCours)
// - type de trait
grTypeTraitSérie(GRF_GrapheComposite, 5, TraitPointillé)
// - épaisseur du trait
grEpaisseurTrait(GRF_GrapheComposite, 5, 2)
// - couleur
grCouleurSérie(GRF_GrapheComposite, 5, RVB(215, 100, 177))

// Dessin du graphe
grDessine(GRF_GrapheComposite)
```







