


## Manipuler un champ Agenda par programmation
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Présentation
<a name="presentation_ELTTEXTE000331"></a>
Un champ Agenda peut être : 

- manipulé par programmation. 

- ![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png) [lié à un fichier de données](../WDChamp/1016325.md). 

- ![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png) [lié à une variable](../WDChamp/1016325.md). 




Pour manipuler un champ Agenda par programmation, WINDEV et WEBDEV propose les fonctions [AgendaXXX](../WDLang1/1000019021.md). 

Cette page d'aide détaille comment manipuler un champ Agenda présent dans une fenêtre ou une page par programmation. L'exemple utilisé en illustration permet de mémoriser les rendez-vous dans une base de données HFSQL. 







## Manipuler le champ Agenda par programmation
<a name="manipuler_champ_agenda_par_programmation_ELTTEXTE000361"></a>


### Ajouter un rendez-vous
<a name="ajouter_rendezvous_ELTPARAGRAPHE000065"></a>L'ajout d'un rendez-vous dans un champ Agenda est réalisé grâce à la fonction [AgendaAjouteRendezVous](../WDLang1/1000018877.md). Cette fonction accepte deux syntaxes : 

- syntaxe permettant de préciser les caractéristiques du rendez-vous : titre, description, etc.
	
	```wl
	// Ajoute un rendez-vous dans le champ Agenda
	AgendaAjouteRendezVous(AGD_MonAgenda, "Réunion commercial", "201003220845", ...
			"201003221230", "Commerciale")
	```


- syntaxe manipulant une variable de type [RendezVous](../WDLang1/1000019244.md).
	
	```wl
	// Déclare une variable de type RendezVous
	MonRendezVous est un RendezVous
	
	// Remplit le rendez-vous
	MonRendezVous.Titre = "Réunion commerciale"
	MonRendezVous.Contenu = "Réunion pour définir les objectifs de la semaine"
	MonRendezVous.DateDébut = "201003220845"
	MonRendezVous.DateFin = "201003221230"
	MonRendezVous.Catégorie = "Commerciale"
	MonRendezVous.ID = 1
	
	// Ajoute le rendez-vous au champ
	AgendaAjouteRendezVous(AGD_Agenda, MonRendezVous)
	```



La propriété **CouleurFond** de la variable [RendezVous](../WDLang1/1000019244.md) permet de définir une couleur d'affichage pour un rendez-vous. Si aucune couleur de fond n'est définie, le champ Agenda utilisera automatiquement la couleur associée à la catégorie du rendez-vous. 


### Remplir un champ Agenda avec les données présentes dans un fichier de données HFSQL
<a name="remplir_champ_agenda_avec_les_donnees_presentes_dans_fichier_donnees_hfsql_ELTPARAGRAPHE000100"></a>Les enregistrements sont stockés dans un fichier de données HFSQL. Le remplissage initial du champ Agenda peut être réalisé par un parcours du fichier de données via la syntaxe POUR TOUT et par l'ajout de chaque rendez-vous via la fonction [AgendaAjouteRendezVous](../WDLang1/1000018877.md).

```wl
// Variable de type RendezVous
MonRendezVous est un RendezVous

// Parcours des rendez-vous mémorisés dans la base de données
POUR TOUT RDV 
	// Remplit les informations de la variable
	MonRendezVous.Titre = RDV.Titre
	MonRendezVous.Contenu = RDV.Contenu
	MonRendezVous.DateDébut = RDV.DateDébut
	MonRendezVous.DateFin = RDV.DateFin
	MonRendezVous.Catégorie = RDV.Catégorie
	MonRendezVous.ID = RDV.IDRDV
	
	// Ajoute le rendez-vous au champ Agenda
	AgendaAjouteRendezVous(AGD_MonAgenda, MonRendezVous)
	
FIN
```
**Rappel** : Il est également possible d'utiliser un champ Agenda lié à un fichier de données. Pour plus de détails, consultez [Champ Agenda lié à un fichier de données](../WDChamp/1016325.md).


### Récupérer une liste de rendez-vous
<a name="recuperer_une_liste_rendezvous_ELTPARAGRAPHE000124"></a>La fonction [AgendaListeRendezVous](../WDLang1/1000018880.md) permet de récupérer : 

- la liste de tous les rendez-vous du champ Agenda : 
	
	```wl
	// Tableau contenant une liste de RendezVous
	tabListeRendezVous est un tableau de RendezVous
	
	// Liste des rendez-vous
	tabListeRendezVous = AgendaListeRendezVous(AGD_MonAgenda)
	```


- la liste des rendez-vous compris entre deux dates : 
	
	```wl
	// Liste des rendez-vous de Janvier 2010
	tabListeRendezVous est un tableau de RendezVous
	
	// Liste des rendez-vous
	tabListeRendezVous = AgendaListeRendezVous(AGD_MonAgenda, "20100101", "20100131")
	```


- le rendez-vous actuellement sélectionné ou survolé : 
	
	```wl
	// Rendez-vous sélectionné
	tabListeRendezVous est un tableau de RendezVous
	
	tabListeRendezVous = AgendaListeRendezVous(AGD_MonAgenda, agRdvSélectionné)
	```






### Afficher un champ Agenda à partir d'une date spécifique
<a name="afficher_champ_agenda_partir_une_date_specifique_ELTPARAGRAPHE000159"></a>Pour afficher le champ Agenda à partir d'une date spécifique, utilisez la fonction [AgendaPosition](../WDLang1/1000018889.md).

```wl
// Positionne le champ Agenda sur la date du jour
AgendaPosition(AGD_Agenda1, DateDuJour())

// Positionne le champ Agenda au 20 décembre 2012
AgendaPosition(AGD_Agenda1, "20121220")
```



### Supprimer un rendez-vous
<a name="supprimer_rendezvous_ELTPARAGRAPHE000170"></a>La fonction [AgendaSupprimeRendezVous](../WDLang1/1000018881.md) permet de supprimer : 

- le rendez-vous sélectionné dans le champ. 

- un rendez-vous précis. 
	
	```wl
	// Supprime le premier rendez-vous
	AgendaSupprimeRendezVous(AGD_MonAgenda, 1)
	```



La fonction [AgendaSupprimeTout](../WDLang1/1000018882.md) permet de supprimer tous les rendez-vous présents dans le champ Agenda. 


### Modifier l'affichage du champ
<a name="modifier_affichage_champ_ELTPARAGRAPHE000195"></a>L'affichage courant d'un champ Agenda peut être modifié via les fonctions suivantes : 

- [AgendaAffichePériodePrécédente](../WDLang1/1000018886.md) et [AgendaAffichePériodeSuivante](../WDLang1/1000018885.md) : Ces deux fonctions permettent de modifier la période d'affichage (identique à l'utilisation du bouton spin présent en haut du champ Agenda). 

- [AgendaChangeMode](../WDLang1/1000019497.md) : Cette fonction permet de modifier le mode d'affichage du champ : jour, semaine, mois.

- [AgendaHeureAffichée](../WDLang1/1000018888.md) : Cette fonction permet de définir la plage horaire affichée. 






## Utilisation du menu contextuel (FAA)
<a name="utilisation_menu_contextuel_faa_ELTTEXTE000422"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png) 

### Possibilités du menu contextuel
<a name="possibilites_menu_contextuel_ELTPARAGRAPHE000218"></a>Via le menu contextuel du champ Agenda, il est possible de : 

- modifier le mode d'affichage du champ,

- ajouter, modifier ou supprimer un rendez-vous. 




Pour sauvegarder les opérations réalisées, il est nécessaire d'utiliser les événements du champ Agenda. 

Dans l'événement concerné, il suffit de récupérer le rendez-vous en cours d'utilisation et de faire le traitement correspondant. 

**Exemple** : Pour mémoriser dans un fichier de données RDV, un rendez-vous que l'utilisateur ajoute via le menu contextuel, il suffit de saisir dans l'événement "Entrée en saisie d'un rendez-vous" : 

```wl
PROCEDURE Edition(rdvEdité est un RendezVous)

// Mémorisation des données
RDV.Titre = rdvEdité.Titre
RDV.DateDébut = rdvEdité.DateDébut
RDV.DateFin = rdvEdité.DateFin
...
HAjoute(RDV)
```
Le même type de code peut être mis en place pour les différents événements du champ Agenda. En effet, pour chaque événement du champ qui manipule un rendez-vous, une procédure a été déclarée automatiquement par le champ Agenda. 

Ces procédures reçoivent en paramètre une variable de type [RendezVous](../WDLang1/1000019244.md). Cette variable contient les caractéristiques du rendez-vous concerné par l'événement. 
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png) 

### Utilisation avancée des événements avec procédure
<a name="utilisation_avancee_des_evenements_avec_procedure_ELTPARAGRAPHE000243"></a>Il est également possible de proposer à l'utilisateur de définir plus précisément les caractéristiques de son rendez-vous lors d'un ajout ou d'une modification. Il suffit pour cela de créer une fenêtre avec les informations à remplir. 

Dans le code, il suffit d'ouvrir la fenêtre par exemple dans l'événement "Entrée en saisie d'un rendez-vous". Pour bloquer la saisie directe par le menu contextuel de l'agenda, l'événement doit alors renvoyer <u><u><u><u>Faux</u></u></u></u>. 

Ce principe peut être appliqué à tous les événements appelés par le menu contextuel du champ Agenda. 

Exemple : 

```wl
PROCEDURE Edition(rdvEdité est un RendezVous)

// Ouvre la fenêtre de saisie d'un rendez-vous 
// avec le rendez-vous sélectionné (en mode Création ou Modification)
Ouvre(FEN_SaisieRDV_HFSQL, rdvEdité)

// Renvoie FAUX pour bloquer la saisie directe dans le champ Agenda
RENVOYER Faux
```




## Gestion des jours fériés
<a name="gestion_des_jours_feries_ELTTEXTE000452"></a>
La gestion des jours fériés est réalisée par programmation. Le WLangage met à votre disposition plusieurs fonctions débutant par JourFériéXXX.

Pour définir les jours fériés affichés dans les champs Agenda et les champs Calendrier, il faut utiliser la fonction [JourFériéAjoute](../WDLang1/1000017304.md). Cette fonction permet de définir la liste des jours fériés à utiliser. Cette fonction permet de personnaliser les jours fériés en fonction du pays et de la réglementation locale. Cette fonction doit être utilisée au début de l'application car elle a un effet global sur l'application.

Les jours fériés seront colorés en vert dans le champ Agenda. 

Exemple :

```wl
// Suppression de tous les jours fériés
JourFériéSupprimeTout()
// Initialisation des 11 jours fériés communs aux départements français et DOM/TOM
JourFériéAjoute("0101")		// 1er Janvier
JourFériéAjoute(jfLundiDePâques)	// Lundi de Pâques
JourFériéAjoute("0501")		// 1er Mai
JourFériéAjoute("0508")		// 8 Mai
JourFériéAjoute(jfJeudiDeLAscension)	// Jeudi de l'Ascension
JourFériéAjoute(jfLundiDePentecôte)	// Lundi de Pentecôte
JourFériéAjoute("0714")		// 14 Juillet
JourFériéAjoute("0815")		// 15 Août (Assomption)
JourFériéAjoute("1101")		// Toussaint
JourFériéAjoute("1111")		// 11 Novembre
JourFériéAjoute("1225")		// Noël

// Ajout de 2 jours fériés supplémentaires pour la Moselle et l'Alsace
JourFériéAjoute("1226" + RC + jfVendrediSaint)
```




## Propriétés spécifiques aux champs Agenda
<a name="proprietes_specifiques_aux_champs_agenda_ELTTEXTE000478"></a>
Les propriétés suivantes sont spécifiques à la gestion d'un champ Agenda par programmation.



|   |   |
| --- | --- |
| [DéplacementRDV](../Proprietes/1000019993.md) | La propriété **DéplacementRDV** permet de savoir et d'indiquer si l'utilisateur peut déplacer un rendez-vous dans un champ Planning ou dans un champ Agenda. |
| [GranularitéDéplacement](../Proprietes/1000019642.md) | La propriété **GranularitéDéplacement** permet de connaître et de modifier la taille de la grille pour le déplacement :  <br><br>- des rendez-vous d'un champ Agenda. <br><br>- des rendez-vous d'un champ Planning.<br><br>- des événements d'un champ TimeLine.<br><br>- des tâches d'une colonne de type Diagramme de Gantt.<br><br><br> |
| [GranularitéDurée](../Proprietes/1000019643.md) | La propriété **GranularitéDurée** permet de connaître et de modifier la taille de la grille utilisée pour le redimensionnement :<br><br>- des rendez-vous d'un champ Agenda. <br><br>- des rendez-vous d'un champ Planning.<br><br>- des événements d'un champ TimeLine.<br><br>- des tâches d'une colonne de type Diagramme de Gantt.<br><br><br> |
| [GranularitéRendezVous](../Proprietes/1000019100.md) | La propriété **GranularitéRendezVous** permet de connaître et de modifier la précision de la grille utilisée par le champ Agenda ou le champ Planning pour définir les rendez-vous. <br>Propriété conservée par compatibilité. |
| [HeureOuvrableDébut](../Proprietes/1000018958.md) | La propriété **HeureOuvrableDébut** permet de connaître et de modifier l'heure de début des heures ouvrables utilisées : <br><br>- par un champ Agenda.<br><br>- par un champ Planning.<br><br>- par une colonne de type Diagramme de Gantt (dans un champ Table ou Table hiérarchique). <br><br><br> |
| [HeureOuvrableFin](../Proprietes/1000019056.md) | La propriété **HeureOuvrableFin** permet de connaître et de modifier l'heure de fin des heures ouvrables utilisées :<br><br>- par un champ Agenda.<br><br>- par un champ Planning.<br><br>- par une colonne de type Diagramme de Gantt (dans un champ Table ou Table hiérarchique).<br><br><br> |
| [MasqueTitreDate](../Proprietes/1000019463.md) | La propriété **MasqueTitreDate** permet de connaître ou de modifier le masque utilisé pour le titre des colonnes des jours dans un champ Agenda ou dans un champ Planning. |
| [ModificationDuréeRDV](../Proprietes/1000019994.md) | La propriété **ModificationDuréeRDV** permet de savoir et d'indiquer si l'utilisateur peut modifier la durée d'un rendez-vous dans un champ Planning ou Agenda. |
| [Num1erJourDeLaSemaine](../Proprietes/1000019004.md) | La propriété **Num1erJourDeLaSemaine** permet de connaître et de modifier le 1er jour de la semaine affiché dans :<br><br>- un champ Calendrier.<br><br>- un champ Agenda.<br><br>- un champ Planning. <br><br>- un champ de saisie au format Date avec Calendrier.<br><br><br> |
| [SaisieDirecteRDV](../Proprietes/1000019995.md) | La propriété **SaisieDirecteRDV** permet de savoir et d'indiquer si l'utilisateur peut modifier directement le titre d'un rendez-vous dans un champ Planning ou dans un champ Agenda. |
| [SélectionPériode](../Proprietes/1000019996.md) | La propriété **SélectionPériode** permet de savoir et d'indiquer si l'utilisateur peut sélectionner une période dans un champ Planning ou dans un champ Agenda. |





Pour connaître la liste complète des propriétés du WLangage pouvant être utilisées avec un champ de type Agenda, consultez [Propriétés associées au champ Agenda](../WDChamp/1016322.md).


