


## Manipuler un champ TimeLine par programmation
			





## Présentation
<a name="presentation_ELTTEXTE000288"></a>
Un champ TimeLine peut être : 

- manipulé par programmation. 

- [lié à un fichier de données](../WDChamp/1000026005.md). 

- [lié à une variable](../WDChamp/1000026005.md). 




Pour manipuler un champ TimeLine par programmation, WINDEV propose les [fonctions TimeLine](../WDLang1/1000020621.md). 

Cette page d'aide détaille comment manipuler un champ TimeLine par programmation. L'exemple utilisé en illustration permet de mémoriser les événements dans une base HFSQL.


<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Manipuler le champ TimeLine par programmation
<a name="manipuler_champ_timeline_par_programmation_ELTTEXTE000318"></a>


### Ajouter une piste
<a name="ajouter_une_piste_ELTPARAGRAPHE000035"></a>L'ajout d'une piste dans un champ TimeLine est réalisé grâce à la fonction [TimelineAjoutePiste](../WDLang1/1000020405.md). 

Exemple :

```wl
// Ajoute des pistes dans un champ TimeLine TL_TimeLine1
TimelineAjoutePiste(TL_TimeLine1, "Sub Bass")
TimelineAjoutePiste(TL_TimeLine1, "Scratchy Drums")
TimelineAjoutePiste(TL_TimeLine1, "Guitar 1")
```



### Ajouter un événement
<a name="ajouter_evenement_ELTPARAGRAPHE000058"></a>L'ajout d'un événement dans un champ TimeLine est réalisé grâce à la fonction [TimelineAjouteEvénement](../WDLang1/1000020400.md). Cette fonction accepte deux syntaxes : 

- syntaxe permettant de préciser les caractéristiques de l'événement : titre, description, ...
	
	```wl
	// Ajoute un événement de 5 secondes dans un champ TimeLine configuré à la seconde
	MaPiste est une chaîne
	MonTitre est une chaîne
	DébutEvt est un entier
	FinEvt est un entier 
	
	MaPiste = "Cordes"
	MonTitre = "Début"
	DébutEvt = 4
	FinEvt = 9
	
	TimeLineAjouteEvénement(TL_Musique, MaPiste, MonTitre, DébutEvt, FinEvt)
	```


- syntaxe manipulant une variable de type [EvénementTimeline](../WDLang1/1000020612.md).
	
	```wl
	TimelineSupprimeTout(TL_Automates)
	Evt est un EvénementTimeline
	
	TimelineAjoutePiste(TL_Automates, "Automate 1")
	Evt.Piste = "Automate 1"
	Evt.Titre = "Démarrage"
	Evt.Début = 10
	Evt.Fin = 150
	Evt.CouleurFond = VertClair
	TimelineAjouteEvénement(TL_Automates, Evt)
	
	Evt.Piste = "Automate 1"	
	Evt.Titre = "Traitement spécial"
	Evt.Début = 150
	Evt.Fin = 450
	Evt.CouleurFond = BleuPastel
	TimelineAjouteEvénement(TL_Automates, Evt)
	```



La propriété **CouleurFond** de la variable [EvénementTimeline](../WDLang1/1000020612.md) permet de définir une couleur d'affichage pour un événement. Si aucune couleur de fond n'est définie, le champ TimeLine utilisera automatiquement la couleur associée à la catégorie de l'événement. 


### Remplir un champ TimeLine avec les données présentes dans un fichier de données HFSQL
<a name="remplir_champ_timeline_avec_les_donnees_presentes_dans_fichier_donnees_hfsql_ELTPARAGRAPHE000088"></a>Les enregistrements sont stockés dans un fichier de données HFSQL. Le remplissage initial du champ TimeLine peut être réalisé par un parcours du fichier de données via la syntaxe POUR TOUT et par l'ajout de chaque événement via la fonction [TimelineAjouteEvénement](../WDLang1/1000020400.md).

```wl
TimelineSupprimeTout(TL_Automates)
// Déclaration des événements
Evt est un EvénementTimeline

// Parcours du fichier de données Automate
POUR TOUT Automate 
	TimelineAjoutePiste(TL_Automates, Automate.NomAutomate)
	// Parcours du fichier de données Evt_Automate
	POUR TOUT Evt_Automate AVEC IDAutomate = Automate.IDAutomate
		Evt.Bulle = Evt_Automate.EvtBulle
		Evt.Titre = Evt_Automate.EvtTitre
		Evt.Début = Evt_Automate.EvtDébut
		Evt.Fin = Evt_Automate.EvtFin
		Evt.Contenu = Evt_Automate.EvtContenu
		Evt.Piste = Automate.NomAutomate
		Evt.CouleurFond = Evt_Automate.EvtCouleur
		TimelineAjouteEvénement(TL_Automates, Evt)
	FIN
FIN
```
Rappel : Il est également possible d'utiliser un champ TimeLine lié à un fichier de données. Pour plus de détails, consultez [Champ TimeLine lié à un fichier de données](../WDChamp/1000026005.md).


### Récupérer une liste d'événements
<a name="recuperer_une_liste_evenements_ELTPARAGRAPHE000108"></a>La fonction [TimelineListeEvénement](../WDLang1/1000020401.md) permet de récupérer : 

- la liste de tous les événements du champ TimeLine : 
	
	```wl
	// Tableau contenant une liste des événements 
	tabListeEvt est un tableau de EvénementTimeline
	
	// Liste des événements
	tabListeEvt = TimelineListeEvénement(TL_TimeLine)
	```


- la liste des événements d'une piste compris entre deux moments : 
	
	```wl
	// Liste des événements de l'automate 1 compris entre 50s et 100s 
	tabListeEvt est un tableau de EvénementTimeline
	tabListeEvt = TimelineListeEvénement(TL_Automates, "Automate 1", 50, 100)
	```


- l'événement actuellement sélectionné ou survolé : 
	
	```wl
	// Evénement sélectionné 
	tabListeEvt est un tableau de EvénementTimeline
	tabListeEvt = TimelineListeEvénement(TL_Automates, plnRdvSélectionné)
	```






### Supprimer un événement
<a name="supprimer_evenement_ELTPARAGRAPHE000143"></a>La fonction [TimelineSupprimeEvénement](../WDLang1/1000020403.md) permet de supprimer : 

- l'événement sélectionné dans le champ. 

- un événement précis.



```wl
// Supprime le premier événement
TimelineSupprimeEvénement(TL_MonTimeLine, 1)
```



### Supprimer une piste
<a name="supprimer_une_piste_ELTPARAGRAPHE000163"></a>La fonction [TimelineSupprimePiste](../WDLang1/1000020760.md) permet de supprimer une piste du champ TimeLine.

```wl
// Supprime la piste SON
ResSup est un booléen
ResSup = TimelineSupprimePiste(TL_MonTimeLine, "Son")
SI ResSup = Vrai ALORS
	Info("Piste supprimée")
FIN
```
La fonction [TimeLineSupprimeTout](../WDLang1/1000020404.md) permet de supprimer tous les événements présents dans le champ TimeLine ainsi que toutes ses pistes. 


### Modifier l'échelle temporelle du champ
<a name="modifier_echelle_temporelle_champ_ELTPARAGRAPHE000183"></a>L'affichage courant d'un champ TimeLine peut être modifié via la fonction [TimelineChangeMode](../WDLang1/1000020397.md). Cette fonction permet de modifier le mode d'affichage du champ : seconde, milliseconde ou microseconde.  



## Utilisation du menu contextuel (FAA)
<a name="utilisation_menu_contextuel_faa_ELTTEXTE000385"></a>
Le champ TimeLine est associé par défaut à un menu contextuel (FAA). Via le menu contextuel du champ TimeLine, il est possible de : 

- réaliser un zoom sur le champ,

- ajouter, modifier ou supprimer un événement. 




Pour sauvegarder les opérations réalisées, il est nécessaire d'utiliser les événements WLangage associés au champ TimeLine. 

Dans l'événement WLangage concerné, il suffit de récupérer l'événement en cours d'utilisation et de faire le traitement correspondant. 

**Exemple** : Pour mémoriser dans un fichier de données "EVT" un événement que l'utilisateur ajoute via le menu contextuel, il suffit de saisir dans l'événement "Entrée en saisie d'un événement" le code suivant : 

```wl
PROCEDURE EntréeEnSaisie(evtEdité est un EvénementTimeline)

// Mémorisation des données
EVT.Titre = evtEdité.Titre
EVT.DébutEvt = evtEdité.Début
EVT.FinEvt = evtEdité.Fin
...
HAjoute(EVT)
```
Le même type de code peut être mis en place pour les différents événements associés au champ TimeLine. En effet, pour chaque événement WLangage associé au champ qui manipule un événement, une procédure a été déclarée automatiquement par le champ TimeLine. 

Ces procédures reçoivent en paramètre une variable de type [EvénementTimeline](../WDLang1/1000020612.md) concernée par le traitement. 



## Utilisation avancée des événements associés au champ TimeLine
<a name="utilisation_avancee_des_evenements_associes_champ_timeline_ELTTEXTE000409"></a>


### Utilisation avancée des événements avec procédure
<a name="utilisation_avancee_des_evenements_avec_procedure_ELTPARAGRAPHE000216"></a>Il est possible de proposer à l'utilisateur de définir plus précisément les caractéristiques de son événement lors d'un ajout ou d'une modification. Il suffit pour cela de créer une fenêtre avec les informations à remplir. 

Dans le code, il suffit d'ouvrir la fenêtre par exemple dans l'événement "Entrée en saisie d'un événement". Pour bloquer la saisie directe, le traitement doit alors renvoyer <u><u><u><u>Faux</u></u></u></u>. 

Ce principe peut être appliqué à tous les événements WLangage appelés par le menu contextuel du champ TimeLine. 

Exemple : Ouverture d'une fenêtre de saisie de l'événement.  

```wl
PROCEDURE EntréeEnSaisie(evtEdité est un EvénementTimeline)

// Ouvre la fenêtre de saisie d'un événement
// avec l'événement sélectionné (en mode Création ou Modification)
Ouvre(FEN_SaisieEvt_HFSQL, evtEdité)

// Renvoie FAUX pour bloquer la saisie directe dans le champ TimeLine
RENVOYER Faux
```




## Propriétés spécifiques aux champs TimeLine
<a name="proprietes_specifiques_aux_champs_timeline_ELTTEXTE000433"></a>
Les propriétés suivantes sont spécifiques à la gestion d'un champ TimeLine par programmation.



|   |   |
| --- | --- |
| [DébutEtendueTotale](../Proprietes/1000020392.md) | La propriété **DébutEtendueTotale** permet de : <br><br>- connaître la première date ou heure affichable dans un champ Planning ou dans un champ TimeLine. <br><br>- modifier la première heure affichable dans un champ TimeLine.<br><br><br> |
| [DébutEtendueVisible](../Proprietes/1000020394.md) | La propriété **DébutEtendueVisible** permet de : <br><br>- connaître la première date ou heure visible dans un champ Planning ou dans un champ TimeLine.  <br><br>- modifier la première heure visible dans un champ TimeLine.<br><br><br> |
| [FinEtendueTotale](../Proprietes/1000020393.md) | La propriété **FinEtendueTotale** permet de : <br><br>- connaître la dernière date ou heure affichable dans un champ Planning ou dans un champ TimeLine. <br><br>- modifier la dernière heure affichable dans un champ TimeLine.<br><br><br> |
| [FinEtendueVisible](../Proprietes/1000020395.md) | La propriété **FinEtendueVisible** permet de : <br><br>- connaître la dernière date ou heure visible dans un champ Planning ou dans un champ TimeLine. <br><br>- modifier la dernière heure visible dans un champ TimeLine.<br><br><br> |
| [GranularitéDéplacement](../Proprietes/1000019642.md) | La propriété **GranularitéDéplacement** permet de connaître et de modifier la taille de la grille pour le déplacement :  <br><br>- des rendez-vous d'un champ Agenda. <br><br>- des rendez-vous d'un champ Planning.<br><br>- des événements d'un champ TimeLine.<br><br>- des tâches d'une colonne de type Diagramme de Gantt.<br><br><br> |
| [GranularitéDurée](../Proprietes/1000019643.md) | La propriété **GranularitéDurée** permet de connaître et de modifier la taille de la grille utilisée pour le redimensionnement :<br><br>- des rendez-vous d'un champ Agenda. <br><br>- des rendez-vous d'un champ Planning.<br><br>- des événements d'un champ TimeLine.<br><br>- des tâches d'une colonne de type Diagramme de Gantt.<br><br><br> |
| [RégletteModifiable](../Proprietes/1000020381.md) | La propriété **RégletteModifiable** permet de : <br><br>- Savoir si la réglette d'un champ TimeLine peut être déplacée par l'utilisateur. <br><br>- Rendre déplaçable ou non la réglette d'un champ TimeLine par l'utilisateur. <br><br><br> |
| [RégletteValeur](../Proprietes/1000020380.md) | La propriété **RégletteValeur** permet de connaître ou modifier la position de la réglette dans un champ TimeLine. |
| [RégletteVisible](../Proprietes/1000020379.md) | La propriété **RégletteVisible** permet de : <br><br>- Savoir une réglette est visible dans un champ TimeLine. <br><br>- Rendre une réglette visible ou invisible dans un champ TimeLine. <br><br><br> |





Pour connaître la liste complète des propriétés du WLangage pouvant être utilisées avec un champ de type TimeLine, consultez [Propriétés associées au champ TimeLine](../WDChamp/1000026003.md).


