
## Jour (Propriété)

***En anglais : Day***
	



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
La propriété **Jour** permet de :

- Récupérer le jour d'une variable de type [Date](../Motscles/1514066.md), [DateHeure](../Motscles/1514070.md) ou récupérer le nombre de jours d'une variable de type [Durée](../Motscles/1514069.md).

- Modifier le jour d'une variable de type [Date](../Motscles/1514066.md), [DateHeure](../Motscles/1514070.md) ou modifier le nombre de jours d'une variable de type [Durée](../Motscles/1514069.md).

- Récupérer le jour ou le nombre de jours d'une rubrique de type Date (au format "Date", "Date et heure" ou "durée").
	

- Modifier le jour ou le nombre de jours d'une rubrique de type Date (au format "Date", "Date et heure" ou "durée").
	




**Remarque** : La propriété **Jour** permet de simplifier les opérations de décalage sur les dates (ajouter un jour, ...).

**Rappel** : Les rubriques de type Date permettent de gérer :

- les dates simples "Année - Mois - Jour" (format AAAAMMJJ).

- les dates et les heures "Année - Mois - Jour - Heures - Minutes - Secondes - Millièmes de seconde" (format AAAAMMJJHHmmSSCCC).

- les durées "Nombre de jours - Nombre d'heures - Nombre de minutes - Nombre de secondes - Nombre de millièmes de seconde" (format +JHHMMSSCCC).





<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
// Exemple sur une variable
DateDébut est une Date = "20011225"
// Ajouter 5 jours à la date
DateDébut.Jour += 5
// Modifier les jours
DateDébut.Jour = 10
```


<a name="Exemple2"></a>



```wl
// Exemple sur une rubrique
Travail.DateDébut = "20011225"
// Ajouter 5 jours à la date
Travail.DateDébut.Jour += 5
// Modifier les jours
Travail.DateDébut.Jour = 10
```



<a name="XSYNTAXE"></a>
<a name="SYNTAXE1"></a>

## Syntaxe

### Connaître le jour d'une variable Date, DateHeure ou Durée

`<Résultat> = <Date>.Jour`
---

**`<Résultat> : (Entier)`**

Jour sur 2 chiffres.

**`<Date> : (Date ou DateHeure)`**

Nom de la variable de type [Date](../Motscles/1514066.md), [DateHeure](../Motscles/1514070.md) ou [Durée](../Motscles/1514069.md) à manipuler.  


<a name="SYNTAXE2"></a>

### Modifier le jour d'une variable Date, DateHeure ou Durée

`<Date>.Jour = <Nouveau jour>`
---

**`<Date> : (Date ou DateHeure)`**

Nom de la variable de type [Date](../Motscles/1514066.md), [DateHeure](../Motscles/1514070.md) ou [Durée](../Motscles/1514069.md) à manipuler.

**`<Nouveau jour> : (Entier ou chaîne de caractères)`**

Nouveau jour en chiffres (compris entre 1 et 31). Remplace le jour de la date manipulée.  


<a name="SYNTAXE3"></a>

### Connaître le jour d'une rubrique de type Date

`<Résultat> = <Fichier de données>.<Rubrique>.Jour`
---

**`<Résultat> : (Entier)`**

Jour sur 2 chiffres.

**`<Fichier de données> : (Chaîne de caractères)`**

Nom du fichier de données manipulé. Ce nom a été défini sous l'éditeur d'analyses ou grâce au type [Description de Fichier](../WDLang4/1514065.md).

**`<Rubrique> : (Chaîne de caractères)`**

Nom de la rubrique manipulée. Ce nom a été défini sous l'éditeur d'analyses ou grâce au type [Description de Rubrique](../WDLang4/1514071.md).  


<a name="SYNTAXE4"></a>

### Modifier le jour d'une rubrique de type Date

`<Fichier de données>.<Rubrique>.Jour = <Nouveau jour>`
---

**`<Fichier de données> : (Chaîne de caractères)`**

Nom du fichier de données manipulé. Ce nom a été défini sous l'éditeur d'analyses ou grâce au type [Description de Fichier](../WDLang4/1514065.md).

**`<Rubrique> : (Chaîne de caractères)`**

Nom de la rubrique manipulée. Ce nom a été défini sous l'éditeur d'analyses ou grâce au type [Description de Rubrique](../WDLang4/1514071.md).

**`<Nouveau jour> : (Entier ou chaîne de caractères)`**

Nouveau jour en chiffres (compris entre 1 et 31). Remplace le jour de la date manipulée.  



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques




### Plage de dates
<a name="plage_dates_ELTPARAGRAPHE000172"></a>Les types [Date](../Motscles/1514066.md) et [DateHeure](../Motscles/1514070.md) permettent de gérer des dates du 01/01/0001 au 31/12/9999.


<a name="NOTE0_2"></a>




### Gestion des jours et des mois
<a name="gestion_des_jours_des_mois_ELTPARAGRAPHE000187"></a>

** Cas 1 : Affectation directe**
Lors d'une affectation directe (par exemple, *MonJour.Jour = n*), le jour doit être compris entre 1 et 31. Si le jour spécifié est supérieur, une erreur du WLangage est générée.
La notation *MonJour.Jour = MonJour.Jour + 5* risque de générer une erreur en exécution. Par exemple, les codes suivants provoquent une erreur :


```wl
// Code provoquant l'erreur
MaDate est une Date = "20201126"   // le 26/11/2020 
MaDate.Jour = MaDate.Jour + 20   // Provoque une erreur WLangage car le jour est égal à 45
 
// Code correct
MaDate est une Date = "20201126"   // le 26/11/2020 
MaDate.Jour += 20
```


```wl
// Code provoquant l'erreur
MaDate est une Date = "20201126"   // le 26/11/2020 
MaDate1 est une Date 
MaDate1.Jour = MaDate.Jour + 20   // Provoque une erreur WLangage car le jour est égal à 45
 
// Code correct
MaDate1 = MaDate   // le 26/11/2020 
MaDate1.Jour += 20
```


** Cas 2 : Opérations sur les jours**
Lors d'opérations sur les dates, le changement de mois est automatiquement géré. Ainsi, si le nombre de jours est supérieur au nombre de jours valide pour le mois manipulé, le nombre de jours repart de 1 et le numéro du mois est automatiquement modifié. L'année est également modifiée si nécessaire (mois de décembre par exemple).
Par exemple :



```wl
DateDébut est une Date = "20201226"   // Le 26/12/2020
// Ajouter 10 jours à la date
DateDébut.Jour +=10    // DateDébut vaut "20050105"
```

<a name="NOTE0_3"></a>




### Manipulation des durées
<a name="manipulation_des_durees_ELTPARAGRAPHE000211"></a>Une durée n'a pas de limites : son nombre de jours peut dépasser 30 ou 31 jours.


<a name="NOTE0_4"></a>




### Opérateurs disponibles sur les jours
<a name="operateurs_disponibles_sur_les_jours_ELTPARAGRAPHE000220"></a>Les opérateurs arithmétiques suivants peuvent être utilisés avec la propriété **Jour** :

- ++  et  --

- +=  et - =





```wl
DateDébut est une Date = "20201126"   
DateDébut.Jour++     // Ajouter 1 jour à la date
DateDébut.Jour+=5    // Ajouter 5 jours à la date
DateDébut.Jour-=5    // Retrancher 5 jours à la date
```



<a name="NOTE0_5"></a>




### Calculer le dernier jour du mois
<a name="calculer_dernier_jour_mois_ELTPARAGRAPHE000237"></a>Pour obtenir le dernier jour d'un mois, il suffit d'affecter la propriété **Jour** de la date avec 31. Le dernier jour sera alors automatiquement calculé en fonction du mois spécifié.



```wl
DateDébut est une Date = "20201126"   
DateDébut.Jour = 31    
// La date 31 est impossible pour le mois de novembre
// Elle est automatiquement remplacée par 30
```



<a name="NOTE0_6"></a>




### Calculer une échéance à 90 jours fin de mois
<a name="calculer_une_echeance_90_jours_fin_mois_ELTPARAGRAPHE000251"></a>La propriété **Jour** permet de calculer la date de fin d'une échéance à 90 jours fin de mois.


```wl
MaDate est une Date = "20201126"   
MaDate.Jour += 90   
MaDate.Jour = 31
```

<a name="NOTE0_7"></a>




### Calculer la fin d'un mois flottant
<a name="calculer_fin_mois_flottant_ELTPARAGRAPHE000264"></a>Un mois flottant correspond à une période d'un mois à partir d'une date donnée. Les propriétés [Mois](../Proprietes/2512052.md) et **Jour** permettent de calculer simplement la date de fin d'un mois flottant.



```wl
DateDébut est une Date = "20201126"   
DateFin est une Date = DateDébut
DateFin.Mois++    
DateFin.Jour--
```



<a name="NOTE0_8"></a>




### Calculs sur les dates
<a name="calculs_sur_les_dates_ELTPARAGRAPHE000281"></a>Le format de stockage des dates permet de stocker des dates comprises entre le 01/01/0001 et le 31/12/9999.

Les calculs sur les dates réalisés avec les [fonctions WLangage](../WDLang1/3027029.md) ou les [propriétés WLangage](../Proprietes/2514010.md) sont fiables à partir du 1er Janvier 1583.  


