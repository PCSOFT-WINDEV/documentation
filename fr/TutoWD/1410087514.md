
## Leçon 2.2. Les variables


<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Ce que vous allez apprendre dans cette leçon
<a name="que_vous_allez_apprendre_dans_cette_lecon_ELTTEXTE000899"></a>


- Qu'est-ce qu'une variable ?

- Les différents types de variables.

- La portée des variables.

- Le type Chaîne en détail.





|   |   |
| --- | --- |
| ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=dur%E9e.png)<br> | <br>Durée estimée : 1 h |

| [Leçon précédente](../TutoWD/1410087578.md) | [Sommaire](../TutoWD/1410087560.md) | [Leçon suivante](../TutoWD/1410087587.md) |
| --- | --- | --- |







<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Exemple pratique
<a name="exemple_pratique_ELTTEXTE000946"></a>
Pour réaliser directement les manipulations de cette leçon, nous allons travailler dans le projet "WLangage" que nous avons créé dans la leçon précédente.




- Ouvrez si nécessaire le projet "WLangage" que vous avez créé dans la leçon précédente (consultez le paragraphe [Un projet pour découvrir le WLangage](../TutoWD/1410087578.md) de la leçon 2.1).






- Nous allons saisir le code directement dans le code d'initialisation du projet. Ce code étant le tout premier code exécuté lors du test du projet, nous pouvons ainsi réaliser des tests très simplement, sans interface. Pour visualiser le résultat de nos manipulations, nous utiliserons notamment la fonction [Trace](../WDLang1/3013050.md), qui permet d'écrire l'information voulue aussi bien dans une fenêtre de trace que dans le volet du débogueur (c'est cette dernière option que nous allons utiliser). 




- Pour afficher les événements WLangage liés au projet : 

	1. Dans la barre des éléments ouverts, faites un clic droit sur le bouton "P". Le menu contextuel s'affiche. 

	2. Sélectionnez l'option "Code de l'élément". 

	3. L'éditeur de code affiche les différents événements associés au projet. 






<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Qu'est-ce qu'une variable ?
<a name="questce_quune_variable_ELTTEXTE000964"></a>
Dans un langage de programmation, une variable permet de conserver des données. Ce sont des parties de la mémoire qui contiennent des chaînes, des nombres, etc.

Les variables permettent d'effectuer des calculs mathématiques, de faire des comparaisons ou tout simplement de stocker des informations pour les retrouver plus tard.
![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=Declaration27.gif)
Une variable est représentée par : 

- un nom : Nom donné à la variable afin de la manipuler par le langage. 

- un type : Nature de la donnée stockée dans la variable (voir [Les types de variables](../TutoWD/1410087514.md)).

- une valeur : Information stockée dans la variable.

- une portée : Limite d'utilisation de la variable dans le programme (voir [La portée des variables](../TutoWD/1410087514.md)). La portée est définie principalement par l'endroit où la variable est déclarée. 


|   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=note.png)<br> | Note | Nous avons vu dans les premières leçons qu'il était possible d'utiliser une charte de programmation. <br>Par défaut, cette charte est appliquée **automatiquement** aux éléments de votre projet : champs, fenêtres ou pages, états, etc. <br><br>Lors de la création du projet, dans l'étape "Charte de programmation", il est possible de sélectionner l'option "Ajouter automatiquement les préfixes à la création des variables". Si cette option est sélectionnée, un préfixe sera automatiquement ajouté pour identifier le type de la variable. <br>Ainsi, la déclaration : <br><br><pre><code>Prix est un monétaire</code></pre><br> deviendra : <br><pre><code>moPrix est un monétaire</code></pre><br>où "mo" est le préfixe des variables de type "Monétaire".<br><br>Cette option peut être modifiée dans l'onglet "Options" de la fenêtre de description de projet, option "Ajouter automatiquement les préfixes". |




<a name="NOTE3_2"></a>


### Déclarer une variable
<a name="declarer_une_variable_ELTPARAGRAPHE000144"></a>Avant d'utiliser une variable, il faut tout d'abord la déclarer (c'est-à-dire la créer).

- Exemple d'une déclaration simple :
	
	```wl
	Prix est un monétaire
	```


	- **Prix** représente le nom de la variable. 

	- **est un** permet de déclarer la variable. Le WLangage utilise le langage naturel en français. 

	- **monétaire** correspond au type de la variable. 




- Exemple d'une déclaration multiple :
	
	```wl
	Nom, Prénom sont des chaînes
	```


	- **Nom, Prénom** représentent les noms des variables.

	- **sont des** permet de déclarer un ensemble de variables.

	- **chaînes** représente le type des variables. 





|   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=note.png)<br> | Note | En fonction de l'endroit où la variable est déclarée, cette variable peut être globale à l'élément (nous verrons cette notion dans le paragraphe "Portée des variables" dans la suite de cette leçon). <br><br>Si vous utilisez le préfixage automatique des variables, la lettre 'g' (pour globale) sera alors ajoutée devant le nom de la variable. Par exemple, la déclaration de la variable **Nom** devient : <br><br><pre><code>gsNom est une chaîne</code></pre><br>Dans la suite de ce cours, nous n'utiliserons pas le préfixage automatique des variables. <br><br>Nous verrons en détail les différents types de variables (locale et globale) dans les paragraphes suivants. |







- Nous allons saisir ces déclarations sous l'éditeur de code. Dans l'événement "Initialisation" du projet "WLangage", saisissez le code suivant : 
	
	```wl
	Prix est un monétaire
	Nom, Prénom sont des chaînes
	```

	Vous pouvez observer la coloration spécifique des différents éléments de la déclaration : une couleur spécifique est utilisée pour le nom de la variable et une autre pour son type. Ces couleurs permettent d'identifier rapidement les différents éléments de votre code. 



<a name="NOTE3_3"></a>


### Affectation et utilisation
<a name="affectation_utilisation_ELTPARAGRAPHE000189"></a>Lorsque la variable est déclarée, il est possible de l'affecter (ou lui donner une valeur). L'opérateur **=** permet d'effectuer cette affectation. 



- Nous allons tout de suite tester cet opérateur. Dans l'événement "Initialisation" du projet "WLangage", ajoutez le code suivant : 
	
	```wl
	// Affectation d'une variable de type monétaire
	Prix = 1256.67
	// Affichage du contenu de la variable
	Trace(Prix)
	// Affectation d'une variable de type chaîne
	Nom = “Dupont”
	// Affichage du contenu de la variable
	Trace(Nom)
	```

	Dans ce code, les variables **Prix** et **Nom** sont affectées avec une valeur. Pour lire et manipuler le contenu de la variable, il suffit d'utiliser le nom donné à la variable pour y accéder. Dans cet exemple, le contenu des variables est affiché dans le volet "Trace du débogueur" grâce à la fonction [Trace](../WDLang1/3013050.md).






- Testons notre code : 

	1. Lancez le test du projet en cliquant sur ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=ICO_GO_Projet_WD_GAF.jpg)
 parmi les boutons d'accès rapide.

	2. Le projet se lance en exécution et l'éditeur de code réapparaît. 

	3. Affichez si nécessaire le volet "Trace du débogueur" pour voir le résultat : sous le volet "Accueil", dans le groupe "Environnement", déroulez "Volets", sélectionnez "Volets" puis "Trace du débogueur". 

	4. Le volet "Trace du débogueur" contient le contenu de nos variables :
			
		```txt
		1256.67
		Dupont
		```








<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Les types de variables
<a name="les_types_variables_ELTTEXTE001036"></a>
Le type de la variable permet d'indiquer quelle est la forme de l'information que l'on stocke dans la variable. Les types les plus basiques sont :

- booléen (Vrai ou Faux),

- chaîne ("Dupont"),

- entier (1234),

- monétaire (12.32),

- réel (7.766666),

- etc.


|   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=avertissement.png)<br> | Important | Utilisez le type correspondant à l'information que vous voulez stocker. Vous optimisez ainsi l'utilisation de la mémoire mais surtout vous évitez des erreurs de calcul ou de traitement lors de l'utilisation des variables dans les fonctions du WLangage. |




Au cours de ce tutoriel, nous allons utiliser la plupart de ces types de variables. Nous verrons également les types Tableau, Date, Heure, Structure, etc. 

Il existe également des variables de type "avancé". Ces types avancés regroupent toutes les caractéristiques de l'élément manipulé en une seule variable. Ces types avancés permettent par exemple de manipuler les documents XML, les emails, les fichiers XLS, etc. Nous manipulerons également ce type de variable dans la suite de ce tutoriel.

Pour plus de détails, consultez l'aide concernant le type voulu (voir [Les différents types de variables](../Motscles/1514013.md)).

<a name="NOTE5"></a>
<a name="NOTE5_1"></a>


## La portée des variables
<a name="portee_des_variables_ELTTEXTE001078"></a>
Il est possible de déclarer les variables à n'importe quel endroit dans le code. Cependant, en fonction de la position de sa déclaration, la variable n'est pas utilisable (ou visible) pour effectuer des traitements ou des calculs. On parle alors de portée des variables.

Il existe 2 types de portée :

- Globale.

- Locale.



<a name="NOTE5_2"></a>


### Portée Globale
<a name="portee_globale_ELTPARAGRAPHE000294"></a>Le terme Global(e) signifie que la variable a une visibilité étendue dans le code. La variable est visible en dehors de l'événement (ou du traitement) où elle a été déclarée. Il y a plusieurs niveaux de Globalité :

- niveau Projet et Collection de procédures,

- niveau Fenêtre, Fenêtre Mobile, Page, Etat,

- niveau Champ. 




Une variable déclarée **au niveau du projet** a la visibilité la plus importante dans le programme. Dans ce cas, le variable est déclarée dans l'événement "Initialisation du projet". La variable est visible partout, dans tous les événements et traitements du programme. Attention toutefois à ne pas trop déclarer de variables de cette portée : en effet, la mémoire occupée par la variable est toujours réservée même si la variable n'est pas utilisée. L'utilisation massive de variables globales n'est pas conseillée dans l'architecture d'un programme informatique. Pour passer les variables à un événement ou à un traitement, il est conseillé d'utiliser des paramètres (pour plus de détails, consultez ["Paramètres et résultat d'une procédure"](../TutoWD/1410087517.md)).

Une variable déclarée **au niveau d'une Collection de procédures** a la même visibilité qu'une variable déclarée au niveau du projet. L'avantage de déclarer une variable au niveau d'une Collection est de regrouper (ou classer) les variables par thème (collection) afin de rendre plus lisible l'événement "Initialisation" du projet.

Une variable déclarée **au niveau d'une Fenêtre, Fenêtre Mobile, Page ou Etat** limite la portée de la variable aux événements ou traitements de l'élément (Fenêtre, Fenêtre Mobile, Page ou Etat) et de ses champs. Cela permet d'encapsuler et de limiter les utilisations.

Une variable globale déclarée **au niveau d'un champ** limite la portée de la variable aux événements du champ. Cela permet de regrouper les variables par champ et ainsi de rendre plus lisible l'événement "Initialisation" de la fenêtre.|   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=note.png)<br> | Note | Par défaut, les variables globales sont colorées en bleu dans l'éditeur de code. Pour identifier les variables globales et leur portée, il est recommandé d'utiliser une norme d'écriture. |




<a name="NOTE5_3"></a>


### Portée Locale
<a name="portee_locale_ELTPARAGRAPHE000321"></a>Le terme Local(e) signifie que la variable a une visibilité très limitée dans le code. La variable est visible uniquement dans le traitement où elle a été déclarée. Cela permet ainsi de restreindre l'utilisation de la variable au traitement ou à l'événement.
<a name="NOTE5_4"></a>


### Schéma récapitulatif de la portée
<a name="schema_recapitulatif_portee_ELTPARAGRAPHE000330"></a>![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=Projet%20Variables%20et%20Collections%20-%204-.gif)
Une variable est globale par défaut lorsqu'elle est déclarée : 

- dans l'événement "Initialisation" du projet (ou dans l'événement "Déclaration" de la collection de procédures). La variable est globale au projet.

- dans l'événement "Déclarations globales" de la fenêtre, de la page ou de l'état. La variable est globale à l'élément (fenêtre, page ou état) dans lequel elle a été déclarée.




Dans tous les autres cas, une variable est locale au traitement ou à l'événement dans lequel elle est déclarée.

**Attention** : Pour déclarer une variable globale à un champ, il est nécessaire d'indiquer le mot-clé "GLOBAL" lors de sa déclaration dans l'événement "Initialisation" du champ. 

<a name="NOTE6"></a>
<a name="NOTE6_1"></a>


## Opérations simples sur les variables
<a name="operations_simples_sur_les_variables_ELTTEXTE001138"></a>
Pour effectuer des calculs sur des variables, le WLangage met à disposition les opérateurs mathématiques classiques :

- + pour faire une addition.

- - pour faire une soustraction.

- \* pour faire une multiplication.

- / pour faire une division.




D'autres opérateurs peuvent être utilisés pour réaliser des calculs :

- ++ pour incrémenter de 1 (additionner 1 à la variable).

- - - pour décrémenter de 1 (soustraire 1 à la variable).

- += pour affecter en additionnant une valeur.

- - = pour affecter en soustrayant une valeur.






- Pour avoir un aperçu des opérations réalisables, nous allons faire un petit test : 

	1. Supprimez le code saisi dans l'événement "Initialisation" du projet (Ctrl + A pour tout sélectionner, et Suppr pour supprimer). 

	2. Saisissez le code suivant : 
			
		```wl
		// Déclaration de variables
		Compteur est un entier
		V1 est un entier
		Res est un numérique
		
		// Affectation
		Compteur = 10
		V1 = 3
		
		// Utilisation des opérateurs
		Compteur = Compteur + 3   // Compteur vaut 13
		Trace(Compteur)
		Compteur ++ // Compteur vaut 14
		Trace(Compteur)
		Compteur -= 8 // Compteur vaut 6
		Trace(Compteur)
		Compteur = Compteur * V1 // Compteur vaut 18
		Trace(Compteur)
		Res = Compteur / 5 // Res vaut 3.6
		Trace(Res)
		```
Ce code permet de faire diverses opérations et affiche à chaque fois le résultat dans la fenêtre de trace. 









- Testons notre code : 

	1. Lancez le test du projet en cliquant sur ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=ICO_GO_Projet_WD_GAF.jpg)
 parmi les boutons d'accès rapide. 

	2. Affichez si nécessaire le volet "Trace du débogueur" pour voir le résultat : sous le volet "Accueil", dans le groupe "Environnement", déroulez "Volets" et sélectionnez "Volets" puis "Trace du débogueur". 

	3. Le volet "Trace du débogueur" contient la valeur de la variable Compteur pour chaque opération réalisée :
			
		```txt
		13
		14
		6
		18
		3.6
		```








Des opérateurs de comparaison sont également disponibles :

- &lt; inférieur strict

- &gt; supérieur strict

- &lt;= inférieur ou égal

- &gt;= supérieur ou égal

- &lt;&gt; différent de 

- = égal à 




D'autres opérateurs sont disponibles. Pour plus de détails sur les opérateurs, consultez [Liste des opérateurs](../Motscles/1512010.md).
<a name="NOTE6_2"></a>


### Conseils
<a name="conseils_ELTPARAGRAPHE000426"></a>

- Il est très pratique de nommer les variables avec des noms longs (et éviter les noms très courts i, j, k par exemple). Lors de la relecture du programme, il est ainsi possible de se rappeler à quoi sert la variable.

- Pour définir le nom des variables, le WLangage accepte tous les caractères Unicode, y compris les accents. Autant en profiter pour plus de lisibilité ! Attention toutefois : certains caractères sont interdits : espace, =, point, virgule, etc.

- Il est très important de donner le bon type à la variable en fonction de son utilisation. Par exemple, pour stocker une suite de chiffres, il peut être nécessaire :

	- d'utiliser une variable de type numérique si cette variable doit être utilisée pour des calculs.

	- d'utiliser une variable de type chaîne si cette variable permet de stocker des chiffres sans effectuer de calculs (par exemple pour mémoriser le numéro de sécurité sociale).







<a name="NOTE7"></a>
<a name="NOTE7_1"></a>


## Détail d'un type de variable : les variables de type Chaîne
<a name="detail_type_variable_les_variables_type_chaine_ELTTEXTE001168"></a>
Les variables de type Chaîne sont un des types de variables les plus utilisés.

Nous allons détailler une partie des fonctionnalités disponibles sur ce type de variable.
<a name="NOTE7_2"></a>


### Le type Chaîne
<a name="type_chaine_ELTPARAGRAPHE000451"></a>Nous savons déjà que le type Chaîne permet de stocker et manipuler les caractères et les chaînes de caractères.

Nous avons déjà vu comment initialiser une variable de type chaîne :

```wl
Nom est une chaîne
// Affectation d'une variable de type chaîne
Nom = “Dupont”
```
|   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=note.png)<br> | Note | En WLangage, le caractère " (double quote) est le caractère utilisé pour délimiter une chaîne de caractères. Dans l'exemple ci-dessus, les doubles quotes sont utilisées pour affecter la valeur Dupont à la variable Nom. |







Il n'est pas nécessaire de déclarer la longueur de la chaîne : le WLangage adapte automatiquement cette longueur lors de l'utilisation de la variable. |   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=astuce.png)<br> | Astuce | Pour initialiser une variable de type chaîne avec un texte sur plusieurs lignes, utilisez la syntaxe suivante : <br><br><pre><code><Nom variable> = "<br><Texte de la ligne 1><br><Texte de la ligne 2><br>"</code></pre><br>Par exemple :<br><br><pre><code>MaChaîne est une chaîne<br>MaChaîne = "<br>Exemple de<br>chaîne multi-ligne<br>"</code></pre><br> |



Il est également possible d'affecter une variable de type chaîne avec le contenu d'un champ manipulant des chaînes. Ainsi, le code permettant d'affecter une variable de type chaîne avec le contenu d'un champ de saisie est le suivant : 

```wl
Nom est une chaîne
// Affectation d'une variable de type chaîne 
// avec le contenu du champ de saisie SAI_Nom
Nom = SAI_Nom
```

<a name="NOTE7_4"></a>


### Construction d'une chaîne
<a name="construction_une_chaine_ELTPARAGRAPHE000482"></a>Comme nous l'avons vu précédemment, la déclaration et l'affectation d'une chaîne sont très simples. Par exemple : 

```wl
MaChaîneConstruite est une chaîne
NomProduit est une chaîne = "WINDEV"
```


Cependant, bien souvent, une chaîne peut être composée d'éléments variables, comme le nom d'un produit par exemple. Dans ce cas, plusieurs méthodes de construction des chaînes sont disponibles. Nous allons tester ces différentes méthodes, et voir leurs avantages et leurs inconvénients. 



- Reprenons notre exemple "WLangage" : 

	1. Supprimez le code saisi dans l'événement "Initialisation" du projet (Ctrl + A pour tout sélectionner, et Suppr pour supprimer). 

	2. Dans l'événement "Initialisation" du projet "WLangage", saisissez le code suivant :  
			
		```wl
		MaChaîneConstruite est une chaîne
		NomProduit est une chaîne = "WINDEV"
		
		// Concaténation
		MaChaîneConstruite = "Mon outil de développement, c'est " + NomProduit + " !"
		Trace("Concaténation : " + MaChaîneConstruite)
		
		// ChaîneConstruit
		MaChaîneConstruite = ChaîneConstruit("Mon outil de développement, c'est %1 !", NomProduit)
		Trace("ChaîneConstruit : " + MaChaîneConstruite)
		
		// Saisie directe de la variable (syntaxe [% %])
		MaChaîneConstruite = "Mon outil de développement, c'est [%NomProduit%] !"
		Trace("Saisie directe : " + MaChaîneConstruite)
		```

			Dans ce code, la variable MaChaîneConstruite est construite en utilisant 3 méthodes différentes : 

		- **1ère méthode : concaténation simple grâce à l'opérateur '+'**. Les chaînes sont simplement mises les unes à la suite des autres. Cette méthode est la plus courante. Cependant, cette méthode atteint ses limites si le message doit être affiché à l'utilisateur dans une application multilingue. En effet, selon la syntaxe de la langue utilisée, il peut être nécessaire de déplacer la position de la variable, ce qui ne sera pas possible dans ce cas. 

		- **2ème méthode : Utilisation de la fonction [ChaîneConstruit](../WDLang1/3024006.md)**. Cette fonction permet d'écrire le message dans son ensemble, la partie variable étant identifiée par '%1', '%2', etc. La valeur de ces identifiants correspond aux paramètres suivants de la fonction. Cette méthode est bien plus simple pour une traduction, mais reste difficilement lisible (le contenu des %x étant des paramètres de la fonction). 

		- **3ème méthode : Utilisation directe de la variable**. Cette méthode regroupe tous les avantages : simplicité pour la traduction et compréhension directe du message. Il suffit de mettre directement le nom de la variable voulue dans la chaîne en l'encadrant par les balises [% et %].  


 Pour chaque type de construction, la fonction [Trace](../WDLang1/3013050.md) permet d'afficher le résultat dans le volet "Trace du débogueur". 







- Testons notre code : 

	1. Lancez le test du projet en cliquant sur ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=ICO_GO_Projet_WD_GAF.jpg)
 parmi les boutons d'accès rapide. 
			 Le projet se lance et l'éditeur de code réapparaît. 

	2. Affichez si nécessaire le volet "Trace du débogueur" pour voir le résultat : sous le volet "Accueil", dans le groupe "Environnement", déroulez "Volets" et sélectionnez "Volets" puis "Trace du débogueur". 

	3. Le volet "Trace du débogueur" contient nos exemples de concaténation :
			
		```txt
		Concaténation : Mon outil de développement, c'est WINDEV !
		ChaîneConstruit : Mon outil de développement, c'est WINDEV !
		Saisie directe : Mon outil de développement, c'est WINDEV !
		```






|   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=note.png)<br> | Note | La méthode utilisée pour la concaténation de chaîne influe sur la simplification de la compréhension et de la traduction, mais aussi sur les performances. Ainsi, la première méthode prend environ deux fois plus de temps qu'une des deux autres méthodes. |




<a name="NOTE7_5"></a>


### Opérations sur une chaîne
<a name="operations_sur_une_chaine_ELTPARAGRAPHE000588"></a>Le WLangage propose de nombreux opérateurs et fonctions permettant de gérer les extractions et les manipulations de chaînes. 



- Pour avoir un aperçu de la puissance et de la simplicité du WLangage pour la manipulation des chaînes, faisons un petit test rapide afin de parcourir une chaîne et d'en extraire les sous-chaînes. 

	1. Supprimez le code saisi dans l'événement "Initialisation" du projet (Ctrl + A pour tout sélectionner, et Suppr pour supprimer). 

	2. Saisissez le code suivant : 
			
		```wl
		ListeProduits est une chaîne = "
		WINDEV
		WEBDEV
		WINDEV Mobile
		"
		
		POUR TOUTE CHAÎNE UnProduit DE ListeProduits SÉPARÉE PAR RC
			Trace(UnProduit)
		FIN
		Produit est une chaîne = ListeProduits.ExtraitChaîne(2, RC)
		Trace(Produit)
		```


	3. Ce code permet à la fois de parcourir la chaîne multiligne et d'en extraire un élément. Examinons ce code : 

		- La chaîne ListeProduits est une chaîne multiligne. 

		- L'instruction "POUR TOUTE CHAINE" nous permet de parcourir les différents éléments de la chaîne. Ces éléments sont séparés par un RC (Retour Chariot).
						|   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=note.png)<br> | Note | En WLangage, il n'est pas nécessaire de se soucier des majuscules ou des minuscules lors de la saisie des instructions dans l'éditeur de code : la bonne casse sera automatiquement appliquée lors de la saisie. |





		- La fonction [&lt;Chaîne&gt;.ExtraitChaîne](../WDLang1/1000024876.md) appliquée à la chaîne permet dans notre cas d'extraire le second élément de la chaîne (c'est-à-dire "WEBDEV"). 




4. Lancez le test du projet en cliquant sur ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=ICO_GO_Projet_WD_GAF.jpg)
 parmi les boutons d'accès rapide. 

5. Le volet "Trace du débogueur" contient les éléments suivants :
			
		```txt
		WINDEV
		WEBDEV
		WINDEV Mobile
		WEBDEV
		```





- Il est également possible d'extraire des parties d'une chaîne en utilisant simplement les opérateurs [ et ]. Ces opérateurs permettent simplement d'extraire la partie du texte voulue, en spécifiant le numéro du caractère de départ et ou d'arrivée. 

	1. Supprimez le code saisi dans l'événement "Initialisation" du projet (Ctrl + A pour tout sélectionner, et Suppr pour supprimer). 

	2. Saisissez le code suivant :
			
		```wl
		Texte est une chaîne = "San Francisco"
		// Affiche le cinquième caractère dans la trace "F"
		Trace(Texte[5])
		// Affiche les caractères de 5 à 10 dans la trace "Franci"
		Trace(Texte[5 À 10])
		// Affiche les caractères à partir du cinquième dans la trace "Francisco"
		Trace(Texte[5 À])	
		// Affiche les 10 premiers caractères dans la trace "San Franci"
		Trace(Texte[À 10])
		// Affiche 3 caractères à partir du 10ème caractère dans la trace "isc"
		Trace(Texte[10 SUR 3])
		```


	3. Lancez le test du projet en cliquant sur ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=ICO_GO_Projet_WD_GAF.jpg)
 parmi les boutons d'accès rapide. 

	4. Le volet "Trace du débogueur" contient les informations suivantes : 
			
		```txt
		F
		Franci
		Francisco
		San Franci
		isc
		```








- Le WLangage permet également de formater la chaîne de caractères grâce à la fonction [&lt;Chaîne&gt;.Formate](../WDLang1/1000024877.md). Il est ainsi possible de demander à afficher une chaîne sans accent, en majuscules ou un minuscules, etc. Voici un petit exemple rapide : 

	1. Supprimez le code saisi dans l'événement "Initialisation" du projet (Ctrl + A pour tout sélectionner, et Suppr pour supprimer). 

	2. Saisissez le code suivant :
			
		```wl
		TexteInitial est une chaîne = "Mon outil de développement, c'est WINDEV !"
		
		// Formate la chaîne initiale sans accent et en minuscule
		TexteInitial = TexteInitial.Formate(ccSansAccent + ccMinuscule)
		Trace(TexteInitial)
		// Formate la chaîne obtenue sans espace et en majuscule
		TexteInitial = TexteInitial.Formate(ccMajuscule + ccSansEspaceIntérieur)
		Trace(TexteInitial)
		```


	3. Lancez le test du projet en cliquant sur ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=ICO_GO_Projet_WD_GAF.jpg)
 parmi les boutons d'accès rapide. 

	4. Le volet "Trace du débogueur" contient les informations suivantes : 
			
		```txt
		mon outil de developpement, c'est windev !
		MONOUTILDEDÉVELOPPEMENT,C'ESTWINDEV!
		```








Pour plus de détails sur la gestion des chaînes de caractères, consultez [Manipuler les chaînes de caractères](../WDLang1/3024020.md) et [Fonctions de gestion des chaînes de caractères](../WDLang1/3024015.md).
<a name="NOTE7_6"></a>


### Recherche d'une chaîne
<a name="recherche_une_chaine_ELTPARAGRAPHE000719"></a>Une manipulation courante sur les chaînes de caractères est la recherche. Qui n'a pas voulu savoir si un mot était présent dans une chaîne de caractères, quelle était sa position, etc. En WLangage, rien de plus simple. 



- Nous allons effectuer une recherche dans une chaîne de caractères :

	1. Supprimez le code saisi dans l'événement "Initialisation" du projet (Ctrl + A pour tout sélectionner, et Suppr pour supprimer).

	2. Saisissez le code WLangage suivant :
			
		```wl
		ChaîneARechercher est une chaîne = "WINDEV"
		
		TexteOriginal est une chaîne = "
		Outils de développement :
		- WINDEV
		- WEBDEV
		- WINDEV Mobile
		"
		
		Pos est un entier
		Pos = Position(TexteOriginal, ChaîneARechercher)
		SI Pos = 0 ALORS
			Trace("La chaîne [%ChaîneARechercher%] n'a pas été trouvée dans le texte")
		SINON
			Trace("La chaîne [%ChaîneARechercher%] a été trouvée à la position [%Pos%]")
		FIN
		```

			Examinons ce code WLangage :

		- Plusieurs variables sont déclarées : 

			- une variable de type Chaîne correspondant à la chaîne à rechercher. 

			- une variable de type Chaîne correspondant au texte dans lequel la recherche doit être effectuée. 

			- une variable de type Entier correspondant à la position de la chaîne recherchée.




- La fonction WLangage [Position](../WDLang1/3024027.md) permet de rechercher une chaîne dans une autre. Dans notre exemple, nous recherchons le contenu de la variable ChaîneARechercher dans le contenu de la variable TexteOriginal.

- Si la fonction [Position](../WDLang1/3024027.md) renvoie 0, cela signifie que la chaîne à rechercher n'a pas été trouvée. 

- Un message est affiché grâce à la fonction [Trace](../WDLang1/3013050.md) indiquant si la chaîne a été trouvée et sa position. 

3. Lancez le test du projet en cliquant sur ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=ICO_GO_Projet_WD_GAF.jpg)
 parmi les boutons d'accès rapide. 

4. Le volet "Trace du débogueur" contient les informations suivantes : 
			
		```txt
		La chaîne WINDEV a été trouvée à la position 30
		```




<a name="NOTE7_7"></a>


### Comparer deux chaînes
<a name="comparer_deux_chaines_ELTPARAGRAPHE000806"></a>Une autre opération courante sur les chaînes de caractères est la comparaison de chaînes : trouver si deux chaînes sont identiques. 

- Nous allons effectuer une comparaison de deux chaînes de caractères :

	1. Supprimez le code saisi dans l'événement "Initialisation" du projet (Ctrl + A pour tout sélectionner, et Suppr pour supprimer).

	2. Saisissez le code WLangage suivant :
			
		```wl
		// Chaînes à comparer
		Chaîne1 est une chaîne = "DEVELOPPEMENT"
		Chaîne2 est une chaîne = "DEV"
		
		// Les 2 chaînes sont comparées avec l'opérateur ~= (égalité souple)
		SI Chaîne1 ~= Chaîne2 ALORS
			Trace("Les 2 chaînes sont équivalentes")
		SINON
			Trace("Les 2 chaînes sont différentes")
		FIN
		```

			Examinons ce code WLangage :

		- Deux variables de type Chaîne sont déclarées. Elles contiennent les chaînes à comparer.

		- La comparaison est effectuée avec l'opérateur ~= (égalité souple). Cet opérateur permet de faire une comparaison sans tenir compte de la casse, des espaces avant et après la chaîne et sans tenir compte des accents.

		- Le résultat de la comparaison est affiché. 




3. Lancez le test du projet en cliquant sur ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=ICO_GO_Projet_WD_GAF.jpg)
 parmi les boutons d'accès rapide. 

4. Le volet "Trace du débogueur" contient les informations suivantes : 
			
		```txt
		Les 2 chaînes sont différentes
		```


5. Revenez dans le code et modifiez la ligne de code correspondant à la déclaration de Chaîne2 : 
			
		```wl
		Chaîne2 est une chaîne = "Développement"
		```


6. Lancez le test du projet en cliquant sur ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=ICO_GO_Projet_WD_GAF.jpg)
 parmi les boutons d'accès rapide. 

7. Le volet "Trace du débogueur" contient les informations suivantes : 
			
		```txt
		Les 2 chaînes sont équivalentes
		```
En effet, même si les deux chaînes n'ont pas la même casse, elles sont identiques. 




Remarque : Il existe différents [opérateurs de comparaison](../Motscles/1512006.md). 

| [Leçon précédente](../TutoWD/1410087578.md) | [Sommaire](../TutoWD/1410087560.md) | [Leçon suivante](../TutoWD/1410087587.md) |
| --- | --- | --- |




