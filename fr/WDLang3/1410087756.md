
## Gestion du protocole SOCKS5
			

<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Présentation
<a name="presentation_ELTTEXTE000149"></a>
SOCKS5 est un protocole réseau simple ayant pour but de standardiser l'utilisation d'un proxy pour les applications. Après une courte phase de transaction entre l'application et le serveur de proxy SOCKS5, l’échange de données sur le socket se fait de manière totalement transparente pour l'application.

<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Comment utiliser le protocole SOCKS5 avec WINDEV, WEBDEV et WINDEV Mobile ?
<a name="comment_utiliser_protocole_socks5_avec_windev_webdev_windev_mobile_ELTTEXTE000173"></a>
Plusieurs fonctions permettent de paramétrer l'utilisation d'un proxy via le protocole SOCKS 5 : 

- [La fonction Proxy](#NOTE2_2). 

- [La fonction FTPProxy utilisée avec la constante *ftpProxySOCKS5*](#NOTE2_3). 

- [La fonction SocketProxy](#NOTE2_4). 



<a name="NOTE2_2"></a>


### Fonction Proxy
<a name="fonction_proxy_ELTPARAGRAPHE000031"></a>Pour utiliser le protocole SOCKS5 avec la fonction [Proxy](../WDLang3/3043002.md), il suffit de préfixer le nom du serveur de proxy par "socks5://" et d'utiliser la fonction comme d'habitude (en passant le port et les identifiants).

ATTENTION : 

- Un proxy SOCKS5 ne peut pas être utilisé si le protocole HTTP est configuré pour utiliser Wininet (mode de gestion utilisant Internet Explorer). Ce mode de gestion est paramétré grâce à la constante *httpParamètreMode* de la fonction [HTTPParamètre](../WDLang3/1000018985.md).

- L’utilisation des fonctions [HTTPRequête](../WDLang3/3043007.md) et [HTTPEnvoieFormulaire](../WDLang3/1000017190.md) quand un proxy utilisant le protocole SOCKS5 est défini forcera l’utilisation de cURL. 
	Il est donc plutôt conseillé d’utiliser une variable de type [httpRequête](../WDLang3/1000021158.md) avec un proxy SOCKS5.




Exemple : 

```wl
// On ne doit pas utiliser wininet pour avoir accès au proxy SOCKS5
HTTPParamètre(httpParamètreMode, 1)
// Les fonctions suivantes utiliseront le proxy SOCKS5 
// à l'adresse 172.12.2.79:1080 avec les identifiants USER:PWD 
Proxy("socks5://172.12.2.79", 1080, "USER", "PWD")
req est un httpRequête
req.URL = "https://www.google.fr"
rep est un httpRéponse = req.Envoie()
SI ErreurDétectée ALORS
	Erreur(ErreurInfo(errComplet))
SINON
	Info("OK")
FIN
Proxy("")
```

<a name="NOTE2_3"></a>


### Fonction FTPProxy
<a name="fonction_ftpproxy_ELTPARAGRAPHE000065"></a>Pour utiliser le protocole SOCKS5 avec la fonction [FTPProxy](../WDLang3/1000019366.md), il est nécessaire d'utiliser la constante *ftpProxySOCKS5* pour préciser le type du proxy à utiliser.

ATTENTION : 

- Un proxy SOCKS5 ne peut pas être utilisé si le protocole FTP est configuré pour utiliser Wininet (mode de gestion utilisant Internet Explorer). Ce mode de gestion est paramétré grâce à la constante *httpParamètreMode* de la fonction [FTPParamètre](../WDLang3/1000020947.md). L'utilisation de la fonction [FTPProxy](../WDLang3/1000019366.md) provoquera alors une erreur fatale.

- Un proxy SOCKS5 peut être utilisé uniquement avec les protocoles FTP et SFTP. Si un proxy SOCKS5 est configuré, l'utilisation de la fonction [FTPConnecte](../WDLang3/3040016.md) sur un serveur via FTPES ou FTPIS provoquera une erreur fatale.




Exemple : 

```wl
// On ne doit pas utiliser wininet pour avoir accès au proxy SOCKS5
FTPParamètre(ftpParamètreMode, 1)
// Les fonctions FTP utiliseront le proxy SOCKS5 
// à l'adresse 172.12.2.79:1080 avec les identifiants USER:PWD 
FTPProxy(ftpProxySOCKS5, "172.12.2.79",1080, "USER", "PWD")
// On se connecte au serveur FTP de test de rebex
nIDFTP est entier = FTPConnecte("ftp://test.rebex.net/", "demo", "password", 21)
SI nIDFTP = -1 ALORS
	Erreur(ErreurInfo(errComplet))
SINON
	Info("OK")
FIN
FTPDéconnecte(nIDFTP)
// On enlève le proxy
FTPProxy(ftpProxyAucun)
```

<a name="NOTE2_4"></a>


### Fonction SocketProxy
<a name="fonction_socketproxy_ELTPARAGRAPHE000094"></a>La fonction [SocketProxy](../WDLang3/1410088761.md) permet de spécifier si les sockets TCP et les WebSocket doivent passer ou non par un proxy SOCKS5 pour exécuter leurs requêtes. 


```wl
// Infos du proxy SOCKS5
SocketProxy("socks5://172.17.2.79",1080,"USER","PWD")

// Connexion au serveur en passant par le proxy
SI PAS SocketConnecte("tcp_via_socks5", 4242, "172.19.5.80") ALORS
	Erreur(ErreurInfo(errComplet))
SINON
	// Opérations d'écriture et de lecture (comme d'habitude)
	Info("OK")
	SocketEcrit("tcp_via_socks5", "écho")
	s est un Buffer = SocketLit("tcp_via_socks5")
	Trace(BufferVersHexa(s))
	SocketFerme("tcp_via_socks5")
FIN

// Suppression du proxy pour les sockets
SocketProxy("")
```





