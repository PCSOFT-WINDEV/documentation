
## AuthIdentifie (Fonction)

***En anglais : AuthIdentify***



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Effectue une authentification utilisant : 

- soit le protocole OAuth 2.0 sur un service Web quelconque.

- ![Windows](https://doc.pcsoft.fr/ext/images/fr/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/fr/LX.png) soit le protocole OpenID sur un service Web quelconque.







<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
// Exemple permettant de récupérer un token pour effectuer une requête sur DropBox
OAuth2Params est OAuth2Paramètres
OAuth2Params.ClientID = "01234567890123456789" 
OAuth2Params.ClientSecret = "98765432109876543210"
OAuth2Params.URLAuth = "https://www.dropbox.com/oauth2/authorize"
OAuth2Params.URLToken = "https://api.dropboxapi.com/oauth2/token"
OAuth2Params.ParamètresSupplémentaires = "force_reapprove=false"
<COMPILE SI TypeConfiguration<>Site>
	//Si ce n'est pas dans un site WEB il faut une URL de redirection en localhost
	OAuth2Params.URLRedirection = "http://localhost:9874/"	
<FIN>

// Demande d'authentification : ouvre la fenêtre de login
MonToken est un AuthToken = AuthIdentifie(OAuth2Params)

// Requête authentifiée sur une API de DropBox
req est un httpRequête
req.Méthode = httpPost
req.URL = "https://api.dropboxapi.com/2/files/list_folder"
req.AuthToken = MonToken // Token d'authentification
req.ContentType = "application/json"
vParamAPI est un variant
vParamAPI.path = "/Homework/math"
vParamAPI.recursive = Faux
vParamAPI.include_media_info = Faux
vParamAPI.include_deleted = Faux
vParamAPI.include_has_explicit_shared_members = Faux
req.Contenu = VariantVersJSON(vParamAPI)

réponseHTTP est un httpRéponse = HTTPEnvoie(req)
soit Données = JSONVersVariant(réponseHTTP.Contenu)
// Utilisation des données reçues ...
```

<a name="XSYNTAXE"></a>

## Syntaxe
<a name="SYNTAXE1"></a>
<a name="SYNTAXE2"></a>
![Windows](https://doc.pcsoft.fr/ext/images/fr/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/fr/LX.png) 
### Réaliser une authentification via le protocole OpenID

`<Résultat> = AuthIdentifie(<Paramètres d'authentification>)`
---

**`<Résultat> : (Variable de type AuthToken)`**

Variable de type [AuthToken](../WDLang3/1000022220.md) correspondant au token (ou jeton) contenant les informations d'accès pour les prochaines requêtes authentifiées.  

**`<Paramètres d'authentification> : (Variable de type OpenIDParamètres)`**

Nom de la variable de type [OpenIDParamètres](../WDLang3/1000023561.md) contenant tous les paramètres décrivant les éléments nécessaires à la récupération du token d'accès.


<a name="SYNTAXE3"></a>

### Réaliser une authentification via le protocole OAuth 2.0 (syntaxe asynchrone)  

`AuthIdentifie(<Paramètres d'authentification> , <Procédure WLangage>)`
---

**`<Paramètres d'authentification> : (Variable de type OAuth2Paramètres)`**

Nom de la variable de type [OAuth2Paramètres](../WDLang3/1000022212.md) contenant tous les paramètres décrivant les éléments nécessaires à la récupération du token d'accès.

**`<Procédure WLangage> : (Nom de procédure)`**

Nom de la procédure WLangage (également nommée "callback") appelée lors de l'authentification. Pour plus de détails sur cette procédure, consultez [Paramètres de la procédure utilisée par la fonction AuthIdentifie](../WDLang3/1000025107.md). 


<a name="SYNTAXE4"></a>
![Windows](https://doc.pcsoft.fr/ext/images/fr/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/fr/LX.png) 
### Réaliser une authentification via le protocole OpenID (syntaxe asynchrone) 

`AuthIdentifie(<Paramètres d'authentification> , <Procédure WLangage>)`
---

**`<Paramètres d'authentification> : (Variable de type OpenIDParamètres)`**

Nom de la variable de type [OpenIDParamètres](../WDLang3/1000023561.md) contenant tous les paramètres décrivant les éléments nécessaires à la récupération du token d'accès.

**`<Procédure WLangage> : (Nom de procédure)`**

Nom de la procédure WLangage (également nommée "callback") appelée lors de l'authentification. Pour plus de détails sur cette procédure, consultez [Paramètres de la procédure utilisée par la fonction AuthIdentifie](../WDLang3/1000025107.md).



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques


### Fonctionnement de l'authentification OAuth 2.0 
<a name="fonctionnement_authentification_oauth_20_ELTPARAGRAPHE000121"></a>Les étapes de l'authentification OAuth 2.0 réalisées par la fonction [AuthIdentifie](../WDLang3/1000022219.md) sont les suivantes : 

- Exécution d'une première requête HTTP pour demander une autorisation (URL d'autorisation spécifiée dans la variable de type [OAuth2Paramètres](../WDLang3/1000022212.md)).

- Ouverture d'une fenêtre d'identification de l'utilisateur conformément au protocole OAuth 2.0. L'interface d'identification est donnée par le service accédé.

- Après identification, le serveur retourne un premier code d'autorisation permettant de demander un token d'accès aux ressources. Ce code est ajouté en paramètre de la deuxième URL (URL de token d'accès spécifiée dans la variable de type [OAuth2Paramètres](../WDLang3/1000022212.md)).

- Exécution de la seconde requête HTTP pour demander le token d'accès. Le résultat est un buffer JSON qui contient, entre autres, le token d'accès ("**access_token**") à utiliser pour les requêtes authentifiées. La variable de type [AuthToken](../WDLang3/1000022220.md) contient les informations contenues dans ce buffer JSON. Ce token d'accès sera utilisé par les appels aux APIs du service Web.




**Pour utiliser des API du service Web**, il suffit d'utiliser la fonction [HTTPEnvoie](../WDLang3/1000021183.md) avec une variable de type [httpRequête](../WDLang3/1000021158.md) définissant la requête à exécuter. 
La variable [AuthToken](../WDLang3/1000022220.md) devra être affectée à la propriété **AuthToken** de la variable [httpRequête](../WDLang3/1000021158.md) (voir exemple). 
Dans ce cas, le serveur recevra alors l'entête HTTP "**Authorization**" avec une valeur de la forme : "Authorization : Bearer xxx_access_token_xxx". 

**Attention** : 

- Si le serveur ne renvoie pas le token d'accès sous forme de code JSON conformément à la norme OAuth2.0, une erreur sera générée et le token ne sera pas récupéré. Il est possible de récupérer la réponse du serveur via la propriété **RéponseServeur** de la variable de type [AuthToken](../WDLang3/1000022220.md).

- Si le serveur ne gère pas l'entête HTTP "**Authorization**" pour la transmission du token d'accès, cette transmission doit être faite par le développeur selon le format attendu par le service demandé. 
	L'exemple suivant permet d'utiliser le service Web de Facebook. Dans ce cas, le token d'accès doit être précisé sur l'URL de la requête.

	- ![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png) Exemple de code pour Facebook
			
		```wl
		// Exemple permettant de récupérer le nom du compte Facebook
		MonToken est un AuthToken
		MonTokenParam est un OAuth2Paramètres
		
		MonTokenParam.ClientID = "123456789012345"
		MonTokenParam.ClientSecret = "45g8jh5kll45579021qsg5444j"
		MonTokenParam.URLAuth = "https://www.facebook.com/dialog/oauth"
		MonTokenParam.URLToken = "https://graph.facebook.com/v2.3/oauth/access_token"
		MonTokenParam.URLRedirection = "http://localhost:9874/"
		MonTokenParam.Scope = "email"
		
		MonToken = AuthIdentifie(MonTokenParam)
		SI MonToken <> Null ALORS
			SI ErreurDétectée ALORS
				Erreur(ErreurInfo())
			SINON
				// Token précisé sur l'URL de la requête
				HTTPRequête("https://graph.facebook.com/me?access_token=" + MonToken.Valeur)
				vMonRes est un Variant = JSONVersVariant(HTTPDonneRésultat(httpRésultat))
				// Récupération du nom du compte
				Trace(vMonRes.name)
			FIN
		FIN
		```













### Fonctionnement de l'authentification OpenID 
<a name="fonctionnement_authentification_openid_ELTPARAGRAPHE000186"></a>Les étapes de l'authentification OpenID réalisées par la fonction [AuthIdentifie](../WDLang3/1000022219.md) sont les suivantes : 

- Exécution d'une première requête HTTP pour demander une autorisation (URL d'autorisation spécifiée dans la variable de type [OpenIDParamètres](../WDLang3/1000023561.md)).

- Ouverture d'une fenêtre d'identification de l'utilisateur conformément au protocole OpenID. L'interface d'identification est donnée par le service accédé.

- Après identification, exécution d'un seconde requête HTTP pour demander le token d'accès. Le résultat est un buffer JSON qui contient, entre autres, le token d'accès ("**access_token**") à utiliser pour les requêtes authentifiées. La variable de type [AuthToken](../WDLang3/1000022220.md) contient les informations contenues dans ce buffer JSON. Ce token d'accès sera utilisé par les appels aux APIs du service Web. 




Pour identifier l'utilisateur, il suffit d'utiliser la fonction [OpenIDLitIdentité](../WDLang3/1000023573.md) en lui spécifiant la variable de type [AuthToken](../WDLang3/1000022220.md). 

**Attention** : Si le serveur ne renvoie pas le token d'accès sous forme de code JSON conformément à la norme OAuth2.0, une erreur sera générée et le token ne sera pas récupéré. Il est possible de récupérer la réponse du serveur via la propriété **RéponseServeur** de la variable de type [AuthToken](../WDLang3/1000022220.md).


<a name="NOTE0_5"></a>


- ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=WDOAuth.gif) ***Exemples didactiques (WINDEV)*** : **WD OAuth** <br>OAuth permet d'agir en tant qu'utilisateur d'une plateforme tierce sans connaître les identifiants (nom d'utilisateur et mot de passe) de cet utilisateur.<br>Beaucoup de plateformes tierces (Google, Twitter, Facebook pour ne citer qu'eux) proposent des API pour lesquelles il est possible de se connecter avec les informations d'un de leurs utilisateurs. Cette identification est faite au travers de OAuth. Ainsi, toutes les requêtes effectuées sur leurs services (API, ...) devront être effectuées avec un "jeton d'accès" (appelé Access Token) identifiant à la fois l'application (le "client") et l'utilisateur.<br>L'exemple propose didactiquement de se connecter à Google et Microsoft grâce à la fonction AuthIdentifie et au type OAuth2Paramètre.



<a name="XComposante"></a>

## Classification Métier / UI :
Code métier
## Composante :
wd280ggl.dll
