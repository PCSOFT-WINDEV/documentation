
## OAuth2Paramètres (Type de variable)

***En anglais : OAuth2Parameters***
				



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Le type **OAuth2Paramètres** permet de définir toutes les informations nécessaires pour s'authentifier sur un service Web implémentant le standard OAuth 2.0. Ces caractéristiques peuvent être définies et modifiées à l'aide de différentes propriétés WLangage. 

Ce type de variable doit être passé en paramètre à la fonction [AuthIdentifie](../WDLang3/1000022219.md). En cas de succès, cette fonction renvoie une variable de type [AuthToken](../WDLang3/1000022220.md) qui pourra être utilisée pour effectuer des requêtes authentifiées sur le service Web.

**Remarque** : Pour plus de détails sur la déclaration de ce type de variable et l'utilisation des propriétés WLangage, consultez [Déclaration d'une variable](../Motscles/1514032.md). 


<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
// Exemple permettant de récupérer un token pour effectuer une requête sur DropBox
OAuth2Params est OAuth2Paramètres
OAuth2Params.ClientID = "01234567890123456789" 
OAuth2Params.ClientSecret = "98765432109876543210"
OAuth2Params.URLAuth = "https://www.dropbox.com/oauth2/authorize"
OAuth2Params.URLToken = "https://api.dropboxapi.com/oauth2/token"
OAuth2Params.ParamètresSupplémentaires = "force_reapprove=false"
<COMPILE SI TypeConfiguration<>Site>
	//Si ce n'est pas dans un site WEB il faut une URL de redirection en localhost
	OAuth2Params.URLRedirection = "http://localhost:9874/"	
<FIN>

// Demande d'authentification : ouvre la fenêtre de login
MonToken est un AuthToken = AuthIdentifie(OAuth2Params)

// Requête authentifiée sur une API de DropBox
req est un httpRequête
req.Méthode = httpPost
req.URL = "https://api.dropboxapi.com/2/files/list_folder"
req.AuthToken = MonToken // Token d'authentification
req.ContentType = "application/json"
vParamAPI est un variant
vParamAPI.path = "/Homework/math"
vParamAPI.recursive = Faux
vParamAPI.include_media_info = Faux
vParamAPI.include_deleted = Faux
vParamAPI.include_has_explicit_shared_members = Faux
req.Contenu = VariantVersJSON(vParamAPI)

réponseHTTP est un httpRéponse = HTTPEnvoie(req)
soit Données = JSONVersVariant(réponseHTTP.Contenu)
// Utilisation des données reçues ...
```





<a name="NOTE0"></a>

## Remarques
<a name="NOTE0_1"></a>


### Propriétés spécifiques à la description des variables de type OAuth2Paramètres
<a name="proprietes_specifiques_description_des_variables_type_oauth2parametres_ELTPARAGRAPHE000045"></a>Les propriétés suivantes peuvent être utilisées pour manipuler les informations nécessaires à la demande d'authentification :

| Nom de la propriété | Type manipulé | Effet |
| --- | --- | --- |
| ClientID | Chaîne de caractères | Identifiant du client fourni par le service lors de l'enregistrement de l'application. |
| ClientSecret | Chaîne de caractères | Code d'accès secret de l'application. Ce code est donné par le service lors de l'enregistrement de l'application. |
| ParamètresSupplémentaires | Chaîne de caractères | Paramètres de la première requête d'authentification. Cette chaîne doit être formatée comme des paramètres d'URL. |
| Scope | Chaîne de caractères | Permissions demandées. Les valeurs possibles sont spécifiques au service Web utilisé. |
| TypeAutorisation | Constante | Type d'autorisation disponible. Les valeurs possibles sont : <br><br>- *taApplicationCliente* : Authentification sans fenêtre de login. L'autorisation de connexion est donnée à l'application (et non à l'utilisateur). Le token fourni est un token lié à l'application, permettant d'accéder aux ressources de l'application. <br><br>- *taCodeAutorisation* (Valeur par défaut) : L'autorisation de connexion est donnée à l'utilisateur. une fenêtre de login est affichée, permettant à l'utilisateur de saisir son login et mot de passe. Le token fourni est un token lié à l'utilisateur. <br><br><br> |
| TypeRéponse | Chaîne de caractères ou constante | Type de réponse attendu. Les valeurs possibles sont : <br><br>- *oauth2TypeRéponseCode* (ou "Code") : La réponse est de type "Code". <br><br>- *oauth2TypeRéponseToken* (ou "Token") : La réponse est de type "Token". <br><br><br><br><br>La valeur par défaut est *oauth2TypeRéponseToken*. <br><br>Remarque : Dans le cas d'une authentification de "personne", le type de réponse utilisé doit être "Token". Dans le cas d'une authentification pour une API ou un service (par exemple, serveur de mails Google), le type de réponse utilisé doit être "Code". <br><br> |
| URLAuth | Chaîne de caractères | URL d'autorisation à utiliser (première URL du standard OAuth 2.0). |
| URLRedirection | Chaîne de caractères | URL de redirection à utiliser pendant le mécanisme d'authentification. <br><br>![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png) Pour une application Windows ou Android, cette URL doit être de la forme "http://localhost:NumeroPort". Cette valeur doit être strictement identique à celle renseignée lors de la déclaration de l'application dans le service Web concerné.<br><br><br><br> |
| URLToken | Chaîne de caractères | URL de récupération du token d'accès à utiliser (seconde URL du standard OAuth 2.0). |




<a name="NOTE0_2"></a>


### Fonctionnement de l'authentification OAuth 2.0 
<a name="fonctionnement_authentification_oauth_20_ELTPARAGRAPHE000277"></a>Les étapes de l'authentification OAuth 2.0 réalisées par la fonction [AuthIdentifie](../WDLang3/1000022219.md) sont les suivantes : 

- Exécution d'une première requête HTTP pour demander une autorisation (URL d'autorisation spécifiée dans la variable de type [OAuth2Paramètres](../WDLang3/1000022212.md)).

- Ouverture d'une fenêtre d'identification de l'utilisateur conformément au protocole OAuth 2.0. L'interface d'identification est donnée par le service accédé.

- Après identification, le serveur retourne un premier code d'autorisation permettant de demander un token d'accès aux ressources. Ce code est ajouté en paramètre de la deuxième URL (URL de token d'accès spécifiée dans la variable de type [OAuth2Paramètres](../WDLang3/1000022212.md)).

- Exécution de la seconde requête HTTP pour demander le token d'accès. Le résultat est un buffer JSON qui contient, entre autres, le token d'accès ("**access_token**") à utiliser pour les requêtes authentifiées. La variable de type [AuthToken](../WDLang3/1000022220.md) contient les informations contenues dans ce buffer JSON. Ce token d'accès sera utilisé par les appels aux APIs du service Web.




**Pour utiliser des API du service Web**, il suffit d'utiliser la fonction [HTTPEnvoie](../WDLang3/1000021183.md) avec une variable de type [httpRequête](../WDLang3/1000021158.md) définissant la requête à exécuter. 
La variable [AuthToken](../WDLang3/1000022220.md) devra être affectée à la propriété **AuthToken** de la variable [httpRequête](../WDLang3/1000021158.md) (voir exemple). 
Dans ce cas, le serveur recevra alors l'entête HTTP "**Authorization**" avec une valeur de la forme : "Authorization : Bearer xxx_access_token_xxx". 

**Attention** : 

- Si le serveur ne renvoie pas le token d'accès sous forme de code JSON conformément à la norme OAuth2.0, une erreur sera générée et le token ne sera pas récupéré. Il est possible de récupérer la réponse du serveur via la propriété **RéponseServeur** de la variable de type [AuthToken](../WDLang3/1000022220.md).

- Si le serveur ne gère pas l'entête HTTP "**Authorization**" pour la transmission du token d'accès, cette transmission doit être faite par le développeur selon le format attendu par le service demandé. 
	L'exemple suivant permet d'utiliser le service Web de Facebook. Dans ce cas, le token d'accès doit être précisé sur l'URL de la requête.

	- ![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png) Exemple de code pour Facebook
			
		```wl
		// Exemple permettant de récupérer le nom du compte Facebook
		MonToken est un AuthToken
		MonTokenParam est un OAuth2Paramètres
		
		MonTokenParam.ClientID = "123456789012345"
		MonTokenParam.ClientSecret = "45g8jh5kll45579021qsg5444j"
		MonTokenParam.URLAuth = "https://www.facebook.com/dialog/oauth"
		MonTokenParam.URLToken = "https://graph.facebook.com/v2.3/oauth/access_token"
		MonTokenParam.URLRedirection = "http://localhost:9874/"
		MonTokenParam.Scope = "email"
		
		MonToken = AuthIdentifie(MonTokenParam)
		SI MonToken <> Null ALORS
			SI ErreurDétectée ALORS
				Erreur(ErreurInfo())
			SINON
				// Token précisé sur l'URL de la requête
				HTTPRequête("https://graph.facebook.com/me?access_token=" + MonToken.Valeur)
				vMonRes est un Variant = JSONVersVariant(HTTPDonneRésultat(httpRésultat))
				// Récupération du nom du compte
				Trace(vMonRes.name)
			FIN
		FIN
		```









- ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=WDOAuth.gif) ***Exemples didactiques (WINDEV)*** : **WD OAuth** <br>OAuth permet d'agir en tant qu'utilisateur d'une plateforme tierce sans connaître les identifiants (nom d'utilisateur et mot de passe) de cet utilisateur.<br>Beaucoup de plateformes tierces (Google, Twitter, Facebook pour ne citer qu'eux) proposent des API pour lesquelles il est possible de se connecter avec les informations d'un de leurs utilisateurs. Cette identification est faite au travers de OAuth. Ainsi, toutes les requêtes effectuées sur leurs services (API, ...) devront être effectuées avec un "jeton d'accès" (appelé Access Token) identifiant à la fois l'application (le "client") et l'utilisateur.<br>L'exemple propose didactiquement de se connecter à Google et Microsoft grâce à la fonction AuthIdentifie et au type OAuth2Paramètre.


