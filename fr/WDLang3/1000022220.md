
## AuthToken (Type de variable)

***En anglais : AuthToken***
				



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Le type **AuthToken** contient les caractéristiques d'un token d'accès à un service Web. Ce token d'accès a été précédemment demandé : 

- soit par la fonction [AuthIdentifie](../WDLang3/1000022219.md).  

- soit par une requête HTTP. Dans ce cas, la requête renvoie le token au format JSON. 


Les caractéristiques de ce token d'accès peuvent être définies et modifiées à l'aide de différentes propriétés WLangage. 

**Remarque** : Pour plus de détails sur la déclaration de ce type de variable et l'utilisation des propriétés WLangage, consultez [Déclaration d'une variable](../Motscles/1514032.md).


<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
// Exemple permettant de récupérer un token pour effectuer une requête sur DropBox
OAuth2Params est OAuth2Paramètres
OAuth2Params.ClientID = "01234567890123456789" 
OAuth2Params.ClientSecret = "98765432109876543210"
OAuth2Params.URLAuth = "https://www.dropbox.com/oauth2/authorize"
OAuth2Params.URLToken = "https://api.dropboxapi.com/oauth2/token"
OAuth2Params.ParamètresSupplémentaires = "force_reapprove=false"
<COMPILE SI TypeConfiguration<>Site>
	//Si ce n'est pas dans un site WEB il faut une URL de redirection en localhost
	OAuth2Params.URLRedirection = "http://localhost:9874/"	
<FIN>

// Demande d'authentification : ouvre la fenêtre de login
MonToken est un AuthToken = AuthIdentifie(OAuth2Params)

// Requête authentifiée sur une API de DropBox
req est un httpRequête
req.Méthode = httpPost
req.URL = "https://api.dropboxapi.com/2/files/list_folder"
req.AuthToken = MonToken // Token d'authentification
req.ContentType = "application/json"
vParamAPI est un variant
vParamAPI.path = "/Homework/math"
vParamAPI.recursive = Faux
vParamAPI.include_media_info = Faux
vParamAPI.include_deleted = Faux
vParamAPI.include_has_explicit_shared_members = Faux
req.Contenu = VariantVersJSON(vParamAPI)

réponseHTTP est un httpRéponse = HTTPEnvoie(req)
soit Données = JSONVersVariant(réponseHTTP.Contenu)
// Utilisation des données reçues ...
```
<a name="Exemple2"></a>



```wl
// Récupère le token (dans du JSON) via une requête HTTP. La fonction AuthIdentifie n'est pas utilisée
// Définition de la requête
httpReq est une httpRequête
httpReq.Méthode = httpPost
httpReq.URL = PAYPAL_TOKEN
httpReq.Utilisateur = PAYPAL_APP_ID
httpReq.MotDePasse = PAYPAL_SECRET
httpReq.Contenu = "grant_type=client_credentials"
httpReq.ContentType = "application/x-www-form-urlencoded"

// Exécution de la requête
httpRep est une httpRéponse = HTTPEnvoie(httpReq)

// Récupération du token
SI httpRep.CodeEtat = 200 ALORS
	// Déclare les paramètres, nécessaire pour le rafraîchissement du token
	oAuth2Param est un OAuth2Paramètres
	oAuth2Param.ClientID = PAYPAL_APP_ID
	oAuth2Param.ClientSecret = PAYPAL_SECRET
	oAuth2Param.URLAuth = PAYPAL_ACCESS_BASEURL
	oAuth2Param.Scope = PAYPAL_SCOPES
	oAuth2Param.URLToken = PAYPAL_TOKEN
	
	// Initialise le token avec le JSON
	MonToken est un AuthToken(oAuth2Param, httpRep.Contenu)
	gMonToken <= MonToken
FIN
```




## Syntaxe
<a name="SYNTAXE1"></a>

### Déclarer une variable de type AuthToken

`MaVariable est un AuthToken`
---

Dans ce cas, la fonction [AuthIdentifie](../WDLang3/1000022219.md) permet de récupérer les paramètres du token. 






<a name="SYNTAXE2"></a>

### Déclarer et décrire une variable de type AuthToken (sans passer par la fonction AuthIdentifie)

`MaVariable est un AuthToken(<Paramètre OAuth2> , <Token>)`
---

**`<Paramètre OAuth2> : (Variable de type OAuthParamètres)`**

Nom de la variable de type [OAuth2Paramètres](../WDLang3/1000022212.md) contenant toutes les informations nécessaires pour s'authentifier sur un service implémentant le standard OAuth 2.0.

**`<Token> : (Chaîne de caractères)`**

Chaîne au format JSON ou UTF8 contenant le token. Correspond au token renvoyé par le service. 







<a name="NOTE0"></a>

## Remarques
<a name="NOTE0_1"></a>


### Propriétés spécifiques à la description des variables de type AuthToken
<a name="proprietes_specifiques_description_des_variables_type_authtoken_ELTPARAGRAPHE000073"></a>Les propriétés suivantes peuvent être utilisées pour manipuler un token d'accès à un Webservice :

| Nom de la propriété | Type manipulé | Effet |
| --- | --- | --- |
| Actualisation | Chaîne de caractères | Valeur renvoyée par le serveur permettant de savoir si le token peut être renouvelé. <br><br>Si cette propriété n'est pas renseignée, il ne sera pas possible d'utiliser la fonction [AuthRenouvelleToken](../WDLang3/1000026168.md) pour rafraichir le token : il faudra alors redemander un nouveau token. |
| DateExpiration | DateHeure | Date et heure d'expiration du token. <br><br> |
| RéponseServeur | Buffer | Valeur renvoyée par le serveur lors de la demande du token d'accès. <br><br>**Cette propriété est disponible en lecture seulement**. |
| Valeur | Chaîne de caractères | Token d'accès. <br><br>Valeur automatiquement remplie lors de l'utilisation de la fonction [AuthIdentifie](../WDLang3/1000022219.md). <br><br><br><br>Cette valeur peut être utilisée pour envoyer des requêtes authentifiées sur le service Web concerné. |
| Valide | Booléen | Validité du token d'accès : <br><br>- <u><u><u><u>Vrai</u></u></u></u> si le token d'accès est valide. <br><br>- <u><u><u><u>Faux</u></u></u></u> dans le cas contraire.<br><br><br>**Cette propriété est disponible en lecture seulement**. |


<a name="NOTE0_2"></a>


### Fonctionnement de l'authentification OAuth 2.0 
<a name="fonctionnement_authentification_oauth_20_ELTPARAGRAPHE000293"></a>Les étapes de l'authentification OAuth 2.0 réalisées par la fonction [AuthIdentifie](../WDLang3/1000022219.md) sont les suivantes : 

- Exécution d'une première requête HTTP pour demander une autorisation (URL d'autorisation spécifiée dans la variable de type [OAuth2Paramètres](../WDLang3/1000022212.md)).

- Ouverture d'une fenêtre d'identification de l'utilisateur conformément au protocole OAuth 2.0. L'interface d'identification est donnée par le service accédé.

- Après identification, le serveur retourne un premier code d'autorisation permettant de demander un token d'accès aux ressources. Ce code est ajouté en paramètre de la deuxième URL (URL de token d'accès spécifiée dans la variable de type [OAuth2Paramètres](../WDLang3/1000022212.md)).

- Exécution de la seconde requête HTTP pour demander le token d'accès. Le résultat est un buffer JSON qui contient, entre autres, le token d'accès ("**access_token**") à utiliser pour les requêtes authentifiées. La variable de type [AuthToken](../WDLang3/1000022220.md) contient les informations contenues dans ce buffer JSON. Ce token d'accès sera utilisé par les appels aux APIs du service Web.




**Pour utiliser des API du service Web**, il suffit d'utiliser la fonction [HTTPEnvoie](../WDLang3/1000021183.md) avec une variable de type [httpRequête](../WDLang3/1000021158.md) définissant la requête à exécuter. 
La variable [AuthToken](../WDLang3/1000022220.md) devra être affectée à la propriété **AuthToken** de la variable [httpRequête](../WDLang3/1000021158.md) (voir exemple). 
Dans ce cas, le serveur recevra alors l'entête HTTP "**Authorization**" avec une valeur de la forme : "Authorization : Bearer xxx_access_token_xxx". 

**Attention** : 

- Si le serveur ne renvoie pas le token d'accès sous forme de code JSON conformément à la norme OAuth2.0, une erreur sera générée et le token ne sera pas récupéré. Il est possible de récupérer la réponse du serveur via la propriété **RéponseServeur** de la variable de type [AuthToken](../WDLang3/1000022220.md).

- Si le serveur ne gère pas l'entête HTTP "**Authorization**" pour la transmission du token d'accès, cette transmission doit être faite par le développeur selon le format attendu par le service demandé. 
	L'exemple suivant permet d'utiliser le service Web de Facebook. Dans ce cas, le token d'accès doit être précisé sur l'URL de la requête.

	- ![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png) Exemple de code pour Facebook
			
		```wl
		// Exemple permettant de récupérer le nom du compte Facebook
		MonToken est un AuthToken
		MonTokenParam est un OAuth2Paramètres
		
		MonTokenParam.ClientID = "123456789012345"
		MonTokenParam.ClientSecret = "45g8jh5kll45579021qsg5444j"
		MonTokenParam.URLAuth = "https://www.facebook.com/dialog/oauth"
		MonTokenParam.URLToken = "https://graph.facebook.com/v2.3/oauth/access_token"
		MonTokenParam.URLRedirection = "http://localhost:9874/"
		MonTokenParam.Scope = "email"
		
		MonToken = AuthIdentifie(MonTokenParam)
		SI MonToken <> Null ALORS
			SI ErreurDétectée ALORS
				Erreur(ErreurInfo())
			SINON
				// Token précisé sur l'URL de la requête
				HTTPRequête("https://graph.facebook.com/me?access_token=" + MonToken.Valeur)
				vMonRes est un Variant = JSONVersVariant(HTTPDonneRésultat(httpRésultat))
				// Récupération du nom du compte
				Trace(vMonRes.name)
			FIN
		FIN
		```







<a name="NOTE0_3"></a>


### Utilisation des variables de type AuthToken
<a name="utilisation_des_variables_type_authtoken_ELTPARAGRAPHE000156"></a>Les variables de type **AuthToken** peuvent être utilisées dans les fonctions : 

- syntaxe classique : 
	


|   |   |
| --- | --- |
| [AuthIdentifie](../WDLang3/1000022219.md) | Effectue une authentification utilisant le protocole OAuth 2.0 sur un webservice quelconque. |
| [OpenIDLitIdentité](../WDLang3/1000023573.md) | Récupère l'identité d'un utilisateur contenue dans une variable de type [AuthToken](../WDLang3/1000022220.md). |

- syntaxe préfixée : 
	


|   |   |
| --- | --- |
| [&lt;Variable AuthToken&gt;.OpenIDLitIdentité](../WDLang3/1000023629.md) | Récupère l'identité d'un utilisateur contenue dans une variable de type [AuthToken](../WDLang3/1000022220.md). |







