


## Gestion de la géolocalisation et du GPS
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Présentation
<a name="presentation_ELTTEXTE000555"></a>
WINDEV Mobile et WEBDEV permettent d'exploiter les fonctionnalités de géolocalisation et GPS des appareils téléphoniques et des navigateurs. 
![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/fr/UNIVERSALAPP.png) **Dans les applications téléphoniques**, il est possible de : 

- Récupérer des informations sur la dernière position connue de l'appareil. 

- Demander à être notifié lorsque l'appareil approche d'une zone spécifique. 

- Connaître la position actuelle de l'appareil. 

- Récupérer une adresse. 

- ...












<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Mettre en place la gestion du GPS
<a name="mettre_place_gestion_gps_ELTTEXTE000579"></a>


### Vérifier si le GPS est activé
<a name="verifier_gps_est_active_ELTPARAGRAPHE000086"></a>Pour vérifier si le GPS est activé, il suffit d'utiliser la fonction [GPSEtat](../WDLang3/1000019207.md). 

Exemple : 

```wl
// Est-ce-que le GPS est actif ?
SI GPSEtat() <> gpsActivé ALORS
	Erreur("Le GPS n'est pas en état de fonctionner.", ...
		"Veuillez l'activer pour avoir accès à cette application.")
	FinProgramme()
FIN
```



### Etre averti du changement d'état du GPS
<a name="etre_averti_changement_etat_gps_ELTPARAGRAPHE000097"></a>Pour être averti du changement d'état du GPS, il suffit d'utiliser la fonction [GPSEtat](../WDLang3/1000019207.md) en spécifiant le nom de la procédure qui va gérer l'état actuel du GPS.  

Exemple : 

```wl
GPSEtat(_ChangementEtatGPS)
```


Le code de la procédure est le suivant : 

```wl
PROCEDURE _ChangementEtatGPS(Etat)

SI EtatActuel = Etat ALORS RETOUR

EtatActuel = Etat

FinTimerSys(TIMER_RECUPERATION)

SELON Etat
	CAS gpsActivé 
		// Le fournisseur a été activé par l'utilisateur.
		LIB_Etat = "GPS activé"
	CAS gpsDésactivé 
		// Le fournisseur a été désactivé par l'utilisateur.
		LIB_Etat = "GPS désactivé"
	CAS gpsHorsService 
		// Le fournisseur est hors-service.
		LIB_Etat = "GPS hors-service"
	CAS gpsIndisponible 
		// Le fournisseur est temporairement indisponible.
		LIB_Etat = "GPS indisponible"
	CAS gpsDisponible 
		// Le fournisseur est disponible.
		LIB_Etat = "GPS disponible"
		// Demande à suivre le déplacement avec un temps maximum entre deux appels de 
		// GPSSuitDéplacement(_RécupèrePosition, 1000)			
	AUTRES CAS		
FIN
```
Dans cette procédure, si le GPS est actif, une procédure spécifique est utilisée pour suivre le déplacement. 



### Suivre le déplacement 
<a name="suivre_deplacement_ELTPARAGRAPHE000115"></a>Pour suivre le déplacement de l'appareil, il suffit d'utiliser la fonction [GPSSuitDéplacement](../WDLang3/1000019206.md) en spécifiant le nom de la procédure qui va gérer le déplacement.  

Exemple : 

```wl
GPSSuitDéplacement(_RécupèrePosition, 1000)
```


Par exemple, le code de la procédure peut être le suivant : 

```wl
PROCEDURE _RécupèrePosition(MaPosition)

// Mise à jour des informations sur la position
// Latitude et longitude
LIB_Latitude = ChaîneConstruit("Latitude : %1", MaPosition.Latitude)
LIB_Longitude = ChaîneConstruit("Longitude : %1", MaPosition.Longitude)

// Vitesse
SI MaPosition.VitesseValide = Vrai ALORS
	LIB_Vitesse = ChaîneConstruit("Vitesse : %1 m/s", MaPosition.Vitesse)
	IMG_VALIDITE_VITESSE = IMG_OK
SINON
	LIB_Vitesse = "Vitesse : N/A"
	IMG_VALIDITE_VITESSE = IMG_PASOK
FIN

// Altitude
SI MaPosition.AltitudeValide = Vrai ALORS
	LIB_Altitude = ChaîneConstruit("Altitude : %1 m", MaPosition.Altitude)
	IMG_VALIDITE_ALTITUDE = IMG_OK
SINON
	LIB_Altitude = "Altitude : N/A"
	IMG_VALIDITE_ALTITUDE = IMG_PASOK
FIN

// Direction
SI MaPosition.DirectionValide = Vrai ALORS
	LIB_Direction = ChaîneConstruit("Direction : %1 ° E", MaPosition.Direction)
	IMG_VALIDITE_DIRECTION = IMG_OK
SINON
	LIB_Direction = "Direction : N/A"
	IMG_VALIDITE_DIRECTION = IMG_PASOK
FIN

LIB_Dernière_mise_à_jour = ChaîneConstruit("Dernière mise à jour le %1 à %2.", ...
	DateVersChaîne(MaPosition.Datemesure.PartieDate), ...
	HeureVersChaîne(MaPosition.Datemesure.PartieHeure))
LIB_Dernière_mise_à_jour.Visible = Vrai
```


Dans ce code, **MaPosition** est une variable de type [géoPosition](../WDLang3/1000019191.md). Les différentes propriétés de ce type de variable permettent de connaître toutes les caractéristiques de la position. 




### Terminer l'utilisation des fonctions GPS
<a name="terminer_utilisation_des_fonctions_gps_ELTPARAGRAPHE000138"></a>Selon le paramétrage choisi et la fréquence d'appel, les fonctions de géolocalisation peuvent consommer beaucoup de ressources sur le périphérique (batterie, bande passante, etc.). La fonction [GPSTermine](../WDLang3/1000019210.md) doit être appelée lorsque les fonctions de localisation ne sont plus utilisées par l'application.



<a name="NOTE3"></a>
<a name="NOTE3_1"></a>
<a name="NOTE3B"></a>
<a name="NOTE3B_1"></a>


## Quand utiliser quelle fonction ?
<a name="quand_utiliser_quelle_fonction_ELTTEXTE000657"></a>


| Fonction | Quand l'utiliser ? | Précision | Consommation d'énergie |
| --- | --- | --- | --- |
| géoSuiviXXX | Suivi permanent des changements de position y compris lorsque l'application est en arrière-plan. | Précision élevée mais la fréquence des notifications est faible et dépendante du système. | Faible |
| [GPSSuitDéplacement](../WDLang3/1000019206.md) | Suivi permanent des changements de position lorsque l'application est en avant plan. | Précision selon la fonction [GPSInitParamètre](../WDLang3/1000019204.md). | Consommation selon la fonction [GPSInitParamètre](../WDLang3/1000019204.md). |
| [GPSDétectePosition](../WDLang3/1000019209.md) | Notification de la proximité d'une position donnée lorsque l'application est au premier plan. | Précision selon la fonction [GPSInitParamètre](../WDLang3/1000019204.md). | Consommation selon la fonction [GPSInitParamètre](../WDLang3/1000019204.md). |
| [GPSRécupèrePosition](../WDLang3/1000019205.md) | Récupération ponctuelle de la position courante lorsque l'application est en avant plan. Selon les conditions (nuages, en intérieur, ...), la récupération de la position peut être longue. | Précision selon la fonction [GPSInitParamètre](../WDLang3/1000019204.md). | Selon le nombre d'appels. |
| [GPSDernièrePosition](../WDLang3/1000019239.md) | Récupération ponctuelle et immédiate de la dernière position connue de l'appareil. | Consommation faible car dépend de quand la dernière récupération réelle a eu lieu. Dépend également de la fonction [GPSInitParamètre](../WDLang3/1000019204.md). | Consommation faible. |



<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Fonctions disponibles selon les plateformes
<a name="fonctions_disponibles_selon_les_plateformes_ELTTEXTE000681"></a>


### Tableau récapitulatif
<a name="tableau_recapitulatif_ELTPARAGRAPHE000319"></a>Le tableau suivant présente les différentes fonctions de géolocalisation / GPS et les différentes plateformes où elles sont disponibles actuellement (ce tableau peut évoluer au fil des versions) : 


|   |   |   |   |   |
| --- | --- | --- | --- | --- |
| **Fonctions** |   |   | ![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/fr/UNIVERSALAPP.png) Universal Windows App |   |
| [géoAzimut](../WDLang3/1000019212.md) | X | X |   |   |
| [géoDistance](../WDLang3/1000019211.md) | X | X | X |   |
| [géoLanceAppli](../WDLang3/1000019954.md) | X | X |   |   |
| [géoRécupèreAdresse](../WDLang3/1000019797.md) | X | X |   |   |
| [géoRécupèreZone](../WDLang3/1000025776.md) | X | X |   | X |
| [géoSuiviActive](../WDLang3/1000021583.md) | X | X |   |   |
| [géoSuiviDésactive](../WDLang3/1000021584.md) | X | X |   |   |
| [géoSuiviEtat](../WDLang3/1000021592.md) | X | X |   |   |
| [géoSuiviProcédure](../WDLang3/1000021585.md) | X | X |   |   |
| [GPSArrêteDétection](../WDLang3/1000019240.md) | X |   | X |   |
| [GPSDernièrePosition](../WDLang3/1000019239.md) | X | X | X | X |
| [GPSDétectePosition](../WDLang3/1000019209.md) | X |   | X |   |
| [GPSEtat](../WDLang3/1000019207.md) | X | X | X | X |
| [GPSInfo](../WDLang3/1000019208.md) | X |   | X |   |
| [GPSInitParamètre](../WDLang3/1000019204.md) | X | X | X |   |
| [GPSRécupèrePosition](../WDLang3/1000019205.md) | X | X | X | X |
| [GPSSuitDéplacement](../WDLang3/1000019206.md) | X | X | X | X |
| [GPSTermine](../WDLang3/1000019210.md) | X | X | X |   |




- ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=WMGeolocalisation.gif) ***Exemples multiplateforme (WINDEV Mobile)*** : **WM Geolocalisation** <br>Cet exemple présente comment gérer des recherches de proximité avec géolocalisation :<br>- recherche autour de moi<br>- recherche dans une ville, à une adresse donnée, proche d'une adresse.<br>Les résultats apparaissent dans une zone répétée et dans une carte avec des marqueurs.


