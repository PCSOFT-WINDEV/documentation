


## HAlias (Fonction)

***En anglais : HAlias***



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Crée un alias logique d'un fichier de données (ou d'une requête) ou annule tous les alias existants. L'annulation de l'alias peut aussi être réalisée grâce à la fonction [HAnnuleAlias](../WDLang4/3044149.md).

**Rappel** : Un alias permet de travailler sur différents fichiers de données physiques ayant une même description logique. Par exemple, pour un fichier de données, la description du fichier est effectuée dans l'analyse et la fonction **HAlias** permet de créer l'alias du fichier.

**Remarque** : Cette page d'aide décrit la création d'un alias avec la fonction **HAlias**. A partir de la version 27, il est possible d'utiliser une variable de type [Alias](../WDLang4/1000021446.md). Ce type de variable simplifie la création et la manipulation des alias. 


<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple
<a class="notetitle" target="_blank" href="$DOC$=1000003044176&name=halias_fonction&product=WD">Voir des exemples supplémentaires</a>

```wl
// Variable source de données pour manipuler l'alias après sa création
CommandeVerification est une Source de Données <description=Commande>
// Nouveauté de la version 19 : 
// <description=Commande> permet d'obtenir la complétion de code 
// pour les rubriques du fichier de données Commande

// Création d'un alias du fichier de données "Commande"
SI HAlias(Commande, CommandeVerification) = Vrai ALORS
	// "CommandeVerification" peut maintenant être utilisé dans les traitements.
	// Il se comporte comme le fichier "Commande" décrit dans l'analyse.
	// Par défaut, le nom du fichier physique associé correspond 
	// au nom de la source de données donc ici "CommandeVerification"
	// Pour manipuler le même fichier physique que celui d'origine, il faut modifier le nom :
    HChangeNom(CommandeVerification, Commande.NomPhysique)
    	// Pour utiliser un fichier sur une autre base de données ou dans un autre répertoire,
	// il faut ensuite utiliser la fonction HChangeConnexion ou la fonction HChangeRep
SINON
    Erreur("Echec de la création de l'alias", HErreurInfo())
    RETOUR
FIN
```

<a name="XSYNTAXE"></a>
<a name="SYNTAXE1"></a>

## Syntaxe

`<Résultat> = HAlias(<Fichier de données> [, <Alias>])`
---

**`<Résultat> : (Booléen)`**



- <u><u><u><u>Vrai</u></u></u></u> si l'alias a été créé, 

- <u><u><u><u>Faux</u></u></u></u> en cas de problème. La fonction [HErreur](../WDLang4/3044088.md) permet d'identifier l'erreur.




**`<Fichier de données> : (Chaîne de caractères)`**

Nom logique du fichier de données (défini dans l'analyse) ou nom de la requête (créée avec l'éditeur de requêtes). 
Si ce paramètre correspond à "\*", tous les alias définis dans l'application en cours sont annulés. Les fichiers de données correspondants sont automatiquement fermés.

**`<Alias> : (Chaîne de caractères optionnelle ou source de données)`**

Nom de l'alias, utilisé pour désigner la structure de l'alias. 
Si ce paramètre n'est pas précisé, tous les alias créés pour &lt;Fichier de données&gt; sont annulés. Les fichiers de données correspondants sont fermés. 
Pour optimiser les performances de vos applications, il est conseillé d'utiliser une variable de type [Source de données](../WDLang4/1514053.md) (voir exemple). 

![OLE DB](https://doc.pcsoft.fr/ext/images/fr/OLEDB.png)![Connecteurs Natifs (Accès Natifs)](https://doc.pcsoft.fr/ext/images/fr/AN.png) Le fichier &lt;Alias&gt; utilise les paramètres de connexion du fichier &lt;Fichier de données&gt; (paramètres définis dans l'analyse).



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques


### Fichier de données physique associé à un alias
<a name="fichier_donnees_physique_associe_alias_ELTPARAGRAPHE000153"></a>Par défaut, le nom spécifié pour l'alias est attribué au fichier de données physique correspondant à l'alias. 

Pour associer l'alias à un fichier de données physique de nom différent, il suffit d'utiliser les fonctions [HChangeConnexion](../WDLang4/3044150.md), [HChangeNom](../WDLang4/3044141.md) et [HChangeRep](../WDLang4/3044168.md).
<a name="NOTE0_2"></a>


### Annuler des alias d'un ou de plusieurs fichiers de données
<a name="annuler_des_alias_plusieurs_fichiers_donnees_ELTPARAGRAPHE000173"></a>L'annulation d'un alias (fonction [HAnnuleAlias](../WDLang4/3044149.md)) entraîne la fermeture du fichier de données associé.

Pour annuler les alias d'un fichier de données, il suffit d'utiliser la fonction **HAlias** sans préciser le nom de l'alias.

Pour annuler tous les alias définis sur tous les fichiers de données du projet en cours, il suffit d'utiliser la syntaxe suivante :

```wl
HAlias("*")
```

<a name="NOTE0_3"></a>


### Alias et requêtes
<a name="alias_requetes_ELTPARAGRAPHE000195"></a>

- **Si la requête a été créée par programmation** (fonction [HExécuteRequêteSQL](../WDLang4/3044084.md)), il suffit de créer la requête alias avec cette même fonction mais en spécifiant le nom de l'alias.

- **Si la requête a été créée avec l'éditeur de requêtes**, pour créer un alias, il suffit de :

	- exécuter la requête initiale. 

	- utiliser la fonction **HAlias** en spécifiant le nom de la requête et le nom de l'alias. 

	- utiliser la fonction [HExécuteRequête](../WDLang4/3044080.md) avec la requête alias. L'alias est initialisé.


 Par exemple :
	
	```wl
	AliasRequête est une Source de Données
	HAlias(REQ_Requête1, AliasRequête)
	HExécuteRequête(AliasRequête)
	```

	**Remarque** : Lors de la création de l'alias, la requête initiale doit avoir été exécutée.
	Exemple :
	
	```wl
	AliasRequête est une Source de Données 
	SI HExécuteRequête(REQ_Requête1) = Faux ALORS
		Erreur(HErreur(hErrEnCours)) 
	SINON
		HLitPremier(REQ_Requête1)
		Trace(REQ_Requête1.Ville)
		HAlias(REQ_Requête1, AliasRequête)
		SI HExécuteRequête(AliasRequête) = Faux ALORS
			Erreur(HErreur(hErrEnCours)) 
		SINON
			POUR TOUT AliasRequête
				Trace(AliasRequête.Ville)
			FIN
		FIN
	FIN
	```




<a name="NOTE0_4"></a>


### Manipuler le fichier alias et ses rubriques
<a name="manipuler_fichier_alias_ses_rubriques_ELTPARAGRAPHE000237"></a>

- Pour éviter l'erreur "*Identificateur inconnu*" lors de l'utilisation d'un fichier alias et de ses rubriques, il est recommandé d'utiliser une variable de type [Source de données](../WDLang4/1514053.md) pour déclarer le nom de l'alias avant l'utilisation de la fonction **HAlias**.
	Par exemple : 
	
	```wl
	Commande2018 est une Source de Données
	HAlias(Commande, Commande2018)
	```

	Attention : l'utilisation du mot-clé **Externe** est possible mais entraîne un ralentissement non négligeable à l'exécution.

- Dans les fonctions [HLitRecherche](../WDLang4/3044050.md), [HLitPremier](../WDLang4/3044051.md), [HLitSuivant](../WDLang4/3044037.md), ... le nom de la rubrique de l'alias ne peut pas être utilisé directement : il faut utiliser le nom de la rubrique entre guillemets. 
	Par exemple :
	
	```wl
	HLitRecherchePremier(srcAlias, "RubriqueClé", "Valeur Recherchée")
	```


- Le nom de la source de données ne doit pas être utilisé entre guillemets, si l'option [Nommage automatique des sources de données](../Editeurs/9500218.md) est activée dans la fenêtre de description du projet.

- Sous l'éditeur de fenêtres ou de pages, les champs ne peuvent pas être directement associés à des rubriques des fichiers alias. Il est nécessaire :

	- soit d'affecter les champs un par un par programmation.

	- soit d'utiliser la fonction [ChampAlias](../WDLang1/3025016.md) ou la propriété [LiaisonFichier](../Proprietes/2510060.md) avant d'utiliser : 

		- la fonction [FichierVersEcran](../WDLang4/3044210.md).

		- la fonction [FichierVersPage](../WDLang4/3044148.md).




- Les fichiers alias peuvent être manipulés par les fonctions [HListeRubrique](../WDLang4/3044230.md) et [HListeClé](../WDLang4/3044060.md). Les fichiers alias peuvent être ré-indexés.

- Par défaut, le **mot de passe d'un fichier alias** est identique au mot de passe du fichier de données d'origine. Il est possible de modifier ce mot de passe grâce à la fonction [HPasse](../WDLang4/3044108.md). Le nom du fichier de données utilisé dans cette fonction correspond alors au nom de l'alias.

- La modification automatique des fichiers de données peut être appliquée aux fichiers alias.

- Les blocages des fichiers de données ou des enregistrements sont effectifs entre les alias d'une même instance d'une application.



<a name="NOTE0_5"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/fr/UNIVERSALAPP.png)![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png)![Code Utilisateur (MCU)](https://doc.pcsoft.fr/ext/images/fr/MCU.png)![Procédures stockées](https://doc.pcsoft.fr/ext/images/fr/PS.png) 

### Changement de répertoire ou modification de la connexion
<a name="changement_repertoire_modification_connexion_ELTPARAGRAPHE000325"></a>Si les fonctions [HChangeRep](../WDLang4/3044168.md) ou [HConnecte](../WDLang4/3044263.md) ont été utilisées avec le fichier de données d'origine, ces changements ne sont pas pris en compte pour le fichier de données &lt;Alias&gt;. 

Après avoir créé un alias, il est conseillé d'utiliser la fonction [HChangeConnexion](../WDLang4/3044150.md) pour définir les paramètres de connexion sur le fichier alias.
<a name="NOTE0_6"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png) 

### Versions précédentes
<a name="versions_precedentes_ELTPARAGRAPHE000345"></a>Cette fonction remplace la fonction **HAlias_55**, conservée par compatibilité avec WINDEV 5.5.



<a name="XComposante"></a>

## Composante :
wd280hf.dll
