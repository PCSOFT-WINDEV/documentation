
## Description d'IndexFullText (Type de variable)

***En anglais : Description of FullTextIndex (Type of variable)***
				



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Une variable de type **Description d'IndexFullText** permet de décrire par programmation un ou plusieurs index full-text. Les caractéristiques de ces index peuvent être définies à l'aide de différentes [propriétés WLangage](#NOTE0_1)

Pour ajouter la description de cet index dans la description d'un fichier de données, utilisez la fonction [HDécritIndexFullText](../WDLang4/1000017407.md). La variable de type "Description d'IndexFullText" est alors réinitialisée et pourra être utilisée pour décrire un autre index full-text par programmation.




<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
UnFichier est une Description de Fichier
UneRubrique est une Description de Rubrique
MonIndexFullText est une Description d'IndexFullText
CLIENT est une Source de Données

// Description du fichier "CLIENT"
UnFichier.Nom = "CLIENT"
UnFichier.Type = hFichierNormal
UnFichier.CryptageFic = hCryptageStandard

// Description de la rubrique "NOM"
UneRubrique.Nom = "NOM"
UneRubrique.Type = hRubTexte
UneRubrique.Taille = 40
UneRubrique.TypeClé = hCléDoublon
HDécritRubrique(UnFichier, UneRubrique)

// Description de la rubrique "PRENOM"
UneRubrique.Nom = "PRENOM"
UneRubrique.Type = hRubTexte
UneRubrique.Taille = 40
HDécritRubrique(UnFichier, UneRubrique)

// Description de l'index full-text
MonIndexFullText.LongueurMinimale = 4
MonIndexFullText.Nom = "MonIndexFullText"
MonIndexFullText.Rubriques = "NOM+PRENOM"
MonIndexFullText.SensibleAccentuation = Vrai
MonIndexFullText.SensibleCasse = Faux
HDécritIndexFullText(UnFichier, MonIndexFullText)

// Valide la description du fichier
HDécritFichier(UnFichier)
// Création du fichier Client (reconnu car déclaré comme source de données)
HCréation(CLIENT)
// Création d'un enregistrement
CLIENT.NOM = "Dupont"
CLIENT.PRENOM = "André"
HAjoute(CLIENT)
// Création d'un enregistrement
CLIENT.NOM = "Dupont"
CLIENT.PRENOM = "Robert"
HAjoute(CLIENT)
```

<a name="XSYNTAXE"></a>
<a name="SYNTAXE1"></a>

## Syntaxe

### Déclarer un ou plusieurs descriptions d'IndexFullText

`<Nom de la variable> est une description d'IndexFullText
<Nom de la variable 1>, <Nom de la variable 2> sont des descriptions d'IndexFullText
`
---

**`<Nom de la variable> : ()`**

Nom de la variable de type "Description d'IndexFullText" à déclarer.

Remarque : Les mots-clés <u><u><u><u>une</u></u></u></u> et <u><u><u><u>des</u></u></u></u> ne sont pas obligatoires : ce sont des mots d'agrément.  


<a name="SYNTAXE2"></a>

### Décrire une variable de type "Description d'IndexFullText"

``
---

**Pour décrire une variable de type "Description d'IndexFullText"** : 
Utilisez les propriétés du WLangage spécifiques aux descriptions d'index full-text. Pour plus de détails, consultez [les remarques](#NOTE0_1).

**Pour valider la description d'une variable de type "Description d'IndexFullText"** :
Utilisez la fonction [HDécritIndexFullText](../WDLang4/1000017407.md).



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques




### Propriétés spécifiques à la description d'un index full-text
<a name="proprietes_specifiques_description_index_fulltext_ELTPARAGRAPHE000092"></a>Les propriétés spécifiques à la description d'un index full-text sont détaillées dans le tableau suivant : 

| Nom | Type manipulé | Effet |
| --- | --- | --- |
| [GèreHTML](../Proprietes/1000017717.md) | Booléen | Paramètre le mode de gestion du HTML dans un index full-text. |
| [GèreRTF](../Proprietes/1000017718.md) | Booléen | Paramètre le mode de gestion du RTF dans un index full-text. |
| [LongueurMinimale](../Proprietes/1000017716.md) | Entier | Permet de définir et de connaître la longueur minimale des mots à indexer. |
| [MotVide](../Proprietes/1000021126.md) | Chaîne de caractères | Permet de définir les mots vides à ne pas indexer. |
| [Nom](../Proprietes/2510082.md) | Chaîne de caractères | Propriété obligatoire permettant de définir et de connaître le nom de la rubrique de type index full-text. |
| Option | Constante de type Entier | Permet de définir des options spécifiques sur la rubrique de type index full-text. <br><br>Cette propriété peut correspondre à : <br><br>- la constante *hftPonctuationUnicode* pour prendre en compte des spécificités de la ponctuation Unicode. <br><br>- la constante *hftPonctuationSimple* pour ne pas prendre en compte les spécificités de la ponctuation Unicode. <br><br><br> |
| [Racinisation](../Proprietes/1000021132.md) | Constante de type Entier | Propriété obligatoire permettant de définir la langue dans laquelle la racinisation doit être effectuée. |
| [Rubriques](../Proprietes/1000017462.md) | Chaîne de caractères | Permet de définir et de connaître les différentes rubriques composant l'index full-text. |
| [SensibleAccentuation](../Proprietes/2512077.md) | Booléen | Permet de définir et de savoir si la ponctuation (virgule, point, ...) est prise en compte dans les différentes recherches effectuées sur la rubrique |
| [SensibleCasse](../Proprietes/2512085.md) | Booléen | Permet de définir et de savoir si la casse (majuscules/minuscules) est prise en compte dans les différentes recherches effectuées sur la rubrique. |
| [Synonyme](../Proprietes/1000021127.md) | Chaîne de caractères | Permet de définir les synonymes pris en compte lors de l'indexation. |




<a name="NOTE0_2"></a>




### Comment décrire des fichiers de données par programmation ? 
<a name="comment_decrire_des_fichiers_donnees_par_programmation_ELTPARAGRAPHE000197"></a>Pour décrire des fichiers de données par programmation, il faut :

1. Déclarer des variables de type "[Description de Fichier](../WDLang4/1514065.md)", "[Description de Rubrique](../WDLang4/1514071.md)", "**Description d'IndexFullText**" et "[Description de Liaison](../WDLang4/1514039.md)" (si nécessaire).

2. Pour chaque fichier de données :

	- Décrire les caractéristiques du fichier de données grâce aux [propriétés HFSQL](../Proprietes/2512070.md).

	- Décrire les caractéristiques des rubriques grâce aux [propriétés HFSQL](../Proprietes/2512070.md).

	- Valider la description de chaque rubrique (fonction [HDécritRubrique](../WDLang4/3044233.md)).

	- Décrire si nécessaire les caractéristiques des index full-text grâce aux [propriétés HFSQL](../Proprietes/2512070.md).

	- Valider la description de chaque index full-text (fonction [HDécritIndexFullText](../WDLang4/1000017407.md)).

	- Valider la description du fichier de données (fonction [HDécritFichier](../WDLang4/3044206.md)).




3. Décrire si nécessaire les caractéristiques des liaisons grâce aux [propriétés HFSQL](../Proprietes/2512070.md).

4. Valider si nécessaire la description de chaque liaison (fonction [HDécritLiaison](../WDLang4/3044191.md)).





