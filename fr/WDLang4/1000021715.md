
## hbParamètreScan (Type de variable)

***En anglais : hbScanParameter***
				



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Le type **hbParamètreScan** permet de définir les paramètres de parcours des enregistrements d'une base de données HBase. Les caractéristiques de ces paramètres de parcours peuvent être définies et modifiées à l'aide de différentes propriétés WLangage. 

**Remarque** : Pour plus de détails sur la déclaration de ce type de variable et l'utilisation des propriétés WLangage, consultez [Déclaration d'une variable](../Motscles/1514032.md).
<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
// Connexion
Connexion est une hbConnexion
Connexion.Serveur = "MonServeur"
// Description du filtre
MonScan est un hbParamètreScan
MonScan.Connexion = Connexion
MonScan.Table = "blog"
// Filtre pour toutes les lignes les valeurs des colonnes dont le nom commence par "Titre"
MonScan.Filtre = "{ ""type"": ""ColumnPrefixFilter"", ""value"": """ + ...
	Encode("Titre", encodeBASE64) + """ }"
// Parcours selon le filtre et affichage des données filtrées
// La variable de parcours MonRésultat est de type hbRésultatScan
POUR TOUT MonRésultat DE MonScan
	Trace("La colonne " + MonRésultat.Colonne.Famille + ":" + ...
		MonRésultat.Colonne.Qualificatif + ...
		" de la ligne " + MonRésultat.Ligne + " vaut " + MonRésultat.Valeur)
FIN
```





<a name="NOTE0"></a>

## Remarques
<a name="NOTE0_1"></a>


### Propriétés spécifiques à la description des variables de type hbParamètreScan
<a name="proprietes_specifiques_description_des_variables_type_hbparametrescan_ELTPARAGRAPHE000031"></a>Les propriétés suivantes peuvent être utilisées pour manipuler la description des paramètres de parcours :

| Nom de la propriété | Type manipulé | Effet |
| --- | --- | --- |
| Colonne | Tableau de [hbColonne](../WDLang4/1000021698.md) | Familles ou colonnes (rubriques) présentes dans le résultat du parcours. |
| Connexion | Variable de type [hbConnexion](../WDLang4/1000021685.md) | Connexion vers le serveur HBase à utiliser pour le parcours. |
| DateHeureDébut | DateHeure | Date et heure de début de sélection des lignes. Le parcours renverra uniquement les lignes contenant des cellules à partir de cette date et heure. |
| DateHeureFin | DateHeure | Date et heure de fin de sélection des lignes. Le parcours renverra uniquement les lignes contenant des cellules jusqu'à cette date et heure. |
| Filtre | Chaîne de caractères | Filtre à appliquer lors du parcours. Ce filtre est au format JSON. Les valeurs littérales doivent être encodées en Base64, en utilisant par exemple la fonction [Encode](../WDLang1/1000022258.md).<br><br>Pour plus de détails sur la syntaxe des filtres, consultez la documentation HBase. |
| LigneDébut | Buffer | Identifiant de la ligne (enregistrement) de début du parcours. Cette ligne sera comprise dans le parcours. |
| LigneFin | Buffer | Identifiant de la ligne (enregistrement) de fin du parcours. Cette ligne ne sera pas comprise dans le parcours. |
| MaxVersion | Entier | Nombre maximal de versions renvoyées pour chaque cellule (valeur de rubrique). En effet, pour chaque cellule, un historique des valeurs est conservé dans la base HBase. |
| Table | Chaîne de caractères | Nom de la table HBase à parcourir. |




<a name="NOTE0_2"></a>


### Parcourir les données selon le filtre
<a name="parcourir_les_donnees_selon_filtre_ELTPARAGRAPHE000109"></a>Pour parcourir les données selon un filtre, il faut : 

- Définir la propriété **Filtre** de la variable. 

- Utiliser la syntaxe POUR TOUT pour initialiser le parcours avec le filtre. La syntaxe à utiliser est : 
	
	```txt
	POUR TOUT <Résultat> SUR <Variable de paramétrage de scan>
	```
Dans cette syntaxe, &lt;Résultat&gt; est de type [hbRésultatScan](../WDLang4/1000021724.md) (voir exemple). 



<a name="NOTE0_3"></a>


### Exemples de filtres
<a name="exemples_filtres_ELTPARAGRAPHE000125"></a>

1. Filtrer sur la valeur d'une colonne : MaFamille:MonQualificatif = ‘Valeur'
	
	```wl
	MonScan.Filtre = ...
	"{" + ...
	"""type"": ""SingleColumnValueFilter""," + ...
	"""op"": ""EQUAL""," + ...
	"""family"": """ + Encode("MaFamille", encodeBASE64) + """," + ...
	"""qualifier"": """ + Encode("MonQualificatif", encodeBASE64) + """," + ...
	"""latestVersion"": true," + ...
	"""comparator"": {" + ...
			"""type"": ""BinaryComparator""," + ...
			"""value"": """ + Encode("Valeur", encodeBASE64) + """}" + ...
	"}"
	```


2. Filtrer les colonnes dont le nom commence par "Titre" :
	
	```wl
	MonScan.Filtre = ...
	"{" + ...
	  """type"": ""ColumnPrefixFilter""," + ...
	  """value"": """ + Encode("Titre", encodeBASE64) + """," + ...
	"}"
	```


3. Filtrer les données selon plusieurs filtres : 
	
	```wl
	MonScan.Filtre = ...
	"{" + ...
	  """type"": ""FilterList""," + ...
	  """op"": ""MUST_PASS_ALL""," + ...
	  """filters"": [" + ...
	    "{" + ...
			filtre1 + ...
	    "}," + ...
		"{" + ...
			filtre2 + ...
		"}" + ...
	  "]" + ...
	"}"
	```








