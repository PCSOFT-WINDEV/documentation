


## HConstruitValClé (Fonction)

***En anglais : HBuildKeyValue***



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Construit la valeur d'une clé composée pour réaliser un filtre (fonction [HFiltre](../WDLang4/3044100.md)) ou effectuer une recherche (fonctions [HLitRecherchePremier](../WDLang4/3044036.md), [HLitRechercheDernier](../WDLang4/3044034.md), ...).

Cette fonction peut être remplacée par un tableau de valeurs, plus simple à utiliser. Par exemple :

```wl
HLitRecherchePremier(NomFichier, CLECOMPREL, ["Le nom", "Le prénom"])
```
Equivalent à :

```wl
HLitRecherchePremier(NomFichier, CLECOMPREL, ...
	HConstruitValClé(NomFichier, CLECOMPREL, "Le nom", "Le prénom"))
```
**Astuce** : Pour parcourir un fichier de données selon une clé composée, utilisez l'instruction [POUR TOUT](../Motscles/1510013.md) sur clé composée (syntaxe 5).

Pour plus de détails sur la gestion des clés composées dans WINDEV, consultez [Clés composées](../WDLang4/3044178.md).

**Rappel** : Les clés composées sont des chaînes binaires dont le contenu ne peut pas être affiché directement.


<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple
<a class="notetitle" target="_blank" href="$DOC$=1000003044258&name=hconstruitvalcle_fonction&product=WD">Voir des exemples supplémentaires</a>

```wl
bufValRech est un Buffer

// Construction de la clé composée
bufValRech = HConstruitValClé(Client, NOM_PRENOM, "MOULIN", "Françoise")	

// Construction d'une clé composée Chaîne/Entier
bufValRech  = HConstruitValClé(Client, NOM_NUMCLI, "MOULIN", 12128)

// Construction d'une clé composée Entier/Entier
bufValRech  = HConstruitValClé(Commande, IDCOMMANDE_IDCOMMANDE, 12119,593)
```

<a name="XSYNTAXE"></a>
<a name="SYNTAXE1"></a>

## Syntaxe

`<Résultat> = HConstruitValClé(<Fichier de données> , <Clé composée> , <Valeur des composantes>)`
---

**`<Résultat> : (Buffer)`**

Valeur de la clé composée.

**`<Fichier de données> : (Chaîne de caractères)`**

Nom du fichier de données HFSQL utilisé.

**`<Clé composée> : (Chaîne de caractères)`**

Nom de la rubrique correspondant à la clé composée

**`<Valeur des composantes> : (Chaîne de caractères)`**

Valeur à affecter à chaque composante de la clé composée. Ce paramètre est de la forme : 

```txt
<Composante 1>, <Composante 2>, ..., <Composante N>
```
Par exemple : "MOULIN", "Françoise".  



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques


### Quand utiliser la fonction HConstruitValClé ? 
<a name="quand_utiliser_fonction_docparampagetitleshort_ELTPARAGRAPHE000126"></a>La fonction **HConstruitValClé** peut et doit être utilisée pour la recherche sur les clés reliées (de type chaîne binaire) à des clés composées.

Quelques exemples :

- **Fichier FichierA** :
	CLECOMP est une clé composée du fichier FichierA. Composition : FichierA.Nom+FichierA.Prénom

- **Fichier FichierB** :
	CLECOMPREL est une clé de type chaîne binaire reliée à FichierA.CLECOMP.




**Faire une recherche sur la valeur de la clé composée :**

```wl
HLitRecherchePremier(FichierB, CLECOMPREL, HConstruitValClé(FichierA, CLECOMP, ...
		"Le nom", "Le prénom"))
```
ou bien

```wl
bufCle est un Buffer
bufCle = HConstruitValClé(FichierA, CLECOMP, "Le nom", "Le prénom")
HLitRecherchePremier(FichierB, CLECOMPREL, bufCle)
```
ou bien

```wl
HLitRecherchePremier(FichierB, CLECOMPREL, FichierA.CLECOMP)
```
si FichierA est positionné sur l'enregistrement que l'on veut rechercher dans FichierB.

Il est également possible d'utiliser un tableau de valeurs à la place de la fonction **HConstruitValClé** :

```wl
HLitRecherchePremier(FichierB, CLECOMPREL, ["Le nom", "Le prénom"])
```

<a name="NOTE0_2"></a>


### Recherche générique
<a name="recherche_generique_ELTPARAGRAPHE000184"></a>Lorsque la recherche est générique, il n'est pas nécessaire de définir les valeurs de toutes les composantes. Seules les n premières valeurs sont nécessaires (avec n compris entre 1 et le nombre de composantes de la clé, bornes comprises).
<a name="NOTE0_3"></a>


### Construire une clé composée utilisée dans un filtre avec bornes
<a name="construire_une_cle_composee_utilisee_dans_filtre_avec_bornes_ELTPARAGRAPHE000193"></a>Pour construire la valeur d'une clé composée utilisée dans un filtre, utilisez la fonction **HConstruitValClé**.

- **Si la borne minimale et la borne maximale du filtre doivent être identiques**, il est nécessaire de compléter les bornes par les constantes *hValMin* et *hValMax*. 
	L'exemple suivant permet de rechercher tous les enregistrements du fichier Client correspondant au nom "Françoise Moulin" :
	
	```wl
	HFiltre(Client, ...
		HConstruitValClé(Client, NomPrénom, "Moulin", "Françoise") + hValMin, ...
		HConstruitValClé(Client, NomPrénom, "Moulin", "Françoise") + hValMax)
	```


- **Si la borne minimale et la borne maximale doivent être identiques et si tous les composants de la clé ne sont pas spécifiés**, il est nécessaire de compléter les bornes par les constantes *hValMin* et *hValMax*. 
	L'exemple suivant permet de rechercher tous les enregistrements du fichier Client correspondant à "Moulin" :
	
	```wl
	HFiltre(Client, NomPrénom, ...
		HConstruitValClé(Client, NomPrénom, "Moulin" + hValMin), ...
		HConstruitValClé(Client, NomPrénom, "Moulin" + hValMax))
	```



Remarques :

- *hValMin* est équivalent à Caract(0)

- *hValMax* est équivalent à caract(255)



<a name="NOTE0_4"></a>


### Construire une clé composée sans utiliser la fonction HConstruitValClé
<a name="construire_une_cle_composee_sans_utiliser_fonction_docparampagetitleshort_ELTPARAGRAPHE000231"></a>Pour construire la valeur d'une clé composée sans utiliser la fonction **HConstruitValClé**, il est possible d'utiliser un tableau de valeurs. 
Par exemple :

```wl
HLitRecherchePremier(FichierB, CLECOMPREL, ["Le nom", "Le prénom"])
```


<a name="XComposante"></a>

## Composante :
wd280hf.dll
