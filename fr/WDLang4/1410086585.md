
## Gestion des bases HBase
			

<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Présentation
<a name="presentation_ELTTEXTE000300"></a>
HBase est une base de données non relationnelle (NoSQL) distribuée, orientée colonne disposant d'un stockage non structuré pour les grandes tables. 

HBase est une composante essentielle de Hadoop.

HBase est conçue pour traiter des données aux volumes importants (plusieurs milliards de lignes, plusieurs millions de colonnes par ligne) et non uniformes (chaque ligne peut avoir une structure différente des autres lignes).

<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Comment est organisée une base HBase ?
<a name="comment_est_organisee_une_base_hbase_ELTTEXTE000324"></a>
Dans une base HBase, les données sont stockées dans des tables (fichiers), de manière dé-normalisée. 
Chaque table est découpée en familles de colonnes (rubriques), qui contiennent elles-mêmes des colonnes. 
Chaque ligne ("row", équivalent d'un enregistrement dans un SGBD traditionnel) est identifiée par un et un seul "rowKey". 
Pour chaque ligne, dans une même famille de colonnes, le nombre et le nom des colonnes est libre.<br>![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=Table-HBase.gif)
Le "rowKey" est la seule clé de la table.
Une cellule est l'intersection d'une ligne et d'une colonne.
L'historique du contenu d'une cellule est conservé de façon horodatée (avec la date/heure d'écriture).
Il est possible de poser des filtres sur une table HBase (un peu à la manière d'un filtre sur HFSQL).

<a name="NOTE3"></a>


## Comment manipuler une base de données HBase en programmation ?
<a name="comment_manipuler_une_base_donnees_hbase_programmation_ELTTEXTE000348"></a>
Le WLangage permet de :

- [créer des tables](#CREE_TABLE)

- manipuler des tables existantes :

	- [récupérer la liste des tables](#RECUP_LISTE)

	- [récupérer la description d'une table](#RECUP_DESC_TABLE)

	- [supprimer une table](#SUPPR_TABLE)




- manipuler les données des tables :

	- [lire des données](#LIRE)

	- [écrire des données](#ECRIRE)

	- [supprimer des données](#SUPPR)

	- [parcourir les données selon avec un filtre](#FILTRE)







Attention : Les fonctions HBase sont disponibles en édition et en exécution uniquement à partir de Windows Vista ou Windows Server 2008.

<a name="CREE_TABLE"></a>


### Comment créer une table HBase ?
<a name="comment_creer_une_table_hbase_ELTPARAGRAPHE000068"></a>Pour créer une table HBase, il faut : 

1. Définir et initialiser une variable de type [hbConnexion](../WDLang4/1000021685.md).

2. Définir et initialiser une variable de type [hbDescriptionTable](../WDLang4/1000021688.md). Cette variable contient un tableau de variables de type [hbDescriptionColonne](../WDLang4/1000021694.md) décrivant les colonnes.

3. Utiliser la fonction [hbCréeTable](../WDLang4/1000021696.md).


Exemple :

```wl
// Connexion
Connexion est une hbConnexion
Connexion.Serveur = "MonServeur"
// Description de la table
DescTable est une hbDescriptionTable
DescTable.Nom = "blog"
// Description de la colonne "Post"
DescCol est une hbDescriptionColonne
DescCol.Nom = "Post"
Ajoute(DescTable.Colonne, DescCol)
// Création de la table
hbCréeTable(Connexion, DescTable)
```

<a name="RECUP_LISTE"></a>


### Comment récupérer la liste des tables d'une base HBase ?
<a name="comment_recuperer_liste_des_tables_une_base_hbase_ELTPARAGRAPHE000094"></a>Pour récupérer la liste des tables d'une base HBase, il faut : 

1. Définir et initialiser une variable de type [hbConnexion](../WDLang4/1000021685.md).

2. Utiliser la fonction [hbListeTable](../WDLang4/1000021687.md).


Exemple :

```wl
// Connexion
Connexion est une hbConnexion
Connexion.Serveur = "MonServeur"
// Affichage de la liste des tables
Trace(hbListeTable(Connexion))
```

<a name="RECUP_DESC_TABLE"></a>


### Comment récupérer la description d'une table HBase ?
<a name="comment_recuperer_description_une_table_hbase_ELTPARAGRAPHE000113"></a>Pour récupérer la description d'une table HBase, il faut : 

1. Définir et initialiser une variable de type [hbConnexion](../WDLang4/1000021685.md).

2. Utiliser la fonction [hbRécupèreDescriptionTable](../WDLang4/1000021695.md).

3. Récupérer la description de la table dans une variable de type [hbDescriptionTable](../WDLang4/1000021688.md). Cette variable contient un tableau de variables de type [hbDescriptionColonne](../WDLang4/1000021694.md) décrivant les colonnes.


Exemple :

```wl
// Connexion
Connexion est une hbConnexion
Connexion.Serveur = "MonServeur"
// Récupération de la description de la table
DescTable est une hbDescriptionTable = hbRécupèreDescriptionTable(Connexion, "blog")
// Affichage du nom de la table
Trace("table : " + DescTable.Nom)
// Affichage de la description des colonnes
POUR TOUT DescCol DE DescTable.Colonne
	Trace(TAB + "colonne : " + DescCol.Nom)
	Trace(TAB + TAB + "attribut : ")
	POUR TOUT ValAttribut, NomAttribut DE DescCol.Attribut
		Trace(TAB + TAB + NomAttribut + " = " + ValAttribut)
	FIN
FIN
```

<a name="SUPPR_TABLE"></a>


### Comment supprimer une table HBase ?
<a name="comment_supprimer_une_table_hbase_ELTPARAGRAPHE000139"></a>Pour supprimer une table HBase, il faut : 

1. Définir et initialiser une variable de type [hbConnexion](../WDLang4/1000021685.md).

2. Utiliser la fonction [hbSupprimeTable](../WDLang4/1000021713.md) en précisant le nom de la table à supprimer.


Exemple :

```wl
// Connexion
Connexion est une hbConnexion
Connexion.Serveur = "MonServeur"
// Suppression de la table
hbSupprimeTable(Connexion, "blog")
```

<a name="LIRE"></a>


### Comment lire les données d'une table HBase ?
<a name="comment_lire_les_donnees_une_table_hbase_ELTPARAGRAPHE000158"></a>Pour lire les données d'une table HBase, il faut : 

1. Définir et initialiser une variable de type [hbConnexion](../WDLang4/1000021685.md).

2. Définir et initialiser une variable de type [hbLecture](../WDLang4/1000021701.md). Cette variable contient un tableau de variables de type [hbColonne](../WDLang4/1000021698.md) décrivant les colonnes à récupérer.

3. Utiliser la fonction [hbLit](../WDLang4/1000021706.md).

4. Lire les données dans le résultat de type [hbCellule](../WDLang4/1000021707.md)


Exemple :

```wl
// Connexion
Connexion est une hbConnexion
Connexion.Serveur = "MonServeur"
// Description des données à lire
Lecture est une hbLecture
Lecture.Ligne = "post1"
Lecture.Version = 2
// Description colonne
Col est une hbColonne
Col.Famille = "Post"
Ajoute(Lecture.Colonne, Col)
// Lecture des données
MonRésultat est un tableau de hbCellules = hbLit(Connexion, "blog", Lecture)
// Affichage des données lues
POUR TOUT Cellule DE MonRésultat FAIRE
	Trace("Colonne : " + Cellule.Colonne.Qualificatif + "; Valeur : " + ...
	Cellule.Valeur + "; timestamp : " + Cellule.Timestamp)
FIN
```

<a name="ECRIRE"></a>


### Comment écrire des données dans une table HBase ?
<a name="comment_ecrire_des_donnees_dans_une_table_hbase_ELTPARAGRAPHE000187"></a>Pour écrire les données d'une table HBase, il faut : 

1. Définir et initialiser une variable de type [hbConnexion](../WDLang4/1000021685.md).

2. Définir et initialiser une variable de type [hbEcriture](../WDLang4/1000021701.md). Cette variable contient un tableau de variables de type [hbCellule](../WDLang4/1000021707.md) décrivant les données à écrire.

3. Utiliser la fonction [hbEcrit](../WDLang4/1000021712.md).


Exemple :

```wl
// Connexion
Connexion est une hbConnexion
Connexion.Serveur = "MonServeur"
// Description des données à écrire
Ecriture est une hbEcriture
Ecriture.Ligne = "post1"
Cellule est une hbCellule 
Cellule.Colonne.Famille = "post"
Cellule.Colonne.Qualificatif = "titre"
Cellule.Valeur = "Ceci est mon message sur le blog."
Ajoute(Ecriture.Cellule, Cellule)
// Ecriture des données
hbEcrit(Connexion, "blog", Ecriture)
```

<a name="SUPPR"></a>


### Comment supprimer des données dans une table HBase ?
<a name="comment_supprimer_des_donnees_dans_une_table_hbase_ELTPARAGRAPHE000213"></a>Pour supprimer des données dans une table HBase, il faut : 

1. Définir et initialiser une variable de type [hbConnexion](../WDLang4/1000021685.md).

2. Utiliser la fonction [hbSupprime](../WDLang4/1000021714.md) en précisant l'identifiant de la ligne à supprimer.


Exemple :

```wl
// Connexion
Connexion est une hbConnexion
Connexion.Serveur = "MonServeur"
// Suppression de toute la ligne
hbSupprime(Connexion, "blog", "post1")
```

<a name="FILTRE"></a>


### Comment faire un parcours avec filtre d'une table HBase ?
<a name="comment_faire_parcours_avec_filtre_une_table_hbase_ELTPARAGRAPHE000232"></a>Pour faire un parcours avec filtre d'une table HBase, il faut : 

1. Définir et initialiser une variable de type [hbConnexion](../WDLang4/1000021685.md).

2. Définir et initialiser une variable de type [hbParamètreScan](../WDLang4/1000021715.md). Cette variable définit le filtre à réaliser. 

3. Parcourir les données filtrées (correspondant à la variable de type [hbParamètreScan](../WDLang4/1000021715.md)) via l'instruction POUR TOUT. Le résultat du parcours est lu dans une variable de type [hbRésultatScan](../WDLang4/1000021724.md). 


Exemple :

```wl
// Connexion
Connexion est une hbConnexion
Connexion.Serveur = "MonServeur"
// Description du filtre
MonScan est un hbParamètreScan
MonScan.Connexion = Connexion
MonScan.Table = "blog"
// Filtre pour toutes les lignes les valeurs des colonnes dont le nom commence par "Titre"
MonScan.Filtre = "{ ""type"": ""ColumnPrefixFilter"", ""value"": """ + ...
	Encode("Titre", encodeBASE64) + """ }"
// Parcours selon le filtre et affichage des données filtrées
// La variable de parcours MonRésultat est de type hbRésultatScan
POUR TOUT MonRésultat DE MonScan
	Trace("La colonne " + MonRésultat.Colonne.Famille + ":" + ...
		MonRésultat.Colonne.Qualificatif + ...
		" de la ligne " + MonRésultat.Ligne + " vaut " + MonRésultat.Valeur)
FIN
```



