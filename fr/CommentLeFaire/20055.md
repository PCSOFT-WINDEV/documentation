
## Comment écrire dans un fichier texte ou CSV ? 
			

<a name="NOTE1"></a>
<a name="NOTE1_1"></a>
Deux méthodes permettent d'écrire dans un fichier texte ou CSV : 

- [Ecriture ligne par ligne dans le fichier en utilisant la fonction fEcritLigne](#NOTE2_1).

- [Ecriture en mémoire puis sauvegarde complète dans un fichier en utilisant la fonction fSauveTexte](#NOTE3_1). 




<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Méthode 1 : Utilisation la fonction fEcritLigne
<a name="methode_1_utilisation_fonction_fecritligne_ELTTEXTE000140"></a>
Pour écrire une ligne dans un fichier texte ou CSV : 

1. Ouvrez le fichier (ou créez-le) à l'aide de la fonction [fOuvre](../WDLang1/3036036.md).

2. Ecrivez la ligne à l'aide de la fonction [fEcritLigne](../WDLang1/3036025.md). La fonction [fEcritLigne](../WDLang1/3036025.md) ajoute automatiquement un caractère RC en fin de ligne.

3. Fermez le fichier à l'aide de la fonction [fFerme](../WDLang1/3036027.md). 





### Exemples de code
<a name="exemples_code_ELTPARAGRAPHE000042"></a>

- Ecriture d'une ligne : 
	
	```wl
	// Ecriture d'une chaîne simple
	nNumFichier est un entier
	sLigneAEcrire est une chaîne
	
	nNumFichier = fOuvre("MonFichier.txt", foCréation)
	SI nNumFichier <> -1 ALORS
		sLigneAEcrire = "Bonjour, nous sommes le " + DateVersChaîne(DateDuJour(), "JJJJ JJ MMMM AAAA")
	
		// Ecriture de la ligne
		fEcritLigne(nNumFichier, sLigneAEcrire)
	
		fFerme(nNumFichier)
	FIN
	```


- Conversion d'un fichier de données en TXT (export) 
	
	```wl
	<code wl>
	nNumFichier est un entier
	sLigneAEcrire est une chaîne
	
	nNumFichier = fOuvre ("ExportProduits.txt", foCréation)
	SI nNumFichier <> -1 ALORS
		POUR TOUT Produit
			// La ligne est composée de :
			// - la référence du produit
			// - le libellé du produit
			// - le prix du produit
			sLigneAEcrire = Produit.Reference + TAB + Produit.LibProd + TAB + 
					NumériqueVersChaine(Produit.Prix, "10,2fS")
	
			// Ecriture de la ligne
			fEcritLigne(nNumFichier, sLigneAEcrire)
		FIN
		fFerme(nNumFichier)
	FIN
	```





<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Méthode 2 : Utilisation de la fonction fSauveTexte
<a name="methode_2_utilisation_fonction_fsauvetexte_ELTTEXTE000170"></a>


### Principe
<a name="principe_ELTPARAGRAPHE000056"></a>Pour écrire (créer) un fichier texte ou CSV en une seule opération : 

1. Déclarez une variable de type chaîne.  

2. Ecrivez et cumulez en mémoire dans cette variable toutes les lignes du fichier. 

3. Enregistrez le contenu de cette variable dans un fichier à l'aide de la fonction [fSauveTexte](../WDLang1/3036046.md). 





### Exemples de code
<a name="exemples_code_ELTPARAGRAPHE000068"></a>


```wl
// Conversion d'un fichier de données en TXT (export)
sLigneAEcrire est une chaîne
sContenuFichier est une chaîne

POUR TOUT Produit
	// La ligne est composée de :
	// - la référence du produit
	// - le libellé du produit
	// - le prix du produit
	sLigneAEcrire = Produit.Reference + TAB + Produit.LibProd + TAB + 
			NumériqueVersChaine(Produit.Prix, "10,2fS")

	// Ecriture de la ligne en mémoire
	sContenuFichier += sLigneAEcrire + RC
FIN
fSauveTexte ("ExportProduits.txt", sContenuFichier)
```





