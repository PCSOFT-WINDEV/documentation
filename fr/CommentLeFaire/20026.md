
## Comment cloner et manipuler des colonnes dans un champ Table ?
			

<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Présentation
<a name="presentation_ELTTEXTE000165"></a>
Dans certains cas, il est nécessaire de remplir un champ Table avec un nombre de colonnes inconnu à l'avance (lors de la création du champ). Il existe 2 méthodes pour construire dynamiquement des colonnes dans un champ Table : 

- [Méthode 1](#NOTE2_1) : clonage (duplication d'une colonne existante) : utilisation de la fonction [ChampClone](../WDLang1/3025017.md). 

- ![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png) [Méthode 2](#NOTE3_1) : création (la nouvelle colonne est créée directement) : utilisation de la fonction [ChampCrée](../WDLang1/1000020876.md). 




<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Méthode 1 : Clonage de colonnes
<a name="methode_1_clonage_colonnes_ELTTEXTE000189"></a>
La fonction [ChampClone](../WDLang1/3025017.md) permet de cloner (dupliquer) un champ existant pour en créer une copie. Cette fonction est utilisable également sur une colonne d'un champ Table. La nouvelle colonne est ajoutée automatiquement à la fin des colonnes existantes.

Pour créer une nouvelle colonne : 

1. Créez un champ Table. 

2. Définissez dans le champ Table la colonne qui servira de "modèle pour le clonage". 

3. Videz le contenu du champ Table (fonction [TableSupprimeTout](../WDLang1/3074034.md)). 

4. Supprimez les précédentes colonnes clonées (fonction [ChampSupprime](../WDLang1/3025037.md)). 

5. Créez les nouvelles colonnes (fonction [ChampClone](../WDLang1/3025017.md)). 

6. Remplissez le champ Table avec les données. 




Exemple de code : 

```wl
NbColonnesFixe est un entier
NbNouvellesColonnes est un entier

NomNouvelleColonne est une chaîne


// On vide la table
TableSupprimeTout(TABLE_statAnnuelle)


// On supprime les anciennes colonnes clonées
NbColonnesFixe = 3 // par exemple, on a 2 colonnes fixes + la colonne à cloner

POUR nInd = TableOccurrence(TABLE_statAnnuelle,toColonne) A NbColonnesFixe PAS -1
	ChampSupprime(TableEnumèreColonne(TABLE_statAnnuelle, nInd))
FIN

// On clone les nouvelles colonnes
NbNouvellesColonnes = 5 // par exemple, on veut 5 nouvelles colonnes
POUR nInd = 1 A NbNouvellesColonnes
	// On construit le nom de la nouvelle colonne
	NomNouvelleColonne = "COL_" + nInd
	// On crée la nouvelle colonne par clonage
	ChampClone(COL_Modèle, NomNouvelleColonne)
	
	// On modifie le libellé d'entête de la nouvelle colonne
	{NomNouvelleColonne, indChamp}..Libellé = "Colonne " + nInd 
	
FIN

// On remplit le champ Table...

// Mettre ici votre code de remplissage de la table
```


**Attention** :

- Il faut cloner les colonnes avant d'effectuer le remplissage du champ Table.  

- Il ne peut pas y avoir 2 colonnes de même nom. 








<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Méthode 2 : Création des colonnes
<a name="methode_2_creation_des_colonnes_ELTTEXTE000213"></a>
La fonction [ChampCrée](../WDLang1/1000020876.md) permet de créer un champ sans utiliser un modèle comme la méthode par clonage. Cette fonction est utilisable également sur une colonne d'un champ Table. La nouvelle colonne est ajoutée automatiquement à la fin des colonnes existantes.

Pour créer une nouvelle colonne : 

1. Créez un champ Table. 

2. Videz le contenu du champ Table (fonction [TableSupprimeTout](../WDLang1/3074034.md)). 

3. Supprimez les précédentes colonnes créées (fonction [ChampSupprime](../WDLang1/3025037.md)). 

4. Créez les nouvelles colonnes (fonction [ChampCrée](../WDLang1/1000020876.md)).

5. Remplissez le champ Table avec les données. 




Exemple de code : 

```wl
NouvelleColonne est un Champ

NbColonnesFixe est un entier
NbNouvellesColonnes est un entier

NomNouvelleColonne est une chaîne


// On vide la table
TableSupprimeTout(TABLE_statAnnuelle)


// On supprime les anciennes colonnes créées
NbColonnesFixe = 3 // Par exemple, on a 2 colonnes fixes + la colonne à créer


POUR nInd = TableOccurrence(TABLE_statAnnuelle, toColonne) A NbColonnesFixe PAS -1
	ChampSupprime(TableEnumèreColonne(TABLE_statAnnuelle, nInd))
FIN

// On crée les nouvelles colonnes
NbNouvellesColonnes = 5 // par exemple, on veut 5 nouvelles colonnes
POUR nInd = 1 A NbNouvellesColonnes
	// On construit le nom de la nouvelle colonne
	NomNouvelleColonne = TABLE_statAnnuelle.Nom + ".COL_" + nInd
	// On crée la nouvelle colonne par création
	NouvelleColonne <- ChampCrée(NomNouvelleColonne, typColonne)
	
	// On modifie le libellé d'entête de la nouvelle colonne
	NouvelleColonne.Libellé = "Colonne " + nInd 
FIN

// On remplit le champ Table...
// Mettre ici votre code de remplissage de la table
```


**Attention** :

- Il faut créer les colonnes avant d'effectuer le remplissage du champ Table.  

- Il ne peut pas y avoir 2 colonnes de même nom. 








<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Manipulation de la nouvelle colonne
<a name="manipulation_nouvelle_colonne_ELTTEXTE000237"></a>
Pour manipuler la colonne créée (par clonage ou création), vous disposez de deux méthodes :

- les opérateurs d'indirection { }. Pour plus de détails, consultez [Opérateurs d'indirection](../Motscles/1512005.md).

- le nom de la nouvelle colonne, méthode valable uniquement si la colonne a été créée par la fonction [ChampCrée](../WDLang1/1000020876.md). 













