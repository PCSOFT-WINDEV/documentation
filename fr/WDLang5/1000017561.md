


## Gestion des agendas Google
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Présentation
<a name="presentation_ELTTEXTE000305"></a>
Le service Agenda Google Calendar est une application Internet fournie par Google qui permet de gérer un agenda sur Internet.

WINDEV et WEBDEV permettent par exemple de réaliser une application de synchronisation de planning avec un applicatif déjà en place : utilisation d'une salle de réunion, d'un véhicule, ...

Ces fonctions WLangage permettent également de réaliser des interfaces spécifiques (adaptées au métier, plus conviviales, ...) et de rajouter des traitements particuliers (impressions, ...).

Exemples de traitements réalisables nativement en WLangage :

- Récupération de la liste des agendas avec leurs détails (agendas professionnels, personnels, ...).

- Récupération de la liste des rendez-vous d'un agenda.

- Recherche dans les rendez-vous d'un agenda.

- Ajout, modification, suppression de rendez-vous.  




**Avertissement** : Avant d'utiliser une fonctionnalité reliée à des services Google, nous vous conseillons vivement de vous reporter à la licence d'utilisation de ce service. Certaines restrictions peuvent s'appliquer. Le contenu des licences peut varier avec le temps. 

PC SOFT dégage toute responsabilité de l'usage qui peut être fait des fonctions d'accès natif. Il vous appartient de vérifier que l'usage que vous faites respecte la licence du fournisseur du service.

<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Comment gérer un agenda Google ?
<a name="comment_gerer_agenda_google_ELTTEXTE000329"></a>


### Gérer un agenda Google
<a name="gerer_agenda_google_ELTPARAGRAPHE000037"></a>Pour gérer un agenda Google :

1. Créez si nécessaire un compte Google. La création de ce compte peut être réalisée grâce à l'adresse suivante :  [https://www.google.com/accounts/NewAccount?hl=fr](https://www.google.com/accounts/NewAccount?hl=fr)
	Attention : l'adresse de cette page peut être modifiée depuis la date de rédaction de cette page.
	Le compte Google est identifié par une adresse email et le mot de passe associé. 

2. Dans le code de votre application, créez une variable de type [gglConnexion](../WDLang5/1000018009.md). Cette variable contient les caractéristiques de la connexion à votre compte Google.



<a name="NOTE2_2"></a>


### Créer un agenda Google
<a name="creer_agenda_google_ELTPARAGRAPHE000058"></a>Un agenda Google peut être créé soit par l'interface de Google, soit par programmation à l'aide des fonctions WLangage.

Pour créer un agenda Google grâce aux fonctions WLangage :

1. Créez une variable de type [gglAgenda](../WDLang5/1000017418.md).

2. Définissez les caractéristiques de l'agenda grâce aux propriétés du type [gglAgenda](../WDLang5/1000017418.md).

3. Définissez si nécessaire les événements liés à l'agenda (variable de type [gglEvénement](../WDLang5/1000017440.md)).

4. Validez la création de l'agenda grâce à la fonction [GglEcrit](../WDLang5/1000017401.md).



<a name="NOTE2_3"></a>


### Remarque
<a name="remarque_ELTPARAGRAPHE000086"></a>Si vous utilisez un proxy pour accéder à Internet, il est nécessaire de configurer le proxy (fonction [Proxy](../WDLang3/3043002.md)) pour utiliser les fonctions Google.

<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Comment récupérer un agenda Google et ses éléments ?
<a name="comment_recuperer_agenda_google_ses_elements_ELTTEXTE000365"></a>


### 1ère méthode : récupération de la liste des agendas puis de leurs événements
<a name="1ere_methode_recuperation_liste_des_agendas_puis_leurs_evenements_ELTPARAGRAPHE000100"></a>Pour récupérer un agenda Google à partir de la liste des agendas :

1. Déclarez un tableau de variables de type [gglAgenda](../WDLang5/1000017418.md) (pour récupérer plusieurs agendas).

2. Utilisez la fonction [GglListeAgenda](../WDLang5/1000017330.md). Cette fonction permet de lister les agendas disponibles. Les agendas trouvés sont affectés au tableau de variables de type gglAgenda.

3. Utilisez la fonction [GglRemplitAgenda](../WDLang5/1000017337.md) pour récupérer les événements. Il est possible de récupérer les événements d'un seul ou de plusieurs agendas. Il est possible de filtrer les événements à récupérer (par exemple entre deux dates).




Exemple :

```wl
Cnx est un gglConnexion
...
tabAgendas est un tableau de 0 gglAgenda 
tabAgendas = GglListeAgenda(Cnx) 
// Premier agenda
Agenda est un gglAgenda = tabAgendas[1]
// Récupère les événements entre le 01/01/2008 et le 01/01/2009 inclus
GglRemplitAgenda(Cnx, Agenda, "20080101", "20090102")
// Parcours des événements d'un agenda
Evt est un gglEvénement
POUR TOUT Evt DE Agenda 
	Trace(Evt.Titre)
FIN
```

<a name="NOTE3_2"></a>


### 2ème méthode : récupération d'un agenda spécifique
<a name="2eme_methode_recuperation_agenda_specifique_ELTPARAGRAPHE000130"></a>Pour récupérer un agenda Google spécifique et ses événements : 

1. Déclarez une variable de type [gglAgenda](../WDLang5/1000017418.md).

2. Utilisez la fonction [GglRécupèreAgenda](../WDLang5/1000017410.md). Cette fonction permet de récupérer l'agenda Google (et ses événements) correspondant au titre spécifié. 




Exemple :

```wl
Cnx est un gglConnexion
...

// Récupération de l'agenda nommé "Travail"
Agenda est un gglAgenda = GglRécupèreAgenda(Cnx, "Travail")
// Parcours des événements de l'agenda
SI ErreurDétectée = Faux ALORS
	Evt est un gglEvénement
	POUR TOUT Evt de gglAgenda
		Trace(Evt.Titre)
	FIN
FIN
```


<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Comment ajouter, modifier ou supprimer des événements dans un agenda Google ?
<a name="comment_ajouter_modifier_supprimer_des_evenements_dans_agenda_google_ELTTEXTE000395"></a>


### Principe
<a name="principe_ELTPARAGRAPHE000158"></a>Le principe de la modification des événements est simple : l'agenda est récupéré en local, les modifications sont effectuées en local, puis l'agenda est mis à jour sur le serveur.

Remarque : Dans le cas d'agendas partagés, il est conseillé de mettre à jour régulièrement les agendas sur le serveur.
<a name="NOTE4_2"></a>


### Ajouter des événements à un agenda
<a name="ajouter_des_evenements_agenda_ELTPARAGRAPHE000169"></a>Pour ajouter des événements à un agenda :

1. Récupérez l'agenda voulu (et si nécessaire ses événements).

2. Déclarez une variable de type [gglEvénement](../WDLang5/1000017440.md).

3. Définissez les caractéristiques de l'événement grâce aux propriétés de la variable.

4. Utilisez la fonction [GglEcrit](../WDLang5/1000017401.md) pour mettre à jour l'agenda sur le serveur.




Exemple  :

```wl
Cnx est un gglConnexion
...

// Récupération de l'agenda nommé "Travail"
Agenda est un gglAgenda = GglRécupèreAgenda(Cnx, "Travail")

// Création d'un événement
MonEvénement est un gglEvénement
MonEvénement.DateDébut = "20081201085000"
MonEvénement.DateFin = "20081201093000"
MonEvénement.Titre = "Rendez-vous"
MonEvénement.Contenu = "Rendez-vous concernant le bilan du mois de novembre"

// Ajout de l'événement dans l'agenda
Ajoute(Agenda.Evénement, MonEvénement)

// Mise à jour de l'agenda sur le serveur
GglEcrit(Cnx, Agenda)
```

```wl
Cnx est un gglConnexion
...

tabAgendas est un tableau de 0 gglAgenda 
tabAgendas = GglListeAgenda(Cnx) 

// Récupère les événements futurs du premier agenda
GglRemplitAgenda(Cnx, tabAgendas[1])

// Création d'un événement
MonEvénement est un gglEvénement
MonEvénement.DateDébut = "20081201085000"
MonEvénement.DateFin = "20081201093000"
MonEvénement.Titre = "Rendez-vous"
MonEvénement.Contenu = "Rendez-vous concernant le bilan du mois de novembre"

// Ajout de l'événement dans l'agenda
Ajoute(tabAgendas[1].Evénement, MonEvénement)

// Mise à jour de l'agenda sur le serveur
GglEcrit(Cnx, tabAgendas[1])
```

<a name="NOTE4_3"></a>


### Modifier les événements d'un agenda :
<a name="modifier_les_evenements_agenda_ELTPARAGRAPHE000203"></a>Pour modifier les événements d'un agenda :

1. Récupérez l'agenda voulu et ses événements.

2. Recherchez l'événement à modifier.

3. Modifiez les caractéristiques de l'événement.

4. Validez les modifications grâce à la fonction [GglEcrit](../WDLang5/1000017401.md).




**Remarque** : Il est possible d'effectuer plusieurs modifications avant d'utiliser la fonction [GglEcrit](../WDLang5/1000017401.md).

**Exemple** :

```wl
Cnx est un gglConnexion
...

// Récupération de l'agenda nommé "Travail"
Agenda est un gglAgenda = GglRécupèreAgenda(Cnx, "Travail")
// Modification du premier événement de l'agenda
Agenda.Evénement[1].Titre = "Rdv patron"
Agenda.Evénement[1].DateFin = "200810131530"
// Mise à jour effective des changements sur le serveur
GglEcrit(Cnx, Agenda)
```

<a name="NOTE4_4"></a>


### Supprimer un événement d'un agenda
<a name="supprimer_evenement_agenda_ELTPARAGRAPHE000233"></a>Pour supprimer un événement d'un agenda :

1. Récupérez l'agenda voulu et ses événements.

2. Recherchez l'événement à supprimer.

3. Supprimez l'événement.

4. Validez les modifications grâce à la fonction [GglEcrit](../WDLang5/1000017401.md).


**Remarque** : Il est possible d'effectuer plusieurs suppressions avant d'utiliser la fonction [GglEcrit](../WDLang5/1000017401.md).

**Exemple** :

```wl
Cnx est un gglConnexion
...

// Récupération de l'agenda nommé "Travail"
Agenda est un gglAgenda = GglRécupèreAgenda(Cnx, "Travail")
// Suppression du deuxième événement de l'agenda
Supprime(Agenda.Evénement, 2)
// Mise à jour effective des changements sur le serveur
GglEcrit(Cnx, Agenda)
```



- ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=LechampAgenda_affichaged'unagendaGoogle_.gif) ***Exemples unitaires (WINDEV)*** : **Le champ Agenda (affichage d'un agenda Google)** <br>Récupération des événements d'un agenda Google via l'utilisation des fonctions gglXxx et affichage de ces événements dans un champ Agenda.
- ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=WDAgenda.gif) ***Exemples complets (WINDEV)*** : **WD Agenda** <br>Cet exemple permet de synchroniser des rendez-vous entre vos agendas Outlook, Lotus Notes et Google.<br><br>Basé sur le champ Agenda, vous pouvez créer, modifier, déplacer, supprimer des rendez-vous.<br>Il est également possible de classer les rendez-vous par catégorie et de les lier à plusieurs agendas externes.
- ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=WDPlanning.gif) ***Exemples complets (WINDEV)*** : **WD Planning** <br>Cet exemple illustre la gestion d'un planning graphique.<br><br>Dans cet exemple, nous abordons les principaux thèmes suivants :<br>1/ le champ planning<br>2/ les fonctions Google agenda


