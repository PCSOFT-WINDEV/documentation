


## XMLExécuteXPath (Fonction)

***En anglais : XMLExecuteXPath***



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Exécute une requête XPATH : 

- dans une variable de type [xmlDocument](../WDLang5/1000018782.md).
	

- dans un document XML.
	















<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
clDoc est un xmlDocument = XMLOuvre(n, depuisChaîne)
c est un xpathRésultat = XMLExécuteXPath(clDoc, "/root/text()")
SI c.EstValeur ALORS
	Trace(c.Valeur)
SINON
	POUR TOUT UnNoeud DE c.Noeud
		Trace("Noeud : " + UnNoeud..Texte)
	FIN
	POUR TOUT attrib DE c.Attribut
		Trace("Noeud : " + attrib.Nom)
	FIN
FIN
```
<a name="Exemple2"></a>

```wl
// Chargement du fichier XML
MonDoc est une chaîne
MonDoc = fChargeTexte("exemple.xml")
// Texte de la requête : Recherche de la balise Index
RequêteXPATH est une chaîne = "Aide/Fonction/Index"
// Création du document
XMLDocument("Exemple", MonDoc)
SI ErreurDétectée = Vrai ALORS
	Erreur("Le fichier n'est pas au format XML.")
	RETOUR
FIN
b est un booléen
b = XMLExécuteXPath("Exemple", RequêteXPATH)
// Est-ce une requête de calcul ?
SI b = Vrai ET XMLEnDehors("Exemple") = Vrai ALORS
	 // Voilà le résultat du calcul
	 Trace("XMLRésultat=" + XMLRésultat("Exemple"))
FIN
// Est-ce une requête XPath de sélection ?
SI XMLTrouve("Exemple") = Vrai ALORS
	// Parcours de la sélection résultat
	TANTQUE XMLTrouve("Exemple") = Vrai
		Trace(XMLPosition("Exemple"))
		XMLSuivant("Exemple")
	FIN
	// Parcours terminé, on termine la requête
	XMLAnnuleRecherche("Exemple")
FIN
```

<a name="XSYNTAXE"></a>
<a name="SYNTAXE1"></a>

## Syntaxe

### Exécuter une requête XPATH sur une variable de type xmlDocument

`<Résultat> = XMLExécuteXPath(<Document XML> , <Texte de la requête XPath>)`
---

**`<Résultat> : (Variable de type xpathRésultat)`**

Variable de type [xpathRésultat](../WDLang5/1000021967.md) contenant le résultat de la requête XPATH. 

Lecture du résultat : 

- La propriété **EstValeur** permet de savoir si le résultat est une valeur. 

- Pour une requête de sélection (le résultat n'est pas une valeur), il est nécessaire de parcourir la variable [xpathRésultat](../WDLang5/1000021967.md). 

- Pour une requête de calcul (le résultat est une valeur), la propriété **Valeur** permet de lire le résultat.




**`<Document XML> : (Variable de type xmlDocument)`**

Nom de la variable de type [xmlDocument](../WDLang5/1000018782.md) contenant le code XML à analyser.

**`<Texte de la requête XPath> : (Chaîne de caractères)`**

Texte de la requête XPath à exécuter.  


<a name="SYNTAXE2"></a>

### Exécuter une requête XPATH sur un document XML (syntaxe conservée par compatibilité)

`<Résultat> = XMLExécuteXPath(<Nom du Document XML> , <Texte de la requête XPath>)`
---

**`<Résultat> : (Booléen)`**



- <u><u><u><u>Vrai</u></u></u></u> si la requête a été exécutée, 

- <u><u><u><u>Faux</u></u></u></u> dans le cas contraire ou si aucun élément ne correspond à la requête.




Lecture du résultat : 

- Pour une requête de sélection, si la requête XPATH a été exécutée, les fonctions de parcours XML permettent de parcourir le résultat de la requête XPATH.

- Pour une requête de calcul, la fonction [XMLRésultat](../WDLang5/3081027.md) permet de lire le résultat.




**`<Nom du Document XML> : (Chaîne de caractères)`**

Nom du document XML manipulé. Ce document contient le code XML à analyser et a été créé avec la fonction [XMLDocument](../WDLang5/3081008.md).

**`<Texte de la requête XPath> : (Chaîne de caractères)`**

Texte de la requête XPath à exécuter.  



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/fr/UNIVERSALAPP.png)![Code Utilisateur (MCU)](https://doc.pcsoft.fr/ext/images/fr/MCU.png) 

### Syntaxe 2 : Exécuter une requête XPATH sur un document XML : Lecture du résultat de la requête
<a name="syntaxe_2_executer_une_requete_xpath_sur_document_xml_lecture_resultat_requete_ELTPARAGRAPHE000186"></a>Si la fonction **XMLExécuteXPath** renvoie <u><u><u><u>Vrai</u></u></u></u>, la requête a été correctement exécutée. Le résultat de la requête peut être lu de la manière suivante :

- **Requête de calcul** : la fonction [XMLTrouve](../WDLang5/3081020.md) renvoie <u><u><u><u>Faux</u></u></u></u>.
	Le résultat peut être lu avec la fonction [XMLRésultat](../WDLang5/3081027.md).
	Quelques exemples de requêtes de calcul :
	


|   |   |
| --- | --- |
| Comptage du nombre de balises dont le nom est "ITEM", c'est-à-dire le nombre d'articles différents dans la commande. | count(//ITEM) |
| Nombre de balises contenant un sous élément &lt;PRICE&gt; | count(//\*[PRICE]) |
| Total de la facture | sum(//ITEM[PRICE])- sum(//COUPON[PRICE]) |



- **Requête de sélection d'enregistrements** : la fonction [XMLTrouve](../WDLang5/3081020.md) renvoie <u><u><u><u>Vrai</u></u></u></u>.
	Lors d'une requête de sélection, la fonction **XMLExécuteXPath** change la position courante dans le document XML en cours pour se positionner sur le premier élément résultat de la sélection. Ensuite, il suffit d'utiliser la fonction [XMLSuivant](../WDLang5/3081030.md) pour parcourir la sélection.
	Quelques exemples de requêtes de sélection :
	


|   |   |
| --- | --- |
| Parcours de tous les éléments qui ont un attribut "unit". | //\*[@unit] |
| Parcours de toutes les balises dont le nom est "ITEM". | //ITEM |


	Pour annuler la requête et pouvoir parcourir à nouveau tout le document, utilisez la fonction [XMLAnnuleRecherche](../WDLang5/3081001.md).



<a name="NOTE0_2"></a>


### Limitations
<a name="limitations_ELTPARAGRAPHE000289"></a>

- Il n'est pas possible d'imbriquer plusieurs requêtes XPath de parcours.

- Syntaxe 1 : Exécuter une requête XPATH sur une variable de type [xmlDocument](../WDLang5/1000018782.md) : 

	- La liste des noeuds renvoyée n'est pas triée. 

	- La norme utilisée pour la requête XPath est XPath 1.0. 




- Syntaxe 2 : Exécuter une requête XPATH sur un document XML : 
	Les fonctions [XMLSauvePosition](../WDLang5/3081033.md) et [XMLRetourPosition](../WDLang5/3081032.md) ne permettent pas de sauver / restaurer une position lors du parcours du résultat d'une requête XPath de sélection.



<a name="NOTE0_3"></a>


### Fonctions XML et threads
<a name="fonctions_xml_threads_ELTPARAGRAPHE000477"></a>Si votre application utilise des threads, le document XML est partagé entre tous ces threads. Pour plus de détails sur la gestion des threads, consultez [Gestion des threads](../WDLang1/3077026.md).
Si la position courante dans un document XML est modifiée dans un thread, la position courante dans ce document XML est modifiée pour tous les threads.

<a name="XComposante"></a>

## Classification Métier / UI :
Code métier
## Composante :
wd280xml.dll
