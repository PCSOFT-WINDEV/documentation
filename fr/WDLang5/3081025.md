


## XMLRecherche (Fonction)

***En anglais : XMLFind***



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Lance une recherche dans un document XML. Cette recherche peut être annulée par la fonction [XMLAnnuleRecherche](../WDLang5/3081001.md).

Remarque : Pour effectuer une requête XPath, utilisez la fonction [XMLExécuteXPath](../WDLang5/3081015.md). 








<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
// Recherche à partir de la position courante
XMLRecherche("DocXML", "pri", XMLBalise + XMLSousElément, XMLCommencePar)
TANTQUE XMLTrouve("DocXML") = Vrai
	Info("Balise trouvée " + XMLNomElément("DocXML"))
	// Elément suivant dans la recherche
	XMLSuivant("DocXML")
FIN
XMLAnnuleRecherche("DocXML")
// Positionne à la racine du document
XMLRacine("DocXML")
// Recherche à l'identique à partir du début du document
XMLRecherche("DocXML", "prix", XMLBalise + XMLContinue, XMLExact)
TANTQUE XMLTrouve("DocXML") = Vrai
	Info("Balise trouvée " + XMLNomElément("DocXML"))
	// Elément suivant dans la recherche
	XMLSuivant("DocXML")
FIN
XMLAnnuleRecherche("DocXML")
```

<a name="XSYNTAXE"></a>
<a name="SYNTAXE1"></a>

## Syntaxe

`<Résultat> = XMLRecherche(<Document XML> , <Valeur recherchée> [, <Type de parcours> [, <Options de recherche>]])`
---

**`<Résultat> : (Booléen)`**



- <u><u><u><u>Vrai</u></u></u></u> si l'élément recherché est trouvé, 

- <u><u><u><u>Faux</u></u></u></u> dans le cas contraire.




**`<Document XML> : (Chaîne de caractères)`**

Nom du document XML manipulé. Ce document contient le code XML à analyser et a été créé avec la fonction [XMLDocument](../WDLang5/3081008.md).

**`<Valeur recherchée> : (Chaîne de caractères (avec guillemet) ou mot-clé NULL)`**

Valeur recherchée dans le document XML.

Si ce paramètre est égal à NULL, toutes les balises et attributs du document XML seront lues à partir de l'élément courant, en fonction des paramètres de parcours.

**`<Type de parcours> : (Constante (ou combinaison de constantes) optionnelle)`**

Type de parcours à réaliser : 


|   |   |
| --- | --- |
| *XMLAttribut* | Recherche sur les noms d'attributs. |
| *XMLBalise* | Recherche sur les noms des balises. |
| *XMLContinue* | <br><br>- Combinée avec *XMLBalise*, *XMLAttribut* ou *XMLValeur*, continue la recherche dans la suite du document en remontant dans les niveaux de l'arborescence (si nécessaire).<br><br>- Combinée avec *XMLSousElément*, continue la recherche dans la suite du document, en remontant et en redescendant dans les niveaux de l'arborescence.<br><br><br> |
| *XMLElément*<br>(Valeur par défaut) | Recherche sur les noms de balises et d'attributs (équivalent à la combinaison *XMLBalise* + *XMLAttribut*). |
| *XMLNiveauCourant* | Recherche uniquement dans le niveau courant de l'arborescence. |
| *XMLSousElément* | Recherche également dans toutes les balises filles. Doit être combiné avec *XMLBalise*, *XMLAttribut* ou *XMLValeur*. |
| *XMLValeur* | Recherche sur les valeurs des balises et des attributs. |



**`<Options de recherche> : (Constante (ou combinaison de constantes) optionnelle)`**

Options de recherche à prendre en compte : 


|   |   |
| --- | --- |
| *XMLAvecNamespace* | Recherche sur les éléments contenant un namespace.<br> |
| *XMLCommencePar* | Recherche sur les éléments commençant par la valeur recherchée. |
| *XMLContient* | Recherche sur les éléments contenant la valeur recherchée. |
| *XMLExact*<br>(valeur par défaut) | Recherche à l'identique et sensible à la casse. |
| *XMLIgnoreLaCasse* | Recherche sans tenir compte de la casse de la balise. Peut être combiné avec *XMLContient*, *XMLCommencePar* ou *XMLExact*. |





<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques


### Recherche et parcours de document XML
<a name="recherche_parcours_document_xml_ELTPARAGRAPHE000149"></a>La fonction **XMLRecherche** débute la recherche à partir de la position courante dans le document XML. Pour effectuer une recherche dans tout le document, il est conseillé d'utiliser la fonction [XMLRacine](../WDLang5/3081019.md) avant d'utiliser la fonction **XMLRecherche**.

La fonction **XMLRecherche** affecte le parcours en cours. Les fonctions [XMLSuivant](../WDLang5/3081030.md) et [XMLPrécédent](../WDLang5/3081023.md) se positionneront sur les éléments suivants et précédents correspondants à la recherche. Si la recherche échoue (aucun élément trouvé), la position en cours avant la recherche est conservée.

Pour annuler la recherche, utilisez la fonction [XMLAnnuleRecherche](../WDLang5/3081001.md). De plus, les fonctions [XMLPremier](../WDLang5/3081024.md), [XMLDernier](../WDLang5/3081013.md), [XMLRacine](../WDLang5/3081019.md), [XMLParent](../WDLang5/3081022.md) et [XMLFils](../WDLang5/3081012.md) annulent la recherche en cours.
<a name="NOTE0_2"></a>


### Fonctions XML et threads
<a name="fonctions_xml_threads_ELTPARAGRAPHE000340"></a>Si votre application utilise des threads, le document XML est partagé entre tous ces threads. Pour plus de détails sur la gestion des threads, consultez [Gestion des threads](../WDLang1/3077026.md).

Si la position courante dans un document XML est modifiée dans un thread, la position courante dans ce document XML est modifiée pour tous les threads.
<a name="NOTE0_3"></a>


### Suppression lors d'une recherche
<a name="suppression_lors_une_recherche_ELTPARAGRAPHE000203"></a>La fonction [XMLSupprime](../WDLang5/3081036.md) utilisée lors d'une recherche annule la recherche. 

Exemple à ne pas faire (ce code ne fonctionne pas) :

```wl
// Positionne à la racine du document
XMLRacine(:m_sNomDoc)
// Recherche tous les "txsr" à partir de la racine pour les supprimer
XMLRecherche(:m_sNomDoc, CParagraphe::CST_TAG , XMLBalise + XMLSousElément + XMLContinue, XMLExact)
TANTQUE XMLTrouve(:m_sNomDoc) = Vrai
	XMLSupprime(:m_sNomDoc)
	// Elément suivant dans la recherche
	XMLSuivant(:m_sNomDoc)
FIN
XMLAnnuleRecherche(:m_sNomDoc)
```
Le code à utiliser est le suivant :

```wl
// Suppression lors d'une recherche
// Positionne à la racine du document 
XMLRacine(:m_sNomDoc)
BOUCLE
	// Recherche tous les "txsr" à partir de la racine pour les supprimer 
	XMLRecherche(:m_sNomDoc, CParagraphe::CST_TAG , ...
					  XMLBalise + XMLSousElément + XMLContinue, XMLExact)
	SI XMLTrouve(:m_sNomDoc) = Vrai ALORS
		XMLSupprime (:m_sNomDoc)
	SINON
		SORTIR
	FIN
FIN
```


<a name="XComposante"></a>

## Composante :
wd280xml.dll
