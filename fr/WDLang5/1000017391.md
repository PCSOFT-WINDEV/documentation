
## Créer un flux RSS
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Présentation
<a name="presentation_ELTTEXTE000144"></a>
WINDEV, WEBDEV et WINDEV Mobile permettent de produire et/ou de consommer un flux RSS (Rapid Simple Syndication).

Un flux RSS permet de mettre à disposition (produire) un flux de données dans un format standard. Ce flux de données pourra être lu (consommé) par une application.

Un lecteur de flux RSS permet d'afficher un ou plusieurs flux RSS. 



<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Comment le faire ?
<a name="comment_faire_ELTTEXTE000168"></a>


### Créer un flux RSS
<a name="creer_flux_rss_ELTPARAGRAPHE000044"></a>Pour créer un flux RSS :

1. Déclarez des variables de type [rssFlux](../WDLang5/1000017795.md), [rssCanal](../WDLang5/1000017769.md) et [rssEntrée](../WDLang5/1000017792.md).

2. Décrivez le canal grâce aux propriétés du type [rssCanal](../WDLang5/1000017769.md).

3. Décrivez les entrées grâce aux propriétés du type [rssEntrée](../WDLang5/1000017792.md).

4. Ajoutez le canal puis ses entrées dans le flux.




**Remarques** : 

- Un flux est constitué de canaux. Un canal est constitué d'entrées.

- Il est conseillé d'utiliser les fonctions [Ajoute](../WDLang1/1000017348.md), [Supprime](../WDLang1/1000017354.md), … pour manipuler le tableau des entrées et le tableau des canaux.





<a name="NOTE2_2"></a>


### Exemples
<a name="exemples_ELTPARAGRAPHE000083"></a>Exemple de création d'un flux RSS  :


```wl
MonFlux est un rssFlux
MonCanal est un rssCanal
MonEntrée est un rssEntrée
MonChemin est une chaîne

// Renseignements du canal
MonCanal.Titre = "Mon site"
MonCanal.Description = "Exemple de flux RSS 2.0"
MonCanal.DateMiseAJour = DateSys() + HeureSys()
MonCanal.Lien = "http://www.example.org"
// Renseignements des entrées du canal 1
MonEntrée.Auteur = "pcsoft@pcsoft.fr"
MonEntrée.Description = "Première actualité"
MonEntrée.Titre = "Actualité N°1"
// Ajoute l'entrée dans le canal
Ajoute(MonCanal.Entrée, MonEntrée)
// Ajoute le canal dans le flux
Ajoute(MonFlux.Canal, MonCanal)

// Sauve le flux dans un fichier
MonChemin = ComplèteRep(fRepEnCours()) + "Rss.xml"
rssSauve(MonFlux, MonChemin)

// Affiche le fichier XML du flux
SI PAS LanceAppliAssociée(MonChemin) ALORS
	Erreur("LanceAppliAssociée('" + MonChemin + "'). '" + ErreurInfo() + "'")
FIN
```


**Attention** : Dans cet exemple, les ajouts réalisés avec la fonction [Ajoute](../WDLang1/1000017348.md) doivent être réalisés en dernier pour manipuler l'élément voulu. Il est également possible d'utiliser le code suivant : 


```wl
MonFlux est un rssFlux 
NumCanal est un entier
NumEntrée est un entier
MonChemin est une chaîne

MonChemin  = ComplèteRep(fRepEnCours()) + "Rss.xml"

// Description et ajout du canal
NumCanal = 1
NumCanal = Ajoute(MonFlux.Canal)
MonFlux.Canal[NumCanal].Titre = "Mon site"
MonFlux.Canal[NumCanal].Description = "Exemple de flux RSS 2.0"
MonFlux.Canal[NumCanal].DatePublication = DateDuJour()
MonFlux.Canal[NumCanal].Lien = "http://www.example.org"

// Description et ajout de l'entrée 1
NumEntrée = Ajoute(MonFlux.Canal[NumCanal].Entrée)
MonFlux.Canal[NumCanal].Entrée[NumEntrée].Titre = "Première actualité"
MonFlux.Canal[NumCanal].Entrée[NumEntrée].Description = "Actualité N°1"
MonFlux.Canal[NumCanal].Entrée[NumEntrée].Lien = "http://www.example.org"
MonFlux.Canal[NumCanal].Entrée[NumEntrée].DatePublication = DateDuJour()
NumEntrée = Ajoute(MonFlux.Canal[NumCanal].Entrée)

// Description et ajout de l'entrée suivante
MonEntree est un rssEntrée dynamique = MonFlux.Canal[NumCanal].Entrée[NumEntrée]
MonEntree.Titre = "Seconde actualité"
MonEntree.Description = "Actualité N°2"
MonEntree.Lien = "http://www.example.org"
MonEntree.DatePublication = DateDuJour()

// Sauve le flux dans un fichier
rssSauve(MonFlux, MonChemin)

// Affiche le fichier XML du flux
SI PAS LanceAppliAssociée(MonChemin) ALORS
	Erreur("LanceAppliAssociée('" + MonChemin + "'). '" + ErreurInfo() + "'")
FIN
```





- ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=LesfonctionsRSS.gif) ***Exemples unitaires (WINDEV)*** : **Les fonctions RSS** <br>Utilisation des fonctions RSS du WLangage.<br>Ces fonctions permettent de lire et de créer un flux.<br>Ces fonctions se manipulent avec des types de variables spécifiques : rssFlux, rssCanal,...


