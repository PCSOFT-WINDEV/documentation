
## Structure (Type de variable)

***En anglais : Structure (Type of variable)***
				



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Une structure est un type de données personnalisé. Une structure regroupe des éléments de types différents.

**Remarque** : Si vous manipulez des variables personnalisées, il est conseillé de déclarer :

- un type ***structure***, si plusieurs variables de ce type sont utilisées dans différents traitements du projet.

- une ***variable composée***, si une seule variable de ce type est utilisée dans votre projet (par exemple utilisation ponctuelle pour passer des paramètres à une API).





<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
// Déclarer une structure
RefProduit est une structure
	CodeF est un entier
	CodePr est une chaîne fixe sur 10
FIN
```
<a name="Exemple2"></a>

```wl
// Déclarer une variable de type structure
RefProduit est une structure
	CodeF est un entier
	CodePr est une chaîne fixe sur 10
FIN
Fauteuil est une RefProduit
```
<a name="Exemple3"></a>

```wl
// Manipuler un membre d'une variable de type structure
RefProduit est une structure
	CodeF est un entier
	CodePr est une chaîne fixe sur 10
FIN
Fauteuil est une RefProduit
Fauteuil:CodeF = 7
Fauteuil:CodePr = "Meuble"
```
<a name="Exemple4"></a>

```wl
// Déclarer et initialiser les membres d'une structure
point3D est une structure
	x est un entier
	y est un entier
	z est un entier
FIN

p1 est un point3D = [0, 0, 50]
```

<a name="XSYNTAXE"></a>
<a name="SYNTAXE1"></a>

## Syntaxe

### Déclarer une structure

`<Nom de la structure> est une structure
    <Membres de la structure>
FIN
`
---

**`<Nom de la structure> : ()`**

Nom de la structure à déclarer

**`<Membres de la structure> : ()`**

Déclarations des variables contenues dans la structure. Ces variables sont appelées les ***membres*** de la structure.

Lors de la déclaration des membres de la structure, il est possible d'indiquer les paramètres de sérialisation de ces membres. Pour plus de détails, consultez le paragraphe ["Sérialisation des membres d'une structure"](#NOTE0_4). 

**`<FIN> : ()`**

Fin de déclaration des variables.  


<a name="SYNTAXE2"></a>

### Déclarer une variable de type structure

`<Nom de la variable> est une <Nom de la structure>
`
---

**`<Nom de la variable> : ()`**

Nom de la variable de type structure à déclarer.

**`<Nom de la structure> : ()`**

Nom d'une structure précédemment déclarée.  


<a name="SYNTAXE3"></a>

### Manipuler un membre d'une variable de type structure

`<Nom de la variable structure>:<Nom du membre de la structure>
`
---

**`<Nom de la variable structure> : ()`**

Nom de la variable structure à manipuler.

**`<Nom du membre de la structure> : ()`**

Nom du membre de la structure à manipuler.

Il est possible d'affecter les membres d'une variable de type structure grâce à [l'affectation multiple](../Motscles/1514031.md).



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques




### Déclaration de structures
<a name="declaration_structures_ELTPARAGRAPHE000109"></a>Si une structure &lt;Nom de la structure&gt; est déclarée :

- ***dans le code du projet***, il sera possible de déclarer une variable du type &lt;Nom de la structure&gt; dans l'ensemble du projet.

- ***dans le code de déclaration des globales d'une fenêtre***, il sera possible de déclarer une variable du type &lt;Nom de la structure&gt; dans cette fenêtre, dans les champs de cette fenêtre et dans les procédures locales associées.

- *** dans le code d'ouverture d'un état***, il sera possible de déclarer une variable du type &lt;Nom de la structure&gt; dans cet état, dans les champs de cet état et dans les procédures locales associées.

- ***dans le code de déclaration d'une classe***, il sera possible de déclarer une variable du type &lt;Nom de la structure&gt; dans cette classe et dans les méthodes de cette classe. Attention : il est nécessaire de placer "Est une structure" avant le "est une classe".

- ***dans un traitement***, il sera possible de déclarer une variable du type &lt;Nom de la structure&gt; dans ce traitement.






![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png) La structure doit être déclarée dans un traitement de déclaration des globales (d'une fenêtre, d'une page ou d'une collection de procédures).


<a name="NOTE0_2"></a>




### Type des membres d'une structure
<a name="type_des_membres_une_structure_ELTPARAGRAPHE000137"></a>Les membres d'une structure peuvent être de type :

- [Entier](../Motscles/1514049.md) (ou sélectionné parmi les [entiers avancés](../Motscles/1514049.md)).

- [Réel](../Motscles/1514048.md) (ou sélectionné parmi les [réels avancés](../Motscles/1514048.md)).

- [Monétaire](../Motscles/1514043.md).

- [Chaîne](../Motscles/1514044.md) (ou sélectionné parmi les [chaînes avancées](../Motscles/1514044.md)).

- Tableau ([tableau "simple"](../Motscles/1514030.md), [tableau dynamique](../Motscles/1514057.md) ou [tableau fixe](../Motscles/1514025.md)).

- [Automation](../Motscles/1514014.md) ou [Automation Dynamique](../Motscles/1514015.md).

- [Classe](../POO/6010006.md). Cette classe doit posséder un constructeur sans paramètre (ou avec des paramètres facultatifs).

- [Variant](../Motscles/1514019.md).




Tous les autres types de données (variable composée, constante, ...) sont interdits.




<a name="NOTE0_3"></a>




### Déclarer un membre de type tableau dans une structure
<a name="declarer_membre_type_tableau_dans_une_structure_ELTPARAGRAPHE000198"></a>

- ***Déclarer un membre de type tableau simple*** :
	Les dimensions du tableau sont fixées dès la déclaration. Il est possible de modifier les dimensions du tableau avec la fonction [Dimension](../WDLang1/3013022.md).

- ***Déclarer un membre de type tableau dynamique*** : 
	Il est nécessaire de définir les dimensions du tableau (c'est-à-dire d'allouer le tableau) avant d'utiliser le tableau. Par exemple :
	
	```wl
	// Déclaration d'une structure
	Struct est une structure
		x1 est un entier
		x2 est un tableau dynamique // Déclaration du tableau
	FIN
	 
	// Déclaration d'une variable de type structure
	MaStruct est une struct
	 
	// Allocation du tableau
	MaStruct:x2 = allouer un tableau dynamique de 4,7 entiers
	// Utilisation du tableau
	MaStruct:x2[1,1] = 25
	```








<a name="NOTE0_3b"></a>




### Copie homonymique d'une structure
<a name="copie_homonymique_une_structure_ELTPARAGRAPHE000217"></a>L'opérateur **=>** permet de réaliser une copie homonymique d'une variable de type structure. 

Cette copie homonymique est également réalisée en utilisant l'opérateur **=**. 

Attention, la copie s'effectue **membre à membre** : seuls les membres de même nom seront copiés. Les membres de noms différents ou inexistants seront ignorés.

Exemple :

```wl
// Déclaration d'une structure
StructClientImport est une structure
	Nom est une chaîne
	Prénom est une chaîne
	Société est une chaîne
	Adresse est une chaîne
	CP est une chaîne
	Ville est une chaîne
FIN

StructClientInterne est une structure
	Nom est une chaîne
	Prénom est une chaîne
	Société est une chaîne
	Adresse est une chaîne
	AdresseComp est une chaîne
	CP est une chaîne
	Ville est une chaîne
	Pays est une chaîne
FIN

 
NouveauClient est un StructClientImport 

ClientEnCours est un StructClientInterne 

ClientEnCours = NouveauClient // les membres AdresseComp, Pays se seront pas renseignés
```





<a name="NOTE0_4"></a>


### Sérialisation des membres d'une structure
<a name="serialisation_des_membres_une_structure_ELTPARAGRAPHE000237"></a>Par défaut, tous les membres d'une structure sont sérialisés. 

Il est possible de gérer précisément la sérialisation de chaque membre d'une structure : 

- en indiquant quel membre sera sérialisé lors de l'appel à la fonction [Sérialise](../WDLang1/3013065.md). 
	Cette opération est possible sur tous les types de sérialisation (WXML, JSON, binaire). 

- en changeant le nom du membre lors de la sérialisation par la fonction [Sérialise](../WDLang1/3013065.md).  
	Cette opération n'est pas possible lors d'une sérialisation binaire. 




Cette gestion de la sérialisation est réalisée en utilisant la syntaxe suivante : 

- Sérialisation ou non d'un membre : 
	
	```txt
	<Nom du membre> est un <Type du membre> [, Sérialise = <Vrai / Faux>]
	```


- Sérialisation et changement du nom d'un membre : 
	
	```txt
	<Nom du membre> est un <Type du membre> [, Sérialise = <Nouveau nom>]
	```





Exemple : 


```wl
Cl est une structure
	// Membre sérialisé
	MembreSérialisé est un entier 
	// Membre non sérialisé
	MembreNonSérialisé est une chaîne, Sérialise = Faux  
	// Membre renommé lors de la sérialisation
	MembreRenommé est un entier, Sérialise = "NouveauNomMembre" 
FIN
```

<a name="NOTE0_5"></a>




### Quels types "avancés" peuvent être membre ?
<a name="quels_types_avances_peuvent_etre_membre_ELTPARAGRAPHE000273"></a>Une variable "avancée" peut être membre d'une variable "avancée". Le tableau ci-dessous présente les différentes combinaisons :

| ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=Fleche_1.gif)<br> | Variable composée | Structure | Classe | Tableau |
| --- | --- | --- | --- | --- |
| **Variable composée** | Non | Non | Non | Non |
| **Structure** | Oui | Oui | Oui | Oui |
| **Classe** | Oui | Oui | Oui | Oui |
| **Tableau** | Oui | Oui | Oui | Non |




<a name="NOTE0_6"></a>




### Passer une structure en paramètre à une procédure
<a name="passer_une_structure_parametre_une_procedure_ELTPARAGRAPHE000333"></a>Une variable de type "structure" peut être passée en paramètre à une procédure. Pour cela, utilisez la syntaxe suivante : 

```txt
<Nom de la procédure>(<Nom de la variable de type structure>)
```


Par exemple :


```wl
RefProduit est une structure
	CodeF est un entier
	CodePr est une chaîne fixe sur 10
FIN
Fauteuil est une RefProduit
 
// Appel de la procédure AfficheProduit
AfficheProduit(Fauteuil)
```

<a name="NOTE0_6_1"></a>


### Ajouter le contenu d'une structure dans un tableau de structures sans passer par une variable de cette structure
<a name="ajouter_contenu_une_structure_dans_tableau_structures_sans_passer_par_une_variable_cette_structure_ELTPARAGRAPHE000348"></a>Si vous utilisez un tableau de structures avec peu de membres, il peut être plus simple d'ajouter directement une structure en utilisant l'opérateur [ ] (crochet). 
Par exemple, pour la structure suivante :

```wl
// Structure pour mémoriser une lettre et son code ASCII
STTouche est une Structure
	sTouche est une chaîne
	nCodeAscii est un entier
FIN
// Tableau de structures
tabTouches est un tableau de STTouche
```

L'ajout se fait généralement en passant par une variable du type de la structure :

```wl
// Structure temporaire pour l'ajout
stUneTouche est un STTouche
// Mémorise la lettre A
stUneTouche.sTouche = "A"
stUneTouche.nCodeAscii = Asc("A")
tabTouches.Ajoute(stUneTouche)
```

Avec l'opérateur [ ], vous pouvez gagner en visibilité :

```wl
// Mémorise la lettre A
tabTouches.Ajoute(["A", Asc("A")])
```







<a name="NOTE0_7"></a>


### Limites d'une structure
<a name="limites_une_structure_ELTPARAGRAPHE000384"></a>

- L'héritage de structure est interdit : une structure ne peut pas hériter d'une autre structure.

- Il n'est pas possible de restreindre l'accès à un membre d'une structure : tous les membres d'une structure sont publics.



<a name="NOTE0_8"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png) 

### Différences entre la version 8 et la version 9
<a name="differences_entre_version_8_version_9_ELTPARAGRAPHE000394"></a>Une structure contenant un membre de type caractère n'a pas le même comportement que dans les versions précédentes. En effet, les caractères occupaient 1 octet en version 8 et 2 octets en version 9.

Il est conseillé d'utiliser le type "Entier sur un octet" pour assurer un bon fonctionnement.


