
## Lambda : Expressions et procédures
			

<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Présentation
<a name="presentation_ELTTEXTE000212"></a>
Une lambda ("lambda fonction") est une écriture plus concise des procédures internes.

Les lambdas sont utiles pour toutes les fonctions qui prennent des procédures de type "callback" en paramètre : il est ainsi possible de saisir directement le code de la fonction à la place du paramètre. Dans ce cas : 

- la procédure n'a pas de nom, 

- à l'endroit où l'on devrait trouver le nom de la procédure, on trouve directement le code de la procédure. 




Le WLangage propose plusieurs syntaxes pour utiliser des lambdas. 

<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Procédure lambda
<a name="procedure_lambda_ELTTEXTE000236"></a>
Une procédure lambda est une procédure interne anonyme, pour laquelle les paramètres et la valeur de retour n'ont pas besoin d'être typés. 


### Exemple
<a name="exemple_ELTPARAGRAPHE000026"></a>
```wl
// Remplir un tableau avec les noms de tous les fichiers du répertoire des données
tabFichier est un tableau de chaînes
fListeFichier(fRepDonnées() + "\*.*", (Répertoire,Fichier)=>{tabFichier.Ajoute(Fichier)} )
```


Dans ce code, la procédure lambda correspond au code suivant : 
```txt
(Répertoire,Fichier)=>{tabFichier.Ajoute(Fichier)}
```





### Syntaxes
<a name="syntaxes_ELTPARAGRAPHE000035"></a>

- Procédure lambda n'attendant aucun paramètre : 
	
	```txt
	() => { <Code WLangage de la procédure>}
	```


- Procédure lambda attendant un seul paramètre : 

	- syntaxe complète : 
			
		```txt
		(<Paramètre 1>) => { <Code WLangage de la procédure>}
		```


	- syntaxe simplifiée : 
			
		```txt
		<Paramètre 1> => { <Code WLangage de la procédure>}
		```





- Procédure lambda attendant plusieurs paramètres : 
	
	```txt
	(<Paramètre 1>, ..., <Paramètre N>) => { <Code WLangage de la procédure>}
	```





Dans ces syntaxes : 

- &lt;Paramètre X&gt; correspond au nom du paramètre. 

- &lt;Code WLangage de la procédure&gt; correspond au code WLangage de la procédure. Il est délimité par les signes { et }. Il est possible d'utiliser plusieurs instructions 





### Remarques
<a name="remarques_ELTPARAGRAPHE000058"></a>

- Cette syntaxe peut être utilisée sur les fonctions du WLangage ([fListeFichier](../WDLang1/3036058.md), ...) mais aussi sur les procédures du projet. Pour cela, le paramètre formel auquel la procédure lambda est passée doit être de type "Procédure".

- La procédure est anonyme, elle n'a donc pas de nom et ne peut pas être appelée ailleurs ou récursivement.

- Une procédure lambda n'a pas besoin de récupérer tous les paramètres :

	- les paramètres inutilisés ne provoquent pas de warning à la compilation,

	- les paramètres omis à la fin du prototype ne provoquent pas d'erreur à l'exécution.









<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Expression lambda
<a name="expression_lambda_ELTTEXTE000278"></a>
Une expression lambda est une simplification d'une procédure lambda lorsque le code WLangage à exécuter correspond à : 

```txt
RENVOYER <Expression>
```



### Exemple
<a name="exemple_ELTPARAGRAPHE000081"></a>

- Exemple utilisant une procédure lambda : 
	
	```wl
	// compte les éléments impairs d'un tableau 
	tabValeur est un tableau d'entiers = [1,2,3,4,5]
	Trace(CompteValeurs(tabValeur, x=>{RENVOYER Modulo(x,2)=1}))
	
	PROCÉDURE CompteValeurs(tabValeur est un tableau d'entiers,
		Condition est une Procédure(x entier):booléen)
	soit n = 0
	POUR TOUT x DE tabValeur
		SI Condition(x) ALORS
			n++
		FIN
	FIN
	RENVOYER n
	```






- Simplification en expression lambda : 
	
	```wl
	// compte les éléments impairs d'un tableau 
	tabValeur est un tableau d'entiers = [1,2,3,4,5]
	Trace(CompteValeurs(tabValeur, x=>Modulo(x,2)=1))
	
	PROCÉDURE CompteValeurs(tabValeur est un tableau d'entiers,
		Condition est une Procédure(x entier):booléen)
	soit n = 0
	POUR TOUT x DE tabValeur
		SI Condition(x) ALORS
			n++
		FIN
	FIN
	RENVOYER n
	```








### Syntaxes
<a name="syntaxes_ELTPARAGRAPHE000095"></a>

- Expression lambda n'attendant aucun paramètre : 
	
	```txt
	() => <Expression WLangage>
	```


- Expression lambda attendant un seul paramètre : 

	- syntaxe complète : 
			
		```txt
		(<Paramètre 1>) => <Expression WLangage>
		```


	- syntaxe simplifiée : 
			
		```txt
		<Paramètre 1> => <Expression WLangage>
		```





- Expression lambda attendant plusieurs paramètres : 
	
	```txt
	(<Paramètre 1>, ..., <Paramètre N) => <Expression WLangage>
	```





Dans ces syntaxes : 

- &lt;Paramètre X&gt; correspond au nom du paramètre. 

- &lt;Expression WLangage&gt; correspond à l'expression WLangage à renvoyer. 





### Remarque
<a name="remarque_ELTPARAGRAPHE000118"></a>Cette syntaxe peut être utilisée sur les fonctions du WLangage ([fListeFichier](../WDLang1/3036058.md), ...) mais aussi sur les procédures du projet. Pour cela, le paramètre formel auquel l'expression lambda est passée doit être de type "Procédure".







<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Procédure lambda typée
<a name="procedure_lambda_typee_ELTTEXTE000320"></a>
Une procédure lambda typée est une procédure interne anonyme, pour laquelle les paramètres et la valeur de retour peuvent être typés.  


### Exemple
<a name="exemple_ELTPARAGRAPHE000135"></a>
```wl
// Remplir un tableau avec 
// les noms de tous les fichiers du répertoire des données
tabFichier est un tableau de chaînes
fListeFichier(fRepDonnées() + "\*.*", ...
	Procédure(Répertoire est une chaîne, Fichier est une chaîne):chaîne {tabFichier.Ajoute(Fichier)})
```





### Syntaxe
<a name="syntaxe_ELTPARAGRAPHE000141"></a>
```txt
PROCEDURE (<Paramètre 1>, ..., <Paramètre N>) : <Type de retour> { <Code WLangage de la procédure>}
```


Dans cette syntaxe : 

- &lt;Paramètre X&gt; correspond au nom du ou des paramètres de la procédure lambda. Ce paramètre peut être typé ou non. Si un paramètre est typé, sa syntaxe est 
	
	```txt
	<Identifiant> [est] <Type>
	```


- &lt;Type de retour&gt; correspond au type renvoyé par la procédure lambda. 

- &lt;Code WLangage de la procédure&gt; correspond au code WLangage de la procédure. Il est délimité par les signes { et }.  





### Remarques
<a name="remarques_ELTPARAGRAPHE000154"></a>

- Cette syntaxe peut être utilisée sur les fonctions du WLangage ([fListeFichier](../WDLang1/3036058.md), ...) mais aussi sur les procédures du projet. Pour cela, le paramètre formel auquel la procédure lambda est passée doit être de type "Procédure".

- La procédure est anonyme, elle n'a donc pas de nom et ne peut pas être appelée ailleurs ou récursivement.

- Une procédure lambda n'a pas besoin de récupérer tous les paramètres :

	- les paramètres inutilisés ne provoquent pas de warning à la compilation,

	- les paramètres omis à la fin du prototype ne provoquent pas d'erreur à l'exécution.










