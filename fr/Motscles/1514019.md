
## Variant (Type de variable)

***En anglais : Variant***
				



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Le type Variant permet de :

- stocker n'importe quelle valeur **de type simple** : booléen, numérique (Monétaire, Réel, Entier, ...), caractères et chaîne de caractères, date (Date, Heure, DateHeure et Durée), ...

- stocker des sous-éléments nommés ou indicés.

- stocker n'importe quelle valeur **de type complexe** : structures, classes, types avancés, tableaux, tableaux associatifs, files, piles et listes.

- manipuler la valeur NULL dans le WLangage.

- stocker des interactions avec les objets ActiveX et la programmation Automation.







<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
nValeur est un variant = 10 
nValeur = SAI_Saisie1 
nValeur = Client.Nom
```



<a name="XSYNTAXE"></a>
<a name="SYNTAXE1"></a>

## Syntaxe

### Déclaration et affectation d'un type Variant

`<Nom de la variable> est un Variant = <Valeur>
 
OU 

 
  <Nom de la variable> est un Variant
  <Nom de la variable> = <Valeur>
`
---

**`<Nom de la variable> : ()`**

Nom de la variable à déclarer.

**`<Valeur> : ()`**

Valeur à affecter à la variable. Une variable de type variant peut être affectée avec :

- n'importe quelle valeur littérale,

- le contenu d'une variable,

- le contenu d'un champ de type simple,

- le contenu d'une rubrique.  






<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques




### Type Variant et NULL
<a name="type_variant_null_ELTPARAGRAPHE000089"></a>Pour spécifier qu'une variable de type Variant ne contient pas de valeur, il suffit d'utiliser la constante NULL.

Remarques : 

- pour un type variant, NULL signifie "Pas affecté".

- pour un type numérique, NULL signifie "égal 0".





```wl
nVal est un entier
SI nVal = Null ALORS ... // le test renvoie Vrai car nVal=0
nVal = 5
SI nVal = Null ALORS ... // le test renvoie Faux car nVal=5
```



```wl
vVal est un Variant
SI vVal = Null ALORS ... // renvoie Vrai car le variant n'est pas affecté
vVal = 0
SI vVal = Null ALORS ... // renvoie Faux car le variant est affecté   
	// avec un entier de valeur 0
vVal = 5
SI vVal = Null ALORS ... // renvoie Faux car le variant est affecté
	// avec un entier de valeur 5
```



```wl
vVal est un Variant
vVal = Null
SI vVal = 0 ALORS ... // renvoie Vrai car une variable non affectée 
// et une valeur ne sont pas comparables, Vrai est retourné par convention
```



<a name="NOTE0_2"></a>




### Type d'un variant
<a name="type_variant_ELTPARAGRAPHE000109"></a>Il est possible de connaître le type d'un variant grâce à la propriété [Type](../Proprietes/2510131.md). La propriété [Type](../Proprietes/2510131.md) permet de connaître le type de la valeur stockée dans la variable.

Remarques : 

- La fonction [VariantConvertit](../WDLang1/3013054.md) permet de convertir le type de la valeur stockée dans une variable de type Variant.

- La fonction [TypeVar](../WDLang1/3013056.md) permet de connaître le type d'une variable (type Variant pour une variable Variant). 





```wl
vVal est un Variant
TypeVar(vVal)  // renvoie le numéro du type variant
vVal..Type     // renvoie le type de la valeur stockée dans le variant
```

<a name="NOTE0_3"></a>




### Propriété Classe sur les variants
<a name="propriete_classe_sur_les_variants_ELTPARAGRAPHE000136"></a>La propriété [Classe](../Proprietes/2514004.md) sur les variables de type Variant permet d'obtenir le nom de la classe utilisée si le variant correspond à une classe.


<a name="NOTE0_4"></a>


### Sous-éléments nommés
<a name="souselements_nommes_ELTPARAGRAPHE000149"></a>Il est possible d'utiliser directement et sans déclaration des membres sur une variable de type Variant.

**Lors d'une affectation d'un membre**, si le membre n'existe pas, il est automatiquement créé ; si le membre existe déjà, il est modifié.

Exemple : 


```wl
Personne est un Variant
Personne.Nom = "DUPONT"
Personne.Prénom = "Michel"
```


**Lors d'une lecture d'un membre**, si le membre n'existe pas, il n'est pas créé. Il est possible de tester la présence d'un membre grâce à la propriété **Existe**.
Exemple : 

```wl
Personne est un Variant
SI PAS Personne.Nom..Existe ALORS
	Erreur("Le nom n'a pas été renseigné")
FIN
```


Si le membre n'existe pas, la valeur renvoyée est Null. Pour tester explicitement la valeur Null, plusieurs possibilités : 

- utiliser la propriété **Valeur**. 
	Exemple : 
	
	```wl
	Elément est un variant
	SI Elément.MembreInexistant..Valeur = Null ALORS
		...
	FIN
	```


- tester directement la valeur Null. Exemple : 
	
	```wl
	Elément est un variant
	SI Elément.MembreInexistant = Null ALORS
		...
	FIN
	```





Les sous-éléments nommés peuvent être manipulés grâce aux propriétés suivantes : 


|   |   |
| --- | --- |
| **Existe** | Renvoie : <br><br>- <u><u><u><u>Vrai</u></u></u></u> si l'élément existe, <br><br>- <u><u><u><u>Faux</u></u></u></u> si l'élément n'existe pas. L'élément n'est pas créé. <br><br><br> |
| **Nom** | Nom de l'élément |
| **Type** | Type de l'élément (mêmes valeurs que [TypeVar](../WDLang1/3013056.md)). |
| **Valeur** | Valeur de l'élément. |


La propriété [Membre](../Proprietes/1000021078.md) sur les variables de type Variant permet d'obtenir le tableau des éléments nommés. Ce tableau peut être manipulé par POUR TOUT, la propriété [Occurrence](../Proprietes/2510097.md), l'opérateur [ ], ... 
Il est également possible de supprimer un élément. Exemple : 

```wl
o est un JSON
o.m = 1
o.n = 2
Trace(o)

x est un Variant = o
Supprime(x..Membre, 1)
Trace(x) // Le membre m a été supprimé.
```



<a name="NOTE0_5"></a>


### Sous-éléments indicés
<a name="souselements_indices_ELTPARAGRAPHE000236"></a>Il est possible d'utiliser directement la variable de type variant comme un tableau de variants. L'utilisation de l'opérateur [ ] crée automatiquement le tableau.

Exemple : 


```wl
Jours est un Variant
Jours[1] = "lundi"
Jours[2] = "mardi"
Jours[3] = "mercredi"
Jours[4] = "jeudi"
Jours[5] = "vendredi"
Jours[6] = "samedi"
Jours[7] = "dimanche"
```


Les opérations des tableaux peuvent être directement effectuées sur la variable de type Variant : POUR TOUT, [Occurrence](../Proprietes/2510097.md), opérateur [ ], ...


<a name="NOTE0_6"></a>


### Imbrications des sous-éléments nommés et indicés
<a name="imbrications_des_souselements_nommes_indices_ELTPARAGRAPHE000261"></a>Les sous-éléments nommés et les sous-éléments indicés étant eux-mêmes de type Variant, il est possible de les imbriquer récursivement.

Exemple : 


```wl
Bibliotheque est un Variant
Bibliotheque.Livre[1].Titre = "Les trois mousquetaires"
Bibliotheque.Livre[1].Auteur = "Alexandre Dumas"
Bibliotheque.Livre[2].Titre = "Les misérables"
Bibliotheque.Livre[2].Auteur = "Victor Hugo"
```





