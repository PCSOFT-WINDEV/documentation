
## Opérateurs de comparaison
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Présentation
<a name="presentation_ELTTEXTE000511"></a>
Les opérateurs de comparaison peuvent être divisés en plusieurs catégories :


|   |   |
| --- | --- |
| **Egalité** | <br><br>- Egalité stricte : =<br><br>- Egalité souple : ~=<br><br>- Egalité très souple : ~~ <br><br>- Commence par : [=<br><br><br> |
| **Comparaison** | <br><br>- Différent : &lt;&gt;<br><br>- Inférieur ou égal : &lt;=<br><br>- Supérieur ou égal : &gt;=<br><br>- Strictement inférieur : &lt;<br><br>- Strictement supérieur : &gt;<br><br>- Commence par : [=<br><br><br><br><br>- Commence par : [~ (ignore les espaces en début de chaîne et la casse).<br>	<br><br>- Commence par : [~~ (ignore tous les espaces, la ponctuation et la casse).<br>	<br><br>- Comparaison à une liste de valeurs : DANS<br><br>- Contient : [=]<br>	<br><br>- Contient : [~] (ignore la casse et les espaces situés en début et en fin de chaîne).<br>	<br><br>- Contient : [~~] (ignore tous les espaces, la ponctuation et la casse)<br>	<br><br>- Termine par : =]<br>	<br><br>- Termine par : ~] (ignore les espaces en fin de chaîne et la casse)<br>	<br><br>- Termine par : ~~] (ignore les espaces, la ponctuation et la casse)<br>	<br><br><br> |
| **Intervalle de comparaison** | <br><br>- Strictement compris entre : &lt; .. &lt; (et inversement)<br>	Valeur1 &lt; x &lt; Valeur2<br><br>- Compris entre : &lt; .. &lt;= ET &lt; .. = &lt; (et inversement)<br>	Valeur1 &lt;= x ET x &lt;= Valeur2<br><br>- Compris entre (bornes comprises) : &lt;= &lt;= (et inversement)<br>	Valeur1 &lt;= x &lt;= Valeur2<br><br>- Compris entre (bornes comprises) : x A y <br><br><br> |

Les opérateurs de comparaison peuvent être utilisés avec tous les types d'opérandes.

Le résultat d'une expression de comparaison est de type booléen.





<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Détail
<a name="detail_ELTTEXTE000535"></a>


### Égalité souple et égalité très souple
<a name="egalite_souple_egalite_tres_souple_ELTPARAGRAPHE000268"></a>L'égalité souple (~=) agit uniquement sur les chaînes de caractères (sauf les chaînes fixes). Cet opérateur permet de :

- ne pas différencier les caractères majuscules des caractères minuscules,

- ne pas tenir compte des espaces situés avant et après la chaîne à tester,

- ne pas tenir compte des caractères accentués minuscules.




Pour en plus ne pas tenir compte des espaces, des ponctuations et des Retours Chariots (RC) à l'intérieur des chaînes, utilisez l'égalité très souple (~~).

**Équivalence HFSQL** : Pour retrouver l'équivalence à l'égalité très souple lors d'une recherche sur une clé texte dans un fichier de données HFSQL, il est nécessaire de configurer les options suivantes lors de la description de la rubrique dans l'analyse :
<br>![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=Indexation%20des%20m%E9mos%20-%20HC%20N%B0002.gif)





```wl
"Dupond" = "DUPOND"                // Retourne Faux
"Dupond" ~= "DUPOND"                // Retourne Vrai
" Dupond" ~= "DUPOND"               // Retourne Vrai
" Dupond" ~= "Dupond"              // Retourne Vrai
"C'est l'été" ~= "C'est l'ete"     // Retourne Vrai 
"Ent. S.A.R.L" ~~ "Ent SARL"        // Retourne Vrai
```

<a name="NOTE2_2"></a>


### Egalité et comparaison : Réel contenant plus de 6 décimales de différence
<a name="egalite_comparaison_reel_contenant_plus_6_decimales_difference_ELTPARAGRAPHE000294"></a>Le test d'égalité de deux réels est effectué en ne tenant compte que des 6 premières décimales. En effet, les erreurs d'arrondi dues au codage interne des réels nécessitent un test spécifique.

Ce test doit être réalisé en comparant la différence des deux valeurs à tester avec une valeur de référence. Selon le type de votre application, cette valeur peut être égale à 0,00001 ou moins encore.

Cette gestion d'arrondi n'est pas spécifique à WINDEV, WEBDEV et WINDEV Mobile. Elle est générale à tous les langages manipulant des réels sous forme binaire.


```wl
Diff, R1, R2 sont des réels
Diff = 0.00001
SI Abs(1-(R1/R2)) < Diff ALORS
	// R1 et R2 sont égaux
SINON
	// R1 et R2 sont différents
FIN
```



<a name="NOTE2_3"></a>


### Intervalles de comparaison
<a name="intervalles_comparaison_ELTPARAGRAPHE000308"></a>Les intervalles de comparaison permettent de simplifier l'écriture de comparaisons complexes. 

Quelques exemples de comparaisons identiques : 

- Exemple 1 :  

	- SI x&gt;5 ET x&lt;10 ALORS ...

	- SI 5&lt;x&lt;10 ALORS ...




- Exemple 2 : 

	- SI x&gt;=5 ET x&lt;=10 ALORS ...

	- SI x=5 A 10 ALORS ...
			![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png) Non supporté.








```wl
MonTableau est un tableau de 5 chaînes
I est un entier
MonTableau[1] = "Dupond"
MonTableau[2] = "Aida"
MonTableau[3] = "Parapoline"
MonTableau[4] = "Moulin"
MonTableau[5] = "Foolamour"
POUR I = 1 A 5
	SI "B" < MonTableau[I] <= "M" ALORS Trace(MonTableau[I])
	// Affiche Dupond et Foolamour
FIN
```



<a name="NOTE2_4"></a>


### Opérateurs de comparaison et UNICODE
<a name="operateurs_comparaison_unicode_ELTPARAGRAPHE000333"></a>Les opérateurs disponibles sont :

- "=" : Egalité stricte

- "&lt;&gt;" : Différence

- "[=" : Commence par

- "[~" : Commence par en ignorant les caractères les espaces en début de chaîne et en ignorant la casse.

- "[~~" : Commence par en ignorant tous les espaces, les caractères de ponctuation et la casse. 

- Contient : [=]

- Contient : [~] : Contient en ignorant la casse. 

- Contient : [~~] : Contient en ignorant tous les espaces, les caractères de ponctuation et la casse. 

- Termine par : =]

- Termine par : ~] : Termine par en ignorant les espaces en fin de chaîne et en ignorant la casse. 

- Termine par : ~~] : Termine par en ignorant les espaces, la ponctuation et la casse. 




- "&lt;", "&lt;=", "&gt;=", "&gt;" : Strictement inférieur, inférieur ou égal, supérieur ou égal, strictement supérieur 




Il est possible d'utiliser des chaînes ANSI, Unicode et des buffers dans les différents paramètres de la comparaison.

La règle de conversion utilisée est la suivante pour les systèmes Ansi (Windows ou Linux) :

- Si l'une au moins des chaînes est un buffer, toutes les chaînes sont converties en buffer, et l'opération est réalisée avec des buffers, 

- Si la première condition n'est pas remplie, si l'une au moins des chaînes est Unicode, toutes les chaînes sont converties en Unicode et l'opération est réalisée en Unicode (si nécessaire, la conversion est réalisée avec l'alphabet courant),

- Sinon, l'opération est réalisée en Ansi.




La règle de conversion utilisée est la suivante pour les systèmes Unicode (Windows CE) :

- Si l'une au moins des chaînes est un buffer, toutes les chaînes sont converties en buffers et l'opération est réalisée avec des buffers,

- Sinon, l'opération est réalisée en Unicode.




Pour plus de détails sur la gestion de l'Unicode, consultez [WINDEV et l'Unicode](../WDLang1/3024038.md).


<a name="NOTE2_5"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/fr/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/fr/LX.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/fr/UNIVERSALAPP.png)![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png)![Code Utilisateur (MCU)](https://doc.pcsoft.fr/ext/images/fr/MCU.png) 

### Comparaison d'instances de structures et d'instances de classes
<a name="comparaison_instances_structures_instances_classes_ELTPARAGRAPHE000425"></a>

Les structures dynamiques sont instanciées uniquement lors de leur allocation.

**Il est possible de comparer des instances de structures dynamiques** (ou des instances de classes dynamiques) grâce à l'opérateur "=".

**Exemple** :


```wl
// Déclaration de structures
O est une ST1
P est une ST2

// Déclaration et instanciation de structures dynamiques
P1 est une structure ST1 dynamique
P1 = O
P2 est une structure ST2 dynamique 
P2 = O

// Comparaison 
SI P1 = P2 ALORS ...
```


La même opération peut être réalisée sur **les instances de classes**, afin de savoir si deux variables désignent une unique instance de classe.

La comparaison du contenu de deux instances différentes d'une classe peut être faite en utilisant des buffers. 

**Exemple** :


```wl
Objet1 est un Classe1 dynamique
Objet2 est un Classe1 dynamique
BufferObjet1, BufferObjet2 sont des Buffers

Objet1 = allouer un Classe1
Objet2 = allouer un Classe1

Sérialise(Objet1, BufferObjet1, psdBinaire)
Sérialise(Objet2, BufferObjet2, psdBinaire)

SI BufferObjet1 = BufferObjet2 ALORS
	// Vrai si les deux instances ont des contenus identiques
SINON
	// Faux si au moins un membre diffère
FIN
```



<a name="NOTE2_6"></a>


### Opérateur DANS
<a name="operateur_dans_ELTPARAGRAPHE000447"></a>L'opérateur DANS permet de comparer une valeur à une liste de valeurs. Son évaluation renvoie un booléen. 
Exemple : 

```wl
MaValeur est un entier
MonRésultat est un booléen
MaValeur = 10
MonRésultat = (MaValeur DANS(1,4,6,10))
```


Dans la liste de valeurs, il est possible de placer des valeurs littérales ou des variables dont la valeur sera évaluée avant que la comparaison ne soit effectuée. 
 Exemple : 

```wl
MaValeur est un entier
MonAutreValeur est un entier
MonRésultat est un booléen
MaValeur = 10
MonAutreValeur = 15
MonRésultat = (MaValeur DANS(1,4,6,10,MonAutreValeur))
```

![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png) Cette fonctionnalité n'est pas disponible.




