
## Procédure d'extension
			

<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Présentation
<a name="presentation_ELTTEXTE000131"></a>
Une **procédure d'extension** permet d'ajouter des procédures WLangage "personnelles" aux types du langage (chaîne, date, couleur, etc.). Ces procédures peuvent ensuite être appelées comme des fonctions natives du WLangage. Le type de base du WLangage est alors "étendu". 

Pour indiquer qu'une procédure est une procédure d'extension, il est nécessaire que :

- le premier paramètre utilise le type qui doit être "étendu",

- le prototype de la procédure comporte l'attribut &lt;extension&gt;.






<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Déclaration
<a name="declaration_ELTTEXTE000155"></a>
Exemple de déclaration d'une procédure d'extension du type chaîne : 

```txt
PROCÉDURE MaProcédurePerso(s est une chaîne) <extension>
```

<a name="NOTE2_2"></a>
Remarques : 

- Les procédures d'extension apparaissent dans l’assistance de code (complétion) du type étendu.

- Les procédures d'extension ne peuvent pas être définies sur le type Booléen, Entier, Réel, Numérique, etc. (tous les types permettant de manipuler des valeurs numériques). 

- Seuls les types du WLangage peuvent être étendus. 

- Seules les procédures globales peuvent avoir l'attribut &lt;extension&gt;. 

- Si la procédure d'extension surcharge une fonction WLangage, la syntaxe classique de la fonction WLangage sera également surchargée. Par exemple, la création d'une procédure d'extension "Formate" sur le type chaîne surchargera la fonction [&lt;Chaîne&gt;.Formate](../WDLang1/1000024877.md) et la fonction [ChaineFormate](../WDLang1/3024019.md). 

- Il est possible d'enchainer les appels à des procédures d'extension.




<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Exemple
<a name="exemple_ELTTEXTE000185"></a>


### Remplacer des tabulation par des espaces
<a name="remplacer_des_tabulation_par_des_espaces_ELTPARAGRAPHE000063"></a>Procédure permettant de remplacer les tabulations par des espaces : 

```wl
PROCEDURE RemplaceTABParXEspace(s est une chaîne, n est un entier) <extension> : chaîne
RENVOYER s.Remplace(TAB, Répète(" ", n))
```


L'appel de la procédure d'extension est le suivant : 

```wl
sTexte est une chaîne = TAB + "XXX"
Trace(sTexte) // <TAB>XXX
Trace(sTexte.RemplaceTABParXEspace(4)) //     XXX
```



### Extension du type Couleur
<a name="extension_type_couleur_ELTPARAGRAPHE000074"></a>Procédure permettant d'étendre le type Couleur, afin de récupérer une couleur de texte qui s'affichera lisiblement sur une couleur de fond donnée.

```wl
PROCÉDURE CouleurLisible(CouleurOrigine est une Couleur) <extension> : Couleur

nLuminosité est un entier
rLuminance est un réel
CouleurRetour est une Couleur


// Calcul de la luminance de la couleur passée en paramètre
rLuminance = 1 - (0.299 * CouleurOrigine..Rouge + ...
		0.587 * CouleurOrigine..Vert + 0.114 * CouleurOrigine..Bleu)/255

// En fonction de la luminance, la couleur d'écriture doit avoir une faible ou une forte luminosité
SI rLuminance < 0.5 ALORS
	// Faible luminosité
	nLuminosité = 12
SINON
	// Forte luminosité
	nLuminosité = 88
FIN

// La couleur renvoyée conserve la tonalité et la saturation
CouleurRetour = TSL(CouleurTeinte(CouleurOrigine), ...
	CouleurSaturation(CouleurOrigine), nLuminosité)

RENVOYER CouleurRetour
```


L'appel de la procédure d'extension est le suivant : 

```wl
// Tableau de couleurs
tabCouleursHTML est un tableau de chaînes
tabCouleursHTML.Ajoute("#F48FB1")
tabCouleursHTML.Ajoute("#C2185B")
tabCouleursHTML.Ajoute("#64B5F6")
tabCouleursHTML.Ajoute("#1565C0")
tabCouleursHTML.Ajoute("#81C784")
tabCouleursHTML.Ajoute("#2E7D32")

MaCouleur est une Couleur

// Récupère une couleur aléatoire
MaCouleur = HTMLVersRVB(tabCouleursHTML[Hasard(1, tabCouleursHTML..Occurrence)] )

// Applique la couleur initiale en couleur de fond
LIB_TestCouleurFond.CouleurFond = MaCouleur

// Applique une couleur lisible en couleur de texte
LIB_TestCouleurFond.Couleur = MaCouleur.CouleurLisible()
```



- ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=ProceduresDExtension.gif) ***Exemples didactiques*** : **ProceduresDExtension** <br>Cet exemple montre l'utilisation de l'attribut , pour ajouter des fonctionnalités sur les types du WLangage.

