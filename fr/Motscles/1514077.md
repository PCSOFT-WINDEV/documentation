
## Passage de paramètres
			

<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Présentation
<a name="presentation_ELTTEXTE000316"></a>
Les paramètres peuvent être passés à une procédure : 

- [par référence (également appelé par variable ou par adresse)](#NOTE2_1). 

- [par valeur](#NOTE3_1). 




<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Passage de paramètres par référence
<a name="passage_parametres_par_reference_ELTTEXTE000340"></a>


### Fonctionnement par défaut
<a name="fonctionnement_par_defaut_ELTPARAGRAPHE000023"></a>Par défaut, lors de l'appel d'une procédure, les paramètres sont ***passés par référence (par variable ou par adresse)***. Si le paramètre est modifié dans la procédure, le traitement appelant la procédure récupérera le paramètre avec **sa valeur modifiée**.

Pour passer un paramètre par variable à une procédure, utilisez la syntaxe suivante :


```txt
<Nom de la procédure>(<Nom de la variable passée en paramètre>)
```


Par exemple :


```wl
Indice est un entier = 1
// Avant l'appel de la procédure, Indice vaut 1
AjouteUn(Indice)
// Après l'appel de la procédure, Indice vaut 2
```

```wl
// -- Déclaration de la procédure
PROCEDURE AjouteUn(Compteur)
Compteur += 1
```

<a name="NOTE2_2"></a>


### Passage d'un champ, d'une fenêtre ou d'un état
<a name="passage_champ_une_fenetre_etat_ELTPARAGRAPHE000039"></a>Lors de l'appel d'une procédure, les paramètres peuvent être ***un champ, une fenêtre ou un état***. Cet objet (champ, fenêtre ou état) est alors manipulé comme un objet "classique".

Pour passer un élément en paramètre à une procédure, utilisez la syntaxe suivante :


```txt
<Nom de la procédure>(<Nom de l'élément>)
```


Par exemple :


```wl
// Appel de la procédure ChampNonVisible
ChampNonVisible(ChampAdresse)
```

```wl
// -- Déclaration de la procédure
PROCEDURE ChampNonVisible(ChampAdresse)
ChampAdresse..Visible = Faux
```


Remarques :

- Le mot-clé [MoiMême](../Motscles/1511007.md) permet de manipuler le champ en cours.

- Le mot-clé [MaFenêtre](../Motscles/1511004.md) permet de manipuler la fenêtre en cours.

- Le mot-clé [MonEtat](../Motscles/1511010.md) permet de manipuler l'état en cours.




<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Passage de paramètres par valeur
<a name="passage_parametres_par_valeur_ELTTEXTE000370"></a>


### Présentation
<a name="presentation_ELTPARAGRAPHE000072"></a>Lors de l'appel d'une procédure, les paramètres peuvent être ***passés par valeur***. Si le paramètre est modifié dans la procédure, le traitement appelant la procédure récupérera le paramètre avec **sa valeur NON modifiée**.

Il existe deux possibilités pour passer les paramètres par valeur : 

- [Utilisation des doubles parenthèses](#NOTE3_2).

- [Utilisation de paramètres "locaux" dans la procédure](#NOTE3_3).



<a name="NOTE3_2"></a>


### Solution 1 : Utilisation des doubles parenthèses
<a name="solution_1_utilisation_des_doubles_parentheses_ELTPARAGRAPHE000088"></a>Pour passer un paramètre par valeur à une procédure, utilisez la syntaxe suivante :


```txt
<Nom de la procédure>((<Nom de la variable passée en paramètre>))
```


Par exemple :


```wl
Indice est un entier = 1
// Avant l'appel de la procédure, Indice vaut 1
AjouteUn((Indice))
// Après l'appel de la procédure, Indice vaut toujours 1
```

```wl
// -- Déclaration de la procédure
PROCEDURE AjouteUn(Compteur)
Compteur += 1
```

<a name="NOTE3_3"></a>


### Solution 2 : Utilisation de paramètres "locaux" dans la procédure
<a name="solution_2_utilisation_parametres_locaux_dans_procedure_ELTPARAGRAPHE000102"></a>Lors de la déclaration d'une procédure, les variables passées en paramètre peuvent devenir locales à cette procédure. Pour cela, il suffit de faire précéder le paramètre local par le mot-clé LOCAL. Par exemple :

```txt
PROCEDURE MaProc(LOCAL Indice, LOCAL Compteur, Numéro)
```


Si ce paramètre est modifié dans la procédure, le traitement appelant la procédure récupérera le paramètre avec **sa valeur NON modifiée**.


**Remarques** : 

- Le mot-clé LOCAL force la copie de l'élément passé en paramètre dans les cas suivants :

	- instance de classe ou de tableau passée à un paramètre non typé. 
			Exemple : 
			
		```txt
		Procédure MaProcédure(LOCAL Paramètre)
		```


	- instance de classe passée à un paramètre typé. 
			Exemple : 
			
		```txt
		Procédure MaProcédure(LOCAL Paramètre est CClasse)
		```





- Le mot-clé **LOCAL** peut être remplacé par le mot-clé **LOCALE** ou **LOCALES**.



<a name="NOTE3_4"></a>


### Paramètre correspondants à un élément du projet
<a name="parametre_correspondants_element_projet_ELTPARAGRAPHE000130"></a>Si votre procédure utilise des éléments du projet, jusqu'à la version 19, le nom de l'élément devait être mis entre guillemets lors de l'appel de la procédure. Par exemple : 


```wl
MaProcédure("FEN_Exemple")
```


A partir de la version 19, il est possible de passer directement le nom de l'élément sans guillemets et de disposer de la complétion sur le paramètre lors de la saisie. 

![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png) **Cas des fenêtres**
Il suffit d'utiliser l'attribut d'extension &lt;nom de fenêtre&gt;. La syntaxe à utiliser est la suivante : 

```txt
PROCEDURE NomProcédure(NomFenêtre est une chaîne <nom de fenêtre>)
```


Exemple : 


```wl
// Déclaration de la procédure
PROCEDURE ContrôleSaisie(sNomFenêtre est une chaîne <nom de fenêtre>)

soit i = 1
soit ResChamp = EnumèreChamp(sNomFenêtre, i)
TANTQUE ResChamp <> ""
	i++
	Trace("Traitement du champ "+ResChamp+" de la fenêtre " + sNomFenêtre)
	ResChamp = EnumèreChamp(sNomFenêtre, i)
FIN
```


Dans cet exemple, lors de l'appel de la procédure, le paramètre sNomFenêtre contient le nom de la fenêtre. La complétion proposera toutes les fenêtres présentes dans le projet. 

Les appels de la procédure pourront correspondre aux syntaxes suivantes : 

- ContrôleSaisie(NomFenêtre)

- ContrôleSaisie("NomFenêtre")

- ContrôleSaisie(Nom variable chaîne contenant le nom de la fenêtre)






**Cas des états**
Il suffit d'utiliser l'attribut d'extension &lt;nom d'état&gt; (ou &lt;nom de état&gt;). La syntaxe à utiliser est la suivante : 

```txt
PROCEDURE NomProcédure(NomEtat est une chaîne <nom d'état>)
```


Exemple : 


```wl
// Déclaration de la procédure
PROCEDURE VérifDonnées(sNomEtat est une chaîne <nom d'état>)
```


Dans cet exemple, lors de l'appel de la procédure, le paramètre sNomEtat contient le nom de l'état. La complétion proposera tous les états présents dans le projet. 

Les appels de la procédure pourront correspondre aux syntaxes suivantes : 

- ContrôleSaisie(NomEtat)

- ContrôleSaisie("NomEtat")

- ContrôleSaisie(Nom variable chaîne contenant le nom de l'état)




**Cas des sources de données**
La syntaxe à utiliser est la suivante : 

```txt
PROCEDURE NomProcédure(NomFichier est une Source de Données)
```


Exemple : 


```wl
// Déclaration de la procédure
PROCEDURE VérifDonnées(sNomFicher est une Source de Données)
```


Dans cet exemple, lors de l'appel de la procédure, le paramètre sNomFichier contient le nom d'une source de données. La complétion proposera toutes les sources de données (fichiers, requêtes, variables de type Source de données) présentes dans le projet. 

Les appels de la procédure pourront correspondre aux syntaxes suivantes : 

- VérifDonnées(Fichier)

- VérifDonnées(Requête)

- VérifDonnées("Fichier")

- VérifDonnées("Requête")

- VérifDonnées(Nom variable chaîne contenant le nom du fichier ou de la requête)

- VérifDonnées(Nom variable de type source de données)




Remarque : Pour associer la source de données à un fichier de données (ou une requête) existant, il suffit d'ajouter un attribut d'extension spécifique. Dans ce cas, la complétion automatique permettra d'accéder directement aux rubriques. Pour plus de détails, consultez [Variable de type Source de données](../WDLang4/1514053.md). 



<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Cas particulier
<a name="cas_particulier_ELTTEXTE000412"></a>


### Passage d'une liste de valeurs
<a name="passage_une_liste_valeurs_ELTPARAGRAPHE000226"></a>Lors de l'appel d'une procédure, les paramètres peuvent être **une liste de valeurs** ("[1,2,3]" par exemple).

Pour passer une liste de valeurs en paramètre à une procédure, utilisez la syntaxe suivante :


```txt
<Nom de la procédure>(<Liste de valeurs>)
```


Par exemple :


```wl
// Appel de la procédure
MaProcédure([1,2,3])
```

```wl
// -- Déclaration de la procédure
PROCEDURE MaProcédure(Valeurs)
MonTableau est un tableau d'entiers = Valeurs
```


**Remarques** :

- Si le paramètre de la procédure n'est pas typé, la liste de valeurs est passée directement à la procédure. Comme les listes de valeurs ne sont pas manipulables directement, ce paramètre peut être utilisé uniquement dans des fonctionnalités qui acceptent les listes de valeurs :

	- appel d'une fonction du WLangage qui autorise les listes de valeurs (HFiltre, HLitRecherche, ...).

	- affectation dans une variable de type structuré (tableau, classe, structure, ...).

	- appel d'une procédure utilisateur qui autorise une liste de valeurs.




- Si le paramètre de la procédure est de type tableau, un tableau est automatiquement alloué et rempli avec les valeurs de la liste de valeurs. 

- Si le paramètre de la procédure est d'un autre type, une erreur de compilation apparaîtra (ou une erreur WLangage se produira à l'exécution si le paramètre réel n'est pas typé).




**Surcharge de fonctions WLangage** : Grâce aux paramètres de type "liste de valeurs", il est possible de surcharger les fonctions WLangage acceptant des listes de valeurs, notamment : 

- [HFiltre](../WDLang4/3044100.md)

- [HLitRecherche](../WDLang4/3044050.md)

- [HLitRechercheDernier](../WDLang4/3044034.md)

- [HLitRecherchePremier](../WDLang4/3044036.md)

- [HRecherche](../WDLang4/3044130.md)

- [HRechercheDernier](../WDLang4/3044129.md)

- [HRecherchePremier](../WDLang4/3044125.md)

- [HStatNbEnrIntervalle](../WDLang4/3044029.md)





