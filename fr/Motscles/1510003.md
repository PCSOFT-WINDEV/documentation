


## Instruction CONTINUER / Etiquette "CONTINUER :"

***En anglais : Continue***
				



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Le mot-clé **CONTINUER** peut correspondre : 

- à l'instruction **CONTINUER** : 
	L'instruction **CONTINUER** permet de retourner directement au début de la prochaine itération sans terminer le code de l'itération en cours.
	L'instruction **CONTINUER** peut être utilisée dans les boucles de type :

	- [POUR](../Motscles/1510001.md)

	- [POUR TOUT](../Motscles/1510013.md)

	- [BOUCLE](../Motscles/1510004.md)

	- [TANTQUE](../Motscles/1510010.md)




- à l'étiquette **CONTINUER:** : 
	L'étiquette "CONTINUER:" permet de déclarer un code commun dans une boucle. Ce code sera exécuté : 

	- à chaque itération de la boucle. 

	- si l'instruction "CONTINUER" est présente dans la boucle. Dans ce cas, le code entre l'instruction "CONTINUER" et l'étiquette "CONTINUER:" ne sera pas exécuté.






<a name="XSYNTAXE"></a>
<a name="SYNTAXE1"></a>

## Syntaxe

### Instruction CONTINUER utilisée dans une instruction POUR

`POUR <Variable de contrôle> = <Valeur initiale> A <Valeur finale> [PAS <x>]
	...    
	SI <Condition> ALORS CONTINUER
	...
FIN`
---

**Remarque** : Si la &lt;Condition&gt; est vérifiée :

- Le code suivant l'instruction **CONTINUER** n'est pas exécuté.

- La boucle est exécutée directement depuis le début de l'instruction [POUR](../Motscles/1510001.md).

- La &lt;Variable de contrôle&gt; est incrémentée.





<a name="SYNTAXE2"></a>

### Instruction CONTINUER utilisée dans une instruction POUR TOUT

`POUR TOUT <Fichier> SUR <Rubrique clé>
    ...
    SI <Condition> ALORS CONTINUER
    ...
FIN`
---

**Remarque** : Si la &lt;Condition&gt; est vérifiée :

- Le code suivant l'instruction **CONTINUER** n'est pas exécuté.

- La boucle est exécutée directement depuis le début de l'instruction [POUR TOUT](../Motscles/1510013.md).

- Le passage à l'enregistrement suivant est automatique.  





<a name="SYNTAXE3"></a>

### Instruction CONTINUER utilisée dans une instruction TANTQUE

`TANTQUE <Condition 1>
    ...    
	SI <Condition 2> ALORS CONTINUER
    ...
FIN`
---

**Remarque** : Si la &lt;Condition 2&gt; est vérifiée :

- Le code suivant l'instruction **CONTINUER** n'est pas exécuté.

- La boucle est exécutée directement depuis le début de l'instruction [TANTQUE](../Motscles/1510010.md).






<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques


### Instruction CONTINUER : exemple à ne pas faire :
<a name="instruction_continuer_exemple_pas_faire_ELTPARAGRAPHE000080"></a>Dans ce code, l'utilisation du mot-clé **CONTINUER** entraîne la non-exécution de la fonction [HLitSuivant](../WDLang4/3044037.md) :

```wl
HLitPremier(Compte, NoCompte)
TANTQUE PAS HEnDehors()
	SI Compte.NoCompte = "XXX" ALORS CONTINUER
	Trace(Compte.NoCompte)
	HLitSuivant(Compte, NoCompte)
FIN
```

<a name="NOTE0_2"></a>


### Exemple d'utilisation de l'étiquette "CONTINUER:"
<a name="exemple_utilisation_etiquette_continuer_ELTPARAGRAPHE000093"></a>Dans cet exemple, sont cumulés  :

- le montant de chaque facture dans la variable TotFact.

- le montant si c'est un avoir dans la variable TotAvoir.



```wl
TotFact est un Monétaire
TotAvoir est un Monétaire

HLitPremier(Facture, NumFact)
TANTQUE PAS HEnDehors()
	SI Facture.Type_Facture <> "Avoir" ALORS CONTINUER  
	// Saute directement au label CONTINUER:
	// Cumule ici uniquement les avoirs
	TotAvoir += Facture.Montant

	CONTINUER:
	// Cumule tous les montants
	TotFact += Facture.Montant
	HLitSuivant(Facture, NumFact)
FIN
```

<a name="NOTE0_3"></a>


### Instruction "CONTINUER:" : Cas des boucles imbriquées
<a name="instruction_continuer_cas_des_boucles_imbriquees_ELTPARAGRAPHE000107"></a>Il est parfois nécessaire lors de plusieurs boucles imbriquées de continuer sur l'itération suivante d'un des niveaux précédents sans terminer la boucle en cours.

Pour cela, l'instruction **CONTINUER** accepte un paramètre indiquant le nombre de niveau à remonter. La syntaxe est la suivante : 

```txt
CONTINUER(<Nombre de niveau>)
```
où &lt;Nombre de niveau&gt; est un entier entre 1 et N, N représentant le nombre de niveau d'imbrications de boucles.

Par exemple :

```wl
x, y, z sont des entiers

POUR X = 1 A 15 // Boucle 1
	POUR Y = 1 A 25 // Boucle 2
		POUR Z = 1 A 50 // Boucle 3
			SI <Ma_condition_est_vérifiée> ALORS
				CONTINUER (2)  
					// Arrête le parcours de la boucle 3 
					// et passe à l'itération suivante de la boucle 2
			FIN
		FIN
	FIN
FIN
```



