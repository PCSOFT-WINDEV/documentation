
## Requête SQL (type WLangage)

***En anglais : SQL query (WLanguage type)***
				



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Le type "Requête SQL" permet d'écrire une requête SQL directement dans le code WLangage. La requête SQL est définie et déclarée. Elle peut ensuite être exécutée par la fonction [HExécuteRequête](../WDLang4/3044080.md) ou la fonction [HExécuteRequêteSQL](../WDLang4/3044084.md).

L'utilisation du type "Requête SQL" permet de profiter de nombreux avantages : 

- Coloration automatique du code SQL sous l'éditeur, 

- Assistance à la saisie de code SQL,

- Assistance à la saisie sur les rubriques résultat et les paramètres de la requête, 

- Erreurs de compilation : 

	- en cas d'erreur dans le code SQL

	- en cas d'utilisation d'une rubrique de sortie inexistante. 




- Data binding sur la requête si elle est déclarée comme globale à une fenêtre, 

- Libération automatique en fin de portée de la variable. 



<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
reqStatVols est une Requête SQL =
[
	SELECT * FROM  Vols
	WHERE Vols.IDAéroportDépart = {ParamIDAéroportDépart}
	AND Vols.IDAéroportArrivée = {ParamIDAéroportArrivée}
]
reqStatVols.ParamIDAéroportDépart = 12
reqStatVols.ParamIDAéroportArrivée = 3

HExécuteRequête(reqStatVols)
POUR TOUT reqStatVols
	...
FIN
```





<a name="XSYNTAXE"></a>
<a name="SYNTAXE1"></a>

## Syntaxe

### Déclarer une requête SQL

`<Nom Requête SQL> est une Requête SQL =
[    
    <Code SQL de la requête>
]
`
---

**`<Nom Requête SQL> : ()`**

Nom de la variable correspondant à la requête SQL. 

**`<Code SQL de la requête> : ()`**

Code SQL correspondant à la requête SQL associé à la variable. 



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques


### Syntaxe de déclaration : Remarques 
<a name="syntaxe_declaration_remarques_ELTPARAGRAPHE000053"></a>

- La requête doit être définie dès sa déclaration. Il n'est pas possible de déclarer une variable de type Requête SQL puis de saisir le code SQL correspondant après quelques lignes de code. 

- Lors de la déclaration de la variable de type Requête SQL, il est possible de lancer l'assistant de l'éditeur de requête pour écrire le code SQL de la requête. Il suffit de choisir l'option "Requête SQL (assistant)". <br>![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=Requ%EAteSQL_WL%20-%20HC%20N%B0001.gif)
L'assistant de l'éditeur de requêtes s'ouvre et vous permet de saisir les options de votre requête. Lors de la validation de l'assistant, le code SQL de la requête est intégré dans le code WLangage. 

- Les fichiers de données utilisés dans le FROM de la requête doivent être déclarés dans l'analyse du projet. Ils ne peuvent pas provenir de sources de données initialisées par un alias, une déclaration externe ou une autre requête.

- La requête SQL ne peut pas être construite avec des concaténations de chaînes. Pour construire une requête par concaténations de chaînes, il faut utiliser la fonction [HExécuteRequêteSQL](../WDLang4/3044084.md).

- La requête SQL n'est pas exécutée lors de sa déclaration. Il est nécessaire de l'exécuter à l'aide de la fonction [HExécuteRequête](../WDLang4/3044080.md) ou de la fonction [HExécuteRequêteSQL](../WDLang4/3044084.md).

- Lorsque la requête est déclarée et exécutée, elle peut être manipulée par toutes les fonctions HFSQL qui manipulent des requêtes. 

- Lorsque la requête SQL est déclarée et exécutée, elle peut être utilisée dans une autre requête SQL en utilisant le nom de la variable correspondante : 
	
	```wl
	MonInterrogation est une Requête SQL = 
	[
		SELECT * FROM CLIENT
	]
	HExécuteRequête(MonInterrogation)
	
	AutreInterrogation est une chaîne = "SELECT * FROM %1"
	AutreInterrogation = ChaîneConstruit(AutreInterrogation, MonInterrogation.Nom)
	
	ReqAvecMonInterrogation est une Source de Données
	HExécuteRequêteSQL(ReqAvecMonInterrogation, AutreInterrogation)
	```






<a name="NOTE0_2"></a>


### Code SQL de la requête
<a name="code_sql_requete_ELTPARAGRAPHE000082"></a>

- Le code SQL saisi bénéficie de la coloration syntaxique. Les mots-clés du SQL sont colorés (voir exemple). 

- Complétion : La complétion est disponible : 

	- sur les mots-clés SQL, 

	- sur les rubriques et les fichiers de données, 

	- sur les rubriques de sortie de la requête. <br>![La saisie SQL assistée](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=151-sql_completion_rubriques.jpg)





- Erreurs de compilation : En cas d'erreur dans le code SQL de la requête, une erreur de compilation est générée (comme pour du code WLangage). <br>![Erreur de code SQL détectée en saisie](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=152-sql_code_erreur.jpg)


- De la même manière que dans une requête SQL saisie sous l'éditeur de requêtes, il est possible de : 

	- saisir des commentaires en les faisant précéder des caractères "--" ou "//". 

	- saisir des fonctions WLangage. Il suffit de préfixer les fonctions WLangage utilisées par les lettres "WL.". La saisie assistée est disponible. 
			![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png) Il n'est pas possible d'exécuter des requêtes SQL contenant des fonctions WLangage. 

	- ...






<a name="NOTE0_5"></a>


### Utilisation de [%  %] dans les requêtes SQL
<a name="utilisation_dans_les_requetes_sql_ELTPARAGRAPHE000109"></a>Les requêtes SQL acceptent la nouvelle syntaxe : 

```txt
[% nom_de_variable %]
```

A l'exécution, le nom de variable compris entre les "%" sera remplacé par la valeur de cette variable. 

Exemple : 

```wl
IDClientAChercher est un entier = xxx
// Recherche de toutes les commandes du client
ToutesCommandes est une requête SQL = [
	SELECT * FROM Commande WHERE IDClient = [%IDClientAChercher%]
	]
HExécuteRequêteSQL(ToutesCommandes)
POUR CHAQUE ToutesCommandes
	// une commande du client
FIN
```


Remarques : 

- L'évaluation des paramètres est effectuée une seule fois, à la déclaration de la requête.

- Toutes les exécutions de la requête (appels à la fonction [HExécuteRequêteSQL](../WDLang4/3044084.md)) utiliseront donc la valeur de la variable au moment de la déclaration de la requête, même si la valeur de la variable change entre deux appels à la fonction [HExécuteRequêteSQL](../WDLang4/3044084.md). 

- Pour paramétrer la requête à chaque appel : 

	- il est possible d'utiliser un paramètre de requête HFSQL "{&lt;paramètre&gt;}".

	- Il est également possible d'utiliser une autre requête SQL ou une source de données "alias" contenant l'alias d'un fichier et décrite par un attribut &lt;description="fichier"&gt;. Exemple : 
			
		```wl
		SélectionClient est une Source de Données <description="Client">
		// Recherche de tous les clients de la sélection vérifiant la condition
		FiltreClient est une requête SQL = [
				SELECT * FROM [%SélectionClient%] WHERE ...
			]
		HExécuteRequêteSQL(FiltreClient)
		POUR CHAQUE FiltreClient
			// Clients de la sélection qui sont filtrés par la requête
		FIN
		```









<a name="NOTE0_4"></a>


### Contexte HFSQL
<a name="contexte_hfsql_ELTPARAGRAPHE000143"></a>

- Une requête SQL est définie dans le contexte HFSQL courant. Dans une fenêtre à contexte HFSQL indépendant, une requête SQL peut ne pas exister. 

- Une requête SQL peut être définie comme une variable globale. Dans ce cas, il est nécessaire de faire attention aux contextes HFSQL. 









