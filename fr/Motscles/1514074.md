


## Combinaison (Type de variable)

***En anglais : Combination***
				



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Une combinaison est un ensemble d'options. Une variable ou un paramètre formel de type combinaison peut être affectée avec aucune, une ou plusieurs options de la combinaison. 




<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
// Code de déclaration
// Déclarer une combinaison
TypeCombinaison est une Combinaison
	CréationFic
	LectureFic
	EcritureFic
	SuppressionFic
FIN
```


<a name="Exemple2"></a>



```wl
// Déclaration d'une variable de type Combinaison
ActionFichier est un TypeCombinaison
// Affectation de la variable
ActionFichier = LectureFic + EcritureFic
// ActionFichier = 0 // Désactive toutes les options
...

// Pour tester la valeur de la combinaison :
SI ActionFichier[EcritureFic] ALORS
	// L'option EcritureFic est activée
SINON
	// L'option EcritureFic n'est pas activée
FIN
```

<a name="XSYNTAXE"></a>
<a name="SYNTAXE1"></a>

## Syntaxe

### Déclarer une combinaison

`<Nom de la combinaison> est une Combinaison
    <Options de la combinaison>
FIN
`
---

**`<Nom de la combinaison> : ()`**

Nom de la combinaison à déclarer

**`<Options de la combinaison> : ()`**

Différentes options pouvant être prises par la combinaison. 

**`<FIN> : ()`**

Fin de déclaration.  


<a name="SYNTAXE2"></a>

### Déclarer une variable de type Combinaison

`<Nom de la variable> est une <Nom de la combinaison>
`
---

**`<Nom de la variable> : ()`**

Nom de la variable de type Combinaison à déclarer.

**`<Nom de la combinaison> : ()`**

Nom d'une combinaison précédemment déclarée.   



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques




### Déclaration des combinaisons
<a name="declaration_des_combinaisons_ELTPARAGRAPHE000080"></a>Une combinaison doit être déclarée dans :

- Le code d'initialisation du projet pour être utilisable dans tous les traitements du projet (codes du projet, codes des fenêtres, codes des champs, procédures, ...).

- Le code de déclaration des globales d'une fenêtre pour être utilisable dans tous les traitements de la fenêtre (codes de la fenêtre, codes des champs présents dans la fenêtre, procédures locales, ...).

- Le code d'ouverture d'un état pour être utilisable dans tous les traitements de l'état (codes de l'état, codes des champs présents dans l'état, procédures locales, ...).

- Le code de déclaration de classe pour être utilisable dans la classe.

	- Pour accéder à cette combinaison depuis une méthode de la classe, utilisez la syntaxe "::&lt;Nom de la combinaison&gt;".

	- Pour accéder à cette combinaison depuis un code extérieur à la classe, utilisez la syntaxe "&lt;Nom de la classe&gt;::&lt;Nom de la combinaison&gt;".




- Le code de déclaration des collections de procédures pour être utilisable dans toutes les procédures de la collection.




**Remarque** : Une combinaison est toujours globale :

- soit à un projet,

- soit à une fenêtre,

- soit à un état,

- soit à une classe.



<a name="NOTE0_2"></a>


### Activer ou désactiver les options d'une combinaison
<a name="activer_desactiver_les_options_une_combinaison_ELTPARAGRAPHE000103"></a>Plusieurs syntaxes permettent d'activer ou de désactiver les options d'une combinaison. Quelques exemples : 

- Déclaration de la combinaison utilisée pour les exemples : 
	
	```wl
	TypeCombinaison est une Combinaison
		CréationFic
		LectureFic
		EcritureFic
		SuppressionFic
	FIN
	
	// Déclaration d'une variable de type Combinaison
	ActionFichier est un TypeCombinaison
	```


- Désactivation de toutes les options : 
	Syntaxe : 
	
	```txt
	<Variable Combinaison> = 0
	```

	Exemple : 
	
	```wl
	ActionFichier = 0
	```


- Activation d'une ou plusieurs options : 
	Syntaxe : 
	
	```txt
	<Variable Combinaison> = Option 1
	<Variable Combinaison>[Option 1] = Vrai
	
	<Variable Combinaison> = Option 1 + Option 2
	<Variable Combinaison>[Option 1 + Option 2] = Vrai
	```

	Exemples : 
	
	```wl
	ActionFichier = LectureFic  // Active une option
	ActionFichier = LectureFic + EcritureFic // Active 2 options
	
	ActionFichier[LectureFic] = Vrai  // Active une option
	ActionFichier[LectureFic + EcritureFic] = Vrai // Active 2 options
	```


- Désactivation d'une ou plusieurs options : 
	Syntaxe : 
	
	```txt
	<Variable Combinaison>[Option 1] = Faux
	
	<Variable Combinaison>[Option 1 + Option 2] = Faux
	```

	Exemples : 
	
	```wl
	ActionFichier[LectureFic] = Faux  // Désactive une option
	ActionFichier[LectureFic + EcritureFic] = Faux // Désactive 2 options
	```




<a name="NOTE0_3"></a>


### Lire les options d'une combinaison
<a name="lire_les_options_une_combinaison_ELTPARAGRAPHE000141"></a>Plusieurs syntaxes permettent de connaître les options d'une combinaison. Quelques exemples : 

- Déclaration de la combinaison
	
	```wl
	TypeCombinaison est une Combinaison
		CréationFic
		LectureFic
		EcritureFic
		SuppressionFic
	FIN
	
	// Déclaration d'une variable de type Combinaison
	ActionFichier est un TypeCombinaison
	```


- Tester si toutes les options sont désactivées : 
	Syntaxe : 
	
	```txt
	SI <Variable Combinaison> = 0
	```

	Exemples : 
	
	```wl
	SI ActionFichier = 0 ALORS ...
	```


- Tester si une option est activée :  
	Syntaxe : 
	
	```txt
	SI <Variable Combinaison> = Option 1 ...
	SI <Variable Combinaison>[Option 1] = Vrai ...
	```

	Exemples :
	
	```wl
	SI ActionFichier = LectureFic ALORS ...
	SI ActionFichier[LectureFic] = Vrai ALORS ...
	```


- Tester si plusieurs options sont activées : 
	Syntaxe : 
	
	```txt
	SI <Variable Combinaison> = Option 1 + Option 2 ALORS ...
	SI <Variable Combinaison>[Option 1 + Option 2] = Vrai ...
	```

	Exemples :
	
	```wl
	SI ActionFichier = LectureFic + EcritureFic ALORS ...
	SI ActionFichier[LectureFic + EcritureFic] = Vrai ALORS ...
	```


- Tester si au moins une option est activée : 
	Syntaxe : 
	
	```txt
	SI <Variable Combinaison>[Option 1] OU <Variable Combinaison>[Option 2] ALORS ...
	```

	Exemples :
	
	```wl
	SI ActionFichier[LectureFic] OU ActionFichier[EcritureFic] ALORS ...
	```




<a name="NOTE0_4"></a>


### Passer une combinaison en paramètre à une procédure
<a name="passer_une_combinaison_parametre_une_procedure_ELTPARAGRAPHE000188"></a>Une variable de type Combinaison peut être passée en paramètre à une procédure. Pour cela, utilisez la syntaxe suivante :

```txt
<Nom de la procédure>(<Nom de la variable de type Combinaison>)
```


Par exemple :


```wl
// Procédure avec un paramètre de type Combinaison
PROCEDURE TypeFichier(p est TypeCombinaison)

// Appels avec un paramètre de type Combinaison
TypeFichier(LectureFic + EcritureFic)
TypeFichier(ActionFichier)
```

<a name="NOTE0_5"></a>


### Saisie assistée
<a name="saisie_assistee_ELTPARAGRAPHE000201"></a>La saisie assistée pour un paramètre de type Combinaison propose les différentes options de la combinaison.
<a name="NOTE0_6"></a>


### Extension de combinaison
<a name="extension_combinaison_ELTPARAGRAPHE000208"></a>Une combinaison peut étendre les valeurs d'une ou plusieurs autres combinaisons en reprenant les options de celles-ci et en y ajoutant de nouvelles options.

La syntaxe utilisée est la suivante : 


```txt
<Nom de la combinaison> est une Combinaison
    [Combinaison de base]
    <Options supplémentaires de la combinaison>
FIN
```


où : 

- &lt;Nom de la combinaison&gt; : Nom d'une combinaison précédemment déclarée. 

- Combinaison de base : Nom de la combinaison de base à utiliser.

- &lt;Options supplémentaires de la combinaison&gt; : Options supplémentaires à prendre en compte dans la combinaison. 




Exemple : 


```wl
TypeCombinaison est une Combinaison
	CréationFic
	LectureFic
	EcritureFic
	SuppressionFic
FIN


TypeCombinaisonAvancé est une Combinaison
	[TypeCombinaison]
	AutresAttributs
FIN
```







<a name="NOTE0_7"></a>


### Combinaisons prédéfinies
<a name="combinaisons_predefinies_ELTPARAGRAPHE000231"></a>Plusieurs options sont combinables pour forcer une combinaison prédéfinie. Les options combinées doivent appartenir à la combinaison (ou à des combinaisons de base)

La syntaxe utilisée est la suivante : 


```txt
<Nom de la combinaison> est une Combinaison
	<Option 1 de la combinaison>
	<Option 2 de la combinaison>
	<Option 3 de la combinaison> = <Option 1 de la combinaison> + <Option 2 de la combinaison>
FIN
```


Exemple : 


```wl
TypeCombinaison est une Combinaison
	CréationFic
	LectureFic
	EcritureFic
	SuppressionFic
	Lecture_Ecriture_Fic = LectureFic + EcritureFic
FIN
```

<a name="NOTE0_8"></a>


### Exclusions mutuelles
<a name="exclusions_mutuelles_ELTPARAGRAPHE000246"></a>Il est possible de décrire des exclusions mutuelles entre des options d'une combinaison. 

Par exemple, une combinaison contient trois options : Option1, Option2 et Option3. Option2 et Option3 sont exclusives :

- Option1 et Option2 peuvent être actives.

- Option1 et Option3 peuvent être actives. 

- Option2 et Option3 ne peuvent pas être actives.

- Option1, Option2 et Option3 ne peuvent pas être actives.




La syntaxe à utiliser est la suivante : 


```txt
<Nom de la combinaison> est une Combinaison
	<Option 1 de la combinaison>
	<Option 2 de la combinaison>
	<Option 3 de la combinaison>
	// Exclusions
	<Option 2 de la combinaison> - <Option 3 de la combinaison>
FIN
```


Remarque : En combinant les extensions et les exclusions, il est possible d'obtenir pour les mêmes options : 

- un paramètre d'une fonction qui impose une exclusion, 

- un paramètre d'une autre fonction qui n'impose aucune exclusion ou qui impose une exclusion différente.





<a name="NOTE0_9"></a>


### Propriétés associées
<a name="proprietes_associees_ELTPARAGRAPHE000270"></a>Les propriétés associées aux variables de type Combinaison sont les suivantes : 


|   |   |
| --- | --- |
| [Nom](../Proprietes/2510082.md) | Permet de connaître le nom d'une variable de type Combinaison. |
| [Valeur](../Proprietes/2510130.md) | Permet de connaître la valeur associée d'une variable de type Combinaison. |








<a name="NOTE0_10"></a>


### Fonctions WLangage disponibles
<a name="fonctions_wlangage_disponibles_ELTPARAGRAPHE000292"></a>Une combinaison peut être stockée dans une rubrique HFSQL ou dans un autre format de fichier. Selon les besoins, il peut être possible de stocker le nom (propriété [Nom](../Proprietes/2510082.md)) ou la valeur de la combinaison (propriété [Valeur](../Proprietes/2510130.md)). 

Les fonctions WLangage suivantes permettent de retrouver les caractéristiques d'une combinaison stockée : 



|   |   |
| --- | --- |
| [CombinaisonDepuisNom](../WDLang1/1000020771.md) | Renvoie une combinaison d'une ou de plusieurs options connues par leurs noms. |
| [CombinaisonDepuisValeur](../WDLang1/1000020770.md) | Renvoie une combinaison d'une ou plusieurs options connues par leurs valeurs associées. |
| [CombinaisonVérifieNom](../WDLang1/1000020772.md) | Vérifie si une combinaison d'une ou plusieurs options connues par leurs noms est valide. |






<a name="NOTE0_11"></a>


### Limites
<a name="limites_ELTPARAGRAPHE000309"></a>

- Les combinaisons ne sont pas disponibles en compilation dynamique. 

- Les combinaisons ne sont pas utilisables hors projet. 









