
## Enumération (Type de variable)

***En anglais : Enumeration***
				

<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Une énumération est un ensemble de valeurs. Une variable ou un paramètre formel de type **Enumération** peut être affectée uniquement avec une valeur de l'énumération.


<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
// Code de déclaration
// Déclarer une énumération
TypeAutomate est une Enumération
	Marche
	Arrêté
	EnMaintenance
	EnPanne
FIN
```


<a name="Exemple2"></a>



```wl
// Déclaration d'une variable de type Enumération
Poste1 est un TypeAutomate
// Affectation de la variable
Poste1 = Marche
// Poste1 = 1 // Affiche une erreur de compilation
// Poste1 = "Marche" // Affiche une erreur de compilation
```

<a name="XSYNTAXE"></a>
<a name="SYNTAXE1"></a>

## Syntaxe

### Déclarer une énumération

`<Nom de l'énumération> est une Enumération
    <Valeurs de l'énumération>
FIN
`
---

**`<Nom de l'énumération> : ()`**

Nom de l'énumération à déclarer

**`<Valeurs de l'énumération> : ()`**

Différentes valeurs pouvant être prises par l'énumération. 

**`<FIN> : ()`**

Fin de déclaration.  


<a name="SYNTAXE2"></a>

### Déclarer une variable de type Enumération

`<Nom de la variable> est une <Nom de l'énumération>
`
---

**`<Nom de la variable> : ()`**

Nom de la variable de type Enumération à déclarer.

**`<Nom de l'énumération> : ()`**

Nom d'une énumération précédemment déclarée. La valeur par défaut de l'énumération correspond à la première valeur de l'énumération.  



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques




### Déclaration des énumérations
<a name="declaration_des_enumerations_ELTPARAGRAPHE000078"></a>Une énumération doit être déclarée dans :

- Le code d'initialisation du projet pour être utilisable dans tous les traitements du projet (codes du projet, codes des fenêtres, codes des champs, procédures, ...).

- Le code de déclaration des globales d'une fenêtre pour être utilisable dans tous les traitements de la fenêtre (codes de la fenêtre, codes des champs présents dans la fenêtre, procédures locales, ...).

- Le code d'ouverture d'un état pour être utilisable dans tous les traitements de l'état (codes de l'état, codes des champs présents dans l'état, procédures locales, ...).

- Le code de déclaration de classe pour être utilisable dans la classe.

	- Pour accéder à cette énumération depuis une méthode de la classe, utilisez la syntaxe : 
			
		```txt
		"::<Nom de l'énumération>"
		```


	- Pour accéder à cette énumération depuis un code extérieur à la classe, utilisez la syntaxe : 
			
		```txt
		"<Nom de la classe>::<Nom de l'énumération>"
		```





- Le code de déclaration des collections de procédures pour être utilisable dans toutes les procédures de la collection. 




**Remarque** : Une énumération est toujours globale :

- soit à un projet,

- soit à une fenêtre,

- soit à un état,

- soit à une classe.



<a name="NOTE0_2"></a>


### Passer une énumération en paramètre à une procédure
<a name="passer_une_enumeration_parametre_une_procedure_ELTPARAGRAPHE000105"></a>Une variable de type Enumération peut être passée en paramètre à une procédure. Pour cela, utilisez la syntaxe suivante : 

```txt
<Nom de la procédure>(<Nom de la variable de type Enumération>)
```


Par exemple :


```wl
// Procédure avec un paramètre de type Enumération
PROCEDURE Fonctionnement(p est TypeAutomate)

// Appels avec un paramètre de type Enumération
Fonctionnement(Marche)
Fonctionnement(1)		// Affiche une erreur de compilation
Fonctionnement("A")		// Affiche une erreur de compilation
```

<a name="NOTE0_3"></a>


### Cas particuliers
<a name="cas_particuliers_ELTPARAGRAPHE000117"></a>

- **Saisie assistée** : La saisie assistée pour un paramètre de type énumération propose les différentes valeurs de l'énumération.

- **Ordre des valeurs** : Les opérateurs &lt;, &lt;=, &gt;, &gt;= sont disponibles sur les énumérations ainsi que les opérations automatiques de tri ([TableauTrie](../WDLang1/3075007.md), [TableauCherche](../WDLang1/3075002.md), ...). L'ordre des valeurs dans l'énumération est celui de la déclaration des valeurs. 
	Exemple : 
	
	```wl
	// Code de déclaration
	// Déclarer une énumération
	TypeAutomate est une Enumération
		Marche
		Arrêté
		EnMaintenance
		EnPanne
	FIN
	
	// Déclaration d'une variable de type Enumération
	Poste1 est un TypeAutomate
	// Affectation de la variable
	Poste1 = Marche
	
	...
	
	SI Poste1>Arrêté ALORS
		Info("L'automate a actuellement un problème")
	FIN
	```






<a name="NOTE0_4"></a>


### Extension d'énumération
<a name="extension_enumeration_ELTPARAGRAPHE000136"></a>Une énumération peut étendre les valeurs d'une ou plusieurs autres énumérations en reprenant les valeurs de celles-ci et en y ajoutant de nouvelles valeurs.

La syntaxe utilisée est la suivante : 


```txt
<Nom de l'énumération> est une Enumération
    [Enumération de base]
    <Valeurs supplémentaires de l'énumération>
FIN
```


où : 

- Nom de l'énumération : Nom d'une énumération précédemment déclarée. 

- Enumération de base : Nom de l'énumération de base à utiliser.

- Valeurs supplémentaires de l'énumération : Valeurs supplémentaires à prendre en compte dans l'énumération. 




Exemple : 


```wl
// Code de déclaration
// Déclarer une énumération
TypeAutomate est une Enumération
	Marche
	Arrêté
	EnMaintenance
	EnPanne
FIN

TypeAutomateAvancé est une Enumération
	[TypeAutomate]
	EnBoucle
FIN
```


L'affectation d'une valeur étendue dans une énumération de base est interdite.




<a name="NOTE0_5"></a>


### Créer des alias dans une énumération 
<a name="creer_des_alias_dans_une_enumeration_ELTPARAGRAPHE000160"></a>Il est possible de déclarer des alias pour assurer une compatibilité temporaire par exemple.

La syntaxe utilisée est la suivante : 


```txt
<Nom de l'énumération> est une Enumération
	<Valeur 1 de l'énumération>
	<Valeur 2 de l'énumération>
	<Valeur 3 de l'énumération> = <Valeur 1 de l'énumération>
FIN
```


Exemple : 


```wl
// Code de déclaration
// Déclarer une énumération
TypeAutomate est une Enumération
	Marche
	Arrêté
	EnMaintenance
	EnPanne
	// Déclaration des alias temporaires
	Maintenance = EnMaintenance
	Panne = EnPanne
FIN
```

<a name="NOTE0_6"></a>


### Valeurs associées
<a name="valeurs_associees_ELTPARAGRAPHE000175"></a>En WLangage, il est possible d'associer une valeur constante de type simple à chaque valeur d'une énumération. Cette valeur est associée à la valeur de l'énumération dans la déclaration de l'énumération.


```wl
// Déclaration de l'énumération avec valeurs associées
TypeAutomate est une Enumération
	Marche = "Exécution de l'automate"
	Arrêté = "Automate arrêté"
	EnMaintenance = "Automate en cours de maintenance"
	EnPanne = "Automate en panne"
FIN
```


Il est ensuite possible de lire la valeur associée d'une variable de type Enumération grâce à la propriété [Valeur](../Proprietes/2510130.md).


```wl
// Accès à la valeur associée
e est un TypeAutomate
MessageAutomate est une chaîne = e..Valeur
```


La valeur associée n'a aucune incidence sur les autres comportements des énumérations. En particulier l'ordre de tri reste celui de déclaration des valeurs dans l'énumération.


<a name="NOTE0_7"></a>


### Propriétés associées
<a name="proprietes_associees_ELTPARAGRAPHE000194"></a>Les propriétés associées aux variables de type Enumération sont les suivantes : 


|   |   |
| --- | --- |
| [Nom](../Proprietes/2510082.md) | Permet de connaître le nom d'une variable de type Enumération. |
| [Valeur](../Proprietes/2510130.md) | Permet de connaître la valeur associée d'une variable de type Enumération. |




<a name="NOTE0_8"></a>


### Fonctions WLangage disponibles
<a name="fonctions_wlangage_disponibles_ELTPARAGRAPHE000215"></a>Une énumération peut être stockée dans une rubrique HFSQL ou dans un autre format de fichier. Selon les besoins, il peut être possible de stocker le nom (propriété [Nom](../Proprietes/2510082.md)) ou la valeur de l'énumération (propriété [Valeur](../Proprietes/2510130.md)). 

Les fonctions WLangage suivantes permettent de retrouver les caractéristiques d'une énumération stockée : 



|   |   |
| --- | --- |
| [EnumérationDepuisNom](../WDLang1/1000020769.md) | Renvoie une valeur d'énumération connue par son nom. |
| [EnumérationDepuisValeur](../WDLang1/1000020768.md) | Renvoie une valeur d'énumération connue par sa valeur associée. |
| [EnumérationVérifieNom](../WDLang1/1000020767.md) | Vérifie si une valeur d'énumération connue par son nom est valide. |
| [EnumérationVérifieValeur](../WDLang1/1000020866.md) | Vérifie si une valeur d'énumération connue par sa valeur associée est valide. |






<a name="NOTE0_9"></a>


### Limites
<a name="limites_ELTPARAGRAPHE000232"></a>

- Les énumérations ne sont pas disponibles en compilation dynamique. 

- Les énumérations ne sont pas utilisables hors projet. 









