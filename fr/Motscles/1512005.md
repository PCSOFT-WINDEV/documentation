
## Opérateurs d'indirection

***En anglais : Indirection operators***
				



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Les opérateurs **{**et **}** permettent d'accéder à un champ, une variable, un membre d'une classe ou une rubrique de fichier en construisant dynamiquement le nom du champ, de la variable, du membre de la classe ou de la rubrique.

L'intérêt des opérateurs **{** et **}** est multiple :

- Passer en paramètre un nom de champ, de variable, de membre d'une classe ou de rubrique.

- Construire le nom d'un champ, de la variable, d'un membre d'une classe ou d'une rubrique par programme.






![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png) Les indirections ne sont pas gérées sur les variables locales. **Les indirections sont gérées uniquement sur les variables globales**. Une indirection sur une variable locale d'un traitement provoquera l'erreur "élément inconnu".




<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
// Indirection simple 
{"NOM"} = NomCli		// Est équivalent à NOM=NomCli 
{"NOM"} = {"CL.NOMCLI"}	// Est équivalent à NOM=CL.NOMCLI 
{"FENCLI.NOM"} = NomCli	// Est équivalent à FENCLI.NOM=NomCli 
{"FENCLI"+".NOM"} = NomCli	// Est équivalent à FENCLI.NOM=NomCli 

// Indirection typée
{"Nom",indChamp} = NomCli

// Indirection avec un nom de fichier dans une variable
nNomFic est une chaîne = "Client"
{nNomFic + ".Nom"} = "Durand"
HAjoute({nNomFic})
```




<a name="Exemple2"></a>





```wl
// Indirection dans une procédure 
NomChamp est une chaîne
NomChamp = "SAI_SAISIE1"			// SAI_SAISIE1 est le nom du champ 
// Appel d'une procédure rendant un champ invisible 
Rend_Invisible(NomChamp)

PROCEDURE Rend_Invisible(NChamp)
{NChamp}..Etat = Invisible
```




<a name="Exemple3"></a>



```wl
// Indirection avec variable 
NomAlias est une chaîne
NomAlias = FenPrécédente()
// NOM est le nom du champ 
// NomCli est la valeur à affecter 
{NomAlias+".NOM"} = NomCli 
Abrev est une chaîne
NomRub est une chaîne
{Abrev+"."+NomRub} = NomCli 
{Abrev+"."+NomRub} = {NomChamp}
```


<a name="Exemple4"></a>





```wl
// Déclaration d'une classe avec deux membres 
Maclasse est une classe
Membre1 est une chaîne
Membre2 est une chaîne
// Affichage de la valeur d'un membre 
PROCEDURE GLOBALE AfficheMembre(Numéro)
// Récupération de la valeur du membre global choisi
Valeur est une chaîne = {"::Membre"+Numéro}
Info(Valeur)
```



```wl
c est un cWord
var est une Description de Variable
Def est une Definition

Def = RécupèreDéfinition(c)
// Récupère la 2ème variable de la classe : version de Word
var = Def..Variable[2]

Trace(var..Nom) // Affiche le nom de la variable
Trace("Version de Word: ",{c, var}) // Affiche la version de Word
```

<a name="XSYNTAXE"></a>
<a name="SYNTAXE1"></a>

## Syntaxe

### Indirection simple

`{ <Expression> }
`
---

**`<Expression> : (Chaîne de caractères)`**

Expression permettant d'identifier le champ, la variable ou la rubrique à manipuler. L'élément est tout d'abord recherché :

- parmi les variables,

- selon le nombre de points dans le nom :

	- 0 point : recherche parmi les champs, puis les rubriques HFSQL.

	- 1 point : recherche parmi les rubriques HFSQL, les champs, puis les paramètres de requête.

	- supérieur à 1 point : recherche parmi les champs.




- parmi les éléments spéciaux conservés par compatibilité (par exemple _Ongletx, ...).




Si cette expression correspond à une chaîne vide (""), une erreur du WLangage est générée.


<a name="SYNTAXE2"></a>

### Indirection sur un membre de classe

`{ <Expression> }
`
---

**`<Expression> : (Chaîne de caractères)`**

Expression permettant d'identifier le membre de la classe.

Si le membre est :

- global, utilisez la syntaxe suivante : {" **::** NomDuMembre"}

- n'est pas global, utilisez la syntaxe suivante : {" **:** NomDuMembre"}




Si cette expression correspond à une chaîne vide (""), une erreur du WLangage est générée.  


<a name="SYNTAXE3"></a>

### Indirection sur un membre de classe

`{ <Classe> , <Membre> }`
---

**`<Classe> : (Chaîne de caractères)`**

Instance de la classe

**`<Membre> : (Chaîne de caractères ou variable de type Description de variable)`**

Correspond : 

- soit au nom du membre,

- soit à une variable de type [Description de Variable](../WDLang1/1000019560.md). 





<a name="SYNTAXE4"></a>

### Indirection en spécifiant le type de l'élément (optimise la vitesse d'exécution)

`{ <Expression>, <Type> }
`
---

**`<Expression> : (Chaîne de caractères)`**

Expression permettant d'identifier le champ, la variable ou la rubrique à manipuler. Si cette expression correspond à une chaîne vide (""), une erreur du WLangage est générée.

**`<Type> : (Constante)`**

Constante permettant de préciser le type de l'élément recherché :


|   |   |
| --- | --- |
| *indChamp* | Élément recherché parmi les champs et les groupes. |
| *indConnexion* | Élément recherché parmi les connexions. |
| *indEtat* | Élément recherché parmi les états.<br><br> |
| *indFenêtre* | Élément recherché parmi les fenêtres.<br><br> |
| *indFichier* | Élément recherché parmi les fichiers. |
| *indGPW* | Élément recherché parmi les éléments du groupware utilisateur (permet de récupérer l'état initial des champs).<br><br> |
| *indLiaison* | Élément recherché parmi les liaisons.<br><br> |
| *indParamètreRequête* | Élément recherché parmi les paramètres des requêtes. |
| *indRubrique* | Élément recherché parmi les rubriques. |
| *indVariable* | Élément recherché parmi les variables.<br><br>![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png) L'élément est recherché uniquement parmi les variables globales. |
| *indPage* | Élément recherché parmi les pages.<br><br> |







<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques




### Quand utiliser l'indirection ?
<a name="quand_utiliser_indirection_ELTPARAGRAPHE000232"></a>L'indirection peut être utilisée par exemple :

- Pour accéder à la valeur d'un élément (champ, variable, rubrique, ...) :
	
	```wl
	{s_NomChamp} = 10
	ValeurChamp = {s_NomChamp}
	```


- Pour utiliser une propriété :
	
	```wl
	{s_NomChamp}..Hauteur = 10
	```


- Pour manipuler une colonne d'un champ Table par programmation :
	
	```wl
	{s_NomColonne, indChamp}[Numéro_Ligne] = 10
	```


- Pour manipuler une colonne d'une table :
	
	```wl
	{s_NomTable + "." +s_NomColonne, indChamp}[Numéro_Ligne] = 10
	```






<a name="NOTE0_2"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/fr/WINDOWS.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/fr/UNIVERSALAPP.png)![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png)![Code Utilisateur (MCU)](https://doc.pcsoft.fr/ext/images/fr/MCU.png) 

### Désigner l'objet en cours
<a name="designer_objet_cours_ELTPARAGRAPHE000255"></a>Pour désigner l'objet en cours, utilisez les mots-clés suivants :

- [MoiMême](../Motscles/1511007.md) : le champ en cours

- [MaFenêtre](../Motscles/1511004.md) : la fenêtre en cours

- [MaPage](../Motscles/1511017.md) : la page en cours
	![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/fr/UNIVERSALAPP.png)![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png) Ce mot-clé n'est pas disponible.

- [MonEtat](../Motscles/1511010.md) : l'état en cours
	![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png) Ce mot-clé n'est pas disponible.

- [MaSource](../Motscles/1511001.md) : la source de données courante.

- [MonFichier](../Motscles/1511019.md) : le fichier qui a déclenché le trigger en cours.



<a name="NOTE0_3"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/fr/WINDOWS.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/fr/UNIVERSALAPP.png)![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png)![Code Utilisateur (MCU)](https://doc.pcsoft.fr/ext/images/fr/MCU.png) 

### Indirection sur une structure
<a name="indirection_sur_une_structure_ELTPARAGRAPHE000291"></a>Une indirection ne peut pas contenir un accès à une structure ou à une instance de classe. Cependant, il est possible d'utiliser la syntaxe suivante :


```txt
{<Variable structure>} : membre
```

<a name="NOTE0_4"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/fr/WINDOWS.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/fr/UNIVERSALAPP.png)![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png)![Code Utilisateur (MCU)](https://doc.pcsoft.fr/ext/images/fr/MCU.png) 

### Utiliser l'indirection sur les chemins dans un document XML
<a name="utiliser_indirection_sur_les_chemins_dans_document_xml_ELTPARAGRAPHE000302"></a>Il est possible d'utiliser les opérateurs d'indirection pour construire le chemin d'un élément dans un document XML. 

Exemple : 

```wl
sDocXML est une chaîne = [
	<?xml version="1.0" encoding="UTF-8"?>
	<Document>
	<InfoVersion>
	<Version>21</Version>
	<Millesime>2016</Millesime>
	</InfoVersion>
	</Document>
]

xmlExemple est un xmlDocument = XMLOuvre(sDocXML, depuisChaîne)

sParam est une chaîne = "WinDev_Param"

// Lecture directe d'une information ...
Trace(xmlExemple.Document.InfoVersion.Version) 

// Lecture avec une partie du chemin XML contenu dans une variable
// (idem si rubrique d'un fichier, paramètre d'une fenêtre, méthode, ...)
sParamètre est une chaîne = "InfoVersion"
Trace ({"xmlExemple.Document." + sParamètre + ".Version"})
```

<a name="NOTE0_5"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/fr/WINDOWS.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/fr/UNIVERSALAPP.png)![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png)![Code Utilisateur (MCU)](https://doc.pcsoft.fr/ext/images/fr/MCU.png) 

### Limitations
<a name="limitations_ELTPARAGRAPHE000311"></a>L'opérateur d'indirection permet de retrouver un élément par son nom.

Les opérations suivantes ne sont donc pas autorisées dans une indirection :

- accès à un membre d'une instance de classe ou de structure autre que l'instance de classe courante (opérateurs : et :: ).

- accès à un sous-élément de champ ou de tableau (opérateur [] ).

- accès à une sous-partie de chaîne (opérateur [[]] ).

- accès à une propriété (opérateur ..).

- indirection (opérateur { } ).

- appel de procédure ou de méthode d'objet Automation.

- déclaration de variable.




**Remarques** :

- Les opérateurs d'accès sur un élément (opérateurs : et [] et [[]] et .. ) peuvent être utilisés sur l'élément obtenu par indirection. Quelques exemples :
	
	```txt
	{ <indirection> } : membre
	
	{ <indirection> } [ indice ]
	
	{ <indirection> } [[ indice ]]
	
	{ <indirection> }..propriété
	```


- Pour appeler une procédure connue par son nom, utilisez les fonctions [Exécute](../WDLang1/3013041.md) ou [ExécuteTraitement](../WDLang1/3013031.md).

- Pour combiner ces opérations de manière dynamique, il est possible d'utiliser la compilation dynamique (voir la fonction [Compile](../WDLang1/3013015.md)).



<a name="NOTE0_6"></a>
![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png) 

### Indirections en Java
<a name="indirections_java_ELTPARAGRAPHE000379"></a>Les indirections ne sont pas gérées sur les variables locales.


- ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=Lesindirections.gif) ***Exemples unitaires (WINDEV)*** : **Les indirections** <br>Manipulation des indirections à l'aide des opérateurs { et }.<br>Les opérateurs { et } permettent d'accéder à un champ ou une rubrique de fichier en construisant dynamiquement le nom du champ ou le nom de la rubrique. <br>Ce programme réalisé avec WINDEV met en œuvre le mécanisme d'indirection sur les champs des fenêtres pour gérer le "Undo-Redo" et le "couper-copier-coller" de manière générique.
- ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=Lesindirections.gif) ***Exemples unitaires (WEBDEV)*** : **Les indirections** <br>Utilisation des indirections sur une variable.<br><br>Il est à noter que les indirections sont possibles sur les champs, les rubriques, les liaisons, etc. Le principe est identique à celui présenté dans cet exemple.
- ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=Lesindirections.gif) ***Exemples unitaires (WINDEV Mobile)*** : **Les indirections** <br>Utilisation des opérateurs d'indirection { et }.<br>Les opérateurs { et } permettent d'accéder à un champ ou une rubrique de fichier en construisant dynamiquement le nom du champ ou le nom de la rubrique. <br>Cet exemple met en œuvre le mécanisme d'indirection sur les champs des fenêtres pour gérer le "Undo-Redo" et le "couper-copier-coller" de manière générique.


