
## Tableau associatif (Type de variable)

***En anglais : Associative array (Type of variable)***
				



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Un tableau associatif est un type "avancé" de tableau : il permet de regrouper une série d'éléments du même type. Chaque élément du tableau est indexé sur n'importe quel type d'information (et non plus simplement sur un indice numérique, comme dans les autres types de tableau).

**Remarque** : Ce type de tableau permet un accès rapide à un élément quelconque, avec certaines options sur les chaînes.


<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
// Remplit un tableau associatif avec les tailles de fichiers
taTailleFichier est un tableau associatif d'entiers
taTailleFichier["Fichier1.txt"] = fTaille("Fichier1.txt")
taTailleFichier["Fichier2.txt"] = fTaille("Fichier2.txt")

// Récupère la taille d'un fichier
Trace(taTailleFichier["Fichier2.txt"])

// Parcourt les tailles des fichiers
sFichier est une chaîne
nTaille est un entier
POUR TOUT ELEMENT nTaille,sFichier DE taTailleFichier
	Trace(ChaîneConstruit("%1 : %2", sFichier, nTaille)) 
FIN
```



<a name="XSYNTAXE"></a>
<a name="SYNTAXE1"></a>

## Syntaxe

### Déclarer un tableau associatif

`<Nom du tableau> est un tableau associatif [(<Options> [, <Valeur par défaut> [, <Type de clé> [,<Taille initiale>]]])] de <Type>
`
---

**`<Nom du tableau> : ()`**

Nom de la variable tableau à déclarer.

**`<Options> : ()`**

Combinaison de constantes permettant de paramétrer le fonctionnement du tableau : 



|   |   |
| --- | --- |
| *AvecDoublon* | Cette constante permet d'autoriser les doublons. En effet, par défaut, un tableau associatif ne peut pas contenir de doublons. |
| *ccSansAccent* | Si le type de clé est une chaîne (par défaut), l'indexation ne tient pas compte des accents.<br><br> |
| *ccSansCasse* | Si le type de clé est une chaîne (par défaut), l'indexation ne tient pas compte de la casse. |
| *ccSansEspace* | Si le type de clé est une chaîne (par défaut), l'indexation ne tient pas compte des espaces. |
| *ccSansPonctuationNiEspace* | Si le type de clé est une chaîne (par défaut), l'indexation ne tient pas compte de la ponctuation et des espaces.<br><br> |
| *SansDoublon*(valeur par défaut) | Par défaut, un tableau associatif ne peut pas contenir de doublons.<br>Il est possible de ne pas indiquer cette constante. |





La valeur "\*" permet de conserver la valeur par défaut de l'option.

**`<Valeur par défaut> : ()`**

Valeur renvoyée lors de l'accès à un élément inexistant dans un tableau associatif sans doublon. Ce paramètre est ignoré dans un tableau associatif avec doublons. Par défaut, la valeur par défaut du type du tableau est utilisé (0, "", Faux).

La valeur "\*" permet de conserver la valeur par défaut du type.

**`<Type de clé> : ()`**

Type de la clé utilisée pour parcourir le tableau. Par défaut, le parcours est réalisé sur une clé de type chaîne. Les types autorisés sont les suivants :



|   |   |
| --- | --- |
| *wlChaîne* | Chaîne de caractères |
| *wlChaîneUnicode* | Chaîne de caractères Unicode |
| *wlDate* | Date |
| *wlDateHeure* | DateHeure |
| *wlEntier* | Entier signé sur 4 octets |
| *wlEntier_8* | Entier signé sur 8 octets |
| *wlEntierSansSigne_4* | Entier non signé sur 4 octets |
| *wlEntierSansSigne_8* | Entier non signé sur 8 octets |
| *wlHeure* | Heure |
| *wlMonétaire* | Monétaire |
| *wlNumérique* | Numérique |
| *wlRéel* | Réel sur 8 octets |





La valeur "\*" permet de conserver le type de clé par défaut (chaîne).



**`<Taille initiale> : ()`**

Entier. Correspond à la taille initiale de la zone mémoire pour le tableau associatif.

L'ajout peut être long dans un tableau associatif en cas de redimensionnement du tableau. En cas d'ajouts multiples, il est conseillé de surdimensionner le tableau pour optimiser les ajouts.

La valeur "\*" permet de conserver la taille par défaut.



**`<Type> : ()`**

Type des éléments constituant le tableau. 
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/fr/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/fr/LX.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/fr/UNIVERSALAPP.png) Remarque : Les éléments constituant le tableau peuvent être également des tableaux, des tableaux fixes, des tableaux associatifs, des files, des piles ou des listes. 


<a name="SYNTAXE2"></a>

### Déclarer un paramètre de type tableau associatif

`<Nom du paramètre> est un tableau associatif de <Type>
`
---

**`<Nom du paramètre> : ()`**

Nom de la variable tableau à déclarer.

**`<Type> : ()`**

Type des éléments constituant le tableau. 
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/fr/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/fr/LX.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/fr/UNIVERSALAPP.png) Remarque : Les éléments constituant le tableau peuvent être également des tableaux, des tableaux fixes, des tableaux associatifs, des files, des piles ou des listes. 



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques




### Accès aux tableaux associatifs
<a name="acces_aux_tableaux_associatifs_ELTPARAGRAPHE000206"></a>Pour manipuler un tableau associatif, il est possible d'utiliser les propriétés suivantes :


|   |   |
| --- | --- |
| [Occurrence](../Proprietes/2510097.md) | Renvoie le nombre d'occurrences dans le tableau. |
| [Vide](../Proprietes/2510126.md) | Permet de savoir si le tableau est vide ou non. |





Les syntaxes ***&Tableau*** et ***Tableau1 = Tableau2*** sont interdites. La copie et la prise de référence sur les tableaux associatifs ne sont pas disponibles.


<a name="NOTE0_2"></a>




### Accès aux éléments des tableaux associatifs
<a name="acces_aux_elements_des_tableaux_associatifs_ELTPARAGRAPHE000229"></a>L'accès aux éléments des tableaux associatifs peut être différent si le tableau associatif est avec ou sans doublons.


|   |   |
| --- | --- |
| **Tableau associatif sans doublons** |   |
| Propriété [Existe](../Proprietes/1000022167.md) | <br><pre><code>MonTableau[MaClé]..Existe</code></pre><br><br>Renvoie <u><u><u><u>Vrai</u></u></u></u> si l'élément MaClé existe, <u><u><u><u>Faux</u></u></u></u> sinon.<br><br> |
| Propriété [Vide](../Proprietes/2510126.md) | <br><pre><code>MonTableau[MaClé]..Vide</code></pre><br><br>Renvoie <u><u><u><u>Vrai</u></u></u></u> si l'élément MaClé n'existe pas, <u><u><u><u>Faux</u></u></u></u> sinon. |
| Propriété [Occurrence](../Proprietes/2510097.md) | <br><pre><code>MonTableau[MaClé]..Occurrence</code></pre><br><br>Renvoie 1 si l'élément MaClé existe, 0 si l'élément n'existe pas. |
| Affectation d'un élément | <br><pre><code>MonTableau[MaClé] = <Valeur></code></pre><br><br>Si l'élément MaClé n'existe pas, un élément MaClé est créé et affecté avec &lt;Valeur&gt;.<br>Si l'élément MaClé existe, sa valeur est modifiée. |
|   | <br><pre><code>MonTableau[MaClé, Indice] = Valeur</code></pre><br><br>Si Indice est différent de 1, une erreur WLangage est affichée.<br>Si l'élément MaClé n'existe pas, une erreur WLangage est affichée.<br>Si l'élément MaClé existe, sa valeur est modifiée. <br><br>Syntaxe compatible avec tableaux associatifs avec doublons. |
|   | <br><pre><code>MonTableau[MaClé]++</code></pre><br><br>Crée l'entrée dans le tableau associatif, avec la valeur par défaut spécifiée lors de la déclaration du tableau. |
| Accès à un élément | <br><pre><code>MonTableau[MaClé]</code></pre><br><br>Si l'élément MaClé n'existe pas, renvoie la valeur par défaut du tableau.<br>Si l'élément MaClé existe, renvoie la valeur de l'élément. |
|   | <br><pre><code>MonTableau[MaClé, Indice]</code></pre><br><br>Si Indice est différent de 1, une erreur WLangage est affichée.<br>Si l'élément MaClé n'existe pas, une erreur WLangage est affichée.<br>Si l'élément MaClé existe, renvoie la valeur de l'élément.<br><br>Syntaxe compatible avec tableaux associatifs avec doublons. |
| Adresse d'un élément | <br><pre><code>&MonTableau[MaClé]</code></pre><br><br>Si l'élément MaClé n'existe pas, affichage d'une erreur WLangage.<br>Si l'élément MaClé existe, renvoie l'adresse de l'élément.<br><br> |
|   | <br><pre><code>&MonTableau[MaClé, Indice]</code></pre><br><br>Si Indice est différent de 1, une erreur WLangage est affichée.<br>Si l'élément MaClé n'existe pas, une erreur WLangage est affichée.<br>Si l'élément MaClé existe, renvoie l'adresse de l'élément.  <br><br>Syntaxe compatible avec tableaux associatifs avec doublons.<br><br> |






|   |   |
| --- | --- |
| **Tableau associatif avec doublons** |   |
| Propriété [Existe](../Proprietes/1000022167.md) | <br><pre><code>MonTableau[MaClé]..Existe</code></pre><br><br>Renvoie <u><u><u><u>Vrai</u></u></u></u> si au moins un élément MaClé existe, <u><u><u><u>Faux</u></u></u></u> si l'élément MaClé n'existe pas.<br><br> |
| Test de l'existence d'une clé : Propriété [Vide](../Proprietes/2510126.md) | <br><pre><code>MonTableau[MaClé]..Vide</code></pre><br><br>Renvoie <u><u><u><u>Vrai</u></u></u></u> si l'élément MaClé n'existe pas, <u><u><u><u>Faux</u></u></u></u> si au moins un élément MaClé existe. |
| Nombre d'éléments d'une clé : Propriété [Occurrence](../Proprietes/2510097.md) | <br><pre><code>MonTableau[MaClé]..Occurrence</code></pre><br><br>Renvoie le nombre d'éléments MaClé. |
| Affectation d'un élément | <br><pre><code>MonTableau[MaClé] = <Valeur></code></pre><br><br>Crée un élément MaClé, et affecte cet élément avec la valeur indiquée (même si des éléments MaClé existent déjà). |
|   | <br><pre><code>MonTableau[MaClé, Indice] = Valeur</code></pre><br><br>Si Indice est supérieur au nombre d'éléments existants, une erreur WLangage est affichée.<br>Si l'élément MaClé n'existe pas, une erreur WLangage est affichée. <br>Si l'élément MaClé existe, la valeur de l'élément connu par son indice est modifiée. |
| Accès à un élément | <br><pre><code>MonTableau[MaClé]</code></pre><br><br>Syntaxe interdite, provoquant une erreur WLangage (car il peut y avoir plusieurs éléments correspondant à la même clé). |
|   | <br><pre><code>MonTableau[MaClé, Indice]</code></pre><br><br>Si Indice est supérieur au nombre d'éléments existants, une erreur WLangage est affichée.<br>Si l'élément MaClé n'existe pas, une erreur WLangage est affichée.<br>Si l'élément MaClé existe, renvoie la valeur de l'élément connu par son indice. |
| Adresse d'un élément | <br><pre><code>&MonTableau[MaClé]</code></pre><br><br>Syntaxe interdite |
|   | <br><pre><code>&MonTableau[MaClé, Indice]</code></pre><br><br>Si Indice est supérieur au nombre d'éléments existants, une erreur WLangage est affichée.<br>Si l'élément MaClé n'existe pas, une erreur WLangage est affichée.<br>Si l'élément MaClé existe, renvoie l'adresse de l'élément identifié par son indice.<br><br> |


Il est possible d'utiliser [l'affectation multiple](../Motscles/1514031.md) pour les tableaux associatifs.


<a name="NOTE0_2_1"></a>


### Comment initialiser un tableau associatif dès sa déclaration?
<a name="comment_initialiser_tableau_associatif_des_declaration_ELTPARAGRAPHE000457"></a>Pour initialiser un tableau associatif dès sa déclaration, il suffit d'ajouter directement les couples "clé / valeur" en utilisant
l'opérateur [ ] (crochet). La syntaxe est la suivante :

```txt
taMonTableau est un tableau associatif de xxx = ...
	[ [<clé1>, <valeur1>], [<clé2>, <valeur2>], ... ]
```

Par exemple : 
```wl
// Mémorise les taux de TVA par leur dénomination
taTauxTVA est un tableau associatif de réels = ...
	[["Normale", 0.2], ["Intermédiaire", 0.1], ["Réduite", 0.055]]
```







<a name="NOTE0_3"></a>


### Parcours des tableaux associatifs
<a name="parcours_des_tableaux_associatifs_ELTPARAGRAPHE000474"></a>Le parcours des différents éléments d'un tableau associatif peut être réalisé en utilisant [la syntaxe POUR TOUT spécifique aux tableaux associatifs](../Motscles/1510019.md).


<a name="NOTE0_4"></a>


### Fonctions du WLangage et tableaux
<a name="fonctions_wlangage_tableaux_ELTPARAGRAPHE000485"></a>Plusieurs fonctions WLangage permettent de manipuler des tableaux associatifs. Pour plus de détails, consultez [Fonctions de gestion des tableaux](../WDLang1/3075006.md).

Il est ainsi par exemple possible de supprimer un élément d'un tableau associatif en utilisant la fonction [Supprime](../WDLang1/1000017354.md) (ou [TableauSupprime](../WDLang1/3075008.md)) en utilisant la syntaxe suivante : 

```txt
Supprime(NomTableau, clé)
TableauSupprime(NomTableau, Clé)
```



<a name="NOTE0_5"></a>




### Paramètres de type tableau associatif
<a name="parametres_type_tableau_associatif_ELTPARAGRAPHE000507"></a>

- Le type des éléments du tableau associatif passé en paramètre doit être le même que celui de la déclaration.

- Un tableau associatif ne peut pas être passé par valeur. Il faut obligatoirement passer une variable en paramètre. 
	Exemple :
	
	```wl
	// Ajoute un client dans le tableau
	PROCEDURE AjouteClient(taClient est un tableau associatif de CClient,
		sNom est une chaîne,
		sInfo est une chaîne)
	// Construit le nouvel objet client
	c est CClient(sNom, sInfo)
	// Ajoute le nouvel objet dans le tableau
	taClient[sNom] = c
	```


- Aucune vérification n'est effectuée à la compilation : les vérifications sont uniquement effectuées en exécution.

- La copie et la prise de référence sur les tableaux associatifs ne sont pas disponibles.





<a name="NOTE0_6"></a>




### Tableau associatif de classes ou de structures
<a name="tableau_associatif_classes_structures_ELTPARAGRAPHE000523"></a>Si vous utilisez un tableau associatif de classes ou de structures, les différentes instances sont automatiquement recopiées lors des ajouts. 

Si vous utilisez un tableau associatif de classes **dynamiques** ou de structures **dynamiques**, il est nécessaire d'allouer les classes ou les structures au moment de l'ajout (les libérations sont automatiquement réalisées).

Exemples :


```wl
// Cas d'un tableau d'objets NON dynamiques
// Objet
clObj est un cLMaClasse
// Tableau associatif d'objets
taAssociatif est un tableau associatif de cLMaClasse
// Affectation d'un des membres de l'objet
clObj:nMembre = 0
// Ajout dans le tableau associatif. 
// C'est un tableau d'objets non dynamiques, l'objet est donc copié
// L'affectation peut être "longue" s'il y a beaucoup de membres
taAssociatif["Association"] = clObj
// Changement dans l'objet initial
clObj:nMembre = 2
// L'objet a été copié on retrouve donc 0 dans le tableau 
// (valeur au moment de l'affectation)
Info(taAssociatif["Association"]:nMembre)
```



```wl
// Cas d'un tableau d'objets dynamiques
// Objet dynamique
pclObj est un cLMaClasse dynamique
// Tableau associatif d'objets dynamiques
taAssociatif est un tableau associatif de cLMaClasse dynamique
// Allocation d'un objet dynamique
pclObj = allouer un cLMaClasse
// Affectation d'un des membres de l'objet
pclObj:nMembre = 0
// Ajout dans le tableau associatif. 
// C'est un tableau d'objets dynamiques. 
// C'est donc un nouveau "pointeur" sur le même objet (affectation rapide)
taAssociatif["Association"] = pclObj
// Changement dans l'objet initial
pclObj:nMembre = 2
// Le tableau contient le même objet donc 2 dans le tableau aussi
Info(taAssociatif["Association"]:nMembre)
```


Les accès aux membres d'un élément inexistant d'un tableau associatif de structures ou de classes ne créent pas automatiquement l'instance de la structure ou de la classe.

La ligne de code :


```wl
MonTableauAssociatif["Clé"]:Membre = 5
```


échoue si l'élément "Clé" n'a pas été créé auparavant.


<a name="NOTE0_7"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/fr/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/fr/LX.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/fr/UNIVERSALAPP.png) 

### Tableau associatif de tableau, file, pile, liste
<a name="tableau_associatif_tableau_file_pile_liste_ELTPARAGRAPHE000551"></a>Les syntaxes suivantes sont supportées : 

```txt
<variable> est un tableau associatif de tableaux d'entiers
<variable> est un tableau associatif (avecDoublon) de tableaux d'entiers
<variable> est un tableau associatif (avecDoublon,wlEntier) de tableaux d'entiers
<variable> est un tableau associatif de tableaux de 5 entiers
<variable> est un tableau associatif (avecDoublon) de tableaux de 5 entiers
<variable> est un tableau associatif (avecDoublon,wlEntier) de tableaux de 5 entiers

<variable> est un tableau associatif de tableaux fixes de 5 entiers
<variable> est un tableau associatif (avecDoublon) de tableaux fixes de 5 entiers
<variable> est un tableau associatif (avecDoublon,wlEntier) de tableaux fixes de 5 entiers
<variable> est un tableau associatif de tableaux associatifs d'entiers
<variable> est un tableau associatif (avecDoublon) de tableaux associatifs d'entiers
<variable> est un tableau associatif (avecDoublon,wlEntier) de tableaux associatifs d'entiers

<variable> est un tableau associatif de tableaux associatifs (avecDoublon) d'entiers
<variable> est un tableau associatif (avecDoublon) de tableaux associatifs ...
					(avecDoublon) d'entiers
<variable> est un tableau associatif (avecDoublon,wlEntier) de tableaux associatifs ...
					(avecDoublon) d'entiers
<variable> est un tableau associatif de tableaux associatifs ...
					(avecDoublon,wlEntier) d'entiers
<variable> est un tableau associatif (avecDoublon) de tableaux associatifs ...
					(avecDoublon,wlEntier) d'entiers
<variable> est un tableau associatif (avecDoublon,wlEntier) de tableaux associatifs ...
					(avecDoublon,wlEntier) d'entiers

<variable> est un tableau associatif de files d'entiers
<variable> est un tableau associatif (avecDoublon) de files d'entiers
<variable> est un tableau associatif (avecDoublon,wlEntier) de files d'entiers

<variable> est un tableau associatif de piles d'entiers
<variable> est un tableau associatif (avecDoublon) de piles d'entiers
<variable> est un tableau associatif (avecDoublon,wlEntier) de piles d'entiers

<variable> est un tableau associatif de listes d'entiers
<variable> est un tableau associatif (avecDoublon) de listes d'entiers
<variable> est un tableau associatif (avecDoublon,wlEntier) de listes d'entiers
```


Exemple : Tableau associatif de tableaux de chaînes : 

```wl
// Déclaration
tabClasses est un tableau associatif de tableau <agrandissement> de chaînes

// Insère un élément dans le tableau associatif 
Insère(tabClasses, "CM2")

// Remplissage des élèves CM2
tabClasses["CM2"][1] = "ALARD Serge"
tabClasses["CM2"][2] = "BERTAU Kelian"

// Insère un élément dans le tableau associatif 
Insère(tabClasses, "CM1")

// Remplissage des élèves CM1
tabClasses["CM1"][1] = "ALONSO Benoit"
tabClasses["CM1"][2] = "CRISTO Benjamin"

POUR TOUT UneClasse, sNom de tabClasses
	POUR TOUT Elève DE UneClasse
		Trace(sNom + ":" + Elève)
	FIN
FIN
```



- ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=Lestableauxassociatifs.gif) ***Exemples unitaires (WINDEV)*** : **Les tableaux associatifs** <br>Utilisation des tableaux associatifs du WLangage :<br>- Remplir un tableau associatif<br>- Accéder à un tableau associatif


