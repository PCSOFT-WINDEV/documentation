


## Compile (Fonction)

***En anglais : Compile***



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Compile dynamiquement une procédure dont le code source est fourni. La procédure créée et compilée est une procédure globale au projet.

Remarque : Les fonctions [ExécuteCode](../WDLang1/1000019783.md) et [EvalueExpression](../WDLang1/1000019782.md) permettent également d'utiliser du code généré dynamiquement. 


<a name="sample_code"></a>

## Exemple
<a class="notetitle" target="_blank" href="$DOC$=1000003013015&name=compile_fonction&product=WD">Voir des exemples supplémentaires</a>
<a name="Exemple1"></a>

```wl
// Syntaxe 1 : Compilation dynamique d'une procédure globale
// Source : champ dans lequel est saisi le code de procédure

sRésultat est une chaîne 
sRésultatCompile est une chaîne 
sRésultatCompile = Compile("Proc_dynamique", MonCodeSource)
SELON sRésultatCompile
	CAS "" : sRésultat = ExécuteTraitement("Proc_dynamique", trtProcédure)
	CAS "ERR" : sRésultat = "Compilation impossible." + RC + ErreurInfo()
	AUTRE CAS : sRésultat = sRésultatCompile
FIN
RENVOYER sRésultat
```
<a name="Exemple2"></a>

```wl
// Syntaxe 2 : Compilation dynamique d'une procédure globale
// Utilisation du type Procédure
// Source : champ dans lequel est saisi le code de procédure

Formule est une Procédure = Compile("Proc_dynamique", MonCodeSource)
SI ErreurDétectée = Faux ALORS
	ExécuteTraitement("Proc_Dynamique", trtProcédure)
	// Autre possibilité pour exécuter la procédure : Formule()
SINON
	Info(ErreurInfo())
FIN
```
<a name="Exemple3"></a>

```wl
// Syntaxe 3 : Compilation dynamique d'une procédure globale
// Utilisation directe du type Procédure
// Source : champ dans lequel est saisi le code de procédure

Formule est une Procédure = Compile(MonCodeSource)
SI ErreurDétectée = Faux ALORS
	Formule()
SINON
	Info(ErreurInfo())
FIN
```

<a name="XSYNTAXE"></a>
<a name="SYNTAXE1"></a>

## Syntaxe

### Compilation dynamique d'une procédure globale

`<Résultat> = Compile(<Nom de la procédure> , <Code source>)`
---

**`<Résultat> : (Chaîne de caractères)`**

Résultat de la compilation :

- Chaîne vide ("") si la compilation a réussi. La procédure peut être exécutée grâce aux fonctions [ExécuteTraitement](../WDLang1/3013031.md) et [Exécute](../WDLang1/3013041.md) utilisées avec la constante *trtProcédure* .

- ERR si une erreur grave est survenue : fichier wdxxxcpl.dll non trouvé, &lt;Code source&gt; incorrect, pas de projet en cours, procédure globale déjà créée, etc. La fonction [ErreurInfo](../WDLang1/3013008.md) permet d'obtenir le détail de l'erreur.

- Un message d'erreur si une erreur de compilation a été détectée. Ce message correspond au libellé de l'erreur.




**`<Nom de la procédure> : (Chaîne de caractères)`**

Nom de la procédure globale dynamique à créer.

Pour créer une procédure locale, il suffit de préciser le nom de l'élément. Par exemple : "NomFenêtre.NomProcédure". 

Attention : Si la fonction **Compile** est appelée plusieurs fois avec le même nom de procédure, la dernière procédure créée est automatiquement écrasée.

**`<Code source> : (Chaîne de caractères)`**

Code source en WLangage de la procédure à compiler dynamiquement. 

- Si ce code contient des guillemets, il est nécessaire de les doubler (voir Exemple).

- Ce code peut contenir le code d'appel et de déclaration d'une procédure interne.





<a name="SYNTAXE2"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/fr/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/fr/LX.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/fr/UNIVERSALAPP.png)![Code Utilisateur (MCU)](https://doc.pcsoft.fr/ext/images/fr/MCU.png) 
### Compilation dynamique d'une procédure globale associée à une variable de type Procédure 

`<Résultat> = Compile(<Nom de la procédure> , <Code source>)`
---

**`<Résultat> : (Variable de type Procédure)`**

Nom de la variable de type [Procédure](../Motscles/1000018907.md) pointant sur la procédure compilée. 

La procédure peut être exécutée : 

- grâce à la fonction [ExécuteTraitement](../WDLang1/3013031.md).

- grâce à la fonction [Exécute](../WDLang1/3013041.md). 

- par un appel de la variable [Procédure](../Motscles/1000018907.md). 




En cas d'erreur de compilation, la variable [ErreurDétectée](../WDLang1/3087001.md) est à <u><u><u><u>Vrai</u></u></u></u> et la fonction [ErreurInfo](../WDLang1/3013008.md) permet d'obtenir le détail de l'erreur.

**`<Nom de la procédure> : (Chaîne de caractères)`**

Nom de la procédure globale dynamique à créer.

Pour créer une procédure locale, il suffit de préciser le nom de l'élément. Par exemple : "NomFenêtre.NomProcédure". 

Si la fonction **Compile** est appelée plusieurs fois avec le même nom de procédure, la dernière procédure créée est automatiquement écrasée.  

**`<Code source> : (Chaîne de caractères)`**

Code source en WLangage de la procédure à compiler dynamiquement. 

- Si ce code contient des guillemets, il est nécessaire de les doubler (voir Exemple).

- Ce code peut contenir le code d'appel et de déclaration d'une procédure interne.





<a name="SYNTAXE3"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/fr/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/fr/LX.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/fr/UNIVERSALAPP.png)![Code Utilisateur (MCU)](https://doc.pcsoft.fr/ext/images/fr/MCU.png) 
### Compilation dynamique d'une procédure anonyme

`<Résultat> = Compile(<Code source>)`
---

**`<Résultat> : (Variable de type Procédure)`**

Nom de la variable de type [Procédure](../Motscles/1000018907.md) pointant sur la procédure compilée. 

La procédure peut être exécutée par un appel de la variable [Procédure](../Motscles/1000018907.md). 

En cas d'erreur de compilation, la variable [ErreurDétectée](../WDLang1/3087001.md) est à <u><u><u><u>Vrai</u></u></u></u> et la fonction [ErreurInfo](../WDLang1/3013008.md) permet d'obtenir le détail de l'erreur.

La procédure est compilée en tant que procédure globale. Pour compiler une procédure associée à une fenêtre, il est nécessaire de nommer la procédure (syntaxe 2). 

**`<Code source> : (Chaîne de caractères)`**

Code source en WLangage de la procédure à compiler dynamiquement. 

- Si ce code contient des guillemets, il est nécessaire de les doubler.

- Ce code peut contenir le code d'appel et de déclaration d'une procédure interne.






<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques


### Utiliser la procédure dynamique
<a name="utiliser_procedure_dynamique_ELTPARAGRAPHE000194"></a>

- **Si vous utilisez la première syntaxe**, pour utiliser vos procédures dynamiques, il suffit de lancer la procédure grâce à la fonction [ExécuteTraitement](../WDLang1/3013031.md) ou à la fonction [Exécute](../WDLang1/3013041.md).
	Autre solution (déconseillée) : avant d'exécuter la fonction **Compile**, déclarez le nom de la procédure au compilateur du WLangage grâce au mot-clé [EXTERNE](../Motscles/1511013.md) et utilisez directement la procédure.

- **Si vous utilisez la seconde syntaxe**, pour utiliser vos procédures dynamiques, il suffit de lancer la procédure : 

	- soit grâce à la fonction [ExécuteTraitement](../WDLang1/3013031.md) ou à la fonction [Exécute](../WDLang1/3013041.md).

	- soit par un appel direct de la variable de type [Procédure](../Motscles/1000018907.md). 




- **Si vous utilisez la troisième syntaxe**, pour utiliser vos procédures dynamiques, il suffit de lancer la procédure par un appel direct de la variable de type [Procédure](../Motscles/1000018907.md). 


Une procédure compilée dynamiquement peut attendre de paramètres. Exemple : 

```wl
MonCodeSource est une chaîne = [
PROCÉDURE AdditionneXetY(aa, bb)
RENVOYER aa + bb
]
MaProc est une procédure = Compile(MonCodeSource)
Trace(MaProc(2, 3))
```

<a name="NOTE0_2"></a>


### Code dynamique
<a name="code_dynamique_ELTPARAGRAPHE000239"></a>

Les **structures** peuvent être utilisées dans le code dynamique.

Les **constantes ne peuvent pas être utilisées** dans le code dynamique (définies avec le mot-clé [CONSTANTE](../Motscles/1514012.md)).

Lors de l'utilisation de constantes dans un code, toutes les occurrences des constantes sont remplacées par leur valeur au moment de la compilation sous l'éditeur, mais la correspondance entre le nom des constantes et leur valeur n'est pas "embarquée" dans l'application. Donc la compilation dynamique ne peut pas utiliser les constantes.

Voici deux alternatives :

- 1ère solution : Utiliser des variables à la place des constantes. 
	Le code : 
	
	```wl
	CONSTANTE
		CST_Nom = 1
	FIN
	```

	devient par exemple
	
	```wl
	CST_Nom est un entier = 1
	```


- 2ème solution : Dans la chaîne qui contient le code à compiler dynamiquement, il suffit de remplacer le nom de la constante par sa valeur : 
	
	```wl
	sCode est une chaîne
	sCode = [
	Info(CST_Nom)
	]
	
	// Remplace le nom de la constante par sa valeur
	sCode = Remplace(sCode, "CST_Nom", CST_Nom, MotComplet + SansCasse)
	
	// Il est ensuite possible de compiler le code
	SI Compile("ProcDyn", sCode) <> "" ALORS
		Erreur("Erreur de compilation de la procédure dynamique : ", ...
			ErreurInfo())
	SINON
		// Puis de l'exécuter
		QUAND EXCEPTION DANS 
			ExécuteTraitement("ProcDyn", trtProcédure)
		FAIRE
			Erreur("Erreur d'exécution de la procédure dynamique : ", ...
				ExceptionInfo())
		FIN
	FIN
	```




<a name="NOTE0_3"></a>


### Paramètres d'une requête
<a name="parametres_une_requete_ELTPARAGRAPHE000270"></a>Si le code source utilisé pour la compilation dynamique exécute une requête paramétrée (fonction [HExécuteRequête](../WDLang4/3044080.md)), les paramètres attendus par la requête doivent obligatoirement être précisés dans la fonction [HExécuteRequête](../WDLang4/3044080.md).

Par exemple, il faut utiliser :

```wl
HExécuteRequête("MaRequête", hRequêteDéfaut, "Dupont")
```
au lieu de :

```wl
MaRequête.NomClient = "Dupont"
HExécuteRequête("MaRequête", hRequêteDéfaut)
```

<a name="NOTE0_4"></a>


### Déploiement d'une application utilisant la fonction Compile
<a name="deploiement_une_application_utilisant_fonction_docparampagetitleshort_ELTPARAGRAPHE000288"></a>Lors du déploiement d'une application utilisant la fonction **Compile** (création de l'exécutable ou déploiement du site), il est nécessaire d'indiquer toutes les librairies du Framework WINDEV/WEBDEV/WINDEV Mobile utilisées par le code compilé dynamiquement. En effet, ce code étant compilé dynamiquement, WINDEV, WEBDEV et WINDEV Mobile ne détectent pas les librairies du framework utilisées.
<a name="NOTE0_5"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/fr/UNIVERSALAPP.png)![Code Utilisateur (MCU)](https://doc.pcsoft.fr/ext/images/fr/MCU.png) 

### Limitations
<a name="limitations_ELTPARAGRAPHE000302"></a>

- Si une fenêtre (une page) du projet utilise une procédure locale ayant pour nom &lt;Nom de la procédure&gt;, les appels à &lt;Nom de la Procédure&gt; effectués depuis un événement WLangage de la fenêtre (de la page) exécuteront toujours la procédure locale.

- Si une procédure globale &lt;Nom de la procédure&gt; existe déjà, la compilation dynamique d'une procédure &lt;Nom de la procédure&gt; provoquera une erreur. 

- Par défaut, la surcharge des fonctions du WLangage n'est pas prise en compte lors de la compilation dynamique. 
	Par exemple, si vous avez surchargé la fonction [Trace](../WDLang1/3013050.md), l'appel de cette fonction dans un code compilé dynamiquement appellera la fonction WLangage et non la fonction surchargée. Pour forcer l'utilisation de la fonction surchargée dans la compilation dynamique, faites précéder le nom de la fonction par le mot-clé "Externe".

- Les énumérations et les combinaisons ne sont pas disponibles en compilation dynamique.





- ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=Compilationdynamique.gif) ***Exemples unitaires (WINDEV)*** : **Compilation dynamique** <br>Compilation dynamique de code WLangage (stocké sous forme de chaîne), exécution de la procédure générée dynamiquement et traitement des éventuelles erreurs d'exécution.<br>La compilation dynamique est très utile par exemple dans le cas de calculs mathématiques proposés à l'utilisateur final.

<a name="XComposante"></a>

## Composante :
wd280vm.dll
