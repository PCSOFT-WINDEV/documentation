
## tblrSélection (Type de variable)

***En anglais : psheetSelection***
				



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Le type **tblrSélection** permet de connaître toutes les caractéristiques avancées d'une sélection dans un champ Tableur. Les caractéristiques de cette sélection peuvent être connues à l'aide de différentes propriétés WLangage. 

La propriété [.Sélection](../Proprietes/1000023033.md) permet d'obtenir une variable de type **tblrSélection**. 

**Remarque** : Pour plus de détails sur la déclaration de ce type de variable et l'utilisation des propriétés WLangage, consultez [Déclaration d'une variable](../Motscles/1514032.md).
<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
sel est un tblrSélection = TBLR_MonTableur.Sélection
SI sel.Type = tblrSelSimpleCell ALORS
	// Afficher le nom de la cellule dans la barre de message
	MGSBAR = sel.CelluleMin
FIN
```



```wl
t est un tblrSélection
t = TBLR_SansNom1.Sélection
SELON t.Type
	CAS tblrSelSimpleCell : Trace("Vous avez sélectionné une seule case.")
	CAS tblrSelSimpleEntête : Trace("Vous avez sélectionné un seul entête.")
	CAS tblrSelMultiCell : Trace("Vous avez sélectionné plusieurs cases.")
	CAS tblrSelMultiEntête : Trace("Vous avez sélectionné plusieurs entêtes.")	
	CAS tblrSelRectCell : Trace("Vous avez sélectionné plusieurs cases (rectangle).")
	CAS tblrSelVide : Trace("Vous n'avez sélectionné aucune case.")
	CAS tblrSelTout : Trace("Vous avez sélectionné toute la feuille.")	
	AUTRE CAS	
FIN

te est un tableau de chaînes = t.Entête
tc est un tableau de chaînes = t.Cellule
Trc est un tableau de chaînes = t.RectCellule
Trace("-----------------------------")
Trace("")

Trace("Cellule min:", t.CelluleMin)
Trace("Cellule max:", t.CelluleMax)
Trace("-----------------------------")
Trace("")

Trace("Parcours des entêtes")
POUR i = 1 À te.Occurrence
	Trace("entête " + i + ":", te[i])
FIN
Trace("-----------------------------")
Trace("")

Trace("Parcours des cellules")
POUR i = 1 À tc.Occurrence
	Trace("cellule " + i + ":", tc[i])
FIN
Trace("-----------------------------")
Trace("")

Trace("Parcours d'un rectangle de cellules")
POUR i = 1 À Trc.Occurrence
	Trace("rect cellule " + i + ":", Trc[i])
FIN
Trace("-----------------------------")
Trace("")
```

<a name="NOTE0"></a>

## Remarques
<a name="NOTE0_1"></a>


### Propriétés spécifiques à la description des variables de type tblrSélection
<a name="proprietes_specifiques_description_des_variables_type_tblrselection_ELTPARAGRAPHE000035"></a>Les propriétés suivantes peuvent être utilisées pour manipuler une sélection dans un champ Tableur :

| Nom de la propriété | Type manipulé | Effet |
| --- | --- | --- |
| Cellule | Tableau de chaînes | Tableau des cellules sélectionnées. Les cellules dans le tableau sont triées selon l'ordre de sélection. <br><br>**Cette propriété est disponible en lecture seulement**. |
| CelluleMax | Chaîne de caractères | Cellule maximale sélectionnée, c'est-à-dire la cellule en bas à droite du rectangle délimitant la sélection des cellules.  <br><br>**Cette propriété est disponible en lecture seulement**. |
| CelluleMin | Chaîne de caractères | Cellule minimale sélectionnée, c'est-à-dire la cellule en haut à gauche du rectangle délimitant la sélection des cellules.  <br><br>**Cette propriété est disponible en lecture seulement**. |
| Entête | Tableau de chaînes | Entêtes sélectionnés (entêtes de lignes et/ou colonnes sélectionnées). <br><br>**Cette propriété est disponible en lecture seulement**. |
| RectCellule | Tableau de chaînes | Tableau des rectangles des cellules sélectionnées. Exemple : "A1:B2" <br><br>**Cette propriété est disponible en lecture seulement**. |
| Type | Constante de type Entier | Type de sélection : <br><br>- *tblrSelMultiCell* : Plusieurs cases sont sélectionnées. <br><br>- *tblrSelMultiEntête* : Plusieurs entêtes sont sélectionnés. <br><br>- *tblrSelRectCell* : Plusieurs cases sont sélectionnées et elles forment un rectangle. <br><br>- *tblrSelSimpleCell* : Une seule case est sélectionnée. <br><br>- *tblrSelSimpleEntête* : Un seul entête est sélectionné. <br><br>- *tblrSelTout* : Toute la feuille est sélectionnée.<br><br>- *tblrSelVide* : Aucune sélection.<br><br><br>**Cette propriété est disponible en lecture seulement**. |








