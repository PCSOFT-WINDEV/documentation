
## picTexte (Type de variable)

***En anglais : picText***
				



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Le type **picTexte** permet de définir toutes les caractéristiques avancées d'un calque de type Texte. Les caractéristiques de ce calque de type Texte peuvent être définies et modifiées à l'aide de différentes propriétés WLangage. 

Attention : Ce type de variable est utilisé uniquement avec les variables de type [picCalque](../WDLang1/1000024604.md). 

**Remarque** : Pour plus de détails sur la déclaration de ce type de variable et l'utilisation des propriétés WLangage, consultez [Déclaration d'une variable](../Motscles/1514032.md).
<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
sFichier est une chaîne = fRepExe() + ["\"] + "ImgSource.jpg"
MonWDPic est un WDPic = sFichier

CalqueFiligrane est un picCalque
CalqueFiligrane.Nom = "Filigrane"
CalqueFiligrane.Largeur = MonWDPic.Largeur
CalqueFiligrane.Hauteur = MonWDPic.Hauteur
CalqueFiligrane.Type = typeCalqueTexte
CalqueFiligrane.Texte.Police.Nom = "Arial"
CalqueFiligrane.Texte.Police.Taille = 100
CalqueFiligrane.Texte.Police.Angle = -45
CalqueFiligrane.Texte.Police.Couleur = iBleuFoncé
CalqueFiligrane.Texte.CadrageHorizontal = chCentre
CalqueFiligrane.Texte.CadrageVertical = cvMilieu
CalqueFiligrane.Texte = "Mon texte de test"

TableauAjoute(MonWDPic.Calque, CalqueFiligrane)

// Affichage dans le champ Editeur d'images
EI_SansNom1 = MonWDPic

Info("A suivre ...")

// Récupération du calque et modification du texte
CalqueFiligrane<-MonWDPic.Calque["Filigrane"]
CalqueFiligrane.Texte = "Mon filigrane"

// Affichage dans le champ
EI_SansNom1 = MonWDPic

Info("A suivre ...")

// Sauvegarde de l'image dans un fichier wdpic
sNouveauFichier est une chaîne = fRepExe() + ["\"] + "ImgDestination.wdpic"
MonWDPic.Sauve(sNouveauFichier)

// chargement du nouveau fichier
MonNouveauWDPic est un WDPic = sNouveauFichier

// Récupération du calque et modification du texte
CalqueFiligrane<-MonNouveauWDPic.Calque["Filigrane"]
CalqueFiligrane.Texte.Police.Angle = 30
CalqueFiligrane.Texte.Police.Taille = 150
CalqueFiligrane.Texte.Police.Couleur = iRougeFoncé
CalqueFiligrane.Texte = "Mon filigrane version 3"

// Affichage dans le champ
EI_SansNom1 = MonNouveauWDPic
```





<a name="NOTE0"></a>

## Remarques
<a name="NOTE0_1"></a>


### Propriétés spécifiques à la description des variables de type picTexte
<a name="proprietes_specifiques_description_des_variables_type_pictexte_ELTPARAGRAPHE000034"></a>Les propriétés suivantes peuvent être utilisées pour manipuler un calque de type Texte :

| Nom de la propriété | Type manipulé | Effet |
| --- | --- | --- |
| CadrageHorizontal | Constante de type Entier | Cadrage horizontal du texte dans le calque :<br><br>- *chCentre* (Valeur par défaut) : Affichage du message Toast au centre.<br><br>- *chDroite* : Affichage du message Toast sur la droite.<br><br>- *chGauche* : Affichage du message Toast sur la gauche.<br><br><br> |
| CadrageVertical | Constante de type Entier | Cadrage vertical du texte dans le calque :<br><br>- *cvBas* (Valeur par défaut) : Affichage du message Toast en bas.<br><br>- *cvHaut* : Affichage du message Toast en haut.<br><br>- *cvMilieu* : Affichage du message Toast au milieu.<br><br><br> |
| Opacité | Entier | Pourcentage d'opacité du texte. Ce pourcentage est compris entre 0 (complètement invisible) et 100 (complètement opaque). |
| Police | Variable de type [Police](../Motscles/1514045.md) | Caractéristiques de la police utilisée pour le texte. |
| Texte | Chaîne de caractères | Texte du calque de type texte. |






