
## Procédure WLangage appelée par la fonction fSurveilleRépertoire



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Procédure WLangage (également nommée "callback") appelée par la fonction [fSurveilleRépertoire](../WDLang1/1000020523.md) lors d'un changement sur le répertoire surveillé. 

Cette procédure peut être une procédure locale, globale, ou interne.
<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
// Nom du répertoire à surveiller
sNomRepertoire est une chaîne = "C:\Temp\MonRep"
// La procédure fSurveilleRépertoire_Callback sera appelée 
// lorsqu'un fichier ou un répertoire contenu dans le répertoire "C:\Temp\MonRep" sera modifié.  
SI fSurveilleRépertoire(sNomRepertoire, fSurveilleRépertoire_Callback, fsCréationFichier + ...
		fsModificationFichier + fsSuppressionFichier + fsRenommage) ALORS
	// On informe l'utilisateur de la mise sous surveillance du répertoire
	Info("La mise sous surveillance du répertoire " + sNomRepertoire + " a réussi.")
SINON
	// On informe l'utilisateur de l'échec de la mise sous surveillance du répertoire
	Info("La mise sous surveillance du répertoire " + sNomRepertoire + " a échoué.")
FIN
```



```wl
// Code de la procédure
PROCEDURE fSurveilleRépertoire_Callback(sNomRepertoire, sNomFichier, nAction, sAncienNomFichier)
ExécuteThreadPrincipal(AjoutTable, sNomRepertoire, sNomFichier, nAction, sAncienNomFichier)
```



```wl
// Procédure qui agit sur le thread principal
PROCEDURE AjoutTable(sNomRepertoire, sNomFichier, nAction, sAncienNomFichier)

sDesignationAction est une chaîne
// La désignation de l'action subie dépend de nAction
SELON nAction
	CAS fsCréationFichier : sDesignationAction = "Création de fichier"
	CAS fsSuppressionFichier : sDesignationAction = "Suppression de fichier"
	CAS fsModificationFichier : sDesignationAction = "Modification de fichier"
	CAS fsRenommage : sDesignationAction = "Renommage de fichier"	
FIN
// Ajout d'une ligne contenant les informations de la modification
// dans la table TABLE_MODIFICATIONS
TableAjouteLigne(TABLE_MODIFICATIONS, sNomRepertoire, sNomFichier, ...
		sDesignationAction, sAncienNomFichier)
```

<a name="XSYNTAXE"></a>

## Syntaxe
<a name="SYNTAXE1"></a>

`fSurveilleRépertoire_Callback(<Nom du répertoire observé> , <Nom de l'élément modifié> , <Action> , <Ancien nom>)`
---

**`<Nom du répertoire observé> : (Chaîne de caractères)`**

Nom complet du répertoire observé. 

**`<Nom de l'élément modifié> : (Chaîne de caractères)`**

Nom du fichier ou du répertoire modifié.

**`<Action> : (Constante de type Entier)`**

Action effectuée : 



|   |   |
| --- | --- |
| *fsCréationFichier* | Création du fichier ou du répertoire &lt;Nom de l'élément modifié&gt; dans le répertoire &lt;Répertoire&gt;. |
| *fsModificationFichier* | Modification du fichier ou du répertoire &lt;Nom de l'élément modifié&gt; dans le répertoire &lt;Répertoire&gt;. |
| *fsRenommage* | Le fichier ou le répertoire &lt;Nom de l'élément modifié&gt; a été renommé dans le répertoire &lt;Répertoire&gt;. |
| *fsSuppressionFichier* | Suppression du fichier ou du répertoire &lt;Nom de l'élément modifié&gt; dans le répertoire &lt;Répertoire&gt;. |





**`<Ancien nom> : (Chaîne de caractères)`**

Ancien nom du fichier ou du répertoire modifié si l'action effectuée est un renommage.



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques


### Attention
<a name="attention_ELTPARAGRAPHE000062"></a>Cette procédure WLangage est exécutée dans un thread WLangage.
Attention : Il n'est pas possible d'exécuter dans les threads les traitements suivants :

- ouverture de fenêtres avec les fonctions WLangage telles que [Ouvre](../WDLang1/3038035.md), [Utilise](../WDLang1/3038044.md), [Ferme](../WDLang1/3038018.md), ... Si des fenêtres doivent être manipulées dans des threads (cas rare), une gestion spécifique doit être mise en place. Pour plus de détails, consultez [Gérer l'ouverture d'une fenêtre dans un thread secondaire](../WDLang1/3077015.md). 

- gestion d'événements.

- gestion de timers.




Pour plus de détails sur la gestion des threads, consultez [Gestion des threads](../WDLang1/3077026.md).





<a name="XComposante"></a>

## Classification Métier / UI :
Code neutre
## Composante :
wd280std.dll
