
## Procédure WLangage appelée par la fonction fCopieFichier



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Procédure WLangage (également nommée "callback") appelée par la fonction [fCopieFichier](../WDLang1/3036004.md) pour chaque fichier copié. Cette procédure permet de connaître le chemin du fichier à copier et le chemin destination associé. Cette procédure peut être utilisée par exemple pour filtrer les fichiers à copier. 

Cette procédure peut être une procédure locale, globale ou interne.
<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
// Copie les fichiers présents dans le répertoire "C:\MesImages"
ResCopie = fCopieFichier("C:\MesImages\*.*", "D:\MesFichiers\", fCopieFichier_Callback)

PROCEDURE INTERNE fCopieFichier_Callback(ACopier, Destination)
// Pour chaque fichier du répertoire, la procédure est appelée. 
// Le paramètre ACopier contient automatiquement le nom du fichier à copier
// Le paramètre Destination contient automatiquement le nom du fichier destination
// Dans cet exemple, les fichiers d'extension ".xlsx" ne sont pas copiés 
	SI fExtraitChemin(ACopier, fExtension) = ".xlsx" ALORS
		RENVOYER fcIgnore
	SINON
		// Affiche le nom du fichier en cours de copie dans un libellé
		LIB_Fichier_EnCours = ACopier
		RENVOYER fcCopie
	FIN
FIN
```

<a name="XSYNTAXE"></a>

## Syntaxe
<a name="SYNTAXE1"></a>

`<Résultat> = fCopieFichier_Callback(<Fichier à copier> , <Fichier destination> [, <Change> [, <Pointeur de la procédure>]])`
---

**`<Résultat> : (Constante de type Entier)`**

Gestion de la copie de l'élément courant : 


|   |   |
| --- | --- |
| *fcArrête* | Permet d'arrêter définitivement la copie. |
| *fcCopie* | Permet de continuer la copie. |
| *fcIgnore* | Permet d'ignorer la copie d'un fichier. |



**`<Fichier à copier> : (Chaîne de caractères)`**

Chemin et nom du fichier à copier. 

**`<Fichier destination> : (Chaîne de caractères)`**

Chemin et nom du fichier destination. 

**`<Change> : (Constante optionnelle de type entier)`**

Permet de savoir si un changement de répertoire a été effectué. Les valeurs possibles sont les suivantes : 


|   |   |
| --- | --- |
| *flChangeRépertoire* | Le fichier est le premier listé dans un sous-répertoire de &lt;Fichier à copier&gt; (cela signifie qu'il y a eu changement de répertoire). |
| *flFichier* | Tous les autres cas. |
| *flPremierFichier* | Le fichier est le premier listé dans le répertoire de &lt;Fichier à copier&gt;. |



**`<Pointeur de la procédure> : (Entier optionnel)`**

Valeur passée dans le paramètre &lt;Pointeur&gt; de la fonction [fCopieFichier](../WDLang1/3036004.md). Si le paramètre &lt;Pointeur&gt; n'est pas précisé dans la fonction [fCopieFichier](../WDLang1/3036004.md), &lt;Pointeur de la procédure&gt; vaut 0.



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques


### Exemple de valeurs pouvant être prises par le paramètre &lt;Change&gt;
<a name="exemple_valeurs_pouvant_etre_prises_par_parametre_ELTPARAGRAPHE000076"></a>Lors du parcours des fichiers copiés par la fonction [fCopieFichier](../WDLang1/3036004.md), voici les différentes valeurs pouvant être prises par &lt;Change&gt; : 


| Fichier en cours | &lt;Change&gt; |
| --- | --- |
| Rep\\Fichier 1 | *flPremierFichier* |
| Rep\\Fichier n | *flFichier* |
| Rep\\SousRep 1\\Fichier 1 | *flChangeRépertoire* |
| Rep\\SousRep 1\\Fichier m | *flFichier* |
| Rep\\SousRep 2\\Fichier 1 | *flChangeRépertoire* |
| Rep\\SousRep 2\\Fichier x | *flFichier* |


<a name="NOTE0_2"></a>


### Valeurs renvoyées par la procédure
<a name="valeurs_renvoyees_par_procedure_ELTPARAGRAPHE000118"></a>La procédure appelée par la fonction [fCopieFichier](../WDLang1/3036004.md) doit renvoyer une des valeurs suivantes : 


|   |   |
| --- | --- |
| *fcArrête* | Permet d'arrêter définitivement la copie. |
| *fcCopie* | Permet de continuer la copie. |
| *fcIgnore* | Permet d'ignorer la copie d'un fichier. |


Si la procédure ne renvoie pas une de ces valeurs, une erreur WLangage apparaît. 

**Cas 1. Interruption complète de la copie** 

Pour forcer l'interruption de la copie, la procédure appelée par la fonction [fCopieFichier](../WDLang1/3036004.md) doit renvoyer la constante *fcArrête*.

Exemple : la procédure "CopieProduit" est appelée automatiquement par la fonction [fCopieFichier](../WDLang1/3036004.md) :


```wl
PROCEDURE CopieProduit(CheminFichierSource, CheminFichierDestination)
...
// Arrêt demandé ?
Multitâche(-1)
SI ToucheEnfoncée(teEchap) = Vrai ALORS
	Info("La copie va être arrêtée")
	RENVOYER fcArrête
FIN
...
RENVOYER fcCopie
```


Si la touche Echap est utilisée, la procédure appelée par la fonction [fCopieFichier](../WDLang1/3036004.md) renvoie la constante *fcArrête*.

Dans les autres cas (pour poursuivre la copie), la procédure appelée par la fonction [fCopieFichier](../WDLang1/3036004.md) renvoie la constante *fcCopie*.

**Cas 2.  Interruption partielle de la copie** 

Pour que la procédure appelée par la fonction [fCopieFichier](../WDLang1/3036004.md) ne s'exécute pas pour un fichier donné, cette procédure doit renvoyer la constante *fcIgnore*. 

Exemple : La procédure "RechercheProduit" est appelée automatiquement par la fonction [fCopieFichier](../WDLang1/3036004.md) :

```wl
PROCEDURE RechercheProduit(CheminFichierSource, CheminFichierDestination)
...
// Fichier à ne pas prendre en compte
SI ChaîneFinitPar(CheminFichierSource, "MauvaisFichier.XLS") ALORS
	RENVOYER fcIgnore
FIN
...
RENVOYER fcCopie
```


Pour ne pas copier le fichier "MauvaisFichier.XLS", la procédure renvoie la constante *fcIgnore*. La procédure est automatiquement appelée pour le prochain fichier copié, sans avoir copié le fichier en cours.





<a name="XComposante"></a>

## Classification Métier / UI :
Code neutre
## Composante :
wd280std.dll
