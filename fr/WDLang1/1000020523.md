
## fSurveilleRépertoire (Fonction)

***En anglais : fTrackDirectory***



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Détecte les modifications effectuées sur le contenu d'un répertoire. Seules les modifications effectuées sur les fichiers présents dans le répertoire sont détectées. En cas de modification, une procédure spécifique est exécutée dans un thread. 
<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
// Nom du répertoire à surveiller
sNomRepertoire est une chaîne = "C:\Temp\MonRep"
// La procédure fSurveilleRépertoire_Callback sera appelée 
// lorsqu'un fichier ou un répertoire contenu dans le répertoire "C:\Temp\MonRep" sera modifié.  
SI fSurveilleRépertoire(sNomRepertoire, fSurveilleRépertoire_Callback, fsCréationFichier + ...
		fsModificationFichier + fsSuppressionFichier + fsRenommage) ALORS
	// On informe l'utilisateur de la mise sous surveillance du répertoire
	Info("La mise sous surveillance du répertoire " + sNomRepertoire + " a réussi.")
SINON
	// On informe l'utilisateur de l'échec de la mise sous surveillance du répertoire
	Info("La mise sous surveillance du répertoire " + sNomRepertoire + " a échoué.")
FIN
```



```wl
// Code de la procédure
PROCEDURE fSurveilleRépertoire_Callback(sNomRepertoire, sNomFichier, nAction, sAncienNomFichier)
ExécuteThreadPrincipal(AjoutTable, sNomRepertoire, sNomFichier, nAction, sAncienNomFichier)
```



```wl
// Procédure qui agit sur le thread principal
PROCEDURE AjoutTable(sNomRepertoire, sNomFichier, nAction, sAncienNomFichier)

sDesignationAction est une chaîne
// La désignation de l'action subie dépend de nAction
SELON nAction
	CAS fsCréationFichier : sDesignationAction = "Création de fichier"
	CAS fsSuppressionFichier : sDesignationAction = "Suppression de fichier"
	CAS fsModificationFichier : sDesignationAction = "Modification de fichier"
	CAS fsRenommage : sDesignationAction = "Renommage de fichier"	
FIN
// Ajout d'une ligne contenant les informations de la modification
// dans la table TABLE_MODIFICATIONS
TableAjouteLigne(TABLE_MODIFICATIONS, sNomRepertoire, sNomFichier, ...
		sDesignationAction, sAncienNomFichier)
```

<a name="XSYNTAXE"></a>

## Syntaxe
<a name="SYNTAXE1"></a>

`<Résultat> = fSurveilleRépertoire(<Répertoire à surveiller> , <Procédure WLangage> [, <Modifications à notifier> [, <Sous-répertoire>]])`
---

**`<Résultat> : (Booléen)`**



- <u><u><u><u>Vrai</u></u></u></u> si la surveillance du répertoire est activée, 

- <u><u><u><u>Faux</u></u></u></u> dans le cas contraire. Pour plus de détails sur l'erreur, utilisez la fonction [ErreurInfo](../WDLang1/3013008.md).




**`<Répertoire à surveiller> : (Chaîne de caractères)`**

Nom complet du répertoire à surveiller.

**`<Procédure WLangage> : (Nom de procédure)`**

Nom de la procédure WLangage (également nommée "callback") appelée lors d'un changement dans le répertoire spécifié.

Pour plus de détails sur cette procédure, consultez [Paramètres de la procédure utilisée par la fonction fSurveilleRépertoire](../WDLang1/1000025350.md).

**`<Modifications à notifier> : (Constante ou combinaison de constantes optionnelle de type Entier)`**

Modifications du contenu du répertoire à surveiller et pour lesquelles la procédure doit être exécutée : 


|   |   |
| --- | --- |
| *fsCréationFichier* | Création d'un fichier ou d'un répertoire. |
| *fsModificationFichier* | Modification d'un fichier ou d'un répertoire. |
| *fsRenommage* | Renommage d'un fichier ou d'un répertoire. |
| *fsSuppressionFichier* | Suppression d'un fichier ou d'un répertoire. |
| *fsTout* | Surveillance de toutes les actions possibles. Correspond à *fsCréationFichier* + *fsModificationFichier* + *fsRenommage* + *fsSuppressionFichier*. |



**`<Sous-répertoire> : (Booléen)`**



- <u><u><u><u>Vrai</u></u></u></u> (valeur par défaut) pour traiter les sous-répertoires. 

- <u><u><u><u>Faux</u></u></u></u> dans le cas contraire. 






<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques


### Cas particuliers
<a name="cas_particuliers_ELTPARAGRAPHE000091"></a>

- La fonction **fSurveilleRépertoire** surveille uniquement le contenu du répertoire. Les changements de nom ou d'emplacement du répertoire ne sont pas pris en compte.  

- Pour arrêter la surveillance du répertoire, utilisez les fonctions [fSurveilleStop](../WDLang1/1000020575.md) et [fSurveilleStopTout](../WDLang1/1000020576.md).

- Pour surveiller les modifications effectuées sur un fichier, utilisez la fonction [fSurveilleFichier](../WDLang1/1000020522.md). 

- Limitation : Seuls 5 répertoires peuvent être surveillés simultanément. 



<a name="NOTE0_2"></a>


### Procédure WLangage
<a name="procedure_wlangage_ELTPARAGRAPHE000113"></a>La procédure WLangage est exécutée dans un thread WLangage.
Attention : Il n'est pas possible d'exécuter dans les threads les traitements suivants :

- ouverture de fenêtres avec les fonctions WLangage telles que [Ouvre](../WDLang1/3038035.md), [Utilise](../WDLang1/3038044.md), [Ferme](../WDLang1/3038018.md), ... Si des fenêtres doivent être manipulées dans des threads (cas rare), une gestion spécifique doit être mise en place. Pour plus de détails, consultez [Gérer l'ouverture d'une fenêtre dans un thread secondaire](../WDLang1/3077015.md). 

- gestion d'événements.

- gestion de timers.




Pour plus de détails sur la gestion des threads, consultez [Gestion des threads](../WDLang1/3077026.md).





<a name="XComposante"></a>

## Composante :
wd280std.dll
