
## CertificatVérifieChaîne (Fonction)

***En anglais : CertificateCheckString***



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Vérifie la correspondance entre une signature et une chaîne.


<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple

![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Code Utilisateur (MCU)](https://doc.pcsoft.fr/ext/images/fr/MCU.png) 
```wl
MonCertificat est un Certificat

// Ouverture du sélecteur de certificat de Windows
MonCertificat = CertificatSélecteur()

// Annulation ou erreur
SI MonCertificat.Nom = "" ALORS
	RETOUR
FIN

// Teste si le certificat sélectionné est valide pour signer
SI MonCertificat.ValidePourSignature = Faux ALORS
	Info("Le certificat sélectionné ne permet pas de générer une signature.")
	RETOUR
FIN

// Récupération du buffer contenant la signature
bufSignature est un Buffer
bufSignature = CertificatSigneChaîne("Chaîne de caractères à signer", MonCertificat)

// Vérification de la signature
SELON CertificatVérifieChaîne("Chaîne de caractères à signer", bufSignature)
	CAS certificatOk : Info("Signature valide et certificat fiable")
	CAS certificatInvalide : Info("Signature ou certificat invalide")
	CAS certificatExpiré : Info("Signature valide mais certificat expiré")
	CAS certificatNonFiable : Info("Signature valide mais racine de confiance " + ...
		"du certificat non fiable")
FIN
```


<a name="Exemple2"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/fr/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/fr/LX.png) 
```wl
// Vérification de signature de données transmises par PayBox
sDonnées est une chaîne = [
act=ps_validated&secid=190&fpay=1&montant=25000&reference=CMD2019021309341242&auto=XXXXXX
&trans=33015345&abo=0&paiement=CARTE&carte=Maestro
&idtrans=14515770&erreur=00000&payscarte=FRA&fincb=44&debcb=111122
]
sSignatureBase64 est une chaîne = [
DVD+nMNaQatCBdS/qVjZGE8DtwPvIA3jxGhKlU83MyYC98ezKori/
3cceClqNhtmzD6MDhuKbm8Lw5sFTYdaKWzy79tXzxMLCq814u0+fc5KSihExS6
gIaixjCHiOTgqQXmPP29MkFPpfAFz/wKU/fu+FPGp2dpgLcoRAv0+m1o=
]
sFichierCléPbublique est une chaîne = fRepDonnées() + "\pubkey.pem"
// Décodage Base64 de la signature
bufSignature est un Buffer = Décode(Remplace(sSignatureBase64, RC, ""), encodeBASE64URL)
// Condensé SHA-1 ==> certSHA160
SI CertificatVérifieChaîne(sDonnées, bufSignature, sFichierCléPbublique, 
	"", "", certSignatureSeule + certSHA160) = certificatOk ALORS
	Info("La signature est correcte")
SINON
	Erreur("La signature est incorrecte", ErreurInfo())
FIN
```





<a name="XSYNTAXE"></a>

## Syntaxe
<a name="SYNTAXE1"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Linux](https://doc.pcsoft.fr/ext/images/fr/LX.png)![Code Utilisateur (MCU)](https://doc.pcsoft.fr/ext/images/fr/MCU.png) 
### Vérifier la correspondance entre une signature et une chaîne

`<Résultat> = CertificatVérifieChaîne(<Chaîne à vérifier> , <Buffer de signature>)`
---

**`<Résultat> : (Constante de type Entier)`**

Résultat de la vérification de la signature : 


|   |   |
| --- | --- |
| *certificatExpiré* | Signature valide mais certificat expiré. |
| *certificatInvalide* | Signature ou certificat invalide. |
| *certificatNonFiable* | Signature valide mais la racine de confiance du certificat est non fiable. |
| *certificatOk* | Signature et certificat fiables. |



**`<Chaîne à vérifier> : (Chaîne de caractères)`**

Chaîne de caractères sur laquelle la vérification doit être effectuée.

**`<Buffer de signature> : (Buffer)`**

Buffer contenant la signature de la chaîne. Ce buffer est obtenu avec la fonction [CertificatSigneChaîne](../WDLang1/1000019296.md).


<a name="SYNTAXE2"></a>

### Vérifier la correspondance entre une signature et une chaîne (signature numérique)

`<Résultat> = CertificatVérifieChaîne(<Chaîne à vérifier> , <Buffer de signature> , <Fichier de certificat> [, <Mot de passe> [, <Autorités de confiance>]] , <Options>)`
---

**`<Résultat> : (Constante de type Entier)`**

Résultat de la vérification de la signature : 


|   |   |
| --- | --- |
| *certificatExpiré* | Signature valide mais certificat expiré. |
| *certificatInvalide* | Signature ou certificat invalide. |
| *certificatNonFiable* | Signature valide mais la racine de confiance du certificat est non fiable. |
| *certificatOk* | Signature et certificat fiables. |



**`<Chaîne à vérifier> : (Chaîne de caractères ou Buffer)`**

Chaîne de caractères sur laquelle la vérification doit être effectuée. 
Ce paramètre peut correspondre à une chaîne ANSI, une chaîne UNICODE ou un Buffer. La chaîne sera traitée comme un buffer. 
Si ce paramètre correspond à une chaîne (sans précision du type), le type de chaîne pris en compte dépend du type de chaîne défini par défaut dans la configuration courante du projet. 
Remarque : Dans le cas d'une chaîne UNICODE, notez que l'Unicode n'est pas le même sous Linux et sous Windows.

**`<Buffer de signature> : (Buffer)`**

Buffer contenant la signature de la chaîne. Ce buffer est obtenu avec la fonction [CertificatSigneChaîne](../WDLang1/1000019296.md).

**`<Fichier de certificat> : (Chaîne de caractères ou Buffer)`**

Correspond : 

- Chemin complet du fichier de certificat à utiliser (au format PEM, DER ou P12). 

- Buffer contenant le certificat à utiliser.


Remarques : 

- Pour une signature de type PKCS7, ce paramètre permet d'ajouter des certificats pour la recherche de la chaîne de confiance. 

- Pour la signature brute, ce paramètre contient la clé publique. 




**`<Mot de passe> : (Chaîne de caractères)`**

Mot de passe à utiliser pour décrypter le fichier de certificat si celui-ci est protégé par un mot de passe.
Remarque : Ce paramètre est utile notamment si le certificat utilisé est identique à celui utilisé pour la signature.  

**`<Autorités de confiance> : (Chaîne de caractères ou tableau de chaînes)`**

Chemin complet du certificat de confiance à utiliser. 
Ce paramètre peut également correspondre à un tableau de chaînes contenant le chemin complet des différents certificats de confiance à utiliser.

Remarque : Les certificats de confiance ajoutés avec la fonction [CertificatDeConfianceAjoute](../WDLang1/1000024320.md) sont également prise en compte.



**`<Options> : (Constante ou combinaison de constantes de type Entier)`**

Format de la signature : 


|   |   |
| --- | --- |
| *certPKCS7* | Signature au format PKCS7. |
| *certSignatureSeule* | Signature seule. Dans ce cas, il est nécessaire de combiner la constante *certSignatureSeule* avec l'algorithme de hachage : <br><br>- *certSignatureSeule* + *certMD5* : Algorithme MD5. <br><br>- *certSignatureSeule* + *certSHA160* : Algorithme SHA1.<br><br>- *certSignatureSeule* + *certSHA256* : Algorithme SHA2. Le résultat produit est de 256 bits. <br><br>- *certSignatureSeule* + *certSHA384* : Algorithme SHA-384. Le résultat produit est de 384 bits<br><br>- *certSignatureSeule* + *certSHA512* : Algorithme SHA-512. Le résultat produit est de 512 bits.<br><br><br><br><br>Dans ce cas, il n'y a pas de vérification de la chaîne de confiance. Seule la validité de la signature par rapport à la clé publique est vérifiée. |







<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques


### Section LDAP du certificat
<a name="section_ldap_certificat_ELTPARAGRAPHE000178"></a>La section LDAP (qui regroupe les informations sur le créateur du certificat) doit être différente entre le certificat de confiance et le certificat (par exemple avec une sous-section "commonName" différente).
Dans le cas contraire, il est possible que la fonction renvoie une erreur correspondant à la constante *certificatNonFiable*.

Remarque : Selon l'outil utilisé, cette section peut porter différents noms : Emetteur (certificat Windows), ...









<a name="XComposante"></a>

## Classification Métier / UI :
Code métier
## Composante :
wd280std.dll
