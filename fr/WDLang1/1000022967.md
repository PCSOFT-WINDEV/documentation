


## DocInsèreTableau (Fonction)

***En anglais : DocInsertTable***



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Insère un tableau dans un document de type Traitement de texte ou remplace le fragment spécifié par un nouveau tableau. 


<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
// Insère un tableau à la position 1 de taille 3x3
DocInsèreTableau(TT_Tableau, 1, 3, 3)
```

```wl
// Insère un tableau à la position du curseur de taille 3 colonnes et 2 lignes
DocInsèreTableau(TT_Tableau, TT_Tableau.Curseur, 3, 2)
```

<a name="XSYNTAXE"></a>

## Syntaxe
<a name="SYNTAXE1"></a>

### Insérer un tableau dans un document de type Traitement de texte

`<Résultat> = DocInsèreTableau(<Document> , <Position> [, <Nombre de colonnes> [, <Nombre de lignes>]])`
---

**`<Résultat> : (Variable de type docFragment)`**

Variable de type [docFragment](../WDLang1/1000022482.md) contenant le fragment inséré. 

**`<Document> : (Variable de type Document ou chaîne de caractères)`**

Document à manipuler. Ce document correspond : 

- ![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png) au nom d'un champ Traitement de texte.

- à une variable de type [Document](../WDLang1/1000022461.md). 




**`<Position> : (Entier)`**

Position à laquelle le tableau doit être inséré. Cette position est exprimée en nombre de caractères. 

**`<Nombre de colonnes> : (Entier optionnel)`**

Nombre de colonnes du tableau. Par défaut, ce paramètre correspond à 1. 

**`<Nombre de lignes> : (Entier optionnel)`**

Nombre de lignes du tableau. Par défaut, ce paramètre correspond à 1.


<a name="SYNTAXE2"></a>

### Remplacer le fragment existant par un nouveau tableau

`<Résultat> = DocInsèreTableau(<Fragment> [, <Nombre de colonnes> [, <Nombre de lignes>]])`
---

**`<Résultat> : (Variable de type docFragment)`**

Variable de type [docFragment](../WDLang1/1000022482.md) contenant le fragment inséré. 

**`<Fragment> : (Variable de type docFragment)`**

Nom de la variable de type [docFragment](../WDLang1/1000022482.md) correspondant au fragment à manipuler. Le contenu actuel du fragment sera remplacé par le tableau créé. 

**`<Nombre de colonnes> : (Entier optionnel)`**

Nombre de colonnes du tableau. Par défaut, ce paramètre correspond à 1. 

**`<Nombre de lignes> : (Entier optionnel)`**

Nombre de lignes du tableau. Par défaut, ce paramètre correspond à 1.



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques


### Manipuler un tableau par programmation
<a name="manipuler_tableau_par_programmation_ELTPARAGRAPHE000150"></a>Un tableau présent dans un document de type Traitement de texte peut être manipulé avec les fonctions de gestion des tableaux du WLangage. 

Quelques exemples : 

- Fonctions [Ajoute](../WDLang1/1000017348.md) ou [TableauAjoute](../WDLang1/3075003.md) pour ajouter des lignes ou des colonnes, 

- Fonctions [Supprime](../WDLang1/1000017354.md) ou [TableauSupprime](../WDLang1/3075008.md) pour supprimer des lignes, des colonnes ou le tableau lui-même. 




Exemple :

```wl
// Insère un tableau à la position 1 de taille 3x3
DocInsèreTableau(TT_ExempleTT, 1, 3, 3)

// Définition d'un fragment correspondant au tableau
f est un docFragment(TT_ExempleTT.Valeur, TT_ExempleTT.Curseur, 0)

soit para <- f.Paragraphe[1]
SI para.Tableau = Null ALORS
	RETOUR
FIN

doc est un Document <- TT_ExempleTT.Valeur

// Ajoute une ligne au tableau
nIndice est un entier = Ajoute(para.Tableau.Lignes)

// Saisie dans la cellule 2,2
para.Tableau.Cellules[2,2].Contenu.Texte = "Je suis dans la cellule 2,2"

// Supprime la ligne 3
Supprime(para.Tableau.Lignes, 3)

// Supprime la colonne 3
Supprime(para.Tableau.Colonnes, 3)

// Supprime le tableau entier où se trouve le curseur
Supprime(doc.Paragraphe, para.IndiceParagraphe)
```


Exemple de création d'un tableau dans un document avec le contenu d'un champ Table : 

```wl
// Une fenêtre contient un champ Table par programmation nommé TABLE_Démo
// et un champ Traitement de texte nommé TT_Démo
// Le code suivant ajoute un tableau dans le champ Traitement de texte avec :
// - en première ligne du tableau, le titre des colonnes du champ Table,
// - le contenu du champ Table dans les lignes suivantes du tableau.

MonDoc est un Document
cTable est un Champ <- TABLE_Démo
pCol est un Champ

FragmentDeb est un docFragment(MonDoc,1)
FragmentDeb.MiseEnForme.PoliceTaille = 24
FragmentDeb.MiseEnForme.CouleurTexte = RougeFoncé
FragmentDeb.Texte = "Tableau dans TTX avec " + cTable.Libellé + RC + RC

// Insertion du tableau dans le document en mémoire ...
DocInsèreTableau(MonDoc, 20, TableOccurrence(cTable, toColonne), cTable.Occurrence + 1)

POUR TOUT para DE MonDoc.Paragraphe
	SI para.Tableau <> Null ALORS
		// Pour toutes les colonnes du champ table ...
		POUR nColonne = 1 _À_ TableOccurrence(cTable, toColonne)
			
			// Première ligne du tableau contient le titre des colonnes
			pCol <- TableEnumèreColonne(cTable, nColonne)
			para.Tableau.Cellules[1, nColonne].Contenu.Texte = pCol.Libellé
			
			// Remplissage de toutes les lignes de cette colonne
			POUR nLigne = 1 _À_ cTable.Occurrence
				para.Tableau.Cellules[nLigne+1, nColonne].Contenu.Texte = pCol[nLigne]
			FIN
		FIN
		SORTIR
	FIN
FIN

// Document en mémoire affecté au champ Traitement de texte
TT_Démo = MonDoc
```


<a name="XComposante"></a>

## Composante :
wd280mdl.dll
