


## ExécuteCode (Fonction)

***En anglais : ExecuteCode***



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Exécute un code WLangage présent dans une chaîne de caractères.


<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
sTexte est une chaîne = "Texte"
// Affiche le texte dans une fenêtre
ExécuteCode("Info(sTexte)")
```


<a name="Exemple2"></a>

```wl
// Déclaration des variables.
sCodeAExécuter est une chaîne
xRésultatOpération est un numérique

// Code WLangage à exécuter. 
// Ce code renvoie le résultat d'une opération %1.
sCodeAExécuter = [
	MonCalcul est un numérique
	MonCalcul = %1
	RENVOYER MonCalcul
	]

// Code WLangage à exécuter. 
// Remplacement de %1 par l'opération affichée 
// dans le champ de saisie SAI_EXPRESSION.
sCodeAExécuter = ChaîneConstruit(sCodeAExécuter, SAI_EXPRESSION)

// Exécution du code et récupération du résultat.
xRésultatOpération = ExécuteCode(sCodeAExécuter)

// Affiche le résultat de l'opération dans une boîte de message de type Info()
Info("Le résultat de votre opération est : " + xRésultatOpération)
```

<a name="XSYNTAXE"></a>

## Syntaxe
<a name="SYNTAXE1"></a>

`<Résultat> = ExécuteCode(<Code>)`
---

**`<Résultat> : (Tout type)`**



- Résultat du code si celui-ci contient une instruction RENVOYER,

- Rien dans le cas contraire. Dans ce cas, un  message d'erreur peut apparaître par exemple lors de l'affectation du résultat dans une variable. 




**`<Code> : (Chaîne de caractères)`**

Code WLangage à exécuter. 
Remarque : Ce code peut contenir le code d'appel et de déclaration d'une procédure interne.



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques


### Variables locales
<a name="variables_locales_ELTPARAGRAPHE000049"></a>Les variables locales du traitement courant peuvent être utilisées directement dans le code à exécuter.

**Si le code compile sans erreur**, le code est exécuté directement. 

**Si le code ne compile pas**, une erreur fatale est déclenchée.
<a name="NOTE0_2"></a>


### Compilation du code
<a name="compilation_code_ELTPARAGRAPHE000060"></a>Le code est recompilé à chaque appel de la fonction **ExécuteCode**.

Pour éviter la phase de recompilation, vous pouvez utiliser les fonctions [Compile](../WDLang1/3013015.md) et [Exécute](../WDLang1/3013041.md).


### Code dynamique
<a name="code_dynamique_ELTPARAGRAPHE000076"></a>Les **constantes ne peuvent pas être utilisées** dans le code dynamique (définies avec le mot-clé [CONSTANTE](../Motscles/1514012.md)).

Lors de l'utilisation de constantes dans un code, toutes les occurrences des constantes sont remplacées par leur valeur au moment de la compilation sous l'éditeur mais la correspondance entre le nom des constantes et leur valeur n'est pas "embarquée" dans l'application. Donc la compilation dynamique ne peut pas utiliser les constantes.

Voici deux alternatives :

1 - Utiliser des variables à la place des constantes

```wl
CONSTANTE
	CST_Nom = 1
FIN
```
devient par exemple

```wl
CST_Nom est un entier = 1
```
ou

2 - Dans la chaîne qui contient le code que l'on va compiler dynamiquement, au lieu de laisser le nom de la constante, on remplace le nom de la constante par sa valeur :

```wl
sCode est une chaîne
// Dans la chaîne qui contient le code que l'on va compiler dynamiquement
// au lieu de laisser le nom de la constante comme ici:
sCode=[
Info(CST_Nom)
]
// On remplace le nom de la constante par sa valeur
sCode = Remplace(sCode, "CST_Nom", CST_Nom, MotComplet + SansCasse)
// Il est ensuite possible de compiler le code
SI Compile("ProcDyn", sCode) <> "" ALORS
	Erreur("Erreur de compilation de la procédure dynamique : ", ...
		ErreurInfo())
SINON
	// Puis de l'exécuter
	QUAND EXCEPTION DANS 
		ExécuteTraitement("ProcDyn", trtProcédure)
	FAIRE
		Erreur("Erreur d'exécution de la procédure dynamique : ", ...
		ExceptionInfo())
	FIN
FIN
```


<a name="XComposante"></a>

## Composante :
wd280vm.dll
