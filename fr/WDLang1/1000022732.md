


## DocInsère (Fonction)

***En anglais : DocInsert***



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Insère un objet dans un document de type Traitement de texte ou remplace le contenu du fragment en cours. Les objets manipulables sont : 

- un élément,

- un document, 

- un paragraphe,

- un texte, 

- une image, 

- un fragment, 

- une table des matières.





<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
// Insère une image à la position du curseur
// Sélection du fichier image
sFichier est chaîne 
sFichier = fSélecteurImage(fRepEnCours(), "", ...
	"Insérer une image","JPEG (*.JPG)" + TAB + "*.jpg" + RC + ...
	"PNG (*.PNG)" + TAB + "*.png" + RC + "Tous les fichiers (*.*)" + TAB + "*.*")
	
SI sFichier = "" ALORS
	RETOUR
FIN

// Le fichier image est transféré dans une variable de type Image
img est une Image
img = sFichier

// Déclaration d'un élément de type Image (à insérer dans le document)
elt est un docElément
elt.Type = typeDocElémentImage
elt.Image.BufferImage = img
elt.Image.Habillage = docHabillageAlignéSurLeTexte


// Calcul des largeurs et hauteurs de l'image présente dans l'élément
// La taille de l'image est en pixels dans la variable de type Image. 
// Cette taille est convertie en mm dans l'élément (unité utilisée dans un document)
elt.Image.Largeur = img.Largeur * 25.4 / 96.0
elt.Image.Hauteur = img.Hauteur * 25.4 / 96.0
// Position de l'image
elt.Image.X = 0
elt.Image.Y = 0
	
DocInsère(TT_MonDocument, TT_MonDocument.Curseur, elt)
```

```wl
doc est un Document <- TT_MonDocument.Valeur
// Recherche de la position du signet 
fragmentSignet est un docFragment = doc.Signet["Signet 1"]	
SI fragmentSignet <> Null ALORS
	// Insertion du texte à la fin du signet
	soit nPositionInsertion = fragmentSignet.PositionFin
	// Insertion à proprement dite
	DocInsère(TT_MonDocument, nPositionInsertion, "Texte à insérer à la position du signet")
SINON
	Erreur("Signet 'Signet 1' non trouvé dans le document")
FIN
```

<a name="XSYNTAXE"></a>

## Syntaxe
<a name="SYNTAXE1"></a>

### Insérer un objet à une position

`<Résultat> = DocInsère(<Document> , <Position> , <Elément>)`
---

**`<Résultat> : (Variable de type docFragment)`**

Variable de type [docFragment](../WDLang1/1000022482.md) contenant le fragment inséré. 

**`<Document> : (Variable de type Document ou chaîne de caractères)`**

Document à manipuler. Ce document correspond : 

- ![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png) au nom d'un champ Traitement de texte.

- à une variable de type [Document](../WDLang1/1000022461.md).




**`<Position> : (Entier)`**

Position à laquelle l'élément doit être inséré. Cette position est exprimée en nombre de caractères. 

**`<Elément> : (Type correspondant à l'élément à insérer)`**

Elément à insérer : 

- Elément : Nom de la variable de type [docElément](../WDLang1/1000022484.md) correspondant à l'élément à insérer. 

- Document : Nom de la variable de type [Document](../WDLang1/1000022461.md) correspondant au document à insérer. 

- Paragraphe : Nom de la variable de type [docParagraphe](../WDLang1/1000022483.md) correspondant au paragraphe à insérer. 

- Texte sous forme de chaîne de caractères : Texte à insérer.
	Il est possible d'utiliser les constantes suivantes dans le texte à insérer pour gérer les différents sauts possibles : 
	


|   |   |
| --- | --- |
| *docSautDeColonne* | Ajoute un saut de colonne dans une section multicolonne. Si la section n'est pas multicolonne, un saut de page est ajouté. |
| *docSautDeLigne* | Ajoute un saut de ligne. |
| *docSautDePage* | Ajoute un saut de page. |
| *docSautDeParagraphe* | Ajoute un saut de paragraphe. |



- Image : Nom de la variable de type [Image](../WDLang1/1000019650.md) à insérer à la position spécifiée.

- Fragment : Nom de la variable de type [docFragment](../WDLang1/1000022482.md) à insérer à la position spécifiée.  





<a name="SYNTAXE2"></a>

### Remplacer un fragment par un objet (texte, document, fragment, paragraphe, ...)

`<Résultat> = DocInsère(<Fragment> , <Objet à manipuler>)`
---

**`<Résultat> : (Variable de type docFragment)`**

Variable de type [docFragment](../WDLang1/1000022482.md) contenant le fragment inséré. 

**`<Fragment> : (Variable de type docFragment)`**

Nom de la variable de type [docFragment](../WDLang1/1000022482.md) à manipuler. Le contenu de ce fragment sera remplacé par l'objet spécifié. 

**`<Objet à manipuler> : (Variable correspondant au type de l'objet)`**

Objet remplaçant le contenu du fragment. Cet objet peut correspondre : 

- à une chaîne de caractères. 
	Il est possible d'utiliser les constantes suivantes dans le texte pour gérer les différents sauts possibles : 
	


|   |   |
| --- | --- |
| *docSautDeColonne* | Ajoute un saut de colonne dans une section multicolonne. Si la section n'est pas multicolonne, un saut de page est ajouté. |
| *docSautDeLigne* | Ajoute un saut de ligne. |
| *docSautDePage* | Ajoute un saut de page. |
| *docSautDeParagraphe* | Ajoute un saut de paragraphe. |






- au nom d'une variable de type [docElément](../WDLang1/1000022484.md). 

- au nom d'une variable de type [Document](../WDLang1/1000022461.md). 

- au nom d'une variable de type [docParagraphe](../WDLang1/1000022483.md). 

- au nom d'une variable de type [Image](../WDLang1/1000019650.md).

- au nom d'une variable de type [docFragment](../WDLang1/1000022482.md). 

- au nom d'une variable de type [docTableDesMatières](../WDLang1/1000024893.md).






<a name="XComposante"></a>

## Classification Métier / UI :
Code UI
## Composante :
wd280mdl.dll
