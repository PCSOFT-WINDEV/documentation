


## &lt;Traitement de texte&gt;.InsèreTableau (Fonction)

***En anglais : &lt;Word Processing&gt;.InsertTable***



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Insère un tableau dans un champ Traitement de texte. 
<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
// Insère un tableau à la position 1 de taille 3x3
TT_Tableau.InsèreTableau(1, 3, 3)
```

```wl
// Insère un tableau à la position du curseur de taille 3 colonnes et 2 lignes
TT_Tableau.InsèreTableau(TT_Tableau.Curseur, 3, 2)
```

<a name="XSYNTAXE"></a>

## Syntaxe
<a name="SYNTAXE1"></a>

`<Résultat> = <Champ Traitement de texte>.InsèreTableau(<Position> [, <Nombre de colonnes> [, <Nombre de lignes>]])`
---

**`<Résultat> : (Variable de type docFragment)`**

Variable de type [docFragment](../WDLang1/1000022482.md) contenant le fragment inséré. 

**`<Champ Traitement de texte> : (Nom de champ)`**

Nom du champ Traitement de texte à manipuler.

**`<Position> : (Entier)`**

Position à laquelle le tableau doit être inséré. Cette position est exprimée en nombre de caractères.

**`<Nombre de colonnes> : (Entier optionnel)`**

Nombre de colonnes du tableau. Par défaut, ce paramètre correspond à 1.

**`<Nombre de lignes> : (Entier optionnel)`**

Nombre de lignes du tableau. Par défaut, ce paramètre correspond à 1.



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques


### Manipuler un tableau par programmation
<a name="manipuler_tableau_par_programmation_ELTPARAGRAPHE000242"></a>Un tableau présent dans un document de type Traitement de texte peut être manipulé avec les fonctions de gestion des tableaux du WLangage. 

Quelques exemples : 

- Fonctions [&lt;Tableau&gt;.Ajoute](../NonDocumente/1000024295.md) ou [&lt;Tableau&gt;.Ajoute](../WDLang1/1000024299.md) pour ajouter des lignes ou des colonnes, 

- Fonctions [&lt;Tableau&gt;.Supprime](../NonDocumente/1000024297.md) ou [&lt;Tableau&gt;.Supprime](../WDLang1/1000024314.md) pour supprimer des lignes, des colonnes ou le tableau lui-même. 





```wl
// Récupération du document
MonDocument est un Document <- TT_ExempleTT
// Insère un tableau à la position 1 de taille 3x3
TT_ExempleTT.InsèreTableau(1, 3, 3)

// Définition d'un fragment correspondant au tableau
f est un TT_ExempleTT.Valeur.Fragment(TT_ExempleTT.Curseur, 0)

soit para <- f.Paragraphe[1]
SI para.Tableau = Null ALORS
	RETOUR
FIN

doc est un Document <- TT_ExempleTT.Valeur

// Ajoute une ligne au tableau
nIndice est un entier = Ajoute(para.Tableau.Lignes)

// Saisie dans la cellule 2,2
para.Tableau.Cellules[2,2].Contenu.Texte = "Je suis dans la cellule 2,2"

// Supprime la ligne 3
Supprime(para.Tableau.Lignes, 3)

// Supprime la colonne 3
Supprime(para.Tableau.Colonnes, 3)

// Supprime le tableau entier où se trouve le curseur
Supprime(doc.Paragraphe, para.IndiceParagraphe)
```


Exemple de création d'un tableau dans un document avec le contenu d'un champ Table : 

```wl
// Une fenêtre contient un champ Table par programmation nommé TABLE_Démo
// et un champ Traitement de texte nommé TT_Démo
// Le code suivant ajoute un tableau dans le champ Traitement de texte avec :
// - en première ligne du tableau, le titre des colonnes du champ Table,
// - le contenu du champ Table dans les lignes suivantes du tableau.

MonDoc est un Document
cTable est un Champ <- TABLE_Démo
pCol est un Champ

FragmentDeb est un MonDoc.docFragment(1)
FragmentDeb.MiseEnForme.PoliceTaille = 24
FragmentDeb.MiseEnForme.CouleurTexte = RougeFoncé
FragmentDeb.Texte = "Tableau dans TTX avec " + cTable.Libellé + RC + RC

// Insertion du tableau dans le document en mémoire ...
MonDoc.DocInsèreTableau(20, TableOccurrence(cTable, toColonne), cTable.Occurrence + 1)

POUR TOUT para DE MonDoc.Paragraphe
	SI para.Tableau <> Null ALORS
		// Pour toutes les colonnes du champ Table ...
		POUR nColonne = 1 _À_ TableOccurrence(cTable, toColonne)
			
			// Première ligne du tableau contient le titre des colonnes
			pCol <- TableEnumèreColonne(cTable, nColonne)
			para.Tableau.Cellules[1, nColonne].Contenu.Texte = pCol.Libellé
			
			// Remplissage de toutes les lignes de cette colonne
			POUR nLigne = 1 _À_ cTable.Occurrence
				para.Tableau.Cellules[nLigne+1, nColonne].Contenu.Texte = pCol[nLigne]
			FIN
		FIN
		SORTIR
	FIN
FIN

// Document en mémoire affecté au champ Traitement de texte
TT_Démo = MonDoc
```



<a name="XComposante"></a>

## Composante :
wd280mdl.dll
