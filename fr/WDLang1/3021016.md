


## Saisie (Fonction)

***En anglais : Input***



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Affiche un message en permettant à l'utilisateur de saisir une information.<br>![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=fonctions_dialogue%20-%20HC%20N%B0008.gif)
**Remarque** : Plusieurs syntaxes sont disponibles pour cette fonction : 

- Syntaxe simplifiée, saisie directement sous l'éditeur de code. 

- Syntaxe complète avec saisie de la question grâce à un assistant. L'éditeur de code propose un [assistant complet](../WDLang1/3021020.md) (option "&lt;Assistant&gt;" proposée par la [saisie assistée](../Editeurs/2013001.md)). Cet assistant permet de définir les différents éléments apparaissant dans le message, et génère automatiquement le code correspondant. Le libellé des boutons est automatiquement proposé en fonction du texte de la question.

- Syntaxe directe, saisie directement sous l'éditeur de code. 



<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
// Exemple de saisie simplifiée 
NbExemplaire est un entier
RetourValeur est un entier
RetourValeur = Saisie("Combien d'exemplaires voulez-vous imprimer ?", NbExemplaire)
SELON RetourValeur 
	CAS 0 : Info("Vous avez annulé. Le nombre par défaut est de 1.")
	CAS 1 : Info("Vous avez demandé " + nbExemplaire)
FIN
```
<a name="Exemple2"></a>

```wl
// Saisie simplifiée avec utilisation d'un masque de saisie 
NumTel est une chaîne
// Utilisation d'un masque de saisie texte spécifique
Saisie.MasqueSaisie = maskTelFrance
SELON Saisie("Quel est votre numéro de téléphone ?", NumTel)
	// OK
	CAS 1 : SAI_NumTel = NumTel 
	// Annuler
	CAS 0 : SAI_NumTel = "Pas de téléphone"
FIN
```
<a name="Exemple3"></a>

```wl
Commentaires est une chaîne
Commentaires = Répète(RC, 10)
// Saisie de commentaires par l'utilisateur dans un champ multiligne 
// Saisie limitée à 140 caractères
Saisie.MasqueSaisie = "regexp:.{0,140}"
SELON Saisie("Saisissez vos commentaires.", Commentaires)
	// OK
	CAS 1 : SAI_Comment = Commentaires
	// Annuler
	CAS 0 : SAI_Comment = "Pas de commentaire"
FIN
```
<a name="Exemple4"></a>

```wl
AfficherInfo est un booléen = Faux
// On demande à l'utilisateur s'il veut réellement fermer l'application 
// Une coche "Ne plus afficher cette fenêtre" permet à l'utilisateur
// de ne plus afficher cette question à l'avenir
Saisie.LibelléOptionnel = "Ne plus afficher cette fenêtre"
SI AfficherInfo = Faux ALORS
	SELON Saisie("Voulez-vous fermer l'application ?", AfficherInfo)
		// Fermer
		CAS 1 : Ferme()
	FIN
FIN
// Pensez à stocker la valeur de AfficherInfo pour les prochaines fois
```
<a name="Exemple5"></a>

```wl
// Syntaxe directe 
NumTel est une chaîne
SI Saisie("Quel est votre numéro de téléphone?", NumTel, ...
		["Valider", "Pas de téléphone", "Annuler"], 3, 3, dlgIcôneQuestion) = 1 ALORS
	Info(NumTel)
FIN
```

<a name="XSYNTAXE"></a>
<a name="SYNTAXE1"></a>

## Syntaxe

### Syntaxe simplifiée (sans utiliser l'assistant)

`<Résultat> = Saisie(<Question> , <Variable à saisir>)`
---

**`<Résultat> : (Entier)`**

Identifie la réponse sélectionnée par l'utilisateur : 

- 0 : l'utilisateur a cliqué sur le bouton "Annuler".

- 1 : l'utilisateur a cliqué sur le bouton "OK".




**`<Question> : (Chaîne de caractères)`**

Question à poser à l'utilisateur. Cette question peut utiliser la fonction [ChaîneConstruit](../WDLang1/3024006.md).

**`<Variable à saisir> : (Tout type)`**

Variable dans laquelle l'utilisateur doit saisir sa réponse. 

- Cette variable doit avoir été déclarée avant l'appel de la fonction **Saisie**. 

- Le type de la variable définit le type du champ dans lequel l'utilisateur saisit sa réponse. Pour les variables de type Heure ou Date, le masque de saisie est automatiquement déduit de la variable. 
	![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png) Il est possible de spécifier un masque de saisie spécifique (notamment pour les variables de type Texte) grâce à la variable **Saisie.MasqueSaisie** (voir Remarques).

- Si la variable est initialisée avec une valeur, cette valeur correspond à la valeur proposée par défaut.

- Après l'utilisation de la fonction **Saisie**, cette variable contient la réponse de l'utilisateur (quel que soit le bouton utilisé).





<a name="SYNTAXE2"></a>

### Syntaxe complète avec saisie de la question avec l'assistant

`<Résultat> = Saisie(<Question> , <Variable à saisir> [, <Paramètre 1> [... [, <Paramètre N>]]])`
---

**`<Résultat> : (Entier)`**

Identifie la réponse sélectionnée par l'utilisateur (valeur de 1 à le nombre de boutons dans la fenêtre de saisie). 

Cette réponse dépend du nombre de boutons présents dans la fenêtre de description de la question. Les différentes valeurs correspondant aux différentes réponses sont automatiquement intégrées en commentaires dans l'éditeur de code lors de la sélection du message.  

**`<Question> : (Chaîne de caractères)`**

Question à poser à l'utilisateur. Cette question peut contenir des paramètres identifiés par %1, %2, ...

**`<Variable à saisir> : (Tout type)`**

Variable dans laquelle l'utilisateur doit saisir sa réponse. 

- Cette variable doit avoir été déclarée avant l'appel de la fonction **Saisie**. 

- Le type de la variable définit le type du champ dans lequel l'utilisateur saisit sa réponse. Pour les variables de type Heure ou Date, le masque de saisie est automatiquement déduit de la variable. 
	![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png) Il est possible de spécifier un masque de saisie spécifique (notamment pour les variables de type Texte) grâce à la variable **Saisie.MasqueSaisie** (voir Remarques).

- Si la variable est initialisée avec une valeur, cette valeur correspond à la valeur proposée par défaut.

- Après l'utilisation de la fonction **Saisie**, cette variable contient la réponse de l'utilisateur (quel que soit le bouton utilisé).




**`<Paramètre 1> : (Chaîne de caractères)`**

Si la question contient des éléments paramétrables (identifiés par %1, %2, ...), ce paramètre permet de donner la valeur voulue. Ainsi &lt;Paramètre 1&gt; remplacera %1.



**`<Paramètre N> : (Chaîne de caractères)`**

Si le message choisi contient des éléments paramétrables (identifiés par %1, %2, ...), ce paramètre permet de donner la valeur voulue. Ainsi &lt;Paramètre 2&gt; remplacera %2.


<a name="SYNTAXE3"></a>

### Syntaxe directe

`<Résultat> = Saisie(<Question> , <Variable à saisir> , <Libellés des boutons> [, <Bouton par défaut> [, <Bouton d'annulation> [, <Icône>]]])`
---

**`<Résultat> : (Entier)`**

Identifie la réponse sélectionnée par l'utilisateur. Cette réponse dépend du message choisi.

**`<Question> : (Chaîne de caractères)`**

Question posée à l'utilisateur.

**`<Variable à saisir> : (Tout type)`**

Variable dans laquelle l'utilisateur doit saisir sa réponse. 

- Cette variable doit avoir été déclarée avant l'appel de la fonction **Saisie**. 

- Le type de la variable définit le type du champ dans lequel l'utilisateur saisit sa réponse. Pour les variables de type Heure ou Date, le masque de saisie est automatiquement déduit de la variable. 
	![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png) Il est possible de spécifier un masque de saisie spécifique (notamment pour les variables de type Texte) grâce à la variable **Saisie.MasqueSaisie** (voir Remarques).

- Si la variable est initialisée avec une valeur, cette valeur correspond à la valeur proposée par défaut.

- Après l'utilisation de la fonction **Saisie**, cette variable contient la réponse de l'utilisateur (quel que soit le bouton utilisé).




**`<Libellés des boutons> : (Tableau)`**

Nom de la variable de type Tableau contenant les libellés des boutons.

**`<Bouton par défaut> : (Entier)`**

Indice du bouton sélectionné par défaut. Par défaut, ce paramètre vaut 1.

**`<Bouton d'annulation> : (Entier)`**

Indice du bouton d'annulation. Par défaut, ce paramètre correspond à l'indice du dernier bouton.

**`<Icône> : (Chaîne de caractères ou constante de type Entier)`**

Icône affichée. Ce paramètre peut correspondre : 

- au chemin du fichier correspondant à l'icône affichée. 

- à une des constantes suivantes : 
	


|   |   |
| --- | --- |
| *dlgIcôneErreur* | Icône représentant une erreur. |
| *dlgIcôneInfo* | Icône représentant une information. |
| *dlgIcôneQuestion*<br>(Valeur par défaut) | Icône représentant une question. |








<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques


### Caractéristiques de la boîte de dialogue et du champ de saisie
<a name="caracteristiques_boite_dialogue_champ_saisie_ELTPARAGRAPHE000168"></a>

- Le titre de la boîte de dialogue correspond au titre de la fenêtre (ou page) en cours.
	

- Pour modifier ou définir le titre de la boîte de dialogue, utilisez la fonction [TitreSuivant](../WDLang1/3038043.md).




- Le gabarit du projet en cours est automatiquement appliqué à la boîte de dialogue. 
	![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png) Le gabarit de l'application n'est pas appliqué sur la fenêtre de saisie. 

- ![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png) Pour modifier les caractéristiques du champ de saisie (par exemple, modifier le cadrage ou remplacer les caractères saisis dans le champ par des astérisques), affichez l'assistant d'édition de la question (icône ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=IconeSaisie.GIF) dans la syntaxe de la fonction **Saisie**) et cliquez sur l'icône ![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=Saise_Exemple%201.GIF).



<a name="NOTE0_2"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png) 

### Gestion des masques de saisie
<a name="gestion_des_masques_saisie_ELTPARAGRAPHE000193"></a>Par défaut, le masque de saisie utilisé est déterminé automatiquement à partir des informations d'internalisation du projet.

Cependant, il est possible de modifier le masque de saisie utilisé grâce à la variable **Saisie.MasqueSaisie**.

Cette variable peut prendre pour valeur une des constantes suivantes :  


|   |   |
| --- | --- |
| *maskAAlphaNum* | Lettre, puis lettres + chiffres |
| *maskAAlphaNumMajus* | Lettre en majuscule, puis lettres en majuscules + chiffres |
| *maskAlpha* | Lettres |
| *maskAlphaMajus* | Lettres en majuscules |
| *maskAlphaNum* | Lettres + chiffres |
| *maskAlphaNumMajus* | Lettres en majuscules + chiffres |
| *maskAucun* | Pas de masque de saisie |
| *maskEMail* | Adresse email |
| *maskFichierSansRep* | Nom de fichier (sans ses répertoires) |
| *maskINSEE* | Numéro INSEE |
| *maskINSEEClé* | Numéro INSEE + clé |
| *maskMajus* | Tout en majuscules |
| *maskMinus* | Tout en minuscules |
| *maskNomFic* | Nom et chemin de fichier |
| *maskNum* | Chiffres |
| *maskNumPlus* | Chiffres, '+', ' ', '.', '-', ',' |
| *maskPatronyme* | Lettres + Chiffres + Espace + Apostrophe + Tiret |
| *maskPatronymeMajuscule* | Lettres Majuscules +Chiffres + Espace + Apostrophe + Tiret |
| *maskPMajus* | Première lettre en majuscule |
| *maskTailleFichier* | Masque numérique pour les tailles de fichier et de disque |
| *maskTel* | Numéro de téléphone |
| *maskTelFrance* | Numéro de téléphone au format français |


Il est également possible d'attribuer une expression régulière comme masque de saisie. Pour plus de détails, consultez l'aide de la propriété [..MasqueSaisie](../Proprietes/2510033.md). 
<a name="NOTE0_3"></a>


### Saisie de texte multiligne
<a name="saisie_texte_multiligne_ELTPARAGRAPHE000298"></a>Si le paramètre &lt;Variable à saisir&gt; est initialisé avec une chaîne de caractères contenant des RC, alors la saisie dans le champ associé sera multiligne.
<a name="NOTE0_4"></a>


### Saisie par l'intermédiaire d'une case à cocher
<a name="saisie_par_intermediaire_une_case_cocher_ELTPARAGRAPHE000305"></a>Si la variable est de type booléen, dans ce cas, la saisie est réalisée dans une case à cocher. Le libellé de la case à cocher est déterminé grâce à la variable ***Saisie.LibelléOptionnel***.
<a name="NOTE0_5"></a>


### Base de messages
<a name="base_messages_ELTPARAGRAPHE000312"></a>Lors de la création d'un message, ce message est automatiquement ajouté à la base de messages. 

Par défaut, la base de messages est présente dans le répertoire "Personnel\\Messages" de WINDEV, WEBDEV et WINDEV Mobile. Ce répertoire peut être modifié dans les options de WINDEV/WEBDEV/WINDEV Mobile : 

- Sous le volet "Accueil", dans le groupe "Environnement", déroulez "Options" et sélectionnez "Options générales de WINDEV/WEBDEV/WINDEV Mobile". 

- Affichez l'onglet "Répertoire".




Pour plus de détails sur la base de messages, consultez l'[assistant de saisie d'une question](../WDLang1/3021020.md).
<a name="NOTE0_6"></a>
![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png) 

### Limites
<a name="limites_ELTPARAGRAPHE000343"></a>

- ![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png) Le gabarit de l'application n'est pas appliqué sur la fenêtre de saisie. 

- La variable à saisir ne peut pas être initialisée avec des RC. Le champ de saisie restera monoligne.
	

- ![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png) Si la variable saisie est de type Booléen, la saisie sera effectuée dans un champ de saisie (et non dans un interrupteur comme avec WINDEV).

- ![Java](https://doc.pcsoft.fr/ext/images/fr/JAVA.png) Le masque de saisie n'est pas géré.



<a name="NOTE0_7"></a>


### Exemple de saisie complète
<a name="exemple_saisie_complete_ELTPARAGRAPHE000382"></a>

1. Saisie de la question sous l'éditeur : <br>![](https://doc.pcsoft.fr/fr-FR/images/image.awp?langid=5&name=Saisie_Exemple.gif&type=thumb)


2. Code généré automatiquement (seule la variable "Destinataire" a été saisie sous l'éditeur de code) : 



```wl
Destinataire est une chaîne
//1 : Créer une relance
//0 : Ne pas créer de relance
//1 : OK 
SELON Saisie("Vous devez indiquer un destinataire.", Destinataire)
	// Valider le destinataire
	CAS 1
		
	// Afficher la liste
	CAS 2
		
	// Annuler
	CAS 3
		
FIN
```

<a name="NOTE0_8"></a>



<a name="XComposante"></a>

## Classification Métier / UI :
Code UI
## Composante :
wd280obj.dll
