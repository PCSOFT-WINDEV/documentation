
## EnumèreSousElément (Fonction)

***En anglais : EnumSubElement***



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Permet d'énumérer les sous-éléments d'un élément du projet. Cette fonction peut être exécutée même si l'élément n'est pas ouvert.

Utilisée dans une boucle, cette fonction peut permettre d'énumérer les champs d'une fenêtre (ou d'une page) d'un projet.



**Remarque** : Cette fonction ne peut pas être utilisée sur les états ou sur les requêtes.
<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple
<a class="notetitle" target="_blank" href="$DOC$=1000003064002&name=enumeresouselement_fonction&product=WD">Voir des exemples supplémentaires</a>
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Code Utilisateur (MCU)](https://doc.pcsoft.fr/ext/images/fr/MCU.png) 
```wl
// Énumération des champs d'une fenêtre d'un projet
NomChamp est une chaîne = EnumèreSousElément("MaFenêtre", enumPremier)
// Parcours tous les champs
TANTQUE NomChamp <> ""
	Trace(NomChamp)
	// Passage au champ suivant
	NomChamp = EnumèreSousElément("MaFenêtre")
FIN
```


<a name="Exemple2"></a>


![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Code Utilisateur (MCU)](https://doc.pcsoft.fr/ext/images/fr/MCU.png) 
```wl
// Parcours des volets d'un champ Onglet
NomChamp est une chaîne = EnumèreSousElément("FEN_Fenêtre1.Onglet1", enumPremier + enumVolet)

// Parcours de tous les volets du champ Onglet
TANTQUE NomChamp <> ""
	// Passage au volet suivant
	Trace(NomChamp)	
	NomChamp = EnumèreSousElément("FEN_Fenêtre1.Onglet1", enumVolet)	
FIN
```


<a name="Exemple3"></a>




<a name="Exemple4"></a>


![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Code Utilisateur (MCU)](https://doc.pcsoft.fr/ext/images/fr/MCU.png) 
```wl
// Parcours des champs d'une fenêtre et affichage de leur type et libellé.
sParent est une chaîne = "FEN_SAISIE_CLIENT"
sNomChamp est une chaîne = EnumèreSousElément(sParent, enumPremier)
TANTQUE sNomChamp <> ""
	
	// Nom complet du champ
	sNomComplet est une chaîne = sParent + "." + sNomChamp
	// Récupération du type de l'élément
	nType est un entier = {sNomComplet, indGPW}..Type
	
	// Si c'est un groupe de champs ou un champ Onglet
	SI nType = typOnglet OU nType = typTable OU nType = typBarreOutils OU ...
		nType = typBoîteAOutils OU nType = typSuperChamp ALORS
		// Enumération des champs qui sont à l'intérieur
		Trace(sNomComplet)
	SINON
		// Ne fait rien si c'est une option de menu
		SI nType <> typOptionMenu ALORS
			// Ajout de l'élément dans la trace
			Trace(sNomComplet, nType, {sNomComplet, indGPW}..Libellé, ...
				{sNomComplet, indGPW}..Groupe)
		FIN
	FIN
	
	// Champ suivant
	sNomChamp = EnumèreSousElément(sParent)
FIN
```

<a name="XSYNTAXE"></a>
<a name="SYNTAXE1"></a>

## Syntaxe

`<Résultat> = EnumèreSousElément(<Elément> [, <Option>])`
---

**`<Résultat> : (Chaîne de caractères)`**



- Nom complet de l'élément trouvé (par exemple &lt;NomFenêtre/Page&gt;.&lt;NomChamp&gt;),

- Chaîne vide ("") si aucun élément n'a été trouvé.




**`<Elément> : (Chaîne de caractères)`**

Nom complet de l'élément à énumérer. Pour un champ, ce paramètre est de la forme &lt;Nom de la fenêtre/page&gt;.&lt;Nom du champ&gt; (par exemple "MaFenêtre.MaTable").

**`<Option> : (Constante optionnelle)`**

Paramètre à spécifier au premier appel à la fonction (pour lancer l'énumération). Pour poursuivre l'énumération, ce paramètre n'est pas nécessaire.


|   |   |
| --- | --- |
| *enumBoiteOutils* | Énumère les volets d'une boîte à outils.<br><br><br> |
| *enumGroupe* | Énumère les groupes d'une fenêtre ou d'une page |
| *enumMenuContextuel* | Énumère les menus contextuels d'une fenêtre.<br><br><br> |
| *enumMenuPrincipal* | Énumère les menus déroulant d'une fenêtre. Il est également possible d'utiliser la fonction [EnumèreMenu](../WDLang1/3038039.md).<br><br> |
| *enumPremier* | Initialise l'énumération |
| *enumVolet* | Énumère les volets d'un champ Onglet ou d'un champ Ruban.<br><br><br> |


Attention : pour initialiser le parcours, il faut utiliser les combinaisons suivantes :

- *enumPremier* pour énumérer les champs d'une fenêtre ou d'une page.

- *enumPremier + enumGroupe* pour énumérer les groupes d'une fenêtre ou d'une page.

- *enumPremier + enumVolet* pour énumérer les volets d'un champ Onglet ou d'un champ Ruban.

- *enumPremier + enumMenuContextuel* pour énumérer les menus contextuels.

- *enumPremier + enumBoiteOutils* pour énumérer les volets d'un champ Boîte à outils.

- *enumPremier + enumMenuPrincipal* pour énumérer les menus principaux d'une fenêtre ou d'une page.






<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Code Utilisateur (MCU)](https://doc.pcsoft.fr/ext/images/fr/MCU.png) 

### Énumération des volets d'un champ Onglet (ou d'un champ Ruban)
<a name="enumeration_des_volets_champ_onglet_champ_ruban_ELTPARAGRAPHE000119"></a>Par défaut, la fonction **EnumèreSousElément** utilisée sur un champ Onglet énumère tous les champs de tous les volets sans faire apparaître le volet.

En précisant la constante *enumVolet*, la fonction **EnumèreSousElément** utilisée sur un champ Onglet énumère tous les volets d'onglet (mais pas les champs contenus dans les volets d'onglet). Le résultat obtenu est du type "&lt;n&gt;" où &lt;n&gt; est le numéro du volet d'onglet. Pour obtenir le libellé du volet d'onglet, utilisez la propriété [Libellé](../Proprietes/2510053.md). 

**Pour énumérer les champs de chaque volet d'onglet**, il faut relancer une énumération sur le volet à partir de son numéro :

```txt
EnumèreSousElement("<fenêtre>.<onglet>.<n>")
```


Remarque : La propriété [Occurrence](../Proprietes/2510097.md) permet de connaître le nombre de volets d'un champ Onglet.

Exemple : 

```wl
// Parcours des volets d'un champ Onglet
NomChamp est une chaîne = EnumèreSousElément("FEN_Menu.Ong_Visualisation", ...
			enumPremier + enumVolet)

// Parcours de tous les volets d'onglet
TANTQUE NomChamp <> ""
	// Passage au volet suivant
	Trace(NomChamp)
	Trace(ONG_Visualisation[NomChamp].Libellé)
	// Parcours des champs du volet
	NomChamp2 est une chaîne = 
	NomChamp2 = EnumèreSousElément("FEN_Menu.Ong_Visualisation." + NomChamp, enumPremier)
	TANTQUE NomChamp2 <> ""
		Trace(NomChamp2)
		NomChamp2 = EnumèreSousElément("FEN_Menu.Ong_Visualisation." + NomChamp)
	FIN
	NomChamp = EnumèreSousElément("FEN_Menu.Ong_Visualisation", enumVolet)
FIN
```

<a name="NOTE0_2"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/fr/WD.png)![Code Utilisateur (MCU)](https://doc.pcsoft.fr/ext/images/fr/MCU.png) 

### Énumération des volets d'un champ Boîte à outils
<a name="enumeration_des_volets_champ_boite_outils_ELTPARAGRAPHE000151"></a>Par défaut, la fonction **EnumèreSousElément** utilisée sur un champ Boîte à outils énumère tous les champs de tous les volets du champ Boîte à outils sans faire apparaître le volet.

En précisant la constante *enumBoiteOutils*, la fonction **EnumèreSousElément** utilisée sur un champ Boîte à outils énumère tous les volets (mais pas les champs contenus dans les volets). Le résultat obtenu est du type "&lt;n&gt;.&lt;libellé&gt;" où &lt;n&gt; est le numéro du volet et &lt;libellé&gt; son libellé.

**Pour énumérer les champs de chaque volet du champ Boîte à outils**, il faut relancer une énumération sur le volet à partir de son numéro :

```txt
EnumèreSousElement("<Fenêtre/Page>.<Boîte à outils>.<n>")
```



<a name="NOTE0_3"></a>




### Limite
<a name="limite_ELTPARAGRAPHE000172"></a>Cette fonction ne permet pas d'énumérer les sous-éléments d'un élément présent dans un composant. 



<a name="XComposante"></a>

## Classification Métier / UI :
Code neutre
## Composante :
wd280gpu.dll


