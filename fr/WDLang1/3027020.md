
## DateHeureDifférence (Fonction)

***En anglais : DateTimeDifference***



<a name="XUtilisation"></a>
<a name="Utilisation"></a>
<a name="description"></a>
Calcule la différence entre deux dates et heures.

**Remarques** : 

- Il est également possible d'utiliser l'opérateur '-' (moins) pour calculer la différence entre deux dates (voir Remarques). 

- Pour afficher le résultat de cette fonction dans une variable de type Durée, utilisez la fonction [ChaîneVersDurée](../WDLang1/3027002.md).





<a name="Exemple1"></a>
<a name="sample_code"></a>

## Exemple


```wl
// Temps écoulé depuis le 01/01/1998 à 12h15
Diff est une chaîne
Diff = DateHeureDifférence("199801011215", DateHeureSys())
// Affichage temps écoulé
Info("Temps écoulé : " + RC + ...
	Gauche(Diff, 8) + "jours" + RC + ...
	Milieu(Diff, 9, 2) + "heures" + RC + ...
	Milieu(Diff, 11, 2) + "minutes" + RC + ...
	Milieu(Diff, 13, 2) + "secondes" + RC + ...
	Milieu(Diff, 15, 2) + "centièmes de seconde")
```


<a name="Exemple2"></a>

```wl
// Pour remplir une durée à partir du résultat de DateHeureDifférence
Diff est une chaîne = DateHeureDifférence("199801011215", DateHeureSys())
// convertir la différence dans une variable de type durée
Durée1 est une Durée = ChaîneVersDurée(Diff, duréeCentième)

// --------------------------------------------
// Autre solution possible 
// DateHeure1 est une DateHeure = "199801011215"
// DateHeure2 est une DateHeure = DateHeureSys()
// Durée1 est une durée = DateHeure2 - DateHeure1
// ----------------------------------------------

// Affichage temps écoulé
Info("Temps écoulé : " + Durée1.Jour + "jours" + RC + ...
	Durée1.Heure + "heures" + RC + ...
	Durée1.Minute + "minutes" + RC + ...
	Durée1.Seconde + "secondes" + RC + ...
	Durée1.Milliseconde + "millièmes de seconde")
```

<a name="XSYNTAXE"></a>
<a name="SYNTAXE1"></a>

## Syntaxe

`<Résultat> = DateHeureDifférence(<Date/Heure de début> , <Date/Heure de fin>)`
---

**`<Résultat> : (Chaîne de caractères)`**

Nombre de jours, heures, minutes, secondes écoulés entre deux dates au format +/-JJJJJJJHHMMSSCC où :

- JJJJJJJ est le nombre de jours écoulés entre les deux "Date - Heure" indiquées,

- HH est le nombre d'heures,

- MM est le nombre de minutes,

- SS est le nombre de secondes,

- CC est le nombre de centièmes de seconde.


Ce résultat est issu de l'opération : &lt;Date/Heure de fin&gt; - &lt;Date/Heure de début&gt;. 

Ce résultat contient le signe '-' si la &lt;Date/Heure de début&gt; est postérieure à la &lt;Date/Heure de fin&gt;.

**Attention** : Pour affecter ce résultat à une variable de type Durée, il est nécessaire d'utiliser la fonction [ChaîneVersDurée](../WDLang1/3027002.md) avec la constante *duréeCentième*.

**`<Date/Heure de début> : (Chaîne de caractères ou variable de type DateHeure)`**

Date/Heure de début du calcul au format :

- AAAAMMJJHHmmSSCC

- AAAAMMJJHHmmSS

- AAAAMMJJHHmm

- AAAAMMJJHH




**`<Date/Heure de fin> : (Chaîne de caractères ou variable de type DateHeure)`**

Date/Heure de fin du calcul au format :

- AAAAMMJJHHmmSSCC

- AAAAMMJJHHmmSS

- AAAAMMJJHHmm

- AAAAMMJJHH  






<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarques


### Validité des dates
<a name="validite_des_dates_ELTPARAGRAPHE000100"></a>La validité des dates et des heures passées en paramètre est vérifiée. Un message est affiché en cas de date ou d'heure invalide. Une "Date - Heure" est considérée invalide si :

- La date est invalide. La validité de la date peut être vérifiée avec la fonction [DateValide](../WDLang1/3027003.md).

- L'heure est invalide. La validité de l'heure peut être vérifiée avec la fonction [HeureValide](../WDLang1/3027021.md).




Il n'est pas possible d'effectuer des différences de dates antérieures au 14 octobre 1582 (le passage du calendrier Julien au calendrier grégorien ne sera pas pris en compte).  

Le format de stockage des dates permet de stocker des dates comprises entre le 01/01/0001 et le 31/12/9999.

Les calculs sur les dates réalisés avec les [fonctions WLangage](../WDLang1/3027029.md) ou les [propriétés WLangage](../Proprietes/2514010.md) sont fiables à partir du 1er Janvier 1583.




### Calcul de la différence entre deux dates avec les opérateurs
<a name="calcul_difference_entre_deux_dates_avec_les_operateurs_ELTPARAGRAPHE000119"></a>La fonction **DateHeureDifférence** peut être remplacée par l'utilisation de l'opérateur '-'. Il est également possible d'affecter directement le résultat à une variable de type Durée, en gérant la possibilité d'une durée négative. 

Exemples : 

```wl
duree_intervalle est une Durée
SI date_heure_1 > date_heure_2 ALORS
	duree_intervalle = date_heure_1 - date_heure_2
SINON
	duree_intervalle = date_heure_2 - date_heure_1
FIN
```

```wl
duree_intervalle est une Durée = date_heure_1 - date_heure_2
SI duree_intervalle < 0 ALORS
	duree_intervalle = - duree_intervalle
FIN
```


<a name="XComposante"></a>

## Classification Métier / UI :
Code neutre
## Composante :
wd280std.dll
