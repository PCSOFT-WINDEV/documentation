
## How to clone and use columns in a Table control?
			

<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Overview
<a name="overview_ELTTEXTE000160"></a>
In some cases, a Table control must be filled with an unknown number of columns (when creating the control). Two methods can be used to dynamically build columns in a Table control: 

- [Method 1](#NOTE2_1): cloning (duplicating an existing column): using [ControlClone](../WDLang1/3025017.md). 

- ![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) [Method 2](#NOTE3_1): creation (the new column is created): using [ControlCreate](../WDLang1/1000020876.md). 




<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Method 1: Cloning columns
<a name="method_1_cloning_columns_ELTTEXTE000184"></a>
[ControlClone](../WDLang1/3025017.md) is used to clone (duplicate) an existing control in order to create a copy. This function can also be used on a column found in a Table control. The new column is automatically added at the end of existing columns.

To create a new column: 

1. Create a Table control. 

2. In the Table control, define the column that will be used as "template for cloning". 

3. Clear the content of Table control ([TableDeleteAll](../WDLang1/3074034.md)). 

4. Delete the previous cloned columns ([ControlDelete](../WDLang1/3025037.md)). 

5. Create the new columns ([ControlClone](../WDLang1/3025017.md)). 

6. Fill the Table control with data. 




Code example: 

```wl
NbFixedColumns is int
NbNewColumns is int

NewColumnName is string


// Clear the table
TableDeleteAll(TABLE_YearlyStat)


// Delete the previous cloned columns
NbFixedColumns = 3 // for example, 2 fixed columns + the column to clone

FOR nIndex = TableCount(TABLE_YearlyStat,toColumn) TO NbFixedColumns STEP -1
	ControlDelete(TableEnumColumn(TABLE_YearlyStat, nIndex))
END

// Clone the new columns
NbNewColumns = 5 // we want 5 new columns for example
FOR nIndex = 1 TO NbNewColumns
	// Build the name of the new column
	NewColumnName = "COL_" + nIndex
	// Create the new column by cloning
	ControlClone(COL_Template, NewColumnName)
	
	// Modify the header caption of new column
	{NewColumnName, indControl}..Caption = "Column " + nIndex 
	
END

// Fill the Table control...

// Put your code for filling the table here
```


**Caution**:

- The columns must be cloned before filling the Table control.  

- Two columns cannot have the same name. 








<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Method 2: Creating the columns
<a name="method_2_creating_the_columns_ELTTEXTE000208"></a>
[ControlCreate](../WDLang1/1000020876.md) allows you to create a control without having to use a template. This function can also be used on a column found in a Table control. The new column is automatically added at the end of existing columns.

To create a new column: 

1. Create a Table control. 

2. Clear the content of Table control ([TableDeleteAll](../WDLang1/3074034.md)). 

3. Delete the previous created columns ([ControlDelete](../WDLang1/3025037.md)). 

4. Create the new columns ([ControlCreate](../WDLang1/1000020876.md)).

5. Fill the Table control with data. 




Code example: 


```wl
NewColumn is Control

NbFixedColumns is int
NbNewColumns is int

NewColumnName is string


// Clear the table
TableDeleteAll(TABLE_YearlyStat)


// Delete the previous created columns
NbFixedColumns = 3 // for example, 2 fixed columns + the column to create


FOR nIndex = TableCount(TABLE_YearlyStat, toColumn) TO NbFixedColumns STEP -1
	ControlDelete(TableEnumColumn(TABLE_YearlyStat, nIndex))
END

// Create the new columns
NbNewColumns = 5 // we want 5 new columns for example
FOR nIndex = 1 TO NbNewColumns
	// Build the name of the new column
	NewColumnName = TABLE_YearlyStat.Name + ".COL_" + nIndex
	// Create the new column by creation
	NewColumn <- ControlCreate(NewColumnName, typColumn)
	
	// Modify the header caption of new column
	NewColumn.Caption = "Column" + nIndex 
END

// Fill the Table control...
// Put your code for filling the table here
```


**Caution**:

- The columns must be created before filling the Table control.  

- Two columns cannot have the same name. 








<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Using the new column
<a name="using_the_new_column_ELTTEXTE000232"></a>
Two methods can be used to handle the created column:

- the indirection operators { }. For more details, see [Indirection operators](../Motscles/1512005.md).

- the name of new column, method valid only if the column was created by [ControlCreate](../WDLang1/1000020876.md). 





