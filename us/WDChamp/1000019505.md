


## Handling a Scheduler control through programming
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Overview
<a name="overview_ELTTEXTE000440"></a>
A Scheduler control can be: 

- handled programmatically. 

- ![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) [linked to a data file](../WDChamp/1000019543.md).

- ![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) [bound to a variable](../WDChamp/1000019543.md).




To handle a Scheduler control through programming, WINDEV and WEBDEV propose the [SchedulerXXX functions](../WDLang1/1000019435.md). 

This help page explains how to handle a Scheduler control through programming. The following example is used to store appointments in an HFSQL database. 




<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Handling the Scheduler control through programming
<a name="handling_the_scheduler_control_through_programming_ELTTEXTE000470"></a>


### Adding a resource
<a name="adding_resource_ELTPARAGRAPHE000053"></a>

Adding a resource into a Scheduler control is performed by [SchedulerAddResource](../WDLang1/1000019279.md). This resource can correspond to a person, a room, ...

Example: 

```wl
// Adds resources into a Scheduler control 
SchedulerAddResource(SCH_Schedule1, "Florence" + gStoredValue("FL"))
SchedulerAddResource(SCH_Schedule1, "Emma" + gStoredValue("EM"))
SchedulerAddResource(SCH_Schedule1, "Fred" + gStoredValue("FR"))
```

In this example, [gStoredValue](../WDLang1/1410088363.md) is used to propose a displayed resource ("Florence", for example) and a resource handled through programming ("FL", for example). 
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) Remark: [SchedulerAddResource](../WDLang1/1000019279.md) is used to insert an image into the title of the resource.


### Adding an appointment
<a name="adding_appointment_ELTPARAGRAPHE000082"></a>

Adding an appointment into a Scheduler control is performed by [SchedulerAddAppointment](../WDLang1/1000019273.md). This function accepts two syntaxes: 

- syntax used to specify the appointment characteristics: title, description, etc, ...
	Example: 
	
	```wl
	// Adds a 1-hour appointment for tonight 
	MyResource is string
	MyTitle is string
	StartAPT is DateTime
	
	MyResource = "Vince"
	MyTitle = "Sales meeting"
	StartAPT = DateSys() + "17000000"
	
	SchedulerAddAppointment(SCH_Schedule, MyResource, MyTitle, StartAPT)
	```


- syntax that handles a variable of type [Appointment](../WDLang1/1000019244.md).
	Example: 
	
	```wl
	// Declares an Appointment variable 
	MyAppointment is Appointment
	
	// Fills the appointment
	MyAppointment.Title = "Sales meeting"
	MyAppointment.Content = "Meeting to discuss the weekly objectives."
	MyAppointment.StartDate = "201003220845"
	MyAppointment.EndDate = "201003221230"
	MyAppointment.Category = "Sales"
	MyAppointment.ID = 1
	
	// Adds the appointment into the control
	SchedulerAddAppointment(SCH_Schedule, MyAppointment)
	```




The **BackgroundColor** property of the [Appointment](../WDLang1/1000019244.md) variable is used to define a display color for an appointment. If no background color is defined, the Scheduler control will automatically use the color associated with the category of the appointment. 


### Populating a Scheduler control with the data from an HFSQL data file
<a name="populating_scheduler_control_with_the_data_from_hfsql_data_file_ELTPARAGRAPHE000122"></a>

The records are stored in an HFSQL file. The initial fill of the Scheduler control can be done by browsing the data file via the FOR EACH syntax and by adding each appointment via [SchedulerAddAppointment](../WDLang1/1000019273.md).

```wl
// Appointment variable 
MyAppointment is Appointment

// Browse the appointments stored in database
FOR EACH APT 
	// Fills the information of the variable
	MyAppointment.Title = APT.Title
	MyAppointment.Content = APT.Content
	MyAppointment.StartDate = APT.StartDate
	MyAppointment.EndDate = APT.EndDate
	MyAppointment.Category = APT.Category
	MyAppointment.ID = APT.APTID
	
	// Adds the appointment into the Scheduler control
	SchedulerAddAppointment(SCH_MySchedule, MyAppointment)
END
```



### Retrieving a list of appointments
<a name="retrieving_list_appointments_ELTPARAGRAPHE000138"></a>

[SchedulerListAppointment](../WDLang1/1000019276.md) is used to get: 

- the list of all the appointments found in the Scheduler control. 
	For example: 
	
	```wl
	// Array containing a list of Appointment 
	arrAppointmentList is array of Appointment
	
	// Lists of appointments 
	arrAppointmentList = SchedulerListAppointment(SCH_MySchedule)
	```


- the list of appointments for a resource included between two dates. 
	For example: 
	
	```wl
	// List of appointments for January 2020
	arrAppointmentList is array of Appointment
	
	// Lists of appointments
	arrAppointmentList = SchedulerListAppointment(SCH_MyScheduler, MyResource, ...
				"20200101", "20200131")
	```


- the appointment currently selected or hovered. 
	For example: 
	
	```wl
	// Selected appointment
	arrAppointmentList is array of Appointment
	
	arrAppointmentList = SchedulerListAppointment(SCH_MyScheduler, schAptSelected)
	```






### Displaying the schedule from a specific resource or from a specific date
<a name="displaying_the_schedule_from_specific_resource_from_specific_date_ELTPARAGRAPHE000180"></a>

To display the Scheduler control from: 

- a specific resource, use [SchedulerPositionResource](../WDLang1/1000019969.md). 
	For example: 
	
	```wl
	SchedulerAddAppointment(SCH_NoName1, "ABC room", "APT 1", ...
			DateSys() + "14000", DateSys() + "16000")
	SchedulerPositionResource(SCH_NoName1, "ABC room")
	```


- from a specific date, use [SchedulerPositionDateTime](../WDLang1/1000019998.md). 
	For example: 
	
	```wl
	// Positions the schedule on today's date
	SchedulerPositionDateTime(SCH_Schedule, Today())
	```






### Deleting an appointment
<a name="deleting_appointment_ELTPARAGRAPHE000214"></a>

[SchedulerDeleteAppointment](../WDLang1/1000019274.md) is used to delete: 

- the appointment selected in the control. 

- a specific appointment.



```wl
// Deletes the first appointment
SchedulerDeleteAppointment(SCH_Schedule, 1)
```



### Deleting a resource
<a name="deleting_resource_ELTPARAGRAPHE000233"></a>

[SchedulerDeleteResource](../WDLang1/1000019282.md) is used to delete a resource from the Scheduler control.

```wl
// Deletes one of the resources
ResDel is boolean
ResDel = SchedulerDeleteResource(SCH_Schedule1, "Flo") 
IF ResDel = True THEN
	Info("Resource deleted")
END
```

[SchedulerDeleteAll](../WDLang1/1000019275.md) deletes all the appointments from the Scheduler control as well as all its resources. 



### Modifying the display of the control
<a name="modifying_the_display_the_control_ELTPARAGRAPHE000252"></a>

The current display of a Scheduler control can be modified via the following functions: 

- [SchedulerDisplayPreviousPeriod](../WDLang1/1000019495.md) and [SchedulerDisplayNextPeriod](../WDLang1/1000019496.md): These two functions are used to modify the display period (same as using the spin button found at the top of the Scheduler control). 

- [SchedulerChangeMode](../WDLang1/1000019440.md): This function changes the control display mode: day, week, month.  

- [SchedulerTimeDisplayed](../WDLang1/1000019373.md): This function is used to define the time period displayed. 



<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Handling an appointment through programming
<a name="handling_appointment_through_programming_ELTTEXTE000536"></a>
You have the ability to handle an appointment by programming: 

- by using the index of the appointment to modify. 

- by pointing by reference on the appointment to modify. 




**1. Using the index**

Each time an appointment is added to a schedule, [SchedulerAddAppointment](../WDLang1/1000019273.md) returns an index. This index represents the added appointment. This index can be used to handle the appointment directly. 

Example:

```wl
APTNum is int
APTNum = 5

// Change the title displayed on the appointment #5
SCH_ROOM[APTNum].Title = "Blue room"
```


**2. Using a reference**

To handle an appointment, use the **&lt;-** operator to associate the appointment found in the Scheduler control with the [Appointment](../WDLang1/1000019244.md) variable. A modification performed on the variable will be automatically applied to the control. 

Example: 

```wl
A is Appointment
A <- SCH_Room[APTNum]
A.Title = "New title"
```


<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Using the context menu (AAF)
<a name="using_the_context_menu_aaf_ELTTEXTE000560"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png) The Scheduler control is associated with a context menu (AAF). The popup menu of the Scheduler control is used to: 

- change the control display mode,

- add, modify or delete an appointment. 




To save the operations performed, you must use the events of the Scheduler control. 

In the corresponding event, simply retrieve the appointment currently used and perform the corresponding process. 

**Example**: To store an appointment added by the user via the context menu in an APT data file, simply write the following in the "Entry in edit in an appointment" event:


```wl
PROCEDURE Edit(aptEdited is Appointment)

// Store the data
APT.Title = aptEdited.Title
APT.StartDate = aptEdited.StartDate
APT.EndDate = aptEdited.EndDate
...
HAdd(APT)
```


The same type of code can be implemented for the different events of the Scheduler control. Indeed, for each control event that handles an appointment, a procedure has been automatically declared by the Scheduler control. 

These procedures receive a variable of type [Appointment](../WDLang1/1000019244.md) as parameter. 

<a name="NOTE5"></a>
<a name="NOTE5_1"></a>


## Advanced use of events associated with the Scheduler control
<a name="advanced_use_events_associated_with_the_scheduler_control_ELTTEXTE000584"></a>


### Advanced use of events with procedures
<a name="advanced_use_events_with_procedures_ELTPARAGRAPHE000339"></a>

You can allow the user to define more precisely the characteristics of his appointment during an addition or a modification. To do so, create a window or a page with the information to fill. 

In the code, simply open the window or the page in the "Entry in edit in an appointment" event. To lock the direct input, the event must return <u><u><u><u>False</u></u></u></u>. 
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png) This principle can be applied to all the events called by the context menu of the Scheduler control. Example: Opening a window used to enter the appointment.  

```wl
PROCEDURE Edit(aptEdited is Appointment)

// Opens the window for entering an appointment 
// with the selected appointment (in Creation or Modification mode)
Open(WIN_InputAPT_HFSQL, aptEdited)

// Returns False to lock the direct input in the Scheduler control
RESULT False
```


<a name="NOTE6"></a>
<a name="NOTE6_1"></a>


## Managing public holidays
<a name="managing_public_holidays_ELTTEXTE000608"></a>
Public holidays can be set programmatically. Several WLanguage functions (starting with BankHolidayXXX) are available.

To define the public holidays displayed in Organizer, Scheduler and Calendar controls, use [BankHolidayAdd](../WDLang1/1000017304.md). This function allows you to define the list of public holidays that will be used. This function allows you to customize the public holidays according to the country and local regulations. This function must be used at the beginning of the application because it has a global effect on the application.

Public holidays will be in green in the schedule. 

Example:

```wl
// Delete all public holidays
BankHolidayDeleteAll()
// Initialize the 11 public holidays common to the French regions and territories
BankHolidayAdd("0101")		// 1st of January
BankHolidayAdd(bhEasterMonday)	// Easter Monday
BankHolidayAdd("0501")		// 1st of May
BankHolidayAdd("0508")		// 8th of May
BankHolidayAdd(bhAscensionDay)	// Ascension day
BankHolidayAdd(bhWhitMonday)	// Whit Monday
BankHolidayAdd("0714")		// 14th of July
BankHolidayAdd("0815")		// 15th of August (Assumption)
BankHolidayAdd("1101")		// All Saints' Day
BankHolidayAdd("1111")		// 11th of November
BankHolidayAdd("1225")		// Christmas

// Add 2 additional public holidays for the regions of Moselle and Alsace
BankHolidayAdd("1226" + CR + bhGoodFriday)
```


<a name="NOTE7"></a>
<a name="NOTE7_1"></a>


## Properties specific to Scheduler controls
<a name="properties_specific_scheduler_controls_ELTTEXTE000632"></a>
The following properties are used to manage a Scheduler control through programming.



|   |   |
| --- | --- |
| [DayBreakHeight](../Proprietes/1000020105.md) | The **DayBreakHeight** property is used to get and change the height of breaks between days in a Scheduler control where days are arranged in rows, and resources are arranged in columns. |
| [DayHeight](../Proprietes/1000020106.md) | The **DayHeight** property is used to get and change the height of days in a Scheduler control where days are arranged in rows, and resources are arranged in columns. |
| [DayWidth](../Proprietes/1000020108.md) | The **DayWidth** property is used to determine and change the width of days: <br><br>- in a Scheduler control where the days are displayed in column and the resources in rows.<br><br>- in a Gantt Chart column.<br><br><br> |
| [DirectInputAPT](../Proprietes/1000019995.md) | The **DirectInputAPT** property is used to determine and specify if the user can directly change the title of an appointment in a Scheduler or Organizer control. |
| [EndDate](../Proprietes/1000017427.md) | The **EndDate** property is used to get and change the selected time period end date: <br><br>- in a Calendar control,<br><br>- in an Organizer control.<br><br>- in a Scheduler control.<br><br><br><br><br> |
| [GranularityAppointment](../Proprietes/1000019100.md) | The **GranularityAppointment** gets and changes the precision of the grid used to define appointments in Organizer or Scheduler controls. <br>Property kept for backward compatibility. |
| [GranularityDuration](../Proprietes/1000019643.md) | The **GranularityDuration** property is used to get and change the size of the grid to resize:<br><br>- appointments in an Organizer control. <br><br>- appointments in a Scheduler control.<br><br>- events in a TimeLine control.<br><br>- tasks in a Gantt Chart column.<br><br><br> |
| [GranularityMovement](../Proprietes/1000019642.md) | The **GranularityMovement** property is used to get and change the size of the grid to move:  <br><br>- appointments in an Organizer control. <br><br>- appointments in a Scheduler control.<br><br>- events in a TimeLine control.<br><br>- tasks in a Gantt Chart column.<br><br><br> |
| [MaskTitleDate](../Proprietes/1000019463.md) | The **MaskTitleDate** property is used to identify and change the input mask used for the title of day columns in Organizer or Scheduler controls |
| [MovementAPT](../Proprietes/1000019993.md) | The **MovementAPT** property allows you to know and specify whether users can move appointments in a Scheduler or Organizer control. |
| [NbDayDisplayed](../Proprietes/1000019005.md) | The **NbDayDisplayed** property is used to: <br><br>- get and change the number of days displayed in an Organizer or Scheduler control. <br><br>- get the number of days displayed in a Gantt Chart control in a report.<br><br><br> |
| [Num1stDayOfTheWeek](../Proprietes/1000019004.md) | The **Num1stDayOfTheWeek** property gets and sets the 1st day of the week displayed in:<br><br>- a Calendar control.<br><br>- an Organizer control.<br><br>- a Scheduler control. <br><br>- an edit control in Date format with Calendar.<br><br><br> |
| [PeriodSelection](../Proprietes/1000019996.md) | The **PeriodSelection** property is used to determine and specify if the user can select a time period in an Scheduler or Organizer control. |
| [Resource](../Proprietes/1000019308.md) | The **Resource** property is used to: <br><br>- find out the resources visible in a Scheduler control. <br><br>- find out the resource of the Scheduler control that corresponds to the specified index. <br><br><br> |
| [ResourceHeight](../Proprietes/1000020109.md) | The **ResourceHeight** property is used to get and change the height of resources in a Scheduler control where resources are arranged in rows. |
| [ResourceWidth](../Proprietes/1000020110.md) | The **ResourceWidth** property is used to get and change the width of resources in a Scheduler control where resources are arranged in columns. |
| [SelectedResource](../Proprietes/1000019445.md) | The **SelectedResource** property returns the name of the resource that corresponds to the user's selection in a Scheduler control. |
| [StartDate](../Proprietes/1000017426.md) | The **StartDate** property is used to determine and change the start date of the selected time period:<br><br>- in a Calendar control. <br><br>- in an Organizer control.<br><br>- in a Scheduler control.<br><br><br> |
| [WorkingHourEnd](../Proprietes/1000019056.md) | The **WorkingHourEnd** property is used to identify and change the end time of working hours used:<br><br>- by an Organizer control.<br><br>- by a Scheduler control.<br><br>- by a Gantt Chart column (in a Table or TreeView Table control).<br><br><br> |
| [WorkingHourStart](../Proprietes/1000018958.md) | The **WorkingHourStart** property is used to get and change the start time of working hours used: <br><br>- by an Organizer control.<br><br>- by a Scheduler control.<br><br>- by a Gantt Chart column (in a Table or TreeView Table control). <br><br><br> |





For a complete list of WLanguage properties that can be used with a Scheduler control, see [Properties associated with Scheduler controls](../WDChamp/1000019502.md).


