


## Window with parameters
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Overview
<a name="overview_ELTTEXTE000295"></a>
A window can:

- Receive parameters when it is opened.

- Return values when it is closed.




This window behaves like a procedure that may (or may not) return values.

This type of window can be useful in the following cases:

- "calendar" windows (fix a default date when it is opened and return a selected date when it is closed), 

- search windows, 

- login windows returning the password typed, ...




This help page presents:

- [Passing parameters to a window,](#NOTE2)

- [Returning a value when a window is closed,](#NOTE3)

- [Running the test of a window with parameters.](#NOTE4)




<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Passing parameters to a window when it is opened
<a name="passing_parameters_window_when_opened_ELTTEXTE000319"></a>


### Passing parameters: general case
<a name="passing_parameters_general_case_ELTPARAGRAPHE000038"></a>

To pass parameters to a window when it is opened:

1. Declare a procedure in the "Global declarations" event of the window. The name of this procedure is the same as the window name. The parameters of this procedure correspond to the parameters that must be passed to the window.
	**Caution**: The procedure declaration (keyword PROCEDURE) must correspond to the first line of the "Global declarations" event.
	For example, the WIN_CALENDAR window is used to manage the date selection in a calendar. This window expects a parameter indicating the date to select.
	
	```wl
	// -- Global declarations of WIN_Calendar 
	
	PROCEDURE WIN_Calendar (sSelDate)
	//  sSelDate: date to select
	```

	This parameter can be handled from any event of the window (button, local procedures, etc.).
	For example, in the "Initializing" event of WIN_Calendar:
	
	```wl
	// -- Initializing WIN_Calendar
	
	// EDT_DateControl is an edit control 
	// It contains the value of the sSelDate parameter
	EDT_DateControl = sSelDate
	```


2. Pass the parameter expected by the window ([Open](../WDLang1/3038035.md), [OpenMobileWindow](../WDLang1/1000021018.md), [OpenChild](../WDLang1/3038021.md), [OpenSister](../WDLang1/3038045.md), [MDIOpen](../WDLang1/3052005.md)). For example, the BTN_Calendar button is used to open the WIN_Calendar window. Today's date is passed as parameter when this window is opened.
	
	```wl
	// -- Click code of BTN_Calendar
	
	// Open the WIN_Calendar window
	// Today's date is passed as parameter
	Open(WIN_Calendar, Today())
	```






### Parameters passed by value
<a name="parameters_passed_value_ELTPARAGRAPHE000075"></a>

If the parameters passed to a window are modified in this window, these modifications will be taken into account in this window only. The value of these parameters is not modified in the calling process.

For example:

1. The *MyDate* variable is declared in the code of the BTN_Calendar button. This variable contains today's date (for example: MyDate = Today()).

2. This variable is passed as a parameter to WIN_Calendar. The *sSelDate* parameter contains the value of the *MyDate* variable.

3. The value of the *sSelDate* parameter is modified in the WIN_Calendar window (for example: sSelDate = "20020701").

4. The value of the *MyDate* variable is not modified.



<a name="NOTE5_2"></a>


### Giving a default value to the parameter in the declaration
<a name="giving_default_value_the_parameter_the_declaration_ELTPARAGRAPHE000089"></a>

A default value can be given to the parameters when declaring the parameters. In this case, the parameter becomes optional. If it is omitted, the default value will be used. 

For example, to give a default value in the previous example, enter the following code in the "Global declarations" event of WIN_Calendar:


```wl
// -- Global declarations of WIN_Calendar
PROCEDURE WIN_Calendar(sSelDate = "20030101")
//  sSelDate: date to select
```



### Named parameters
<a name="named_parameters_ELTPARAGRAPHE000100"></a>

You can also pass named parameters to a window when it is opened:

1. Declare the procedure as usual. Example: 
	
	```wl
	PROCEDURE WIN_Travel(Identifier, Availability)
	```


2. Pass the parameter expected by the window ([Open](../WDLang1/3038035.md), [OpenMobileWindow](../WDLang1/1000021018.md), [OpenChild](../WDLang1/3038021.md), [OpenSister](../WDLang1/3038045.md), [MDIOpen](../WDLang1/3052005.md)) and use the following syntax for each parameter: 
	```txt
	<Name of parameter>: Value
	```

	For example: 
	```wl
	Open(WIN_Travel(<Identifier>:123456, <Availability>:True))
	```



Remark: Using named parameters allows changing the order of the expected parameters. For example, you can write: 
```wl
Open(WIN_Travel(<Availability>:True, <Identifier>:123456))
```

<a name="NOTE5_3"></a>


### Scope of parameters
<a name="scope_parameters_ELTPARAGRAPHE000133"></a>

The parameters passed to a window are global to all the events of this window (initialization, code of a button, code of a local procedure, etc.).

<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Returning a value when a window is closed
<a name="returning_value_when_window_closed_ELTTEXTE000367"></a>
To return a value when a window is closed: 

1. Use [ReturnedValue](../Proprietes/2510134.md) in the processes that call [Close](../WDLang1/3038018.md).
	For example, in the WIN_Calendar window, the BTN_OK button and the BTN_CANCEL button close the window once the value to return was initialized:
	
	```wl
	// -- Click BTN_OK (WIN_Calendar window)
	
	// OK was clicked by the user, 
	// the date entered in the EDT_DateControl control is returned 
	MyWindow.ReturnedValue = EDT_DateControl
	// Close the window
	Close()
	```

	
	```wl
	// -- Click BTN_CANCEL (WIN_Calendar window)
	
	// The BTN_CANCEL button was clicked by the user,
	// an empty string is returned
	MyWindow.ReturnedValue = ""
	// Close the window
	Close()
	```

	You can also use the [ReturnedValue](../Proprietes/2510134.md) property in the "Closing" event of the window:
	
	```wl
	// -- Closing code of WIN_Calendar window
	
	// The date is entered in the EDT_DateControl control
	IF EDT_DateControl = "" THEN
		// No date was entered
		MyWindow.ReturnedValue = ""
	ELSE
		// A date is entered
		MyWindow.ReturnedValue = EDT_DateControl
	END
	```


2. Retrieve in a variable the value returned by the window when it was closed. This variable contains the result of [Open](../WDLang1/3038035.md).
	For example, the BTN_Calendar button is used to open the WIN_Calendar window. When opening this window ([Open](../WDLang1/3038035.md)), today's date is passed as parameter. The value returned by the window when it is closed corresponds to the result returned by [Open](../WDLang1/3038035.md). This result is retrieved in the ResultDate variable:
	
	```wl
	// -- Click BTN_Calendar
	// Open the WIN_Calendar window
	// ResultDate is a string variable containing 
	// the value returned by the WIN_Calendar window
	ResultDate = Open(WIN_Calendar, Today())
	
	// Study the result returned by the window
	IF ResultDate = "" THEN
		Info("No date was selected.")
	ELSE
		Info("Selected date: " + DateToString(ResultDate, maskDateSystem))
	END
	```




The code execution of BTN_Calendar is stopped until the WIN_Calendar window is closed.
![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png) **Special case: Value returned by a child window in a WINDEV Mobile application**
In the mobile applications, the windows are opened by [OpenMobileWindow](../WDLang1/1000021018.md) (or [OpenChild](../WDLang1/3038021.md)) instead of [Open](../WDLang1/3038035.md). 

You can get the value returned by a child window in the "Close a child window" event of the window that opened it (the one that called [OpenMobileWindow](../WDLang1/1000021018.md) or [OpenChild](../WDLang1/3038021.md)).

For example:

- In the closing code of the child window, [Close](../WDLang1/3038018.md) is used to return a parameter: 
	
	```wl
	Close("", MyParameter)
	```


- The name of the child window as well as the returned value can be retrieved in the "Close a child window" event of the calling window, using the [MyChildWindow](../Motscles/1511021.md) keyword: 
	
	```wl
	// Close a child window
	NameChildWindow = MyChildWindow.Name 
	ValueChildWindow = MyChildWindow.ReturnedValue
	```





<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Testing a window with parameters
<a name="testing_window_with_parameters_ELTTEXTE000397"></a>
To run the test of a window with parameters: 

1. Open the window with parameters in the window editor: on the "Home" tab, in the "General" group, click "Open".

2. Click ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=Ico_Go_Fenetre_WD_bl.gif) in the quick access buttons (or press F9). The following window is displayed:<br>![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=fen%EAtre_param%E9tr%E9e%20-%20HC%20N%B0001.gif)


3. Specify the value of the parameters that will be used to test the window. To use the default value of the parameters, type the "\*" character.

4. Validate. The window is displayed according to the specified values.





