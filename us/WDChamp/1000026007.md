


## Handling a TimeLine control through programming (prefix syntax)
			





## Overview
<a name="overview_ELTTEXTE000092"></a>
A TimeLine control can be: 

- handled programmatically. 

- [linked to a data file](../WDChamp/1000026005.md). 

- [bound to a variable](../WDChamp/1000026005.md). 




In WINDEV, you can use [TimeLine functions](../WDLang1/1000020783.md) to handle TimeLine controls programmatically. 

This help page explains how to handle TimeLine controls programmatically. The example below stores the events in an HFSQL database.


<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Handling TimeLine controls programmatically
<a name="handling_timeline_controls_programmatically_ELTTEXTE000140"></a>


### Adding a track
<a name="adding_track_ELTPARAGRAPHE000163"></a>

To add a track to a TimeLine control, call [&lt;TimeLine&gt;.AddTrack](../WDLang1/1000020777.md). 

Example:

```wl
// Adds tracks in TL_TimeLine1 
TL_TimeLine1.AddTrack("Sub Bass")
TL_TimeLine1.AddTrack("Scratchy Drums")
TL_TimeLine1.AddTrack("Guitar 1")
```
 
**Remark**: [&lt;TimeLine&gt;.AddTrack](../WDLang1/1000020777.md) inserts a background image to the track.


### Adding an event
<a name="adding_event_ELTPARAGRAPHE000193"></a>

To add an event to a TimeLine control, use [&lt;TimeLine&gt;.AddEvent](../WDLang1/1000020776.md). This function accepts two syntaxes: 

- syntax used to specify the characteristics of the event: title, description, etc, ...
	
	```wl
	// Adds a 5-second event into a TimeLine control configured to the second
	MyTrack is string
	MyTitle is string
	EvtStart is int
	EvtEnd is int 
	
	MyTrack = "Strings"
	MyTitle = "Start"
	EvtStart = 4
	EvtEnd = 9
	
	TL_Music.AddEvent(MyTrack, MyTitle, EvtStart, EvtEnd)
	```


- syntax that handles a variable of type [EventTimeline](../WDLang1/1000020612.md).
	
	```wl
	TL_Robots.DeleteAll()
	Evt is EventTimeline
	
	TL_Robots.AddTrack("Robot 1")
	Evt.Track = "Robot 1"
	Evt.Title = "Start"
	Evt.Start = 10
	Evt.End = 150
	Evt.BackgroundColor = LightGreen
	TL_Robots.AddEvent(Evt)
	
	Evt.Track = "Robot 1"	
	Evt.Title = "Special process"
	Evt.Start = 150
	Evt.End = 450
	Evt.BackgroundColor = PastelBlue
	TL_Robots.AddEvent(Evt)
	```



The **BackgroundColor** property of the [EventTimeline](../WDLang1/1000020612.md) variable is used to define the color used to display an event. If no background color is defined, the TimeLine control will automatically use the color associated with the category of the event.


### Populating a TimeLine control with the data from an HFSQL data file
<a name="populating_timeline_control_with_the_data_from_hfsql_data_file_ELTPARAGRAPHE000235"></a>

The records are stored in an HFSQL data file. To populate the TimeLine control for the first time, you can use a FOR EACH loop or add each event with [&lt;TimeLine&gt;.AddEvent](../WDLang1/1000020776.md).

```wl
TL_Robots.DeleteAll()
// Declare the events
Evt is EventTimeline

// Browse the Robot data file
FOR EACH Robot 
	TL_Robots.AddTrack(Robot.RobotName)
	// Browse the Evt_Robot data file
	FOR EACH Evt_Robot WITH RobotID = Robot.RobotID
		Evt.Tooltip = Evt_Robot.EvtTooltip
		Evt.Title = Evt_Robot.EvtTitle
		Evt.Start = Evt_Robot.EvtStart
		Evt.End = Evt_Robot.EvtEnd
		Evt.Content = Evt_Robot.EvtContent
		Evt.Track = Robot.RobotName
		Evt.BackgroundColor = Evt_Robot.EvtColor
		TL_Robots.AddEvent(Evt)
	END
END
```
Reminder: You can also use a TimeLine control linked to a data file. For more details, see [TimeLine control linked to a data file](../WDChamp/1000026005.md).


### Retrieving a list of events
<a name="retrieving_list_events_ELTPARAGRAPHE000260"></a>

[TimelineListEvent](../WDLang1/1000020779.md) is used to get: 

- the list of all events in the TimeLine control: 
	
	```wl
	// Array containing a list of events 
	arrEvtList is array of EventTimeLine
	
	// List of events
	arrEvtList = TL_Timeline.ListEvent()
	```


- the list of events of a track between two specific times: 
	
	```wl
	// List of events for robot 1 found between 50s and 100s 
	arrEvtList is array of EventTimeLine
	arrEvtList = TL_Robots.ListEvent("Robot 1", 50, 100)
	```


- the currently selected or hovered event: 
	
	```wl
	// Selected event 
	arrEvtList is array of EventTimeLine
	
	arrEvtList = TL_Robots.ListEvent(schAptSelected)
	```






### Deleting an event
<a name="deleting_event_ELTPARAGRAPHE000297"></a>

[&lt;TimeLine&gt;.DeleteEvent](../WDLang1/1000020780.md) is used to delete: 

- the event selected in the control. 

- a specific event.



```wl
// Deletes the first event 
TL_MyTimeLine.DeleteEvent(1)
```



### Deleting a track
<a name="deleting_track_ELTPARAGRAPHE000321"></a>

[&lt;TimeLine&gt;.DeleteTrack](../WDLang1/1000020781.md) is used to delete a track from the TimeLine control.

```wl
// Deletes the SOUND track 
ResDel is boolean
ResDel = TL_MyTimeLine.DeleteTrack("Sound")
IF ResDel = True THEN
	Info("Track successfully deleted")
END
```
[&lt;TimeLine&gt;.DeleteAll](../WDLang1/1000020782.md) is used to delete all the events from the TimeLine control as well as all its tracks.


### Changing the time scale of the control
<a name="changing_the_time_scale_the_control_ELTPARAGRAPHE000347"></a>

You can change the time scale of a TimeLine control with [&lt;TimeLine&gt;.ChangeMode](../WDLang1/1000020778.md). This function changes the control display mode: second, millisecond or microsecond.



## Using the popup menu (AAF)
<a name="using_the_popup_menu_aaf_ELTTEXTE000357"></a>
The Timeline control is associated with a context menu (AAF). The context menu of the TimeLine control allows you to: 

- zoom or out in the control,

- add, modify or delete an event. 




To save the operations performed, you must use the WLanguage events associated with the TimeLine control. 

In the corresponding WLanguage event, simply retrieve the event currently used and perform the corresponding process. 

**Example**: To store in an "EVT" data file an event that the user adds via the context menu, simply enter the following code in the "Enter input mode in an event" event: 


```wl
PROCEDURE EnterInInput(evtEdited is EventTimeline)

// Store the data
EVT.Title = evtEdited.Title
EVT.EvtStart = evtEdited.Start
EVT.EvtEnd = evtEdited.End
...
HAdd(EVT)
```
The same type of code can be implemented for the different events associated with the TimeLine control. A procedure is automatically declared by the TimeLine control for each WLanguage event associated with the control that handles an event. 

These procedures receive a variable of type [EventTimeline](../WDLang1/1000020612.md) as parameter.



## Advanced use of the events associated with the Timeline control
<a name="advanced_use_the_events_associated_with_the_timeline_control_ELTTEXTE000400"></a>


### Advanced use of events with procedures
<a name="advanced_use_events_with_procedures_ELTPARAGRAPHE000423"></a>

You can also allow users to define more precisely the characteristics of the events when they are added or modified. To do so, create a window with the information to specify. 

In the code, specify that the window must be opened in the "Enter input mode in an event" event. To lock the direct input, the process must return <u><u><u><u>False</u></u></u></u>. 

This principle can be applied to all WLanguage events called via the context menu of the TimeLine control. 

Example: Opening a window for entering the event.  


```wl
PROCEDURE EnterInInput(evtEdited is EventTimeline)

// Opens the window for entering an event
// with the selected event (in Creation or Modification mode)
Open(WIN_InputEvt_HFSQL, evtEdited)

// Returns False to lock the direct input in the TimeLine control
RESULT False
```




## Properties specific to TimeLine controls
<a name="properties_specific_timeline_controls_ELTTEXTE000440"></a>
The following properties are used to handle TimeLine controls.



|   |   |
| --- | --- |
| [EndTotalRange](../Proprietes/1000020393.md) | The **EndTotalRange** property is used to: <br><br>- get the last date or time that can be displayed in a Scheduler or TimeLine control. <br><br>- change the last time that can be displayed in a TimeLine control.<br><br><br> |
| [EndVisibleRange](../Proprietes/1000020395.md) | The **EndVisibleRange** property is used to: <br><br>- find out and modify the last visible date or time in a Scheduler control or in a TimeLine control. <br><br>- modify the last visible time in a TimeLine control.<br><br><br> |
| [GranularityDuration](../Proprietes/1000019643.md) | The **GranularityDuration** property is used to get and change the size of the grid to resize:<br><br>- appointments in an Organizer control. <br><br>- appointments in a Scheduler control.<br><br>- events in a TimeLine control.<br><br>- tasks in a Gantt Chart column.<br><br><br> |
| [GranularityMovement](../Proprietes/1000019642.md) | The **GranularityMovement** property is used to get and change the size of the grid to move:  <br><br>- appointments in an Organizer control. <br><br>- appointments in a Scheduler control.<br><br>- events in a TimeLine control.<br><br>- tasks in a Gantt Chart column.<br><br><br> |
| [RulerModifiable](../Proprietes/1000020381.md) | The **RulerModifiable** property is used to: <br><br>- Find out whether the user can move the playhead in a TimeLine control. <br><br>- Allow or prevent the user from moving the playhead in a TimeLine control. <br><br><br> |
| [RulerValue](../Proprietes/1000020380.md) | The **RulerValue** property is used to get or change the position of the playhead in a TimeLine control. |
| [RulerVisible](../Proprietes/1000020379.md) | The **RulerVisible** property is used to: <br><br>- Determine if a playhead is visible in a TimeLine control. <br><br>- Show or hide a playhead in a TimeLine control. <br><br><br> |
| [StartTotalRange](../Proprietes/1000020392.md) | The **StartTotalRange** property is used to: <br><br>- get the first date or time that will be displayed in a Scheduler or TimeLine control. <br><br>- change the first time that can be displayed in a TimeLine control.<br><br><br> |
| [StartVisibleRange](../Proprietes/1000020394.md) | The **StartVisibleRange** property is used to: <br><br>- get and change the first visible date or time in a Scheduler or TimeLine control.  <br><br>- change the first visible time in a TimeLine control.<br><br><br> |





For a complete list of WLanguage properties that can be used with TimeLine controls, see [Properties associated with TimeLine controls](../WDChamp/1000026003.md).


