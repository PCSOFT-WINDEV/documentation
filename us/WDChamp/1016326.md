


## Handling Organizer controls programmatically
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Overview
<a name="overview_ELTTEXTE000330"></a>
An Organizer control can be: 

- handled programmatically. 

- ![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) [linked to a data file](../WDChamp/1016325.md). 

- ![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) [bound to a variable](../WDChamp/1016325.md). 




WINDEV and WEBDEV include the [OrganizerXXX](../WDLang1/1000019021.md) functions to handle Organizer controls. 

This help page explains how to programmatically handle an Organizer control in a window or page. The example used in the illustration allows you to store appointments in an HFSQL database. 







## Handling Organizer controls programmatically
<a name="handling_organizer_controls_programmatically_ELTTEXTE000360"></a>


### Adding an appointment
<a name="adding_appointment_ELTPARAGRAPHE000065"></a>

You can add an appointment to an Organizer control using [OrganizerAddAppointment](../WDLang1/1000018877.md). This function accepts two syntaxes: 

- syntax used to specify the appointment characteristics: title, description, etc.
	
	```wl
	// Adds an appointment to the Organizer control
	OrganizerAddAppointment(ORG_MyOrganizer, "Sales meeting", "201003220845", ...
			"201003221230", "Sales")
	```


- syntax that handles a variable of type [Appointment](../WDLang1/1000019244.md).
	
	```wl
	// Declares an Appointment variable
	MyAppointment is Appointment
	
	// Fills the appointment
	MyAppointment.Title = "Sales meeting"
	MyAppointment.Content = "Meeting to discuss the weekly objectives."
	MyAppointment.StartDate = "201003220845"
	MyAppointment.EndDate = "201003221230"
	MyAppointment.Category = "Sales"
	MyAppointment.ID = 1
	
	// Adds the appointment into the control
	OrganizerAddAppointment(ORG_Organizer, MyAppointment)
	```



The **BackgroundColor** property of the [Appointment](../WDLang1/1000019244.md) variable is used to define a display color for an appointment. If no background color is defined, the Organizer control will automatically use the color associated with the appointment category. 


### Filling an Organizer control with the data found in an HFSQL data file
<a name="filling_organizer_control_with_the_data_found_hfsql_data_file_ELTPARAGRAPHE000100"></a>

The records are stored in an HFSQL data file. The Organizer control can be initially filled by browsing the data file via the FOR EACH syntax and by adding each appointment via [OrganizerAddAppointment](../WDLang1/1000018877.md).

```wl
// Appointment variable
MyAppointment is Appointment

// Browse through the appointments stored in the database
FOR EACH APT 
	// Fills the information of the variable
	MyAppointment.Title = APT.Title
	MyAppointment.Content = APT.Content
	MyAppointment.StartDate = APT.StartDate
	MyAppointment.EndDate = APT.EndDate
	MyAppointment.Category = APT.Category
	MyAppointment.ID = APT.APTID
	
	// Adds the appointment to the Organizer control
	OrganizerAddAppointment(ORG_MyOrganizer, MyAppointment)
	
END
```
**Reminder**: You also have the ability to use an Organizer control linked to a data file. For more details, see [Organizer control linked to a data file](../WDChamp/1016325.md).


### Retrieving a list of appointments
<a name="retrieving_list_appointments_ELTPARAGRAPHE000124"></a>

[OrganizerListAppointment](../WDLang1/1000018880.md) is used to get: 

- the list of all appointments in the Organizer control: 
	
	```wl
	// Array containing a list of Appointment
	arrAppointmentList is array of Appointment
	
	// Lists of appointments
	arrAppointmentList = OrganizerListAppointment(ORG_MyOrganizer)
	```


- the list of appointments included between two dates: 
	
	```wl
	// List of appointments for January 2010
	arrAppointmentList is array of Appointment
	
	// Lists of appointments
	arrAppointmentList = OrganizerListAppointment(ORG_MyOrganizer, "20100101", "20100131")
	```


- the appointment currently selected or hovered: 
	
	```wl
	// Selected appointment
	arrAppointmentList is array of Appointment
	
	arrAppointmentList = OrganizerListAppointment(ORG_MyOrganizer, orgAptSelected)
	```






### Displaying an Organizer control from a specific date
<a name="displaying_organizer_control_from_specific_date_ELTPARAGRAPHE000158"></a>

To display the Organizer control from a specific date, use [OrganizerPosition](../WDLang1/1000018889.md).

```wl
// Position the Organizer control on today's date
OrganizerPosition(ORG_Organizer1, Today())

// Position the Organizer control on December 20, 2012
OrganizerPosition(ORG_Organizer1, "20121220")
```



### Delete an appointment
<a name="delete_appointment_ELTPARAGRAPHE000169"></a>

[OrganizerDeleteAppointment](../WDLang1/1000018881.md) is used to delete: 

- the appointment selected in the control. 

- a specific appointment. 
	
	```wl
	// Deletes the first appointment
	OrganizerDeleteAppointment(ORG_MyOrganizer, 1)
	```



[OrganizerDeleteAll](../WDLang1/1000018882.md) deletes all the appointments from the Organizer control. 


### Modifying the display of the control
<a name="modifying_the_display_the_control_ELTPARAGRAPHE000192"></a>

The current display of an Organizer control can be modified by the following functions: 

- [OrganizerDisplayPreviousPeriod](../WDLang1/1000018886.md) and [OrganizerDisplayNextPeriod](../WDLang1/1000018885.md): These two functions are used to modify the display period (same as using the spin button found at the top of Organizer control). 

- [OrganizerChangeMode](../WDLang1/1000019497.md): This function changes the control display mode: day, week, month.

- [OrganizerTimeDisplayed](../WDLang1/1000018888.md): This function is used to define the time period displayed. 






## Using the context menu (AAF)
<a name="using_the_context_menu_aaf_ELTTEXTE000421"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) 

### Possibilities of the context menu
<a name="possibilities_the_context_menu_ELTPARAGRAPHE000215"></a>

The context menu of Organizer control is used to: 

- change the control display mode,

- add, modify or delete an appointment. 




To save the operations performed, you must use the events of Organizer control. 

In the corresponding event, simply retrieve the appointment currently used and perform the corresponding process. 

**Example**: To store an appointment added by the user via the context menu in an APT data file, simply write the following in the "Entry in edit in an appointment" event: 


```wl
PROCEDURE Edit(aptEdited is Appointment)

// Store the data
APT.Title = aptEdited.Title
APT.StartDate = aptEdited.StartDate
APT.EndDate = aptEdited.EndDate
...
HAdd(APT)
```
The same type of code can be used for the different events of the Organizer control. Indeed, a procedure has been automatically declared by the Organizer control for each event in the control that handles an appointment. 

These procedures take an [Appointment](../WDLang1/1000019244.md) variable as parameter. This variable contains the characteristics of the appointment used by the event. 
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) 

### Advanced use of events with procedures
<a name="advanced_use_events_with_procedures_ELTPARAGRAPHE000241"></a>

You can also allow the user to define more precisely the characteristics of his appointment during an addition or a modification. To do so, create a window with the information to specify. 

In the code, simply open the window in the "Entry in edit in an appointment" event. To lock the direct input via the context menu of the organizer, the event must return <u><u><u><u>False</u></u></u></u>. 

This principle can be applied to all the events called by the context menu of the Organizer control. 

Example: 


```wl
PROCEDURE Edit(aptEdited is Appointment)

// Opens the window for entering an appointment 
// with the selected appointment (in Creation or Modification mode)
Open(WIN_InputAPT_HFSQL, aptEdited)

// Returns False to lock the direct input in the Organizer control
RESULT False
```




## Managing public holidays
<a name="managing_public_holidays_ELTTEXTE000451"></a>
Public holidays can be set programmatically. Several WLanguage functions (starting with BankHolidayXXX) are available.

To define the bank holidays displayed in the Organizer controls and in the Calendar controls, you must use [BankHolidayAdd](../WDLang1/1000017304.md). This function allows you to define the list of bank holidays that will be used. This function allows you to customize the public holidays according to the country and local regulations. This function must be used at the beginning of the application because it has a global effect on the application.

The bank holidays will be colored in green in the Organizer control. 

Example:

```wl
// Delete all public holidays
BankHolidayDeleteAll()
// Initialize the 11 public holidays common to the French regions and territories
BankHolidayAdd("0101")		// 1st of January
BankHolidayAdd(bhEasterMonday)	// Easter Monday
BankHolidayAdd("0501")		// 1st of May
BankHolidayAdd("0508")		// 8th of May
BankHolidayAdd(bhAscensionDay)	// Ascension day
BankHolidayAdd(bhWhitMonday)	// Whit Monday
BankHolidayAdd("0714")		// 14th of July
BankHolidayAdd("0815")		// 15th of August (Assumption)
BankHolidayAdd("1101")		// All Saints' Day
BankHolidayAdd("1111")		// 11th of November
BankHolidayAdd("1225")		// Christmas

// Add 2 additional public holidays for the regions of Moselle and Alsace
BankHolidayAdd("1226" + CR + bhGoodFriday)
```




## Properties specific to Organizer controls
<a name="properties_specific_organizer_controls_ELTTEXTE000477"></a>
The following properties can be used to handle Organizer controls.



|   |   |
| --- | --- |
| [DirectInputAPT](../Proprietes/1000019995.md) | The **DirectInputAPT** property is used to determine and specify if the user can directly change the title of an appointment in a Scheduler or Organizer control. |
| [GranularityAppointment](../Proprietes/1000019100.md) | The **GranularityAppointment** gets and changes the precision of the grid used to define appointments in Organizer or Scheduler controls. <br>Property kept for backward compatibility. |
| [GranularityDuration](../Proprietes/1000019643.md) | The **GranularityDuration** property is used to get and change the size of the grid to resize:<br><br>- appointments in an Organizer control. <br><br>- appointments in a Scheduler control.<br><br>- events in a TimeLine control.<br><br>- tasks in a Gantt Chart column.<br><br><br> |
| [GranularityMovement](../Proprietes/1000019642.md) | The **GranularityMovement** property is used to get and change the size of the grid to move:  <br><br>- appointments in an Organizer control. <br><br>- appointments in a Scheduler control.<br><br>- events in a TimeLine control.<br><br>- tasks in a Gantt Chart column.<br><br><br> |
| [MaskTitleDate](../Proprietes/1000019463.md) | The **MaskTitleDate** property is used to identify and change the input mask used for the title of day columns in Organizer or Scheduler controls |
| [ModificationDurationAPT](../Proprietes/1000019994.md) | The **ModificationDurationAPT** property is used to determine and specify if the user can modify the duration of an appointment in a Scheduler or Organizer control. |
| [MovementAPT](../Proprietes/1000019993.md) | The **MovementAPT** property allows you to know and specify whether users can move appointments in a Scheduler or Organizer control. |
| [Num1stDayOfTheWeek](../Proprietes/1000019004.md) | The **Num1stDayOfTheWeek** property gets and sets the 1st day of the week displayed in:<br><br>- a Calendar control.<br><br>- an Organizer control.<br><br>- a Scheduler control. <br><br>- an edit control in Date format with Calendar.<br><br><br> |
| [PeriodSelection](../Proprietes/1000019996.md) | The **PeriodSelection** property is used to determine and specify if the user can select a time period in an Scheduler or Organizer control. |
| [WorkingHourEnd](../Proprietes/1000019056.md) | The **WorkingHourEnd** property is used to identify and change the end time of working hours used:<br><br>- by an Organizer control.<br><br>- by a Scheduler control.<br><br>- by a Gantt Chart column (in a Table or TreeView Table control).<br><br><br> |
| [WorkingHourStart](../Proprietes/1000018958.md) | The **WorkingHourStart** property is used to get and change the start time of working hours used: <br><br>- by an Organizer control.<br><br>- by a Scheduler control.<br><br>- by a Gantt Chart column (in a Table or TreeView Table control). <br><br><br> |





For a complete list of WLanguage properties that can be used with an Organizer control, see [Properties associated with Organizer controls](../WDChamp/1016322.md).


