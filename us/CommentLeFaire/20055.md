
## How to write into a text or CSV file? 
			

<a name="NOTE1"></a>
<a name="NOTE1_1"></a>
Two methods can be used to write into a text or CSV file: 

- [Writing line by line into the file by using the fReadLine function](#NOTE2_1).

- [Writing in memory then full backup in a file by using the fSaveText function](#NOTE3_1). 




<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Method 1: Using the fWriteLine function
<a name="method_1_using_the_fwriteline_function_ELTTEXTE000139"></a>
To write a line into a text or CSV file: 

1. Open the file (or create it) with [fOpen](../WDLang1/3036036.md).

2. Write the line with [fWriteLine](../WDLang1/3036025.md). [fWriteLine](../WDLang1/3036025.md) automatically adds a CR character at the end of line.

3. Close the file with [fClose](../WDLang1/3036027.md). 





### Code samples
<a name="code_samples_ELTPARAGRAPHE000042"></a>

- Writing a line: 
	
	```wl
	// Write a simple string
	nFileNum is int
	sLineToWrite is string
	
	nFileNum = fOpen("MyFile.txt", foCreate)
	IF nFileNum <> -1 THEN
		sLineToWrite = "Hello, today is " + DateToString(Today(), "DDDD DD MMMM YYYY")
	
		// Write the line
		fWriteLine(nFileNum, sLineToWrite)
	
		fClose(nFileNum)
	END
	```


- Converting a data file into TXT (export) 
	
	```wl
	<code wl>
	nFileNum is int
	sLineToWrite is string
	
	nFileNum = fOpen ("ExportProducts.txt", foCreate)
	IF nFileNum <> -1 THEN
		FOR EACH Product
			// The line is made of:
			// - the product reference
			// - the product caption
			// - the product price
			sLineToWrite = Product.Reference + TAB + Product.ProdCap + TAB + 
					NumToString(Product.Price, "10,2fS")
	
			// Write the line
			fWriteLine(nFileNum, sLineToWrite)
		END
		fClose(nFileNum)
	END
	```





<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Method 2: Using the fSaveText function
<a name="method_2_using_the_fsavetext_function_ELTTEXTE000169"></a>


### Principle
<a name="principle_ELTPARAGRAPHE000056"></a>

To write (create) a text or CSV file in a single operation: 

1. Declare a string variable.  

2. Write and store all the file lines in memory in this variable. 

3. Save the content of this variable in a file with [fSaveText](../WDLang1/3036046.md). 





### Code samples
<a name="code_samples_ELTPARAGRAPHE000068"></a>


```wl
// Convert a data file into TXT (export)
sLineToWrite is string
sFileContent is string

FOR EACH Product
	// The line is made of:
	// - the product reference
	// - the product caption
	// - the product price
	sLineToWrite = Product.Reference + TAB + Product.ProdCap + TAB + 
			NumToString(Product.Price, "10,2fS")

	// Write the line in memory
	sFileContent += sLineToWrite + CR
END
fSaveText ("ExportProducts.txt", sFileContent)
```



