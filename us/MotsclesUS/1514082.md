
## SQL query (WLanguage type)

***In french: Requêtes SQL (type WLanguage)***
				



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
The "SQL query" type is used to write an SQL query in the WLanguage code. The SQL query is defined and declared. Then, it can be run by [HExecuteQuery](../WDLang4/3044080.md) or [HExecuteSQLQuery](../WDLang4/3044084.md).

Using the "SQL query" type presents several benefits: 

- Automatic coloring of SQL code in the editor,

- Assisted input of SQL code,

- Assisted input on the result items and query parameters,

- Compilation errors: 

	- when an error occurs in the SQL code

	- when using an output item that does not exist. 




- Data binding on the query if it is declared as global to a window,

- Automatic freeing at the end of variable scope. 



<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
qryFlightsStat is SQL Query =
[
	SELECT * FROM  Flights
	WHERE Flights.DepartureAirportID = {ParamDepartureAirportID}
	AND Flights.ArrivalAirportID = {ParamArrivalAirportID}
]
qryFlightsStat.ParamDepartureAirportID = 12
qryFlightsStat.ParamArrivalAirportID = 3

HExecuteQuery(qryFlightsStat)
FOR EACH qryFlightsStat
	...
END
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Syntax

### Declaring an SQL query

`<Name SQL query> is SQL Query =
[    
    <SQL code of query>
]
`
---

**`<Name SQL query>: ()`**

Name of variable corresponding to the SQL query. 

**`<SQL code of the query>: ()`**

SQL code corresponding to the SQL query associated with the variable. 



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks


### Declaration syntax: Remarks 
<a name="declaration_syntax_remarks_ELTPARAGRAPHE000051"></a>

- The query must be defined as soon as it is declared. You cannot declare an SQL Query variable then type the corresponding SQL code after several code lines. 

- When declaring SQL Query variables, you can launch the query editor wizard to write the SQL code of the query. Simply select "SQL query (wizard)". 
![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=Requ%EAteSQL_WL%20-%20HC%20N%B0001.gif)
The query editor wizard opens and allows you to enter the options of your query. Once you go through the steps of the wizard, the SQL code of the query is included in the WLanguage code. 

- The data files used in the FROM of the query must be declared in the project analysis. They cannot come from data sources initialized by an alias, external declaration or other request.

- The query cannot be built with string concatenations. To build a query by concatenating strings, use [HExecuteSQLQuery](../WDLang4/3044084.md).

- The SQL query is not run during its declaration. It must be run by [HExecuteQuery](../WDLang4/3044080.md) or [HExecuteSQLQuery](../WDLang4/3044084.md).

- When the query is declared and run, it can be used by all HFSQL functions for handling queries. 

- When the SQL query is declared and run, it can be used in another SQL query by using the name of corresponding variable: 
	
	```wl
	MyInterrogation is SQL Query = 
	[
		SELECT * FROM CUSTOMER
	]
	HExecuteQuery(MyInterrogation)
	
	OtherInterrogation is string = "SELECT * FROM %1"
	OtherInterrogation = StringBuild(OtherInterrogation, MyInterrogation.Name)
	
	QryWithMyInterrogation is Data Source
	HExecuteSQLQuery(QryWithMyInterrogation, OtherInterrogation)
	```




<a name="NOTE0_2"></a>


### SQL code of the query
<a name="sql_code_the_query_ELTPARAGRAPHE000079"></a>

- This SQL code typed benefits from the syntactic coloring. The SQL keywords are colored (see the example). 

- Completion: The completion is available: 

	- for the SQL keywords,

	- for the items and the data files,

	- for the output items of query. ![The assisted SQL input](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=151-sql_completion_rubriques.jpg)





- Compilation errors: In case of error in the SQL code of query, a compilation error occurs (like for WLanguage code). ![Error of SQL code detected in input](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=152-sql_code_erreur.jpg)


- Like for an SQL query typed in the query editor, you can: 

	- type comments by writing the "--" or "//" character in front of them. 

	- type WLanguage functions. The WLanguage functions must be prefixed by "WL.". Assisted input is available. 
			![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) You cannot run SQL queries containing WLanguage functions. 

	- ...






<a name="NOTE0_5"></a>


### Using [%  %] in SQL queries
<a name="using_sql_queries_ELTPARAGRAPHE000106"></a>

SQL queries support the new syntax: 

```txt
[% variable_name %]
```

At runtime, the variable name between "%" characters will be replaced with the value of this variable. 

Example: 


```wl
IDOfCustomerToFind is int = xxx
// Search for all the customer orders
AllOrders is SQL Query = [
	SELECT * FROM Order WHERE CustomerID = [%IDOfCustomerToFind%]
	]
HExecuteSQLQuery(AllOrders)
FOR EACH AllOrders
	// a customer order
END
```


Remarks: 

- Parameters are evaluated only once, when the query is declared.

- All executions of the query (calls to [HExecuteSQLQuery](../WDLang4/3044084.md)) will therefore use the value the variable held at the time the query was declared, even if the value changes between two calls to [HExecuteSQLQuery](../WDLang4/3044084.md). 

- To pass parameters to the query at each call: 

	- you can use an HFSQL query parameter "{&lt;parameter&gt;}".

	- You can also use another SQL query or an "alias" data source containing the alias of a file and described by an attribute such as &lt;description="file"&gt;. Example: 
			
		```wl
		CustomerSelection is Data Source <description="Customer">
		// Search for all the customers in the selection for which a condition is true
		FilterCustomer is SQL Query = [
				SELECT * FROM [%CustomerSelection%] WHERE ...
			]
		HExecuteSQLQuery(FilterCustomer)
		FOR EACH FilterCustomer
			// Customers in the selection that are filtered by the query
		END
		```







<a name="NOTE0_4"></a>


### HFSQL context
<a name="hfsql_context_ELTPARAGRAPHE000140"></a>

- An SQL query is defined in the current HFSQL context. An SQL query cannot exist in a window with independent HFSQL context. 

- An SQL query can be defined as a global variable. In this case, you must pay attention to the HFSQL contexts. 





