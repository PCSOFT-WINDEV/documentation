
## Lambda: Expressions and procedures
			

<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Overview
<a name="overview_ELTTEXTE000209"></a>
A lambda ("lambda function") is a more concise way to write internal procedures.

Lambdas are useful for all functions that take callback procedures as parameters: this means it possible to enter the function code directly instead of the parameter. In this case: 

- the procedure has no name,

- the code of the procedure is directly where the name of the procedure should be. 




WLanguage offers several syntaxes for using lambdas. 

<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Lambda procedure
<a name="lambda_procedure_ELTTEXTE000233"></a>
A lambda procedure is an anonymous internal procedure, for which the parameters and the return value do not need to be typed. 


### Example
<a name="example_ELTPARAGRAPHE000026"></a>


```wl
// Fill in a array with the names of all the files in the data directory
arrFile is array of strings
fListFile(fdataDir() + "\*.*", (Directory,File)=>{arrFile.Add(File)} )
```


In this code, the lambda procedure corresponds to the following: 
```txt
(Directory,File)=>{arrFile.Add(File)}
```





### Syntaxes
<a name="syntaxes_ELTPARAGRAPHE000035"></a>

- Lambda procedure that expects no parameters: 
	
	```txt
	() => { <WLanguage code of procedure>}
	```


- Lambda procedure that expects only one parameter: 

	- full syntax: 
			
		```txt
		(<Parameter 1>) => { <WLanguage code of procedure>}
		```


	- simplified syntax: 
			
		```txt
		<Parameter 1> => { <WLanguage code of procedure>}
		```





- Lambda procedure that expects several parameters: 
	
	```txt
	(<Parameter 1>, ..., <Parameter N>) => { <WLanguage code of procedure>}
	```





In these syntaxes: 

- &lt;Parameter X&gt; corresponds to the name of the parameter. 

- &lt;WLanguage code of procedure&gt; corresponds to the WLanguage code of the procedure. It is enclosed between { and }. Several statements can be used





### Remarks
<a name="remarks_ELTPARAGRAPHE000058"></a>

- This syntax can be used on WLanguage functions ([fListFile](../WDLang1/3036058.md), etc.) but also on the procedures of the project. To do so, the formal parameter to which the lambda procedure is passed must be of type "Procedure".

- The procedure is anonymous, so it has no name and cannot be called elsewhere or recursively.

- A lambda procedure does not need to retrieve all the parameters:

	- unused parameters do not trigger a warning at compile time,

	- omitted parameters at the end of the prototype do not trigger an error at runtime.







<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Lambda expression
<a name="lambda_expression_ELTTEXTE000275"></a>
A lambda expression is a simplified lambda procedure when the WLanguage code to run corresponds to: 

```txt
RESULT <Expression>
```



### Example
<a name="example_ELTPARAGRAPHE000080"></a>

- Example using a lambda procedure: 
	
	```wl
	// count odd-numbered elements of an array 
	arrValue is array of int = [1,2,3,4,5]
	Trace(CountValues(arrValue, x=>{RESULT Modulo(x, 2)=1}))
	
	PROCEDURE CountValues(arrValue is array of int,
		Condition is Procedure(x int):boolean)
	let n = 0
	FOR EACH x OF arrValue
		IF Condition(x) THEN
			n++
		END
	END
	RESULT n
	```






- Simplification as lambda expression: 
	
	```wl
	// count odd-numbered elements of an array 
	arrValue is array of int = [1,2,3,4,5]
	Trace(CountValues(arrValue, x=>Modulo(x,2)=1))
	
	PROCEDURE CountValues(arrValue is array of int,
		Condition is Procedure(x int):boolean)
	let n = 0
	FOR EACH x OF arrValue
		IF Condition(x) THEN
			n++
		END
	END
	RESULT n
	```








### Syntaxes
<a name="syntaxes_ELTPARAGRAPHE000094"></a>

- Lambda expression that expects no parameters: 
	
	```txt
	() => <WLanguage expression>
	```


- Lambda expression that expects only one parameter: 

	- full syntax: 
			
		```txt
		(<Parameter 1>) => <WLanguage expression>
		```


	- simplified syntax: 
			
		```txt
		(<Parameter 1>) => <WLanguage expression>
		```





- Lambda expression that expects several parameters: 
	
	```txt
	(<Parameter 1>, ..., <Parameter N) => <WLanguage expression>
	```





In these syntaxes: 

- &lt;Parameter X&gt; corresponds to the name of the parameter. 

- &lt;WLanguage expression&gt; corresponds to the WLanguage expression to return. 





### Remark
<a name="remark_ELTPARAGRAPHE000117"></a>

This syntax can be used on WLanguage functions ([fListFile](../WDLang1/3036058.md), etc.) but also on the procedures of the project. To do so, the formal parameter to which the lambda expression is passed must be of type "Procedure".





<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Typed lambda procedure
<a name="typed_lambda_procedure_ELTTEXTE000317"></a>
A typed lambda procedure is an anonymous internal procedure, for which the parameters and the return value can be typed.  


### Example
<a name="example_ELTPARAGRAPHE000133"></a>


```wl
// Fill in a array with 
// the names of all files in the data directory
arrFile is array of strings
fListFile(fdataDir() + "\*.*", ...
	Procedure(Directory is string, File is string):string {arrFile.Add(File)})
```





### Syntax
<a name="syntax_ELTPARAGRAPHE000139"></a>


```txt
PROCEDURE (<Parameter 1>, ..., <Parameter N>): <Type of return> { <WLanguage code of procedure>}
```


In this syntax: 

- &lt;Parameter X&gt; corresponds to the name of the parameter(s) of the lambda procedure. This parameter can be typed or untyped. If a parameter is typed, its syntax is 
	
	```txt
	<Identifier> [is] <Type>
	```


- &lt;Type of return&gt; corresponds to the type returned by the lambda procedure. 

- &lt;WLanguage code of procedure&gt; corresponds to the WLanguage code of the procedure. It is enclosed between { and }.  





### Remarks
<a name="remarks_ELTPARAGRAPHE000152"></a>

- This syntax can be used on WLanguage functions ([fListFile](../WDLang1/3036058.md), etc.) but also on the procedures of the project. To do so, the formal parameter to which the lambda procedure is passed must be of type "Procedure".

- The procedure is anonymous, so it has no name and cannot be called elsewhere or recursively.

- A lambda procedure does not need to retrieve all the parameters:

	- unused parameters do not trigger a warning at compile time,

	- omitted parameters at the end of the prototype do not trigger an error at runtime.








