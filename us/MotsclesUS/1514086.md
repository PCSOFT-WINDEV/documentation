
## Extension procedure
			

<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Overview
<a name="overview_ELTTEXTE000131"></a>
By using **extension procedures**, you can add your own WLanguage procedures to different types of variables (string, date, color etc.). These procedures can then be called as native WLanguage functions. The basic WLanguage type is therefore "extended". 

To indicate that a procedure is an extension procedure, the following conditions must be met:

- the first parameter uses the type that must be "extended",

- the prototype of the procedure has the &lt;extension&gt; attribute.






<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Declaration
<a name="declaration_ELTTEXTE000155"></a>
Declaration of an extension procedure of the String type: 

```txt
PROCEDURE MyCustomProcedure(s is string) <extension>
```

<a name="NOTE2_2"></a>
Remarks: 

- The extension procedures appear in the code completion suggestions of the extended type.

- Extension procedures cannot be used with types such as Boolean, Integer, Real, Numeric, etc. (any type used to handle numeric values). 

- Only WLanguage types can be extended. 

- Only global procedures can have the &lt;extension&gt; attribute. 

- If the extension procedure overrides a WLanguage function, the standard syntax of that function will also be overridden. For example, creating a "Format" extension procedure for the String type will override both the [&lt;String&gt;.Format](../WDLang1/1000024877.md) and [StringFormat](../WDLang1/3024019.md) functions. 

- You can chain the calls to extension procedures.




<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Example
<a name="example_ELTTEXTE000185"></a>


### Replace tabs with spaces
<a name="replace_tabs_with_spaces_ELTPARAGRAPHE000063"></a>

Procedure to replace tabs with spaces: 

```wl
PROCEDURE ReplaceTABWithXSpace(s is string, n is int) <extension>: string
RETURN s.Replace(TAB, RepeatString(" ", n))
```


Call to the extension procedure: 

```wl
sText is string = TAB + "XXX"
Trace(sText) // <TAB>XXX
Trace(sText.ReplaceTABWithXSpace(4)) //     XXX
```



### Extension of the Color type
<a name="extension_the_color_type_ELTPARAGRAPHE000074"></a>

Extension procedure of the Color type, used to get a readable text color on a given background color.

```wl
PROCEDURE ReadableTextColor(SourceColor is Color) <extension>: Color

nLightness is int
rLuminance is real
ColorResult is Color


// Calculate the luminance of the color passed as parameter
rLuminance = 1 - (0.299 * SourceColor..Red + ...
		0.587 * SourceColor..Green + 0.114 * SourceColor..Blue)/255

// According to the luminance, the text color must have a low or high lightness
IF rLuminance < 0.5 THEN
	// Low luminosity
	nLightness = 12
ELSE
	// High luminosity
	nLightness = 88
END

// The returned color keeps the shade and the saturation
ColorResult = HSL(ColorHue(SourceColor), ...
	ColorSaturation(SourceColor), nLightness)

RESULT ColorResult
```


Call to the extension procedure: 

```wl
// Array of colors
arrHTMLColors is array of strings
arrHTMLColors.Add("#F48FB1")
arrHTMLColors.Add("#C2185B")
arrHTMLColors.Add("#64B5F6")
arrHTMLColors.Add("#1565C0")
arrHTMLColors.Add("#81C784")
arrHTMLColors.Add("#2E7D32")

MyColor is Color

// Gets a random color
MyColor = HTMLToRGB(arrHTMLColors[Random(1, arrHTMLColors..Count)] )

// Sets the initial color as background color
STC_BackgroundColorTest.BackgroundColor = MyColor

// Sets an easily readable color as text color
STC_BackgroundColorTest.Color = MyColor.ReadableTextColor()
```



- ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=ExtensionProcedures.gif) ***Training (WINDEV)*** : **ExtensionProcedures** <br>This example illustrates the use of the  attribute, to add functionalities to WLanguage types.

