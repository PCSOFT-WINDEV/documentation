


## Internal procedure
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Principle
<a name="principle_ELTTEXTE000273"></a>
Several features require a procedure called once or several times via a WLanguage function ("Callback"). For example, this is the case for the following functions: [fListFile](../WDLang1/3036058.md), [TreeListItem](../WDLang1/3018016.md), [AlbumPicker](../WDLang3/1000020186.md) in iOS, etc.

A procedure must be created for these functions but several drawbacks appear:

- Loss of code locality: The final algorithm cannot be entirely displayed. Several backs and forths must be performed between the calling code and the WLanguage procedure.

- Difficulty to share information wiht the WLanguage procedure: In most cases, global variables must be used (to pass parameters to the callback or to retrieve the results).




In this case, the internal procedures are used to simplify the writing of callback features and to fix the problems caused when using a standard procedure.



<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Declaring and calling internal procedures
<a name="declaring_and_calling_internal_procedures_ELTTEXTE000297"></a>
An internal procedure is declared in the code of an existing process (process associated with a control, window procedure, global procedure or class method, ...).

The syntax is as follows: 


```txt
INTERNAL PROCEDURE <Procedure name>()
<Procedure code>
END
```
Remarks: 

- The internal procedures must be declared in the process where they are used.

- The assisted input is enabled for the parameters: a tooltip specifying the expected parameter type is displayed when typing the call to the internal procedure. 

- The code of process found before and after the declaration code of internal procedure is run in sequence: the code of internal procedure is not run.
	Example: 
	
	```wl
	// Code before the internal procedure: This code is run. 
	INTERNAL PROCEDURE MyInternalProcedure()
		// Code of the internal procedure. 
		// This code is not run. 
	END
	// Code after the internal procedure: This code is run.
	```


- An internal procedure can be directly called by its name, like any standard procedure. This call can be placed before or after the declaration of internal procedure. 
	Example: 
	
	```wl
	// Call placed before
	MyInternalProcedure()
	
	// Declare the internal procedure
	INTERNAL PROCEDURE MyInternalProcedure()
		// Code of the internal procedure run during the call to MyInternalProcedure()
	END
	
	// Call placed after
	MyInternalProcedure()
	```

	This allows you to get a code that is easy to read. 

- The exceptions triggered by the internal procedures can be processed outside the internal procedure.



![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) 

### Example
<a name="example_ELTPARAGRAPHE000082"></a>The following code is used to list all child elements of a node found in a TreeView control: 


```wl
// List the "children" of the "Desserts" node in the "TREE_TVRecipe" TreeView
// The "ExpandAll" procedure is called
// for each "child" element found in the "Desserts" node
Res = TreeListItem(TREE_TVRecipe, "Recipes" + TAB + "Desserts", ExpandAll)

INTERNAL PROCEDURE ExpandAll(TREE_TVRecipe, ChildPath, ChildFound, Level, Pointer)
	// Is the element found a leaf?
	IF TreeTypeItem(TREE_TVRecipe, ChildPath + ChildFound) = tvLeaf THEN
		Return // Go back to the TreeListItem function
	ELSE
		// Is the element found collapsed?
		IF TreeStatus(TREE_TVRecipe, ChildPath + ChildFound) = tvCollapse THEN
			TreeExpand(TREE_TVRecipe, ChildPath + ChildFound)
		END
	END
	// Collapse the TreeView Table control
	TreeCollapse(TREE_TVRecipe, "Recipes")
END
```


<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Characteristics of internal procedures
<a name="characteristics_internal_procedures_ELTTEXTE000327"></a>


### Use in callback
<a name="use_callback_ELTPARAGRAPHE000093"></a>

The internal procedure can be used in a WLanguage functions that expects a WLanguage procedure as parameter (like [fListFile](../WDLang1/3036058.md), [TreeListItem](../WDLang1/3018016.md), ...).

Caution: In this case, the parameter corresponding to the name of internal procedure must directly correspond to the name of internal procedure (the procedure name must not be enclosed in quotes). 

**Not to do**: 


```wl
// List the "children" of the "Desserts" node in the "TREE_TVRecipe" TreeView
Res = TreeListItem(TREE_TVRecipe, "Recipes" + TAB + "Desserts", "ExpandAll")

INTERNAL PROCEDURE ExpandAll(TREE_TVRecipe, ChildPath, ChildFound, Level, Pointer)
	...
END
```


**Do**: 


```wl
// List the "children" of the "Desserts" node in the "TREE_TVRecipe" TreeView
Res = TreeListItem(TREE_TVRecipe, "Recipes" + TAB + "Desserts", ExpandAll)

INTERNAL PROCEDURE ExpandAll(TREE_TVRecipe, ChildPath, ChildFound, Level, Pointer)
	...
END
```



### Internal Procedure parameter
<a name="internal_procedure_parameter_ELTPARAGRAPHE000114"></a>

The internal procedure can be used as parameter in a procedure. In this case, the parameter type will be "Procedure".

Example: 


```wl
// Declare the internal procedure
INTERNAL PROCEDURE MyInternalProcedure()
	// code run during the call to the procedure passed as parameter
END

MyOtherProcedure(MyInternalProcedure)
```

```wl
PROCEDURE MyOtherProcedure(p is procedure)

p()
```
![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) This feature is not available.


### Access to the variables from an internal procedure
<a name="access_the_variables_from_internal_procedure_ELTPARAGRAPHE000129"></a>

The variables declared in the same process as the declaration of internal procedure can be called in the internal procedure. There is no need to use global variables anymore. This concept is called "Closure". 

Example: 


```wl
sElementList is string
sSeparator is string = CR
TreeListItem(TREE_TreeeViewControl, "", MyInternalProcedure)

INTERNAL PROCEDURE MyInternalProcedure(TreeViewName, Branch)
	sElementList += [sSeparator] + Branch
END
```



### Nesting internal procedures
<a name="nesting_internal_procedures_ELTPARAGRAPHE000138"></a>

The internal procedures can be nested. Each internal procedure can access the variables of procedures that include it.

Example: 


```wl
ExternalVariable is int
Trace(ExternalVariable)
MyInternalProcedure1()
INTERNAL PROCEDURE MyInternalProcedure1()
	InternalVariable1 is int
	Trace(ExternalVariable + InternalVariable1)
	MyInternalProcedure2()
	INTERNAL PROCEDURE MyInternalProcedure2()
		InternalVariable2 is int
		Trace(ExternalVariable + InternalVariable1 + InternalVariable2)
	END
END
```



### Sibling internal procedures
<a name="sibling_internal_procedures_ELTPARAGRAPHE000149"></a>

Two internal procedures found in the same procedure can be called between themselves (sibling internal procedures).

Example: 


```wl
INTERNAL PROCEDURE MyMainProcedure()

	INTERNAL PROCEDURE MyInternalProcedure1()
		...
	END

	INTERNAL PROCEDURE MyInternalProcedure2()
		...
		// Call the sibling procedure
		MyInternalProcedure1()
	END
END
```



### Recursive calls
<a name="recursive_calls_ELTPARAGRAPHE000159"></a>

An internal procedure can be called itself in recursive way. 


### Named parameters
<a name="named_parameters_ELTPARAGRAPHE000165"></a>

If an internal procedure includes parameters with default values, you have the ability to call the internal procedure by naming its parameters. Two syntaxes are possible: 

- Single-line named parameters, 

- Multiline named parameters. 




For more details, see [Named parameters in a procedure](../Motscles/1513007.md). 


### Dynamic compilation
<a name="dynamic_compilation_ELTPARAGRAPHE000179"></a>

You can use an internal procedure in a dynamically compiled code (with [Compile](../WDLang1/3013015.md), for example). 

Example: 


```wl
lsCode is string = [
// Code compiled dynamically
sElementList is string
sSeparator is string = CR
TreeListItem(TREE_TreeeViewControl, "", MyInternalProcedure)

INTERNAL PROCEDURE MyInternalProcedure(TreeViewName, Branch)
	sElementList += [sSeparator] + Branch
END

]

lsResult is string

lsResult = Compile("MyProc",lsCode)
```



### Internal procedure and extension attributes
<a name="internal_procedure_and_extension_attributes_ELTPARAGRAPHE000191"></a>

[extension attributes](../Motscles/1511025.md) can be used with internal procedures. For example, a single process can run an internal procedure in a secondary thread, then call a second procedure in the main thread to refresh the interface.

Example: 


```wl
// Code of a button for example ...
AsynchronousExecution()

INTERNAL PROCEDURE AsynchronousExecution() <thread>
	
	// Here, process run in the background
	// in a secondary thread (no access to the interface)
	ThreadPause(5s)
		
	// Process completed, call process of main thread again 
	// therefore allowed to refresh the interface
	AfterAsynchronousExecution()
END

INTERNAL PROCEDURE AfterAsynchronousExecution() <main thread>
	Trace("Completed ...")
END
```


<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Special case: Using internal procedures with asynchronous functions
<a name="special_case_using_internal_procedures_with_asynchronous_functions_ELTTEXTE000399"></a>
The internal procedures can be used as "Callback" for asynchronous functions. 

CAUTION: In this case, the code after the call to the function that uses an asynchronous WLanguage procedure will be execute BEFORE the code of internal procedure. 

Example: In this WINDEV Mobile example, the code after [AlbumPicker](../WDLang3/1000020186.md) will be executed BEFORE the internal procedure. The internal procedure will be called when the picker is validated by the user. 


```wl
AlbumPicker(albumImage, PhotoSelection)
INTERNAL PROCEDURE PhotoSelection(sImagePath)
	IMG_ImageControl = sImagePath
END
// Code run after the AlbumPicker function but 
// BEFORE the code of internal procedure.
```


<a name="NOTE5"></a>
<a name="NOTE5_1"></a>


## Limitations
<a name="limitations_ELTTEXTE000423"></a>


- An internal procedure cannot be named like the procedures that contain it.

- Two internal procedures of same level cannot have the same name, even if they are declared in different code blocks (IF THEN ELSE, ...).

- The internal procedures are not available in dynamic compilation. 

- The execution of an internal procedure cannot be automated. 

- The automatic management of errors is not available for the internal procedures.





