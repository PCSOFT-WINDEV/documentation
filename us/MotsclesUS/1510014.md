
## "END:" label 

***In french: "FIN :"***
				



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
The **END:** label is used to define a code that will be systematically run at the end of process execution. This code will be run even if the exit from process is performed by [RETURN](../Motscles/1510009.md) or [RETURN](../Motscles/1510007.md) (except for an exception process triggered by [ExceptionThrow](../WDLang1/3034005.md)).


<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
Procedure CompareContent(File1, File2)

// Opens the files
nFile1 is int = fOpen(File1, foRead)
nFile2 is int = fOpen(File2, foRead)

// Returns -3 if an opening error occurred
IF nFile1 = -1 or nFile2 = -1 THEN RESULT -3

// Reads the first line of each file
sLine1 is string = fReadLine(nFile1)
sLine2 is string = fReadLine(nFile2)
nLine is int = 1

LOOP
	// If the two lines are empty, the end of the two files has been reached
	// Returns 0 
	IF sLine1 = EOT _AND_ sLine2 = EOT THEN RESULT 0

	// If end of file 1, the file 1 is shorter
	// returns -1 
	IF sLine1 = EOT THEN RESULT -1

	// If end of file 2, the file 2 is shorter
	// returns -2 
	IF sLine2 = EOT THEN RESULT -2

	// If the lines are different, returns the number of the different line 
	IF sLine1 <> sLine2 THEN RESULT nLine

	// Goes to the next line
	sLine1 = fReadLine(nFile1) 
	sLine2 = fReadLine(nFile2)
	nLine++
END

END:
// Closes the files
fClose(nFile1)
fClose(nFile2)
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Syntax

`// Main code
...
IF ...THEN
   ...
   RESULT 0
END
...
IF ...THEN
   ...
   RESULT 1
END
 
RESULT 2
 
// Statements run in all cases
// at the end of process execution
END:
...`
---



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks




### Operating mode
<a name="operating_mode_ELTPARAGRAPHE000074"></a>

The value to return is stored and the code following the "END:" label is run. The value is returned at the end of execution of the code following the "END:" statement

**Remark**: The **END:** label is also run after the automatic management of errors if this one is enabled (by "CASE ERROR:" and "CASE EXCEPTION:"). 
<a name="NOTE0_2"></a>




### Special case
<a name="special_case_ELTPARAGRAPHE000084"></a>

The code following the "END:" label is not run after a call to [SetFocusAndReturnToUserInput](../WDLang1/1410088107.md) or to [Close](../WDLang1/3038018.md). [SetFocusAndReturnToUserInput](../WDLang1/1410088107.md) can be replaced with the call to [ScreenFirst](../WDLang1/3025039.md) followed by RETURN or RESULT.


