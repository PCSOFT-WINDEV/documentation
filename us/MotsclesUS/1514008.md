
## Structure (Type of variable)

***In french: Structure (Type de variable)***
				



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
A structure is a custom type of data. A structure groups different types of elements.

**Remark**: If you are handling custom variables, we advise you to declare:

- a ***structure*** type if several variables of this type are used in different processes of the project.

- a ***composite variable*** if a single variable of this type is used in your project (to pass parameters to an API for example).





<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
// Declare a structure
ProductRef is structure
	SCode is int
	PdtCode is fixed string on 10
END
```
<a name="Example2"></a>

```wl
// Declare a structure variable
ProductRef is structure
	SCode is int
	PdtCode is fixed string on 10
END
Armchair is ProductRef
```
<a name="Example3"></a>

```wl
// Handle a member of a structure variable
ProductRef is structure
	SCode is int
	PdtCode is fixed string on 10
END
Armchair is ProductRef
Armchair:SCode = 7
Armchair:PdtCode = "Furniture"
```
<a name="Example4"></a>

```wl
// Declare and initialize the members of a structure
3Dpoint is structure
	x is int
	y is int
	z is int
END

p1 is 3Dpoint = [0, 0, 50]
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Syntax

### Declaring a structure

`<Structure name> is structure
    <Structure members>
END
`
---

**`<Structure name>: ()`**

Name of structure to declare

**`<Structure members>: ()`**

Declarations of variables found in the structure. These variables are called ***members*** of the composite variable.

When declaring the members of the structure, you have the ability to specify the serialization parameters of these members. For more details, see ["Serializing the members of a structure"](#NOTE0_4). 

**`<END>: ()`**

End of variable declaration.  


<a name="SYNTAX2"></a>

### Declaring a structure variable

`<Variable name> is <Structure name>
`
---

**`<Variable name>: ()`**

Name of the structure variable to declare.

**`<Structure name>: ()`**

Name of a structure that was declared beforehand.  


<a name="SYNTAX3"></a>

### Handling a member of a structure variable

`<Name of structure variable>:<Name of structure member>
`
---

**`<Name of structure variable>: ()`**

Name of the structure variable to use.

**`<Name of structure member>: ()`**

Name of the structure member to use.

The members of a structure variable can be assigned via [the multiple assignment](../Motscles/1514031.md).



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks




### Declaring structures
<a name="declaring_structures_ELTPARAGRAPHE000109"></a>

If a &lt;Structure name&gt; structure is declared:

- ***in the project code***, a &lt;Structure name&gt; variable can be declared in the entire project.

- ***in the declaration code of the global variables of a window***, a &lt;Structure name&gt; variable can be declared in the window, in its controls and in its associated local procedures.

- ***in the opening code of a report***, a &lt;Structure name&gt; variable can be declared in the report, in its controls and in the associated local procedures.

- ***in the declaration code of a class***, a &lt;Structure name&gt; variable can be declared in the class and in its methods. Caution: "Is structure" must necessarily be positioned before "is class".

- ***in a process/i>**, a &lt;Structure name&gt; variable can be declared in the process.




![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) The structure must be declared in the declaration process of global variables (of a window, page or set of procedures).


<a name="NOTE0_2"></a>




### Type of structure members
<a name="type_structure_members_ELTPARAGRAPHE000136"></a>

The members of a structure can have the following types:

- [Integer](../Motscles/1514049.md) (or selected among the [advanced integers](../Motscles/1514049.md)).

- [Real](../Motscles/1514048.md) (or selected among the [advanced reals](../Motscles/1514048.md)).

- [Currency](../Motscles/1514043.md).

- [String](../Motscles/1514044.md) (or selected among the [advanced strings](../Motscles/1514044.md)).

- Array (["simple" array](../Motscles/1514030.md), [dynamic array](../Motscles/1514057.md) or [fixed array](../Motscles/1514025.md)).

- [Automation](../Motscles/1514014.md) or [Dynamic Automation](../Motscles/1514015.md).

- [Class](../POO/6010006.md). This class must have a constructor without parameter (or with optional parameters).

- [Variant](../Motscles/1514019.md).




Any other type of data (composite variable, constant, ...) is forbidden.


<a name="NOTE0_3"></a>




### Declaring an array member in a structure
<a name="declaring_array_member_structure_ELTPARAGRAPHE000196"></a>

- ******Declaring a member whose type is simple array***:
	The array dimensions are fixed when the array is declared. The array dimensions can be modified by [Dimension](../WDLang1/3013022.md).

- ******Declaring a dynamic array member***: 
	The array dimensions must be defined (which means that the array must be allocated) before using the array. For example:
	
	```wl
	// Declare a structure
	Struct is structure
		x1 is int
		x2 is dynamic array        // Declare the array
	END
	 
	// Declare a structure variable
	MyStruct is struct
	 
	// Allocate the array
	MyStruct.x2 = new dynamic array of 4,7 int
	// Use the array
	MyStruct:x2[1,1] = 25
	```








<a name="NOTE0_3b"></a>




### Homonymic copy of a structure
<a name="homonymic_copy_structure_ELTPARAGRAPHE000215"></a>

The **=>** operator is used to perform an homonymic copy of a structure variable. 

This homonymic copy can also be performed by using the **=** operator. 

Caution, the copy is performed **member to member**: only the members with same names will be copied. The members with different or non-existing names will be ignored.

Example:

```wl
// Declare a structure
StructImportCustomer is structure
	LastName is string
	FirstName is string
	Company is string
	Address is string
	ZC is string
	City is string
END

StructInternalCustomer is structure
	LastName is string
	FirstName is string
	Company is string
	Address is string
	FullAddress is string
	ZC is string
	City is string
	Country is string
END

 
NewCustomer is StructImportCustomer 

CurrentCustomer is StructInternalCustomer 

CurrentCustomer = NewCustomer // the FullAddress and Country members are not filled
```





<a name="NOTE0_4"></a>


### Serializing the members of a structure
<a name="serializing_the_members_structure_ELTPARAGRAPHE000235"></a>

By default, all the members of a structure are serialized. 

You have the ability to precisely manage the serialization of each member of a structure: 

- by specifying the member that will be serialized during the call to [Serialize](../WDLang1/3013065.md). 
	This operation can be performed on all types of serialization (WXML, JSON, binary). 

- by changing the name of the member during the serialization with [Serialize](../WDLang1/3013065.md).  
	This operation can be performed during a binary serialization only. 




This management of serialization is performed by using the following syntax: 

- Serialization (or not) of a member: 
	
	```txt
	<Member name> is <Type of member> [, Serialize = <True/False>]
	```


- Serialization and change of member name: 
	
	```txt
	<Member name> is <Type of member> [, Serialize = <New name>]
	```





Example: 


```wl
Cl is structure
	// Serialized member
	SerializedMember is int 
	// Non-serialized member
	NonSerializedMember is string, Serialize = False  
	// Member renamed during the serialization
	RenamedMember is int, Serialize = "NewMemberName" 
END
```

<a name="NOTE0_5"></a>




### Which "advanced" types can be a member?
<a name="which_advanced_types_can_member_ELTPARAGRAPHE000271"></a>

An "advanced" variable can be a member of another "advanced" variable. The table below presents the different combinations:

| ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=Fleche_1.gif)<br> | Composite variable | Structure | Class | Array |
| --- | --- | --- | --- | --- |
| **Composite variable** | No | No | No | No |
| **Structure** | Yes | Yes | Yes | Yes |
| **Class** | Yes | Yes | Yes | Yes |
| **Array** | Yes | Yes | Yes | No |


<a name="NOTE0_6"></a>




### Passing a structure as a parameter to a procedure
<a name="passing_structure_parameter_procedure_ELTPARAGRAPHE000330"></a>

A "structure" variable can be passed as a parameter to a procedure. To do so, use the following syntax: 

```txt
<Procedure name>(<Name of structure variable>)
```


For example:


```wl
ProductRef is structure
	SCode is int
	PdtCode is fixed string on 10
END
Armchair is ProductRef
 
// Call the DisplayProduct procedure
DisplayProduct(Armchair)
```

<a name="NOTE0_6_1"></a>


### Adding the content of a structure to an array of structures without using a variable of the structure
<a name="adding_the_content_structure_array_structures_without_using_variable_the_structure_ELTPARAGRAPHE000345"></a>

If you are using an array of structures with few members, it may be easier to directly add a structure by using the [ ] operator (square brackets). 
For example, for the following structure:

```wl
// Structure to store a letter and its ASCII code
STKey is Structure
	sKey is string
	nAsciiCode is int
END
// Array of structures
arrKeys is array of STKey
```

In most cases, content is added by using a variable of a type declared in the structure:

```wl
// Temporary structure for the addition
stAKey is STKey
// Stores the letter A
stAKey.sKey = "A"
stAKey.nAsciiCode = Asc("A")
arrKeys.Add(stAKey)
```

With the [ ] operator, you get better legibility:

```wl
// Stores the letter A
arrKeys.Add(["A", Asc("A")])
```





<a name="NOTE0_7"></a>


### Limits of a structure
<a name="limits_structure_ELTPARAGRAPHE000380"></a>

- The inheritance of structure is not allowed: a structure cannot inherit from another structure.

- The access to a member of a structure cannot be restricted: all the members of a structure are public.



<a name="NOTE0_8"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) 

### Differences between version 8 and version 9
<a name="differences_between_version_8_and_version_9_ELTPARAGRAPHE000390"></a>

A structure containing a character member has not the same behavior as in the earlier versions. Indeed, the characters were coded on 1 byte in version 8 and on 2 bytes in version 9.

We recommend that you use the "1-byte integer" type.


