
## Enumeration (Type of variable)

***In french: Enumeration***
				

<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
An enumeration is a set of values. An **Enumeration** variable or parameter can only be assigned with a value of the enumeration.


<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
// Declaration code
// Declare an enumeration
RobotType is Enumeration
	Walk
	Stopped
	InMaintenance
	OutOfOrder
END
```


<a name="Example2"></a>



```wl
// Declare an Enumeration variable
Post1 is RobotType
// Assign the variable
Post1 = Walk
// Post1 = 1 // Displays a compilation error
// Post1 = "Walk" // Displays a compilation error
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Syntax

### Declaring an enumeration

`<Enumeration name> is Enumeration
    <Enumeration values>
END
`
---

**`<Enumeration name>: ()`**

Name of enumeration to declare

**`<Enumeration values>: ()`**

Different values that can be taken by the enumeration. 

**`<END>: ()`**

End of declaration.  


<a name="SYNTAX2"></a>

### Declaring an Enumeration variable

`<Variable name> is <Enumeration name>
`
---

**`<Variable name>: ()`**

Name of Enumeration variable to declare.

**`<Enumeration name>: ()`**

Name of enumeration that was declared beforehand. The default enumeration value corresponds to the first enumeration value.  



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks




### Declaring enumerations
<a name="declaring_enumerations_ELTPARAGRAPHE000078"></a>

An enumeration must be declared in:

- The project initialization code in order to be used in all the processes of the project (code of project, code of windows, code of controls, procedures, etc.).

- The declaration code of global variables of a window in order to be used in all the processes of the window (code of window, code of controls found in the window, local procedures, etc.).

- The opening code of a report in order to be used in all the processes of the report (code of the report, code of controls found in the report, local procedures, etc.).

- The declaration code of a class in order to be used in the class.

	- To access this enumeration from a class method, use the following syntax: 
			
		```txt
		"::<Enumeration name>"
		```


	- To access this enumeration from a code external to the class, use the following syntax: 
			
		```txt
		"<Class name>::<Enumeration name>"
		```





- The declaration code of sets of procedures in order to be used in all the procedures of the set. 




**Remark**: An enumeration is always global:

- to a project,

- to a window,

- to a report,

- to a class.



<a name="NOTE0_2"></a>


### Passing an enumeration as a parameter to a procedure
<a name="passing_enumeration_parameter_procedure_ELTPARAGRAPHE000105"></a>An Enumeration variable can be passed as a parameter to a procedure. To do so, use the following syntax: 


```txt
<Procedure name>(<Name of enumeration variable>)
```


For example:


```wl
// Procedure with an Enumeration parameter
PROCEDURE OperatingMode(p is RobotType)

// Calls with an Enumeration parameter
OperatingMode(Walk)
OperatingMode(1)		// Displays a compilation error
OperatingMode("A")		// Displays a compilation error
```

<a name="NOTE0_3"></a>


### Special cases
<a name="special_cases_ELTPARAGRAPHE000118"></a>

- **Assisted input**: The assisted input for an enumeration parameter proposes the different enumeration values.

- **Order of values**: The &lt;, &lt;=, &gt;, &gt;= operators are available for the enumerations as well as the automatic sort operations ([ArraySort](../WDLang1/3075007.md), [ArraySeek](../WDLang1/3075002.md), etc.). The order of values in the enumeration is the order in which the values are declared. 
	Example: 
	
	```wl
	// Declaration code
	// Declare an enumeration
	RobotType is Enumeration
		Walk
		Stopped
		InMaintenance
		OutOfOrder
	END
	
	// Declare an Enumeration variable
	Post1 is RobotType
	// Assign the variable
	Post1 = Walk
	
	...
	
	IF Post1>Stopped THEN
		Info("The robot encounters a problem")
	END
	```




<a name="NOTE0_4"></a>


### Extension of enumeration
<a name="extension_enumeration_ELTPARAGRAPHE000136"></a>

An enumeration can extend the values of one or more other enumerations by taking the values of this ones and by adding new values.

The following syntax is used: 


```txt
<Enumeration name> is Enumeration
    [Base Enumeration]
    <Additional values of enumeration>
END
```


where: 

- Enumeration name: Name of enumeration that was declared beforehand. 

- Base Enumeration: Name of base enumeration to use.

- Additional Values of Enumeration: Additional values that will be taken into account in the enumeration. 




Example: 


```wl
// Declaration code
// Declare an enumeration
RobotType is Enumeration
	Walk
	Stopped
	InMaintenance
	OutOfOrder
END

AdvancedRobotType is Enumeration
	[RobotType]
	InLoop
END
```


Assigning an extended value in a base enumeration is not allowed.




<a name="NOTE0_5"></a>


### Creating aliases in an enumeration 
<a name="creating_aliases_enumeration_ELTPARAGRAPHE000160"></a>

You have the ability to declare aliases to insure a temporary compatibility for example.

The following syntax is used: 


```txt
<Enumeration name> is Enumeration
	<Value 1 of enumeration>
	<Value 2 of enumeration>
	<Value 3 of enumeration> = <Value 1 of enumeration>
END
```


Example: 


```wl
// Declaration code
// Declare an enumeration
RobotType is Enumeration
	Walk
	Stopped
	InMaintenance
	OutOfOrder
	// Declare the temporary aliases
	Maintenance = InMaintenance
	Breakdown = OutOfOrder
END
```

<a name="NOTE0_6"></a>


### Associated values
<a name="associated_values_ELTPARAGRAPHE000175"></a>

In WLanguage, a constant value of simple type can be associated with each value of an enumeration. This value is associated with the enumeration value in the enumeration declaration.


```wl
// Declare the enumeration with associated values
RobotType is Enumeration
	Walk = "Running the robot"
	Stopped = "Robot stopped"
	InMaintenance = "Robot under maintenance"
	OutOfOrder = "Robot breakdown"
END
```


Then, you will have the ability to read the associated value of an Enumeration variable via [Value](../Proprietes/2510130.md).


```wl
// Access to the associated value
e is RobotType
RobotMessage is string = e..Value
```


The associated value has no incidence on the other behaviors of enumerations. Especially if the sort order is identical to the declaration order of values in the enumeration.
<a name="NOTE0_7"></a>


### Associated properties
<a name="associated_properties_ELTPARAGRAPHE000193"></a>

The following properties are associated with the Enumeration variables: 


|   |   |
| --- | --- |
| [Name](../Proprietes/2510082.md) | Allows you to find out the name of an Enumeration variable. |
| [Value](../Proprietes/2510130.md) | Allows you to find out the associated value of an Enumeration variable. |


<a name="NOTE0_8"></a>


### Available WLanguage functions
<a name="available_wlanguage_functions_ELTPARAGRAPHE000213"></a>

An enumeration can be stored in an HFSQL item or in another file format. Depending on your needs, you can store the enumeration name or value ([Name](../Proprietes/2510082.md) or [Value](../Proprietes/2510130.md) properties, respectively). 

The following WLanguage functions allow you to find out the characteristics of a stored enumeration: 



|   |   |
| --- | --- |
| [EnumerationCheckName](../WDLang1/1000020767.md) | Checks whether an enumeration value known by its name is valid. |
| [EnumerationCheckValue](../WDLang1/1000020866.md) | Checks whether an enumeration value known by its associated value is valid. |
| [EnumerationFromName](../WDLang1/1000020769.md) | Returns an enumeration value known by its name. |
| [EnumerationFromValue](../WDLang1/1000020768.md) | Returns an enumeration value known by its associated value. |




<a name="NOTE0_9"></a>


### Limits
<a name="limits_ELTPARAGRAPHE000229"></a>

- The enumerations are not available in dynamic compilation. 

- Enumerations cannot be used outside the project. 





