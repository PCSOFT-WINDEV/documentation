
## FOR ALL/FOR EACH statement (browse of strings)

***In french: POUR TOUT / POUR TOUS***
				



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
The **FOR EACH** statement is used to browse strings according to different methods:

- Browsing the substrings separated by a separator. 

- Browsing the occurrences of a string inside another one.




**Remark**: The **FOR ALL, FOR EACH** statements are accepted. The **FOR EACH** statement will be used in this documentation but it can be replaced with **FOR ALL**.

The FOR EACH statement can also be used to browse the .Net objects that implement the IEnumerable interface.


<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
// Retrieves the list of libraries loaded in memory
LibraryList is string = ListDLL()

// For each library
FOR EACH STRING ALibrary OF LibraryList SEPARATED BY CR
	// Adds the library into the TABLE_Library table
	TableAddLine(TABLE_Library, ExtractString(ALibrary, 1, TAB))
END
```


<a name="Example2"></a>



```wl
// The "C:\MyDocuments\Exports.TXT" file contains the list
// of exported products, separated by ";"
// Retrieve each product
ExportedProduct is string = fLoadText("C:\MyDocuments\Exports.TXT")
FormerPosition is int

// For each product
FOR EACH POSITION CurrentPosition OF ";" IN ExportedProduct
	// Adds the product into ProductList
	ListAdd(LIST_Product, ...
		ExportedProduct[[FormerPosition + 1 TO CurrentPosition - 1]]
	// Store the position
	FormerPosition = CurrentPosition 
END
```


<a name="Example3"></a>



```wl
FOR EACH STRING sc1, nPosition, nCounter OF "A.B.C" SEPARATED BY "."
	Trace(sc1 + " - " + nPosition + " - " + nCounter)
END
// Returns
// A - 1 - 1
// B - 3 - 2
// C - 5 - 3
```


<a name="Example4"></a>



```wl
s is string = "I am a sentence"+CR+"on several lines"
FOR EACH STRING s2 OF s SEPARATED BY [" ",CR]
	Trace(s2)
END

// Returns
// I
// am
// a
// sentence
// on
// several
// lines
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Syntax

### Browsing the substrings separated by a separator

`FOR EACH STRING <Substring> [, <Position> [, <Counter>]] OF <Initial string> 
                                                                    [SEPARATED BY <Separator>] [<Direction>]

     ...
END
`
---

**`<FOR EACH STRING>: ()`**

Marks the beginning of the statement block.

**`<Substring>: ()`**

String variable containing the text of the substring. There is no need to declare this variable.

**`<Position>: ()`**

Integer variable containing the position of the substring in the string. There is no need to declare this variable.

**`<Counter>: ()`**

Integer variable containing the number of iterations. There is no need to declare this variable.

**`<Initial string>: ()`**

String containing the full text. The iteration does not take place if this string is empty.

**`<Separator>: ()`**

Optional string containing the separator of substrings (TAB by default).

To specify several separators, use the following syntax: 


```txt
[<Separator1> , ..., <Separator N>]
```


For example: [TAB, CR]

**`<Direction>: ()`**

Optional indicator for the browse direction:


|   |   |
| --- | --- |
| *FromBeginning* <br>(default value) | Browse the string from the first character to the last one. |
| *FromEnd* | Browse the string from the last character to the first one. |




<a name="SYNTAX2"></a>

### Browsing the occurrences of a string inside another string

`FOR EACH POSITION <Position> OF <Search> IN <Initial String> [<Direction>]

     ...
END
`
---

**`<FOR EACH POSITION>: ()`**

Marks the beginning of the statement block.

**`<Position>: ()`**

Integer variable containing the current position. There is no need to declare this variable.

**`<Search>: ()`**

Sought string.

**`<Initial string>: ()`**

String containing the full text.

**`<Direction>: ()`**

Optional indicator for the browse direction:


|   |   |
| --- | --- |
| *FromBeginning*<br>(default value) | Browse the string from the first character to the last one. |
| *FromEnd* | Browse the string from the last character to the first one. |




<a name="SYNTAX3"></a>

### Browsing the occurrences of a string inside another string

`FOR EACH POSITION <Position> OF <Search> IN <Initial String> WITH <Options>

     ...
END
`
---

**`<FOR EACH POSITION>: ()`**

Marks the beginning of the statement block.

**`<Position>: ()`**

Integer variable containing the current position. There is no need to declare this variable.

**`<Search>: ()`**

Sought string.

**`<Initial string>: ()`**

String containing the full text.

**`<Options>: ()`**

Indicator for the selected options (can be combined):


|   |   |
| --- | --- |
| *FromBeginning*<br>(default value) | Browse the string from the first character to the last one. |
| *FromEnd* | Browse the string from the last character to the first one. |
| *WholeWord* | Search for the whole word |
| *IgnoreCase* | Case-insensitive search (upper/lowercase) |





<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks




### Syntax 1: Browsing the substrings separated by a separator
<a name="syntax_1_browsing_the_substrings_separated_separator_ELTPARAGRAPHE000174"></a>

Browses all the substrings in &lt;Initial string&gt; separated by &lt;Separator&gt;. The browse is not performed if &lt;Initial String&gt; is an empty string.

For each iteration:

- &lt;Substring&gt; is filled with the current substring.

- &lt;Position&gt; contains the position of the substring inside the string. 

- &lt;Counter&gt; contains the number of iterations performed. 




The behavior is undefined if the initial string or the separator is modified during the browse.

**Remark**: If &lt;Initial string&gt; ends with the separator, &lt;Substring&gt; returns an empty string at the end. Otherwise, &lt;Substring&gt; corresponds to the last checked-out element.


<a name="NOTE0_2"></a>




### Syntax 2 and 3: Finding the occurrences of a string inside another string
<a name="syntax_2_and_3_finding_the_occurrences_string_inside_another_string_ELTPARAGRAPHE000200"></a>

Finds all the occurrences of the &lt;Search&gt; substring in the &lt;Initial string&gt;.

At each iteration, the position of the current substring is assigned to the &lt;Position&gt; variable.

The behavior is undefined if the initial string or the sought string is modified during the browse.
<a name="NOTE0_3"></a>




### FOR EACH STRING, FOR EACH POSITION and UNICODE
<a name="for_each_string_for_each_position_and_unicode_ELTPARAGRAPHE000212"></a>

&lt;Substring&gt;, &lt;Initial string&gt;, &lt;Separator&gt; and &lt;Search&gt; can correspond to:

- ANSI strings.

- UNICODE strings.




However, the ANSI strings **and** the UNICODE strings cannot be used in the same syntax.

For more details, see [Managing UNICODE](../WDLang1/3024038.md).
<a name="NOTE0_4"></a>


### Tip: How to get the separator? 
<a name="tip_how_get_the_separator_ELTPARAGRAPHE000229"></a>

To retrieve the separator in the loop, use the following syntax that allows you to find out the position of the separator. To find out the separator, all you have to do is retrieve the following character. 

For example: 


```wl
FOR EACH STRING sTempString, nPosition OF sStringIN SEPARATED BY [" ","-"]
	sSeparator = sStringIN[[nPosition + Length(sTempString)]]
	...
END
```


<a name="NOTE0_5"></a>


### Tip: Browsing an XML string
<a name="tip_browsing_xml_string_ELTPARAGRAPHE000240"></a>

To iterate over an XML file, call [XMLExecuteXPath](../WDLang5/3081015.md) before using FOR EACH.

