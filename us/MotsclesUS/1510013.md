
## FOR EACH/FOR ALL statement (loop through files)

***In french: POUR TOUT / POUR TOUS***
				



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
The **FOR EACH** statement is used to run different types of HFSQL loops:

- Full loop (according to a specified key or not)

- Loop with filter (simple filter, filter on a key or filter on the search key). In this case, the filters accept the operators of [HFilter](../WDLang4/3044100.md).




You can loop through data files, views, queries or data sources. The records locked in read/write are not read.

**Remarks**: 

- If the key given to the **FOR EACH** statement is the key that was previously returned by [HFilter](../WDLang4/3044100.md), the filter will be respected.

- The **FOR ALL, FOR EACH** statements are accepted. The **FOR EACH** statement will be used in this documentation but it can be replaced with **FOR ALL**.





<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
// Syntax 1
FOR EACH Customer
	// Add customers into the List Box control
	ListAdd(LIST_CustomerList, Customer.CustomerNum)
END
```


<a name="Example2"></a>



```wl
// Syntax 1 
FOR EACH Customer ON CustomerNum
	// Add customers into the List Box control
	ListAdd(LIST_CustomerList, Customer.CustomerNum)
END
```


<a name="Example3"></a>



```wl
// Syntax 2 
// Loop with filter
FOR EACH Customer WHERE "CustomerCity = 'Montpellier'"
	// Add customers into the List Box control
	ListAdd(LIST_CustomerList, Customer.CustomerNum)
END

City = "Lyon"
FOR EACH Customer WHERE "CustomerCity = '"+City+"' and CustomerAge >= 21"
	// Add customers into the List Box control
	ListAdd(LIST_CustomerList, Customer.CustomerNum)
END
```


<a name="Example4"></a>





```wl
// Syntax 2 
// Loop with filter on a specific key
FOR EACH Customer WHERE "CustomerCity = 'Montpellier'" ON CustomerNum
	// Add customers into the List Box control
	ListAdd(LIST_CustomerList, Customer.CustomerNum)
END

City = "Lyon"
FOR EACH Customer WHERE "CustomerCity = '" + City + "' AND " + ...
		 "CustomerAge >= 21" ON CustomerNum
	// Add customers into the List Box control
	ListAdd(LIST_CustomerList, Customer.CustomerNum)
END
```


<a name="Example5"></a>



```wl
// Syntax 3 
// Comparison according to a value
FOR EACH Customer WHERE CustomerName = "Doe"
	// Add customers into the List Box control
	ListAdd(LIST_CustomerList, Customer.CustomerNum)
END

// Comparison according to a value
FOR EACH Orders WHERE OrderDate = "20031231"
	// Add orders into the List Box control
	ListAdd(LIST_OrderList, Order.OrderNum)
END
```


<a name="Example6"></a>



```wl
// Syntax 3 
// Comparison according to a set of values
FOR EACH Orders WHERE OrderDate = "20030101" TO "20031231"
	// Add orders into the List Box control
	ListAdd(LIST_OrderList, Order.OrderNum)
END

// Comparison according to a set of values
FOR EACH Orders WHERE "20030101" <= OrderDate <= "20031231"
	// Add orders into the List Box control
	ListAdd(LIST_OrderList, Order.OrderNum)
END
```


<a name="Example7"></a>





```wl
// Syntax 4 
// Loop with filter on a specific key
FOR EACH Customer WHERE CityCustomer [= "Mont" FromEnd
	// Add customers into the List Box control
	ListAdd(LIST_CustomerList, Customer.CustomerNum)
END
```


<a name="Example8"></a>





```wl
// Syntax 5 
// Loop with filter on a composite key
FOR EACH ContactFile WHERE CCState = ["Prospect", 69]
	// Add contacts into the List Box control
	ListAdd(LIST_ContactList, ContactFile.ContactNum)
END

// Loop with filter on a composite key
FOR EACH ContactFile WHERE CZipCode [= ["Prospect", 69]
	// Add contacts into the List Box control
	ListAdd(LIST_ContactList, ContactFile.ContactNum)
END
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Syntax

### 1 - Full loop

`1. Full loop using the best search key

Caution: Modifying the file in the analysis (adding a key item for example) can modify the search key used. 
 
FOR EACH <File> [[WITHOUTSAVEPOSITION,] [<Direction>]]
...
END
 
2. Full loop using the key passed as parameter
 
FOR EACH <File> ON <Key item> [[WITHOUTSAVEPOSITION,] [<Direction>]]
...
END
`
---

**`<FOR EACH>: ()`**

Marks the beginning of the statement block.

**`<File>: ()`**

Name of the HFSQL data file, view or query to loop through.

**`<ON>: ()`**

Defines the type of loop.

**`<Key item>: ()`**

Name of the key item used to loop through the HFSQL data file, view or query.

**`<WITHOUTSAVEPOSITION>: ()`**

Optional keyword. 
Disables the position save and restore features in the loop. Indeed, FOR EACH automatically saves the position at the beginning of the loop and restores this position in case of "normal" exit from the loop (the position is not restored if the loop is interrupted by the keywords BREAK, RETURN or RESULT).
These position save and restore operations can have a high impact (mainly in terms of time), especially if the code using the instruction FOR EACH itself is called in a loop.

**`<Direction>: ()`**

Optional indicator for the iteration direction:



|   |   |
| --- | --- |
| *FromBeginning*<br>(default value) | Loops through the data file from the first element to the last. |
| *FromEnd* | Loops through the data file from the last element to the first. |



**`<END>: ()`**

Marks the end of the statement block.  


<a name="SYNTAX2"></a>

### 2 - Loop with filter

`1. Loop with filter (the best search key is automatically defined)

Caution: Modifying the file in the analysis (adding a key item for example) can modify the search key used. 
 
FOR EACH <File> WHERE "<1st Condition> 
                [AND/OR/NOT <2nd Condition> 
                [ET/OU/PAS...<Nth Condition>]]" [[WIHTOUTSAVEPOSITION,] [<Direction>]]
...
END
 
2. Loop with filter according to the specified key
 
FOR EACH <File> WHERE "<1st Condition> 
                [AND/OR/NOT <2nd Condition>
                [ET/OU/PAS...<Nth Condition>]]" [[WIHTOUTSAVEPOSITION,] [<Direction>]]
...
ON <Key Item>
END
`
---

**`<FOR EACH>: ()`**

Marks the beginning of the statement block.

**`<File>: ()`**

Name of the HFSQL data file, view or query to loop through.

**`<WITH>: ()`**

Defines the type of loop.

**`<Nth Condition>: ()`**

Nth condition of the filter for the HFSQL loop. For more details, see the [Remarks](#NOTE0_2).

**`<AND/OR/NOT>: ()`**

Optional logical operators used to combine the different filter conditions.

**`<WITHOUTSAVEPOSITION>: ()`**

Optional keyword. 
Disables the position save and restore features in the loop. Indeed, FOR EACH automatically saves the position at the beginning of the loop and restores this position in case of "normal" exit from the loop (the position is not restored if the loop is interrupted by the keywords BREAK, RETURN or RESULT).
These position save and restore operations can have a high impact (mainly in terms of time), especially if the code using the instruction FOR EACH itself is called in a loop.

**`<Direction>: ()`**

Optional indicator for the iteration direction:



|   |   |
| --- | --- |
| *FromBeginning* <br>(default value) | Loops through the data file from the first element to the last. |
| *FromEnd* | Loops through the data file from the last element to the first. |



**`<Key item>: ()`**

Item corresponding to a key of the data file. The filter will be performed on this key.

**`<END>: ()`**

Marks the end of the statement block.  


<a name="SYNTAX3"></a>

### 3 - Loop with selection filter on the search key

`1. Comparison filter according to a value
 
FOR EACH <Fie> WHERE <Key item> [ = / <= / >= ] <Value> [[WIHTOUTSAVEPOSITION,] [<Direction>]]
...
END
 
2. Comparison filter according to an interval of values
 
FOR EACH <File> WHERE <Key item> = <Minimal value> TO <Maximum value> [[WITHOUTSAVEPOSITION,] [<Direction>]]
...
END

FOR EACH <File> WHERE <Minimum value> <= <Key item> <= <Maximum value> [[WIHTOUTSAVEPOSITION,] [<Direction>]]
...
END
`
---

**`<FOR EACH>: ()`**

Marks the beginning of the statement block.

**`<File>: ()`**

Name of the HFSQL data file, view or query to loop through.

**`<WITH>: ()`**

Defines the type of loop.

**`<Key item>: ()`**

Key item of the data file to loop through.

**`<Value>: ()`**

Comparison value of the key item.

**`<Minimum value>, Disables the position save and restore features in the loop. Indeed, FOR EACH automatically saves the position at the beginning of the loop and restores this position in case of "normal" exit from the loop (the position is not restored if the loop is interrupted by the keywords BREAK, RETURN or RESULT).
These position save and restore operations can have a high impact (mainly in terms of time), especially if the code using the instruction FOR EACH itself is called in a loop.

**`<Direction>: ()`**

Optional indicator for the iteration direction:



|   |   |
| --- | --- |
| *FromBeginning*<br>(default value) | Loops through the data file from the first element to the last. |
| *FromEnd* | Loops through the data file from the last element to the first. |



**`<END>: ()`**

Marks the end of the statement block.  

<a name="SYNTAX4"></a>

### 4 - Loop with "Start with" generic search, ascending or descending

`FOR EACH <File> WHERE <Key item> [= <Beginning of sought value> [[WITHOUTSAVEPOSITION,] [<Direction>]]
...
END
`
---

**`<FOR EACH>: ()`**

Marks the beginning of the statement block.

**`<File>: ()`**

Name of the HFSQL data file, view or query to loop through.

**`<WITH>: ()`**

Defines the type of loop.

**`<Key item>: ()`**

Key item of the data file to loop through.

**`<Beginning of sought value>: ()`**

Comparison value of the key item.

**`<WITHOUTSAVEPOSITION>: ()`**

Optional keyword. 
Disables the position save and restore features in the loop. Indeed, FOR EACH automatically saves the position at the beginning of the loop and restores this position in case of "normal" exit from the loop (the position is not restored if the loop is interrupted by the keywords BREAK, RETURN or RESULT).
These position save and restore operations can have a high impact (mainly in terms of time), especially if the code using the instruction FOR EACH itself is called in a loop.

**`<Direction>: ()`**

Optional indicator for the iteration direction: 



|   |   |
| --- | --- |
| *FromBeginning*<br>(default value) | Loops through the data file from the first element to the last. |
| *FromEnd* | Loops through the data file from the last element to the first. |



**`<END>: ()`**

Marks the end of the statement block.  


<a name="SYNTAX5"></a>

### 5 - Loop with filter on a composite key

`1. Exact-match search
 
FOR EACH <File> WHERE <Composite key> = [<Value of component 1>, ..., <Value of component N>] [[WITHOUTSAVEPOSITION,] [<Direction>]]
...
END
 
2. Generic search ("Starts with")
 
FOR EACH <File> WHERE <Composite key> [= [<Value of component 1>, ..., <Value of component N>] [[WITHOUTSAVEPOSITION,] [<Direction>]]

...
END
`
---

**`<FOR EACH>: ()`**

Marks the beginning of the statement block.

**`<File>: ()`**

Name of the HFSQL data file or view to loop through.

**`<WITH>: ()`**

Defines the type of loop.

**`<Composite key>: ()`**

Composite key of the data file to loop through.

**`<Value of component N>: ()`**

Value of the component.

**`<WITHOUTSAVEPOSITION>: ()`**

Optional keyword. 
Disables the position save and restore features in the loop. Indeed, FOR EACH automatically saves the position at the beginning of the loop and restores this position in case of "normal" exit from the loop (the position is not restored if the loop is interrupted by the keywords BREAK, RETURN or RESULT).
These position save and restore operations can have a high impact (mainly in terms of time), especially if the code using the instruction FOR EACH itself is called in a loop.

**`<Direction>: ()`**

Optional indicator for the iteration direction:



|   |   |
| --- | --- |
| *FromBeginning*<br>(default value) | Loops through the data file from the first element to the last. |
| *FromEnd* | Loops through the data file from the last element to the first. |



**`<END>: ()`**

Marks the end of the statement block.  



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks




### Equivalence
<a name="equivalence_ELTPARAGRAPHE000297"></a>

- The syntax FOR EACH &lt;File&gt; WHERE &lt;Item&gt; = &lt;Value&gt; is identical to looping through the data file with [HReadSeek](../WDLang4/3044050.md). This loop uses the filters previously defined on the data file. 
	remark: Crossed-out records are taken into account in the loop. 

- The syntax FOR EACH &lt;File&gt; WHERE &lt;Condition&gt; is used to define a filter and to loop through the data file. This loop ignores the filters that were previously defined.

- When using queries, the "FOR EACH" syntax may be slower than the "WHILE NOT HOut" syntax. When using FOR EACH, the context is automatically saved/restored (equivalent to [HSavePosition](../WDLang4/3044136.md)/[HRestorePosition](../WDLang4/3044137.md)). 



<a name="NOTE0_2"></a>




### Filter (syntax 2)
<a name="filter_syntax_2_ELTPARAGRAPHE000318"></a>

The general syntax of a filter has the following format:


```txt
"<Item name> <Operators> <Item value>"
```


For example:


```wl
"CustomerName > 'Doe' and ZipCode = 75 or CustomerAge >= 32"
```


The supported operators depend on the type of items used in the condition:


|   |   |   |
| --- | --- | --- |
| &lt;&gt; | Different | Valid for all types |
| &gt; | Greater than | Valid for all types |
| &gt;= | Greater than or equal to | Valid for all types |
| &lt; | Less than | Valid for all types |
| &lt;= | Less than or equal to | Valid for all types |
| = | Strictly equal to | Valid for all types |
| ~= | Almost equal to | Valid for the "string" types only |
| ] | Contains | Valid for the "string" types only |
| ]= | Starts with | Valid for the "string" types only |

Remarks:

- Constant strings must be enclosed in single quotes. For example: "CustomerName = '"+Customer+"'"

- &lt;Item name&gt; must only contain letters, digits and underscore characters ("_"). If &lt;Item name&gt; contains other characters (quote, ...), the name of the item must be enclosed in double quotes. For example: "e_mail@"]'com'

- Comparisons between strings are performed according to the ASCII value of the characters and not according to the lexicographic value ('a' > 'Z').

- Binary memos and composite keys cannot be part of an &lt;Item value&gt;.

- If &lt;Item value&gt; contains a single or double quote, the single (or double) quote must be preceded by a backslash.



<a name="NOTE0_3"></a>




### Exiting from FOR EACH loop
<a name="exiting_from_for_each_loop_ELTPARAGRAPHE000403"></a>

Several statements are available:

- [RETURN](../Motscles/1510009.md): Exit from the **FOR EACH** loop and exit from the current process (or procedure).

- [RETURN](../Motscles/1510007.md): Return a status report to the calling process. Exit from the **FOR EACH** loop and exit from the current process (or procedure).

- [BREAK](../Motscles/1510012.md): Exits from the **FOR EACH** loop and runs the rest of the current process.




[Close](../WDLang1/3038018.md) is used to exit from the **FOR EACH** loop and to close the current window.

Caution: [RETURN](../Motscles/1510009.md) and [RETURN](../Motscles/1510007.md) cannot be used in the same process.
<a name="NOTE0_4"></a>


### Influence of the mode for exiting from the loop on the automatic iteration of data files
<a name="influence_the_mode_for_exiting_from_the_loop_the_automatic_iteration_data_files_ELTPARAGRAPHE000432"></a>

- **If the loop ends automatically**: 

	- The position in the data file before the start of the loop is restored.

	- Any filter required for the loop is disabled.




- **If the loop is interrupted (BREAK, RETURN, RESULT, Close, etc.)**: 

	- The position in the data file before the start of the loop is not restored.

	- Any filter required for the loop remains enabled. It must be disabled manually ([HDeactivateFilter](../WDLang4/3044085.md)).







If the keyword WIHTOUTSAVEPOSITION has been used, the position save and restore operations performed in the loop are disabled. Indeed, FOR EACH automatically saves the position at the beginning of the loop and restores this position in case of "normal" exit from the loop (the position is not restored if the loop is interrupted by the keywords BREAK, RETURN or RESULT).
These position save and restore operations can have a high impact (mainly in terms of time), especially if the code using the instruction FOR EACH itself is called in a loop.
<a name="NOTE0_5"></a>




### Running the next iteration
<a name="running_the_next_iteration_ELTPARAGRAPHE000456"></a>

To directly run the next iteration **without ending the code of the current iteration**, use the [Continue](../Motscles/1510003.md) statement:


```wl
// Loop through FileName in the order of KeyItem
FOR EACH FileName ON KeyItem
	...
	IF Condition = True THEN CONTINUE	  // Return to the FOR EACH keyword
				// and go to the next record
	...
END
```

<a name="NOTE0_6"></a>


### Modifying the key used for the automatic iteration of data files or queries
<a name="modifying_the_key_used_for_the_automatic_iteration_data_files_queries_ELTPARAGRAPHE000468"></a>

If the value of the item is modified when looping through a data file (or a query), some records may be looped through several times.

Changing the browse item updates the file index key. This modification is taken into account during the automatic reading of the next records. 

This is also valid for an automatic loop of a sorted query (ORDER BY) without search key.


```wl
// Loop through FileName using KeyItem
FOR EACH FileName ON KeyItem
	...
	IF Condition = True THEN 
		// Modify the value of the search key
		FileName.KeyItem = "New value" 
		HModify(FileName)

	END
	// If the new value of the search key 
	// sets the position on the following record in the iteration order
	// the record will be read again during the loop.
	...
END
```



