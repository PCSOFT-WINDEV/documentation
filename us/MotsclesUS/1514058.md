
## Associative array (Type of variable)

***In french: Tableau associatif (Type de variable)***
				



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
An associative array is an "advanced" type of array: it is used to group a set of elements of the same type. Each element of the array is indexed on any type of information (and not simply on a numeric index, as in the other types of arrays).

**Remark**: This type of array allows you to quickly access any element, with some options on the strings.


<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
// Remplit un tableau associatif avec les tailles de fichiers
taTailleFichier est un tableau associatif d'entiers
taTailleFichier["Fichier1.txt"] = fTaille("Fichier1.txt")
taTailleFichier["Fichier2.txt"] = fTaille("Fichier2.txt")

// Récupère la taille d'un fichier
Trace(taTailleFichier["Fichier2.txt"])

// Parcourt les tailles des fichiers
sFichier est une chaîne
nTaille est un entier
POUR TOUT ELEMENT nTaille,sFichier DE taTailleFichier
	Trace(ChaîneConstruit("%1: %2", sFichier, nTaille)) 
FIN
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Syntax

### Declaring an associative array

`<Array name> is associative array [(<Options> [, <Default value> [, <Key type> [, <Initial size>]]])] of <Type>
`
---

**`<Array name>: ()`**

Name of the array variable to declare.

**`<Options>: ()`**

Combination of constants used to configure the operating mode of the array: 



|   |   |
| --- | --- |
| *WithDuplicates* | This constant is used to allow the duplicates. By default, an associative array cannot contain any duplicates. |
| *ccIgnoreAccent* | If the type of key is a string (by default), the indexing ignores the accented characters.<br><br> |
| *ccIgnoreCase* | If the type of key is a string (by default), the indexing is not case sensitive. |
| *ccIgnoreSpace* | If the type of key is a string (by default), the indexing ignores the space characters. |
| *ccIgnorePunctuationAndSpace* | If the type of key is a string (by default), the indexing is not punctuation sensitive and it ignores the space characters.<br><br> |
| *WithoutDuplicates*(default) | By default, an associative array cannot contain any duplicates.<br>You can leave this constant unspecified. |





The "\*" value allows you to keep the default value of the option.

**`<Default value>: ()`**

Value returned when accessing a non-existing element in an associative array without duplicate. This parameter is ignored in an associative array with duplicates. The default value defined for the array's type is used by default (0, "", False).

The "\*" value is used to keep the default value of the type.

**`<Type of key>: ()`**

Type of the key used to browse the array. By default, the browse operation is performed in a string key. The authorized types are as follows:



|   |   |
| --- | --- |
| *wlString* | Character string |
| *wlUnicodeString* | Unicode string |
| *wlDate* | Date |
| *wlDateTime* | DateTime |
| *wlInt* | Signed 4-byte integer |
| *wlInt_8* | Signed 8-byte integer |
| *wlUnsignedInt_4* | Unsigned 4-byte integer |
| *wlUnsignedInt_8* | Unsigned 8-byte integer |
| *wlTime* | Time |
| *wlCurrency* | Currency |
| *wlNumeric* | Numeric |
| *wlReal* | 8-byte real |

The "\*" value is used to keep the default key type (string).



**`<Initial size>: ()`**

Integer. Corresponds to the initial size of the memory area for the associative array.

The addition in an associative array may be quite long if the array is resized. In case of multiple additions, we recommend that you oversize the array in order to optimize the additions.

The "\*" value allows you to keep the default size.



**`<Type>: ()`**

Type of the elements found in the array. 
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png) Remark: The elements that make up the array can also be arrays, fixed arrays, associative arrays, queues, stacks or lists. 


<a name="SYNTAX2"></a>

### Declaring an associative array parameter

`<Parameter name> is associative array of <Type>
`
---

**`<Parameter name>: ()`**

Name of the array variable to declare.

**`<Type>: ()`**

Type of the elements found in the array. 
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png) Remark: The elements that make up the array can also be arrays, fixed arrays, associative arrays, queues, stacks or lists. 



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks




### Accessing the associative arrays
<a name="accessing_the_associative_arrays_ELTPARAGRAPHE000204"></a>

The following properties can be used to handle an associative array:


|   |   |
| --- | --- |
| [Count](../Proprietes/2510097.md) | Returns the number of occurrences in the array. |
| [Empty](../Proprietes/2510126.md) | Used to find out whether the array is empty or not. |

The syntaxes such as ***&Array*** and ***Array1 = Array2*** are not allowed. Associative arrays do not allow for reference operators or copying.
<a name="NOTE0_2"></a>




### Accessing the elements of the associative arrays
<a name="accessing_the_elements_the_associative_arrays_ELTPARAGRAPHE000225"></a>

The access to the elements found in an associative array differs depending on whether the associative array accepts duplicates or not.


|   |   |
| --- | --- |
| **Associative array without duplicate** |   |
| [Exist](../Proprietes/1000022167.md) property | <br><pre><code>MonTableau[MaClé]..Existe</code></pre><br><br>Returns <u><u><u><u>True</u></u></u></u> if the MyKey element exists, <u><u><u><u>False</u></u></u></u> otherwise.<br><br> |
| [Empty](../Proprietes/2510126.md) property | <br><pre><code>MonTableau[MaClé]..Vide</code></pre><br><br>Returns <u><u><u><u>True</u></u></u></u> if the MyKey element does not exist, <u><u><u><u>False</u></u></u></u> otherwise. |
| [Count](../Proprietes/2510097.md) property | <br><pre><code>MonTableau[MaClé]..Occurrence</code></pre><br><br>Returns 1 if the MyKey element exists, 0 if the element does not exist. |
| Assigning an element | <br><pre><code>MonTableau[MaClé] = <Valeur></code></pre><br><br>If the MyKey element does not exist, a MyKey element is created and assigned with &lt;Value&gt;.<br>If the MyKey element exists, its value is modified. |
|   | <br><pre><code>MonTableau[MaClé, Indice] = Valeur</code></pre><br><br>A WLanguage error occurs if the index is different from 1.<br>A WLanguage error occurs if the MyKey element does not exist.<br>If the MyKey element exists, its value is modified. <br><br>Syntax compatible with the associative arrays with duplicates. |
|   | <br><pre><code>MonTableau[MaClé]++</code></pre><br><br>Creates the entry in the associative array, with the default value specified when declaring the array. |
| Access to element | <br><pre><code>MonTableau[MaClé]</code></pre><br><br>If the MyKey element does not exist, returns the default value of the array.<br>If the MyKey element exists, returns the value of the element. |
|   | <br><pre><code>MonTableau[MaClé, Indice]</code></pre><br><br>A WLanguage error occurs if the index is different from 1.<br>A WLanguage error occurs if the MyKey element does not exist.<br>If the MyKey element exists, returns the value of the element.<br><br>Syntax compatible with the associative arrays with duplicates. |
| Address of element | <br><pre><code>&MonTableau[MaClé]</code></pre><br><br>A WLanguage error occurs if the MyKey element does not exist.<br>If the MyKey element exists, returns the address of the element.<br><br> |
|   | <br><pre><code>&MonTableau[MaClé, Indice]</code></pre><br><br>A WLanguage error occurs if the index is different from 1.<br>A WLanguage error occurs if the MyKey element does not exist.<br>If the MyKey element exists, returns the address of the element.  <br><br>Syntax compatible with the associative arrays with duplicates.<br><br> |






|   |   |
| --- | --- |
| **Associative array with duplicates** |   |
| [Exist](../Proprietes/1000022167.md) property | <br><pre><code>MonTableau[MaClé]..Existe</code></pre><br><br>Returns <u><u><u><u>True</u></u></u></u> if at least one MyKey element exists, <u><u><u><u>False</u></u></u></u> if the MyKey element does not exist.<br><br> |
| Checking the existence of a key: [Empty](../Proprietes/2510126.md) property | <br><pre><code>MonTableau[MaClé]..Vide</code></pre><br><br>Returns <u><u><u><u>True</u></u></u></u> if the MyKey element does not exist, <u><u><u><u>False</u></u></u></u> if there is at least one MyKey element. |
| Number of elements found in a key: [Count](../Proprietes/2510097.md) property | <br><pre><code>MonTableau[MaClé]..Occurrence</code></pre><br><br>Returns the number of MyKey elements. |
| Assigning an element | <br><pre><code>MonTableau[MaClé] = <Valeur></code></pre><br><br>Creates a MyKey element and assigns the specified value to this element (even if MyKey elements already exist). |
|   | <br><pre><code>MonTableau[MaClé, Indice] = Valeur</code></pre><br><br>A WLanguage error occurs if the index is greater than the number of existing elements.<br>A WLanguage error occurs if the MyKey element does not exist. <br>If the MyKey element exists, the value of the element identified by its index is modified. |
| Access to element | <br><pre><code>MonTableau[MaClé]</code></pre><br><br>Invalid syntax that triggers a WLanguage error (because several elements can correspond to the same key). |
|   | <br><pre><code>MonTableau[MaClé, Indice]</code></pre><br><br>A WLanguage error occurs if the index is greater than the number of existing elements.<br>A WLanguage error occurs if the MyKey element does not exist.<br>If the MyKey element exists, returns the value of the element identified by its index. |
| Address of element | <br><pre><code>&MonTableau[MaClé]</code></pre><br><br>Syntax not allowed |
|   | <br><pre><code>&MonTableau[MaClé, Indice]</code></pre><br><br>A WLanguage error occurs if the index is greater than the number of existing elements.<br>A WLanguage error occurs if the MyKey element does not exist.<br>If the MyKey element exists, returns the address of the element identified by its index.<br><br> |


You have the ability to use [the multiple assignment](../Motscles/1514031.md) for the associative arrays.
<a name="NOTE0_2_1"></a>


### How to initialize an associative array as soon as it is declared?
<a name="how_initialize_associative_array_soon_declared_ELTPARAGRAPHE000454"></a>To initialize an associative array as soon as it is declared, all you have to do is add the "key/value" couples by using
the [ ] operator (square brackets). The syntax is as follows:


```txt
taMonTableau est un tableau associatif de xxx = ...
	[ [<clé1>, <valeur1>], [<clé2>, <valeur2>], ... ]
```

For example: 
```wl
// Mémorise les taux de TVA par leur dénomination
taTauxTVA est un tableau associatif de réels = ...
	[["Normale", 0.2], ["Intermédiaire", 0.1], ["Réduite", 0.055]]
```





<a name="NOTE0_3"></a>


### Browsing the associative arrays
<a name="browsing_the_associative_arrays_ELTPARAGRAPHE000471"></a>

The elements of an associative array can be browsed by using [the FOR EACH syntax specific to the associative arrays](../Motscles/1510019.md).
<a name="NOTE0_4"></a>


### WLanguage functions and arrays
<a name="wlanguage_functions_and_arrays_ELTPARAGRAPHE000481"></a>

Several WLanguage functions can be used to handle the associative arrays. For more details, see [Array management functions](../WDLang1/3075006.md).

This allows you to delete an element from an associative array by using [Delete](../WDLang1/1000017354.md) (or [ArrayDelete](../WDLang1/3075008.md)) via the following syntax: 

```txt
Supprime(NomTableau, clé)
TableauSupprime(NomTableau, Clé)
```

<a name="NOTE0_5"></a>




### Associative Array parameters
<a name="associative_array_parameters_ELTPARAGRAPHE000502"></a>

- The type of elements found in the associative array passed as parameter must be the same as the declaration type.

- An associative array cannot be passed by value. A variable must necessarily be passed as parameter. 
	Example:
	
	```wl
	// Ajoute un client dans le tableau
	PROCEDURE AjouteClient(taClient est un tableau associatif de CClient,
		sNom est une chaîne,
		sInfo est une chaîne)
	// Construit le nouvel objet client
	c est CClient(sNom, sInfo)
	// Ajoute le nouvel objet dans le tableau
	taClient[sNom] = c
	```


- No check is performed during the compilation: checks are performed at runtime.

- Associative arrays do not allow for reference operators or copying.



<a name="NOTE0_6"></a>




### Associative array of classes or structures
<a name="associative_array_classes_structures_ELTPARAGRAPHE000517"></a>

If you are using an associative array of classes or structures, the different instances are automatically copied during the additions. 

If you are using an associative array of **dynamic** classes or **dynamic** structures, the classes or the structures must e allocated during addition (the freeing operations are automatically performed).

Examples:


```wl
// Cas d'un tableau d'objets NON dynamiques
// Objet
clObj est un cLMaClasse
// Tableau associatif d'objets
taAssociatif est un tableau associatif de cLMaClasse
// Affectation d'un des membres de l'objet
clObj:nMembre = 0
// Ajout dans le tableau associatif. 
// C'est un tableau d'objets non dynamiques, l'objet est donc copié
// L'affectation peut être "longue" s'il y a beaucoup de membres
taAssociatif["Association"] = clObj
// Changement dans l'objet initial
clObj:nMembre = 2
// L'objet a été copié on retrouve donc 0 dans le tableau 
// (valeur au moment de l'affectation)
Info(taAssociatif["Association"]:nMembre)
```



```wl
// Cas d'un tableau d'objets dynamiques
// Objet dynamique
pclObj est un cLMaClasse dynamique
// Tableau associatif d'objets dynamiques
taAssociatif est un tableau associatif de cLMaClasse dynamique
// Allocation d'un objet dynamique
pclObj = allouer un cLMaClasse
// Affectation d'un des membres de l'objet
pclObj:nMembre = 0
// Ajout dans le tableau associatif. 
// C'est un tableau d'objets dynamiques. 
// C'est donc un nouveau "pointeur" sur le même objet (affectation rapide)
taAssociatif["Association"] = pclObj
// Changement dans l'objet initial
pclObj:nMembre = 2
// Le tableau contient le même objet donc 2 dans le tableau aussi
Info(taAssociatif["Association"]:nMembre)
```


Accessing the members of a non-existing element in an associative array of structures or classes does not automatically create the instance of the structure or class.

The line of code:


```wl
MonTableauAssociatif["Clé"]:Membre = 5
```


fails if the "Key" element was not created beforehand.
<a name="NOTE0_7"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png) 

### Associative array of arrays, queue, stack, list
<a name="associative_array_arrays_queue_stack_list_ELTPARAGRAPHE000544"></a>The following syntaxes are supported: 


```txt
<variable> est un tableau associatif de tableaux d'entiers
<variable> est un tableau associatif (avecDoublon) de tableaux d'entiers
<variable> est un tableau associatif (avecDoublon,wlEntier) de tableaux d'entiers
<variable> est un tableau associatif de tableaux de 5 entiers
<variable> est un tableau associatif (avecDoublon) de tableaux de 5 entiers
<variable> est un tableau associatif (avecDoublon,wlEntier) de tableaux de 5 entiers

<variable> est un tableau associatif de tableaux fixes de 5 entiers
<variable> est un tableau associatif (avecDoublon) de tableaux fixes de 5 entiers
<variable> est un tableau associatif (avecDoublon,wlEntier) de tableaux fixes de 5 entiers
<variable> est un tableau associatif de tableaux associatifs d'entiers
<variable> est un tableau associatif (avecDoublon) de tableaux associatifs d'entiers
<variable> est un tableau associatif (avecDoublon,wlEntier) de tableaux associatifs d'entiers

<variable> est un tableau associatif de tableaux associatifs (avecDoublon) d'entiers
<variable> est un tableau associatif (avecDoublon) de tableaux associatifs ...
					(avecDoublon) d'entiers
<variable> est un tableau associatif (avecDoublon,wlEntier) de tableaux associatifs ...
					(avecDoublon) d'entiers
<variable> est un tableau associatif de tableaux associatifs ...
					(avecDoublon,wlEntier) d'entiers
<variable> est un tableau associatif (avecDoublon) de tableaux associatifs ...
					(avecDoublon,wlEntier) d'entiers
<variable> est un tableau associatif (avecDoublon,wlEntier) de tableaux associatifs ...
					(avecDoublon,wlEntier) d'entiers

<variable> est un tableau associatif de files d'entiers
<variable> est un tableau associatif (avecDoublon) de files d'entiers
<variable> est un tableau associatif (avecDoublon,wlEntier) de files d'entiers

<variable> est un tableau associatif de piles d'entiers
<variable> est un tableau associatif (avecDoublon) de piles d'entiers
<variable> est un tableau associatif (avecDoublon,wlEntier) de piles d'entiers

<variable> est un tableau associatif de listes d'entiers
<variable> est un tableau associatif (avecDoublon) de listes d'entiers
<variable> est un tableau associatif (avecDoublon,wlEntier) de listes d'entiers
```


Example: Associative array of arrays of strings: 


```wl
// Déclaration
tabClasses est un tableau associatif de tableau <agrandissement> de chaînes

// Insère un élément dans le tableau associatif 
Insère(tabClasses, "CM2")

// Remplissage des élèves CM2
tabClasses["CM2"][1] = "ALARD Serge"
tabClasses["CM2"][2] = "BERTAU Kelian"

// Insère un élément dans le tableau associatif 
Insère(tabClasses, "CM1")

// Remplissage des élèves CM1
tabClasses["CM1"][1] = "ALONSO Benoit"
tabClasses["CM1"][2] = "CRISTO Benjamin"

POUR TOUT UneClasse, sNom de tabClasses
	POUR TOUT Elève DE UneClasse
		Trace(sNom + ":" + Elève)
	FIN
FIN
```



- ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=Theassociativearrays.gif) ***Unit examples (WINDEV)*** : **The associative arrays** <br>Using the associative arrays of WLanguage:<br>- Fill an associative array<br>- Access an associative array


