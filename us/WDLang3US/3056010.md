
## Detailed use of WINDEV FTP/RPC
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Implementation
<a name="implementation_ELTTEXTE000167"></a>
To upload and download files, you must comply with the following rules:

1. Connect to a WINDEV FTP server with [NetConnect](../WDLang3/3056004.md). This function establishes a connection between WINDEV and the server and it provides a connection identifier.

2. Upload, download files.

3. Close the connection to the server with [NetDisconnect](../WDLang3/3056011.md).




**Remarks**: 

- The TCP/IP communication protocol must be installed and an IP address must be defined.

- To create a WINDEV FTP/RPC server, all you have to do is use [NetStartServer](../WDLang3/3056013.md). 




<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Details of different steps
<a name="details_different_steps_ELTTEXTE000191"></a>


### Step 1: Establishing a connection to a WINDEV RPC or FTP server
<a name="step_1_establishing_connection_windev_rpc_ftp_server_ELTPARAGRAPHE000036"></a>

To transfer files, a connection must be established to a WINDEV RPC or FTP server. The connection is established by [NetConnect](../WDLang3/3056004.md). The code for establishing a connection must be found before the first "Net" function. The value returned by [NetConnect](../WDLang3/3056004.md) must be stored because this one will be used by the other "RPC" and "FTP" functions.

The code used to connect to an RPC server is as follows:


```wl
Function RPCConnection (Address, User, Password)
// Connection to an RPC server
ConnectionNum is int
ConnectionNum = NetConnect(Address, RPCServer, User, Password)
RESULT ConnectionNum
```


**Remark: How to create a WINDEV RPC or FTP server?**
To create a WINDEV RPC or FTP server, you must create an application that is using [NetStartServer](../WDLang3/3056013.md) to start the server. [NetEndServer](../WDLang3/3056024.md) is used to stop this server.
The WDRPCSRV.INI file that contains the connection rights granted to the users. This text file and the WINDEV RPC/FTP server are found in the same directory. It must contain a "password" section in which each entry point is a username:


```txt
[Passwords]
UserName1=Password1
UserName2=Password2
UserName3=Password3
...
```


**Remarks**:

- The server must be accessible by all the client computers (via TCP/IP).

- To be accessible, the server must be started.



<a name="NOTE2_2"></a>


### Step 2: Transmitting a file to a WINDEV FTP server
<a name="step_2_transmitting_file_windev_ftp_server_ELTPARAGRAPHE000072"></a>

In the following example, a file is transmitted to the WINDEV FTP server ([NetSendFile](../WDLang3/3056005.md)). A progress bar is used to follow the progress of the transfer.


```wl
// Initialization code of "RPCClient" window
GLOBAL
Transfer_Completed is boolean = False
Transfer_InProgress is boolean = False
 
Event("ProgBar_Transfer", "RPCClient", "SendFile")
....
ConnectFTP is int = NetConnect("148.61.125.245", FTPServer, "GUEST", "")
...
```



```wl
// -- Button for sending the transfer
IF Transfer_InProgress = True THEN
	Error("A file transfer is in progress")
ELSE
	Transfer_Completed = False
	Transfer_InProgress = True
	IF NetSendFile(ConnectFTP, "C:\autoexec.bat", "C:\autoexec.cli", ...
			"SendFile", 10) = False THEN
		Info("The transfer failed")
	END
	...
END
```



```wl
// -- ProgBar_Transfer procedure: managing the current transfer
PROCEDURE ProgBar_Transfer
Message("Transfer in progress")
ProgressBar(_EVE.wParam, _EVE.lParam)
IF _EVE.wParam = _EVE.lParam THEN
	Transfer_InProgress = False
	Transfer_Completed = True
	Message("Transfer completed")
	Info("Transfer completed")
END
```

<a name="NOTE2_3"></a>


### Step 3: Retrieving a file from a WINDEV FTP server
<a name="step_3_retrieving_file_from_windev_ftp_server_ELTPARAGRAPHE000088"></a>

[NetGetFile](../WDLang3/3056025.md) is used to retrieve a file found on the WINDEV FTP/RPC server.

**Remark**: you can easily find out the list of directories and files found on a WINDEV FTP server. An example is available in the description of [NetDirList](../WDLang3/3056016.md).


```wl
// -- Window opening code
// Request a message from Windows
GLOBAL
	WM_MYMESSAGE is int
	lpString is string fixed on 20 = "ProgBar_Main"
	ConnectFTP is int
// Connection
ConnectFTP = NetConnect("148.61.125.245", FTPServer, "GUEST", "")
 
WM_MYMESSAGE = CallDLL32("USER32", "RegisterWindowMessageA", &lpString)
 
// Branch the ProgBar procedure on this message
Event("UPDProgBar", "MAIN", WM_MYMESSAGE)
```



```wl
// -- Code of button for file retrieval
Hourglass(True)
IF NOT NetGetFile(ConnectFTP, "C:\autoexec.bat", "C:\autoexec.cli", WM_MYMESSAGE, 10) THEN
	Error("Error while transferring the file")
END
HourGlass(False)
```



```wl
// -- UPDProgBar() procedure
PROCEDURE UPDProgBar()
// Display the progress bar
// If the entire file is transferred, reinitialize the progress bar
IF _EVE.wParam = _EVE.lParam THEN
	// Transfer completed
	ProgressBar()
ELSE
	// Transfer in progress
	ProgressBar(_EVE.wparam, _EVE.lparam, "Transfer in progress")
END
```

<a name="NOTE2_4"></a>


### Step 4: Closing a connection to a WINDEV RPC or FTP server
<a name="step_4_closing_connection_windev_rpc_ftp_server_ELTPARAGRAPHE000108"></a>

Once the files have been transferred, you must disconnect from the WINDEV RPC or FTP server. The disconnection is performed by [NetDisconnect](../WDLang3/3056011.md). The disconnection code must be found after the last "Net" statement. The "ConnectionNum" variable, required for the disconnection, contains the value returned by [NetConnect](../WDLang3/3056004.md).
 
The code used to disconnect from a WINDEV RPC server is as follows:


```wl
// Disconnection from a WINDEV RPC server
// ConnectionNum contains the value returned by NetConnect
NetDisconnect(ConnectionNum)
```



