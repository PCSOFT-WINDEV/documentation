
## AuthToken (Type of variable)

***In french: AuthToken***
				



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
The **AuthToken** type contains the characteristics of a token used to access a Web service. This access token was requested beforehand: 

- by the [AuthIdentify](../WDLang3/1000022219.md) function.  

- by an HTTP request. In this case, the request returns the token in JSON format. 


The characteristics of this access token can be defined and changed using different WLanguage properties. 

**Remark**: For more details on the declaration of this type of variable and the use of WLanguage properties, see [Declaring a variable](../Motscles/1514032.md).


<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
// Example used to retrieve a token to perform a request on DropBox
OAuth2Params is OAuth2Parameters
OAuth2Params.ClientID = "01234567890123456789" 
OAuth2Params.ClientSecret = "98765432109876543210"
OAuth2Params.AuthURL = "https://www.dropbox.com/oauth2/authorize"
OAuth2Params.TokenURL = "https://api.dropboxapi.com/oauth2/token"
OAuth2Params.AdditionalParameters = "force_reapprove=false"
<COMPILE IF ConfigurationType<>Site>
	// If you are not using a website, you need to specify a localhost redirect URL
	OAuth2Params.RedirectionURL = "http://localhost:9874/"	
<END>

// Ask for authentication: opens the login window
MyToken is AuthToken = AuthIdentify(OAuth2Params)

// Request authenticated on a DropBox API
req is httpRequest
req.Method = httpPost
req.URL = "https://api.dropboxapi.com/2/files/list_folder"
req.AuthToken = MyToken // Authentication token
req.ContentType = "application/json"
vAPIParam is variant
vAPIParam.path = "/Homework/math"
vAPIParam.recursive = False
vAPIParam.include_media_info = False
vAPIParam.include_deleted = False
vAPIParam.include_has_explicit_shared_members = False
req.Content = VariantToJSON(vAPIParam)

HTTPresponse is httpResponse = HTTPSend(req)
let Data = JSONToVariant(HTTPresponse.Content)
// Use the incoming data...
```
<a name="Example2"></a>



```wl
// Retrieves the token (in JSON) via an HTTP request. The AuthIdentify function is not used
// Define the request
httpReq is httpRequest
httpReq.Method = httpPost
httpReq.URL = PAYPAL_TOKEN
httpReq.User = PAYPAL_APP_ID
httpReq.Password = PAYPAL_SECRET
httpReq.Content = "grant_type=client_credentials"
httpReq.ContentType = "application/x-www-form-urlencoded"

// Execute query
httpRep is httpResponse = HTTPSend(httpReq)

// Retrieve the token
If httpRep.StatusCode = 200 THEN
	// Declares the parameters, required to refresh the token
	oAuth2Param is OAuth2Parameters
	oAuth2Param.ClientID = PAYPAL_APP_ID
	oAuth2Param.ClientSecret = PAYPAL_SECRET
	oAuth2Param.URLAuth = PAYPAL_ACCESS_BASEURL
	oAuth2Param.Scope = PAYPAL_SCOPES
	oAuth2Param.URLToken = PAYPAL_TOKEN
	
	// Initializes the token with JSON
	MyToken is AuthToken(oAuth2Param, httpRep.Content)
	gMyToken <= MyToken
END
```




## Syntax
<a name="SYNTAX1"></a>

### Declaring an AuthToken variable

`MyVariable is AuthToken`
---

In this case, [AuthIdentify](../WDLang3/1000022219.md) is used to retrieve the token parameters. 






<a name="SYNTAX2"></a>

### Declaring and describing an AuthToken variable (without using the AuthIdentify function)

`MyVariable is AuthToken(<OAuth2 parameter> , <Token>)`
---

**`<OAuth2 parameter>: (OAuthParameters variable)`**

Name of [OAuth2Parameters](../WDLang3/1000022212.md) variable containing the information required to authenticate on a service implementing the OAuth 2.0 standard.

**`<Token>: (Character string)`**

String in JSON or UTF8 format containing the token. Corresponds to the token returned by the service. 







<a name="NOTE0"></a>

## Remarks
<a name="NOTE0_1"></a>


### Properties specific to AuthToken variables
<a name="properties_specific_authtoken_variables_ELTPARAGRAPHE000073"></a>

The following properties can be used to handle a token for accessing a Webservice:

| Property name | Type used | Effect |
| --- | --- | --- |
| ExpirationDate | DateTime | Expiration date and time of token. <br><br> |
| Refresh | Character string | Value returned by the server to determine if the token can be refreshed. <br><br>If this property is not specified, [AuthRefreshToken](../WDLang3/1000026168.md) cannot be used to refresh the token: you will have to request a new token.. |
| ServerResponse | Buffer | Value returned by the server during the request made by the access token. <br><br>**This property is read-only**. |
| Valid | Boolean | Validity of access token: <br><br>- <u><u><u><u>True</u></u></u></u> if the access token is valid. <br><br>- <u><u><u><u>False</u></u></u></u> otherwise.<br><br><br>**This property is read-only**. |
| Value | Character string | Access token. <br><br>Value automatically filled when using [AuthIdentify](../WDLang3/1000022219.md). <br><br><br><br>This value can be used to send authenticated requests onto the relevant Web service. |


<a name="NOTE0_2"></a>


### Operating mode of OAuth 2.0 authentication 
<a name="operating_mode_oauth_20_authentication_ELTPARAGRAPHE000292"></a>

The steps of OAuth 2.0 authentication performed by [AuthIdentify](../WDLang3/1000022219.md) are as follows: 

- Running a first HTTP request to ask for an authorization (authorization URL specified in the [OAuth2Parameters](../WDLang3/1000022212.md) variable).

- Opening a window for user identification according to the OAuth 2.0 protocol. The identification interface is given by the service accessed.

- After identification, the server returns a first authorization code allowing you to ask the resources for an access token. This code is added as parameter of second URL (access token URL specified in the [OAuth2Parameters](../WDLang3/1000022212.md) variable).

- Running the second HTTP request to ask for the access token. The result is a JSON buffer that contains, among other things, the access token ("**access_token**") that will be used for the authenticated requests. The [AuthToken](../WDLang3/1000022220.md) variable contains the information found in this JSON buffer. This access token will be used by the calls to the APIs of Web service.




**To use the APIs of the Web service**, simply use the [HTTPSend](../WDLang3/1000021183.md) function with an [httpRequest](../WDLang3/1000021158.md) variable defining the query to be executed. 
The [AuthToken](../WDLang3/1000022220.md) variable will be assigned to the **AuthToken** property of the [httpRequest](../WDLang3/1000021158.md) variable (see example). 
In this case, the server will receive the HTTP "**Authorization**" header with a value in the following format: "Authorization: Bearer xxx_access_token_xxx". 

**Caution**: 

- If the server does not return the access token in the format of JSON code according to the OAuth2.0 standard, an error will occur and the token will not be retrieved. The server response can be retrieved via the **ServerResponse** property of the [AuthToken](../WDLang3/1000022220.md) variable.

- If the server does not support the HTTP "**Authorization**" header for transmitting the access token, this transmission must be done by the developer according to the format expected by the requested service. 
	The following example allows you to use the Web service of Facebook. In this case, the access token must be specified on the request URL.

	- ![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) Code sample for Facebook
			
		```wl
		// Example used to retrieve the name of the Facebook account
		MyToken is AuthToken
		MyTokenParam is OAuth2Parameters
		
		MyTokenParam.ClientID = "123456789012345"
		MyTokenParam.ClientSecret = "45g8jh5kll45579021qsg5444j"
		MyTokenParam.AuthURL = "https://www.facebook.com/dialog/oauth"
		MyTokenParam.TokenURL = "https://graph.facebook.com/v2.3/oauth/access_token"
		MyTokenParam.RedirectionURL = "http://localhost:9874/"
		MyTokenParam.Scope = "email"
		
		MyToken = AuthIdentify(MyTokenParam)
		IF MyToken <> Null THEN
			IF ErrorOccurred THEN
				Error(ErrorInfo())
			ELSE
				// Token specified on the request URL
				HTTPRequest("https://graph.facebook.com/me?access_token=" + MyToken.Value)
				vMyRes is Variant = JSONToVariant(HTTPGetResult(httpResult))
				// Retrieve the account name
				Trace(vMyRes.name)
			END
		END
		```







<a name="NOTE0_3"></a>


### Using the AuthToken variables
<a name="using_the_authtoken_variables_ELTPARAGRAPHE000156"></a>

**AuthToken** variables can be used in the functions: 

- standard syntax: 
	


|   |   |
| --- | --- |
| [AuthIdentify](../WDLang3/1000022219.md) | Performs an authentication using the OAuth 2.0 protocol on any webservice. |
| [OpenIDReadIdentity](../WDLang3/1000023573.md) | Retrieves the identity of a user contained in an [AuthToken](../WDLang3/1000022220.md) variable. |

- prefix syntax: 
	


|   |   |
| --- | --- |
| [&lt;AuthToken variable&gt;.OpenIDReadIdentity](../WDLang3/1000023629.md) | Retrieves the identity of a user contained in an [AuthToken](../WDLang3/1000022220.md) variable. |





