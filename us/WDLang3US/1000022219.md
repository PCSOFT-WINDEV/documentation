
## AuthIdentify (Function)

***In french: AuthIdentifie***



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Performs an authentication using: 

- the OAuth 2.0 protocol on any Web service.

- ![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png) the OpenID protocol on any Web service.







<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
// Example used to retrieve a token to perform a request on DropBox
OAuth2Params is OAuth2Parameters
OAuth2Params.ClientID = "01234567890123456789" 
OAuth2Params.ClientSecret = "98765432109876543210"
OAuth2Params.AuthURL = "https://www.dropbox.com/oauth2/authorize"
OAuth2Params.TokenURL = "https://api.dropboxapi.com/oauth2/token"
OAuth2Params.AdditionalParameters = "force_reapprove=false"
<COMPILE IF ConfigurationType<>Site>
	// If you are not using a website, you need to specify a localhost redirect URL
	OAuth2Params.RedirectionURL = "http://localhost:9874/"	
<END>

// Ask for authentication: opens the login window
MyToken is AuthToken = AuthIdentify(OAuth2Params)

// Request authenticated on a DropBox API
req is httpRequest
req.Method = httpPost
req.URL = "https://api.dropboxapi.com/2/files/list_folder"
req.AuthToken = MyToken // Authentication token
req.ContentType = "application/json"
vAPIParam is variant
vAPIParam.path = "/Homework/math"
vAPIParam.recursive = False
vAPIParam.include_media_info = False
vAPIParam.include_deleted = False
vAPIParam.include_has_explicit_shared_members = False
req.Content = VariantToJSON(vAPIParam)

HTTPresponse is httpResponse = HTTPSend(req)
let Data = JSONToVariant(HTTPresponse.Content)
// Use the incoming data...
```

<a name="XSYNTAX"></a>

## Syntax
<a name="SYNTAX1"></a>
<a name="SYNTAX2"></a>
![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png) 
### Authentication via the OpenID protocol

`<Result> = AuthIdentify(<Authentication parameters>)`
---

**`<Result>: (AuthToken variable)`**

[AuthToken](../WDLang3/1000022220.md) variable corresponding to the token containing the access information for the next authenticated requests.  

**`<Authentication parameters>: (OpenIDParameters variable)`**

Name of [OpenIDParameters](../WDLang3/1000023561.md) variable containing the parameters describing the elements required to retrieve the access token.


<a name="SYNTAX3"></a>

### Authentication via the OAuth 2.0 protocol (asynchronous syntax)

`AuthIdentify(<Authentication parameters> , <WLanguage procedure>)`
---

**`<Authentication parameters>: (OAuth2Parameters variable)`**

Name of [OAuth2Parameters](../WDLang3/1000022212.md) variable containing the parameters describing the elements required to retrieve the access token.

**`<WLanguage procedure>: (Procedure name)`**

Name of the WLanguage procedure ("callback") called during the authentication. For more details on this procedure, see [Parameters of the procedure used by AuthIdentify](../WDLang3/1000025107.md). 


<a name="SYNTAX4"></a>
![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png) 
### Authentication via the OpenID protocol (asynchronous syntax)

`AuthIdentify(<Authentication parameters> , <WLanguage procedure>)`
---

**`<Authentication parameters>: (OpenIDParameters variable)`**

Name of [OpenIDParameters](../WDLang3/1000023561.md) variable containing the parameters describing the elements required to retrieve the access token.

**`<WLanguage procedure>: (Procedure name)`**

Name of the WLanguage procedure ("callback") called during the authentication. For more details on this procedure, see [Parameters of the procedure used by AuthIdentify](../WDLang3/1000025107.md).



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks


### Operating mode of OAuth 2.0 authentication 
<a name="operating_mode_oauth_20_authentication_ELTPARAGRAPHE000117"></a>

The steps of OAuth 2.0 authentication performed by [AuthIdentify](../WDLang3/1000022219.md) are as follows: 

- Running a first HTTP request to ask for an authorization (authorization URL specified in the [OAuth2Parameters](../WDLang3/1000022212.md) variable).

- Opening a window for user identification according to the OAuth 2.0 protocol. The identification interface is given by the service accessed.

- After identification, the server returns a first authorization code allowing you to ask the resources for an access token. This code is added as parameter of second URL (access token URL specified in the [OAuth2Parameters](../WDLang3/1000022212.md) variable).

- Running the second HTTP request to ask for the access token. The result is a JSON buffer that contains, among other things, the access token ("**access_token**") that will be used for the authenticated requests. The [AuthToken](../WDLang3/1000022220.md) variable contains the information found in this JSON buffer. This access token will be used by the calls to the APIs of Web service.




**To use the APIs of the Web service**, simply use the [HTTPSend](../WDLang3/1000021183.md) function with an [httpRequest](../WDLang3/1000021158.md) variable defining the query to be executed. 
The [AuthToken](../WDLang3/1000022220.md) variable will be assigned to the **AuthToken** property of the [httpRequest](../WDLang3/1000021158.md) variable (see example). 
In this case, the server will receive the HTTP "**Authorization**" header with a value in the following format: "Authorization: Bearer xxx_access_token_xxx". 

**Caution**: 

- If the server does not return the access token in the format of JSON code according to the OAuth2.0 standard, an error will occur and the token will not be retrieved. The server response can be retrieved via the **ServerResponse** property of the [AuthToken](../WDLang3/1000022220.md) variable.

- If the server does not support the HTTP "**Authorization**" header for transmitting the access token, this transmission must be done by the developer according to the format expected by the requested service. 
	The following example allows you to use the Web service of Facebook. In this case, the access token must be specified on the request URL.

	- ![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) Code sample for Facebook
			
		```wl
		// Example used to retrieve the name of the Facebook account
		MyToken is AuthToken
		MyTokenParam is OAuth2Parameters
		
		MyTokenParam.ClientID = "123456789012345"
		MyTokenParam.ClientSecret = "45g8jh5kll45579021qsg5444j"
		MyTokenParam.AuthURL = "https://www.facebook.com/dialog/oauth"
		MyTokenParam.TokenURL = "https://graph.facebook.com/v2.3/oauth/access_token"
		MyTokenParam.RedirectionURL = "http://localhost:9874/"
		MyTokenParam.Scope = "email"
		
		MyToken = AuthIdentify(MyTokenParam)
		IF MyToken <> Null THEN
			IF ErrorOccurred THEN
				Error(ErrorInfo())
			ELSE
				// Token specified on the request URL
				HTTPRequest("https://graph.facebook.com/me?access_token=" + MyToken.Value)
				vMyRes is Variant = JSONToVariant(HTTPGetResult(httpResult))
				// Retrieve the account name
				Trace(vMyRes.name)
			END
		END
		```











### How OpenID authentication works
<a name="how_openid_authentication_works_ELTPARAGRAPHE000181"></a>

The following are OpenID authentication steps performed by [AuthIdentify](../WDLang3/1000022219.md): 

- Running a first HTTP request to ask for an authorization (authorization URL specified in the [OpenIDParameters](../WDLang3/1000023561.md) variable).

- Opening a window for user identification according to the OpenID protocol. The identification interface is given by the service accessed.

- Running a second HTTP request to ask for the access token, after the identification. The result is a JSON buffer that contains, among other things, the access token ("**access_token**") that will be used for the authenticated requests. The [AuthToken](../WDLang3/1000022220.md) variable contains the information found in this JSON buffer. This access token will be used by the calls to the APIs of Web service. 




To identify the user, simply use [OpenIDReadIdentity](../WDLang3/1000023573.md) and specify the [AuthToken](../WDLang3/1000022220.md) variable. 

**Caution**: If the server does not return the access token in the format of JSON code according to the OAuth2.0 standard, an error will occur and the token will not be retrieved. The server response can be retrieved via the **ServerResponse** property of the [AuthToken](../WDLang3/1000022220.md) variable.
<a name="NOTE0_5"></a>


- ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=WDOAuth.gif) ***Training (WINDEV)*** : **WD OAuth** <br>OAuth allows you to act as user of an external platform without knowing the identifiers (user name and password) of this user.<br>Several external platforms (among which Google, Twitter, Facebook) propose APIs for which you have the ability to connect with the information belonging to one of their users. This identification is performed via OAuth. Therefore, all the requests performed on their services (API, ...) will require an access token identifying both the application (the "client") and the user.<br>The example proposes to connect to Google and Microsoft via the AuthConnect function and the OAuth2Parameter type.



<a name="XComponent"></a>

## Business / UI classification:
Business Logic
## Component:
wd280ggl.dll
