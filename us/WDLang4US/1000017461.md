
## FullTextIndex description (Type of variable)

***In french: Description d'IndexFullText (Type de variable)***
				



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
A **FullTextIndex description** variable is used to describe one or more full-text indexes through programming. The characteristics of these indexes can be defined using different [WLanguage properties](#NOTE0_1)

To add the description of this index into the description of a data file, use [HDescribeFullTextIndex](../WDLang4/1000017407.md). The "FullTextIndex Description" variable is reinitialized and it can be used to describe another full-text index through programming.




<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
AFile is File Description
AnItem is Item Description
MyFullTextIndex is FullTextIndex Description
CUSTOMER is Data Source

// Describe the "CUSTOMER" file
AFile.Name = "CUSTOMER"
AFile.Type = hFileNormal
AFile.FicCryptMethod = hEncryptionStandard

// Description of the "LASTNAME" item
AnItem.Name = "LASTNAME"
AnItem.Type = hItemText
AnItem.Size = 40
AnItem.KeyType = hDuplicatesKey
HDescribeItem(AFile, AnItem)

// Describe the "FIRSTNAME" item
AnItem.Name = "FIRSTNAME"
AnItem.Type = hItemText
AnItem.Size = 40
HDescribeItem(AFile, AnItem)

// Describe the full-text index
MyFullTextIndex.MinimumLength = 4
MyFullTextIndex.Name = "MyFullTextIndex"
MyFullTextIndex.Items = "LASTNAME+FIRSTNAME"
MyFullTextIndex.AccentSensitive = True
MyFullTextIndex.CaseSensitive = False
HDescribeFullTextIndex(AFile, MyFullTextIndex)

// Validate the file description
HDescribeFile(AFile)
// Create the Customer file (recognized because declared as data source)
HCreation(CUSTOMER)
// Create a record
CUSTOMER.LASTNAME = "Doe"
CUSTOMER.FIRSTNAME = "Andrew"
HAdd(CUSTOMER)
// Create a record
CUSTOMER.LASTNAME = "Doe"
CUSTOMER.FIRSTNAME = "Vince"
HAdd(CUSTOMER)
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Syntax

### Declaring one or more FullTextIndex descriptions

`<Variable name> is FullTextIndex description
<Variable name 1>, <Variable name 2> are FullTextIndex descriptions
`
---

**`<Variable name>: ()`**

Name of the "hFullTextIndex Description" variable to declare.

Remark: The <u><u><u><u>a</u></u></u></u> and <u><u><u><u>an</u></u></u></u> keywords are optional: they provide better readability.  


<a name="SYNTAX2"></a>

### Describing a "FullTextIndex Description" variable

``
---

**To describe a "FullTextIndex Description" variable**: 
Use the WLanguage properties specific to the descriptions of full-text indexes. For more details, see [remarks](#NOTE0_1).

**To validate the description of a "FullTextIndex Description" variable**:
Use the [HDescribeFullTextIndex](../WDLang4/1000017407.md) function.



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks




### Properties specific to the description of full-text indexes
<a name="properties_specific_the_description_fulltext_indexes_ELTPARAGRAPHE000092"></a>

The table below presents the properties specific to the description of full-text indexes: 

| Name | Type used | Effect |
| --- | --- | --- |
| [AccentSensitive](../Proprietes/2512077.md) | Boolean | Used to define and find out whether the punctuation (comma, dot, ...) is taken into account in the searches performed on the item |
| [CaseSensitive](../Proprietes/2512085.md) | Boolean | Used to define and find out whether the case (uppercase/lowercase characters) is taken into account in the searches performed on the item. |
| [Items](../Proprietes/1000017462.md) | Character string | Used to define and find out the different items included in the full-text index. |
| [ManageHTML](../Proprietes/1000017717.md) | Boolean | Configures the management mode of the HTML format in a full-text index. |
| [ManageRTF](../Proprietes/1000017718.md) | Boolean | Configures the RTF management mode in a full-text index. |
| [MinimumLength](../Proprietes/1000017716.md) | Integer | Used to define and find out the minimum length of the words to index. |
| [Name](../Proprietes/2510082.md) | Character string | Mandatory property used to define and find out the name of the full-text index item. |
| Option | Integer constant | Used to define specific options on the full-text index item. <br><br>This property can correspond to: <br><br>- the *hftPunctuationUnicode* constant to take into account the specific features of the Unicode punctuation. <br><br>- the *hftPunctuationSimple* constant not to take into account the specific features of the Unicode punctuation. <br><br><br> |
| [Stemming](../Proprietes/1000021132.md) | Integer constant | Mandatory property used to define the language in which the stemming must be performed. |
| [StopWord](../Proprietes/1000021126.md) | Character string | Used to define the empty words not to index. |
| [Synonym](../Proprietes/1000021127.md) | Character string | Used to define the synonyms taken into account during the indexing. |


<a name="NOTE0_2"></a>




### How to describe the data files through programming? 
<a name="how_describe_the_data_files_through_programming_ELTPARAGRAPHE000196"></a>

To describe the data files through programming, you must:

1. Declare the variables of type "[File Description](../WDLang4/1514065.md)", "[Item Description](../WDLang4/1514071.md)", "**FullTextIndex Description**" and "[Link description](../WDLang4/1514039.md)" (if necessary).

2. For each data file:

	- Describe the characteristics of the data file using [HFSQL properties](../Proprietes/2512070.md).

	- Describe the characteristics of the items using [HFSQL properties](../Proprietes/2512070.md).

	- Validate the description of each item ([HDescribeItem](../WDLang4/3044233.md)).

	- If necessary, describe the characteristics of full-text indexes using [HFSQL properties](../Proprietes/2512070.md).

	- Validate the description of each full-text index ([HDescribeFullTextIndex](../WDLang4/1000017407.md)).

	- Validate the data file description ([HDescribeFile](../WDLang4/3044206.md)).




3. If necessary, describe the characteristics of the links using [HFSQL properties](../Proprietes/2512070.md).

4. If necessary, validate the description of each link ([HDescribeLink](../WDLang4/3044191.md)).





