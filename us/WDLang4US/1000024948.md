
## &lt;Source&gt;.ExecuteSQLQuery (Function)

***In french: &lt;Source&gt;.ExécuteRequêteSQL***

<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Initializes a query written in SQL language and declares this query to the HFSQL engine. This query can correspond to: 

- the SQL code typed in **&lt;Source&gt;.ExecuteSQLQuery**. 

- an SQL Query variable.


Then, the query result can be read. If a query with the same name already exists, this query is replaced. The result is re-calculated.

**Feature specific to HFSQL**: The SQL queries run on an HFSQL Classic, Mobile or Client/Server database can contain WLanguage functions. For more details, see [Using a WLanguage function in an SQL query](../Motscles/1513004.md).

**Caution**: To set the position on the first record of the query result, use [&lt;Source&gt;.ReadFirst](../WDLang4/1000025022.md), for example.

To free the resources of a query typed in **&lt;Source&gt;.ExecuteSQLQuery**, use: 

- the [&lt;Source&gt;.CancelDeclaration](../WDLang4/1000024192.md) function.

- the [&lt;Source&gt;.FreeQuery](../WDLang4/1000025011.md) function.




**Remark**: a query will not be initialized if:

- the query name is already used in the current analysis (name of data file or name defined by one of the following functions: [&lt;Source&gt;.Declare](../WDLang4/1000024213.md), [&lt;Source&gt;.DeclareExternal](../WDLang4/1000024214.md), [&lt;Source&gt;.DescribeFile](../WDLang4/1000024215.md)).

- a syntax error is found in the query.




![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) You cannot execute queries with SQL code containing WLanguage functions. 
<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
CustomerQry is Data Source
// Initialize the "Customer" query
CustomerQry.ExecuteSQLQuery("SELECT NAME FROM CUSTOMER")
```


<a name="3044084_Example2"></a>

```wl
TaskQry is Data Source
// Initialize the "TaskQry" query
// This query uses a WLanguage function
// This query selects the tasks exceeding 5 days
TaskQry.ExecuteSQLQuery( ...
	"SELECT Task.Caption FROM Task" +...
	"WHERE WL.DateDifference(Task.StartTime, Task.EndDate) > 5")
```


<a name="3044084_Example3"></a>

```wl
// Dynamic modification of the LIMIT condition of an SQL query

QRY is Data Source

sMyQry is string = [
SELECT * FROM
ZIPCODES
%1
]

sMyQry = StringBuild(sMyQry, "LIMIT 1, 10")
IF NOT QRY.ExecuteSQLQuery(hQueryDefault, sMyQry) THEN
	Error(HErrorInfo())
ELSE
	Info("The query contains " + HNbRec(QRY) + " records.")
END
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Syntax

### Executing an SQL query (without connection)

`<Result> = <Source>.ExecuteSQLQuery([<Mode>, ] <SQL query text>)`
---

**`<Result>: (Boolean)`**



- <u><u><u><u>True</u></u></u></u> if the SQL query was initialized,

- <u><u><u><u>False</u></u></u></u> otherwise. [HErrorInfo](../WDLang4/3044071.md) returns more details about the problem.




**`<Source>: (Type corresponding to the source)`**



- Name of the query that will be executed. This name is used to handle the result of &lt;Text of SQL query&gt; through programming. If a query with the same name is already declared, it is replaced with the new query.

- Name of the [SQL query](../Motscles/1514082.md) variable containing the SQL code of the query. In this case, the &lt;Text of SQL query&gt; parameter must not be specified. 
	




**`<Mode>: (Optional Integer constant)`**

Option for initializing the query:


|   |   |
| --- | --- |
| *hCheckDuplicates* | Enables the management of duplicates during the query execution. The query is not run if a duplicate error occurs.<br>This constant forces the *hWithTransaction* constant. A transaction is started, the records are locked in read/write during the execution of the query and they are unlocked at the end of its execution, regardless of the result (failure or success). If the application is stopped during the query execution (power outage for example), the transaction will be canceled when the data files of query are used for the next time.<br>Remark: the query execution may be slowed down.<br>![HFSQL Client/Server](https://doc.pcsoft.fr/ext/images/us/HFCS.png) The management of duplicates is disabled if the *hCheckDuplicates* constant is not specified. An error occurs if the user has no rights to disable the management of duplicates ([&lt;Connection variable&gt;.ModifyDatabaseRights](../WDLang4/1000022707.md)) and if the management of duplicates must be performed.![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Access by JDBC: This constant is not available. |
| *hCheckIntegrity* | Enables the management of integrity during the query execution. The query is not run if an integrity error occurs.<br>This constant forces the **hWithTransaction** constant. A transaction is started, the records are locked in read/write during the execution of the query and they are unlocked at the end of its execution, regardless of the result (failure or success). If the application is stopped during the query execution (power outage for example), the transaction will be canceled when the data files of query are used for the next time.<br>Remark: the query execution may be slowed down.<br>![HFSQL Client/Server](https://doc.pcsoft.fr/ext/images/us/HFCS.png) The integrity management is disabled if the *hCheckIntegrity* constant is not specified. However, an error occurs if an integrity management operation must be performed, but the user does not have the rights to disable integrity management ([&lt;Connection variable&gt;.ModifyDatabaseRights](../WDLang4/1000022707.md)).![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Access by JDBC: This constant is not available. |
| *hModifyFile*<br>(constant that can be combined with the other constants) | <br><br>- **On HFSQL data files**: When the result of the query is modified ([&lt;Source&gt;.Add](../WDLang4/1000024187.md), [&lt;Source&gt;.Write](../WDLang4/1000024331.md), [&lt;Source&gt;.Modify](../WDLang4/1000025038.md), [&lt;Source&gt;.Cross](../WDLang4/1000025051.md) and [&lt;Source&gt;.Delete](../WDLang4/1000025071.md)), these changes are propagated to the data files used in the query. <br>	If this option is not specified, only the query result is modified. <br>	For more details on modifying the result of a query, see [Modifying the query content](../WDLang4/3044234.md).<br><br>- **On data files in a format other than HFSQL**, the *hModifyFile* constant is useless: the data files taking part in the query are automatically modified when modifying the query result.<br><br><br>![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Access by JDBC: This constant is not available. |
| *hNoBind* | ![Native Connectors (Native Accesses)](https://doc.pcsoft.fr/ext/images/us/AN.png) SQL Server, Oracle, Sybase: Used to execute a query without enabling the bind option. |
| *hNoHourglass* | When reading the query result, this read operation may be locked and an hourglass appears by default. This constant is used not to display the hourglass in this case. <br><br> |
| *hQueryBreakable* | The query initialization can be interrupted by the ESC key.<br>![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png) This constant is not available.![Stored procedures](https://doc.pcsoft.fr/ext/images/us/PS.png) This constant is ignored.![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Access by JDBC: This constant is not available.![HFSQL Client/Server](https://doc.pcsoft.fr/ext/images/us/HFCS.png) The Esc key must be pressed on the client computer. |
| *hQueryDefault*<br>(default value) | The query is initialized without interruption. |
| *hQueryWithoutCorrection* | ![OLE DB](https://doc.pcsoft.fr/ext/images/us/OLEDB.png)![Native Connectors (Native Accesses)](https://doc.pcsoft.fr/ext/images/us/AN.png) No check is performed by the HFSQL engine. This option must be used if the query contains commands specific to a connection type (Oracle, SQL Server, etc.).<br><br>Caution: if this constant is used:<br><br>- the connection name must be specified (&lt;Connection&gt; parameter).<br><br>- the following function cannot be used on the query: [&lt;Source&gt;.Filter](../WDLang4/1000024960.md).<br><br>- you cannot cancel a condition by assigning it to NULL. <br><br>- no browse item should be specified in the following functions: [&lt;Source&gt;.Last](../WDLang4/1000024217.md), [&lt;Source&gt;.ReadLast](../WDLang4/1000025020.md), [&lt;Source&gt;.ReadFirst](../WDLang4/1000025022.md), [&lt;Source&gt;.First](../WDLang4/1000025048.md), [&lt;Source&gt;.SeekLast](../WDLang4/1000025054.md), [&lt;Source&gt;.SeekFirst](../WDLang4/1000025055.md).<br><br><br> |
| *hQueryWithoutHFCorrection* | ![HFSQL Classic](https://doc.pcsoft.fr/ext/images/us/HF.png) The file format (filled with space characters or not) is not checked by the HFSQL engine. To be used if the query handles both HFSQL data files in a format that completes items with space characters and HFSQL data files in a format that does not complete items with space characters.![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png) This constant is not available. |
| *hSQLUnicode* | Used to specify that the query text must be sent in Unicode format to the server. |
| *hWithFilter*<br>(constant that can be combined with the other constants) | If this parameter is specified:<br><br>- the query result corresponds to a selection of filter records if the HFSQL engine allows it. Otherwise, the query result corresponds to an HFSQL view.<br><br>- the following operations are not allowed: search on the query result, query on query, view on query, query on view.<br><br>- the browse items specified when looping through the result are ignored.<br><br>- the magnifier is not available for the tables linked to a query.<br><br>- [&lt;Source&gt;.NbRec](../WDLang4/1000025040.md) cannot be used on the queries. <br>	Caution: if this option is specified, the *hModifyFile* constant is automatically used.<br><br><br>If this parameter is not specified (by default):<br><br>- the query result corresponds to an HFSQL view.<br><br>- the following operations can be performed on the queries: search on the query result, query on query, view on query, query on view.<br><br>- the browse items specified when looping through the result are taken into account.<br><br>- the magnifier is available on tables linked to a query.<br><br>- [&lt;Source&gt;.NbRec](../WDLang4/1000025040.md) can be used on the queries.<br><br><br>![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Access by JDBC: This constant is not available. |
| *hWithTransaction* | Only write requests (INSERT, UPDATE and DELETE) are executed in a transaction.<br><br>A transaction is started, the records are locked in read/write during the execution of the query and they are unlocked at the end of its execution, regardless of the result (failure or success). The transaction is always completed at the end of the execution of **&lt;Source&gt;.ExecuteSQLQuery**. There is no data source for write requests: there is no need to call [&lt;Source&gt;.FreeQuery](../WDLang4/1000025011.md).<br><br>If the query cannot end properly (locked records, power failure, ...), the transaction is canceled and the data files are restored to their previous status (before the query execution).<br><br>Remark: the query execution may be slowed down.<br>![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) This constant is not available. |
| *hYield* | For the UPDATE and DELETE queries performed on an important number of records and being quite long, this constant is used to give control back to the other applications that use the data files.<br>In this case:<br><br>- the query runs a little slower,<br><br>- the file server is not saturated,<br><br>- the other users of data files can still access them without being  locked.<br><br><br>![HFSQL Client/Server](https://doc.pcsoft.fr/ext/images/us/HFCS.png)![OLE DB](https://doc.pcsoft.fr/ext/images/us/OLEDB.png)![Native Connectors (Native Accesses)](https://doc.pcsoft.fr/ext/images/us/AN.png) This constant is ignored.<br><br>![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Access by JDBC: This constant is not available. |

![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Access by JDBC: This parameter can only be set to *hQueryDefault*. An error is generated if another constant is used.

**`<SQL query text>: (Character string)`**

Text of the SQL query to execute. This text can correspond to a character string in ANSI format or in Unicode format.  
![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) You cannot execute queries with SQL code containing WLanguage functions. This parameter is not available if &lt;Query name&gt; corresponds to an SQL Query variable.


<a name="SYNTAX2"></a>

### Executing an SQL query via a connection

`<Result> = <Source>.ExecuteSQLQuery([<Connection> [, <Mode>, ]] <SQL query text>)`
---

**`<Result>: (Boolean)`**



- <u><u><u><u>True</u></u></u></u> if the SQL query was initialized,

- <u><u><u><u>False</u></u></u></u> otherwise. [HErrorInfo](../WDLang4/3044071.md) returns more details about the problem.




**`<Source>: (Type corresponding to the source)`**



- Name of the query that will be executed. This name is used to handle the result of &lt;Text of SQL query&gt; through programming. If a query with the same name is already declared, it is replaced with the new query.

- Name of the [SQL query](../Motscles/1514082.md) variable containing the SQL code of the query. In this case, the &lt;Text of SQL query&gt; parameter must not be specified. 
	




**`<Connection>: (Optional character string or Connection variable)`**

Connection used to execute the query. This connection corresponds to: 

- a connection defined in the data model editor or through programming with [HDescribeConnection](../WDLang4/3044205.md) or [&lt;Connection variable&gt;.OpenConnection](../WDLang4/1000023962.md). 

- a variable of type [Connection](../WDLang4/1514073.md). 


&lt;Result&gt; is set to False if this parameter does not correspond to an existing connection.

**`<Mode>: (Optional Integer constant)`**

Option for initializing the query:


|   |   |
| --- | --- |
| *hCheckDuplicates* | Enables the management of duplicates during the query execution. The query is not run if a duplicate error occurs.<br>This constant forces the *hWithTransaction* constant. A transaction is started, the records are locked in read/write during the execution of the query and they are unlocked at the end of its execution, regardless of the result (failure or success). If the application is stopped during the query execution (power outage for example), the transaction will be canceled when the data files of query are used for the next time.<br>Remark: the query execution may be slowed down.<br>![HFSQL Client/Server](https://doc.pcsoft.fr/ext/images/us/HFCS.png) The management of duplicates is disabled if the *hCheckDuplicates* constant is not specified. An error occurs if the user has no rights to disable the management of duplicates ([&lt;Connection variable&gt;.ModifyDatabaseRights](../WDLang4/1000022707.md)) and if the management of duplicates must be performed.![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Access by JDBC: This constant is not available. |
| *hCheckIntegrity* | Enables the management of integrity during the query execution. The query is not run if an integrity error occurs.<br>This constant forces the **hWithTransaction** constant. A transaction is started, the records are locked in read/write during the execution of the query and they are unlocked at the end of its execution, regardless of the result (failure or success). If the application is stopped during the query execution (power outage for example), the transaction will be canceled when the data files of query are used for the next time.<br>Remark: the query execution may be slowed down.<br>![HFSQL Client/Server](https://doc.pcsoft.fr/ext/images/us/HFCS.png) The integrity management is disabled if the *hCheckIntegrity* constant is not specified. However, an error occurs if an integrity management operation must be performed, but the user does not have the rights to disable integrity management ([&lt;Connection variable&gt;.ModifyDatabaseRights](../WDLang4/1000022707.md)).![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Access by JDBC: This constant is not available. |
| *hModifyFile*<br>(constant that can be combined with the other constants) | <br><br>- **On HFSQL data files**: When the result of the query is modified ([&lt;Source&gt;.Add](../WDLang4/1000024187.md), [&lt;Source&gt;.Write](../WDLang4/1000024331.md), [&lt;Source&gt;.Modify](../WDLang4/1000025038.md), [&lt;Source&gt;.Cross](../WDLang4/1000025051.md) and [&lt;Source&gt;.Delete](../WDLang4/1000025071.md)), these changes are propagated to the data files used in the query. <br>	If this option is not specified, only the query result is modified. <br>	For more details on modifying the result of a query, see [Modifying the query content](../WDLang4/3044234.md).<br><br>- **On data files in a format other than HFSQL**, the *hModifyFile* constant is useless: the data files taking part in the query are automatically modified when modifying the query result.<br><br><br>![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Access by JDBC: This constant is not available. |
| *hNoBind* | ![Native Connectors (Native Accesses)](https://doc.pcsoft.fr/ext/images/us/AN.png) SQL Server, Oracle, Sybase: Used to execute a query without enabling the bind option. |
| *hNoHourglass* | When reading the query result, this read operation may be locked and an hourglass appears by default. This constant is used not to display the hourglass in this case. <br><br> |
| *hQueryBreakable* | The query initialization can be interrupted by the ESC key.<br>![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png) This constant is not available.![Stored procedures](https://doc.pcsoft.fr/ext/images/us/PS.png) This constant is ignored.![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Access by JDBC: This constant is not available.![HFSQL Client/Server](https://doc.pcsoft.fr/ext/images/us/HFCS.png) The Esc key must be pressed on the client computer. |
| *hQueryDefault*<br>(default value) | The query is initialized without interruption. |
| *hQueryWithoutCorrection* | ![OLE DB](https://doc.pcsoft.fr/ext/images/us/OLEDB.png)![Native Connectors (Native Accesses)](https://doc.pcsoft.fr/ext/images/us/AN.png) No check is performed by the HFSQL engine. This option must be used if the query contains commands specific to a connection type (Oracle, SQL Server, etc.).<br><br>Caution: if this constant is used:<br><br>- the connection name must be specified (&lt;Connection&gt; parameter).<br><br>- the following function cannot be used on the query: [&lt;Source&gt;.Filter](../WDLang4/1000024960.md).<br><br>- you cannot cancel a condition by assigning it to NULL. <br><br>- no browse item should be specified in the following functions: [&lt;Source&gt;.Last](../WDLang4/1000024217.md), [&lt;Source&gt;.ReadLast](../WDLang4/1000025020.md), [&lt;Source&gt;.ReadFirst](../WDLang4/1000025022.md), [&lt;Source&gt;.First](../WDLang4/1000025048.md), [&lt;Source&gt;.SeekLast](../WDLang4/1000025054.md), [&lt;Source&gt;.SeekFirst](../WDLang4/1000025055.md).<br><br><br> |
| *hQueryWithoutHFCorrection* | ![HFSQL Classic](https://doc.pcsoft.fr/ext/images/us/HF.png) The file format (filled with space characters or not) is not checked by the HFSQL engine. To be used if the query handles both HFSQL data files in a format that completes items with space characters and HFSQL data files in a format that does not complete items with space characters.![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png) This constant is not available. |
| *hSQLUnicode* | Used to specify that the query text must be sent in Unicode format to the server. |
| *hWithFilter*<br>(constant that can be combined with the other constants) | If this parameter is specified:<br><br>- the query result corresponds to a selection of filter records if the HFSQL engine allows it. Otherwise, the query result corresponds to an HFSQL view.<br><br>- the following operations are not allowed: search on the query result, query on query, view on query, query on view.<br><br>- the browse items specified when looping through the result are ignored.<br><br>- the magnifier is not available for the tables linked to a query.<br><br>- [&lt;Source&gt;.NbRec](../WDLang4/1000025040.md) cannot be used on the queries. <br>	Caution: if this option is specified, the *hModifyFile* constant is automatically used.<br><br><br>If this parameter is not specified (by default):<br><br>- the query result corresponds to an HFSQL view.<br><br>- the following operations can be performed on the queries: search on the query result, query on query, view on query, query on view.<br><br>- the browse items specified when looping through the result are taken into account.<br><br>- the magnifier is available on tables linked to a query.<br><br>- [&lt;Source&gt;.NbRec](../WDLang4/1000025040.md) can be used on the queries.<br><br><br>![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Access by JDBC: This constant is not available. |
| *hWithTransaction* | Only write requests (INSERT, UPDATE and DELETE) are executed in a transaction.<br><br>A transaction is started, the records are locked in read/write during the execution of the query and they are unlocked at the end of its execution, regardless of the result (failure or success). The transaction is always completed at the end of the execution of **&lt;Source&gt;.ExecuteSQLQuery**. There is no data source for write requests: there is no need to call [&lt;Source&gt;.FreeQuery](../WDLang4/1000025011.md).<br><br>If the query cannot end properly (locked records, power failure, ...), the transaction is canceled and the data files are restored to their previous status (before the query execution).<br><br>Remark: the query execution may be slowed down.<br>![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) This constant is not available. |
| *hYield* | For the UPDATE and DELETE queries performed on an important number of records and being quite long, this constant is used to give control back to the other applications that use the data files.<br>In this case:<br><br>- the query runs a little slower,<br><br>- the file server is not saturated,<br><br>- the other users of data files can still access them without being  locked.<br><br><br>![HFSQL Client/Server](https://doc.pcsoft.fr/ext/images/us/HFCS.png)![OLE DB](https://doc.pcsoft.fr/ext/images/us/OLEDB.png)![Native Connectors (Native Accesses)](https://doc.pcsoft.fr/ext/images/us/AN.png) This constant is ignored.<br><br>![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Access by JDBC: This constant is not available. |

![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Access by JDBC: This parameter can only be set to *hQueryDefault*. An error is generated if another constant is used.

**`<SQL query text>: (Character string)`**

Text of the SQL query to execute. This text can correspond to a character string in ANSI format or in Unicode format.  
![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) You cannot execute queries with SQL code containing WLanguage functions. This parameter is not available if &lt;Query name&gt; corresponds to an SQL Query variable.



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks


### Reading data during the initialization
<a name="reading_data_during_the_initialization_ELTPARAGRAPHE001048"></a>

Depending on the query (sorts, groups, ...), the data can be read (or not) when the initializing the query.

Remark: [ExecutionCompleted](../Proprietes/2512117.md) is used to find out whether the query is entirely filled (and therefore if the read operations can be performed without lock). 

To access the query items, a [Data source](../WDLang4/1514053.md) variable must be declared (see the Example section). The query is automatically freed when the variable is destroyed. To force the resources of this query to be freed, use: 

- the [&lt;Source&gt;.CancelDeclaration](../WDLang4/1000024192.md) function.

- the [&lt;Source&gt;.FreeQuery](../WDLang4/1000025011.md) function.



<a name="NOTE0_2"></a>


### Text of SQL query
<a name="text_sql_query_ELTPARAGRAPHE001083"></a>

If a name of a data file or a name of item contains space characters, these names must be enclosed in square brackets in the query text. For example:

```sql
SELECT [My File1].MyItem, [My File1].[My item1], MyFile2.[My key1]
From [My File1], MyFile2
WHERE [My File1].[My key1] = MyFile2.[My key1]
```

<a name="NOTE0_2A"></a>


### Executing an SQL query with parameters
<a name="executing_sql_query_with_parameters_ELTPARAGRAPHE001095"></a>

An SQL query can contain parameters. To execute this type of SQL query with **&lt;Source&gt;.ExecuteSQLQuery**, you must: 

1. In the text of SQL query, define the different parameters by using the {Parameter Name} notation. For example: 
	```txt
	"SELECT * FROM client WHERE name={p_name}"
	```


2. Define a **Data source** variable. The name of this variable must correspond to the &lt;Query name&gt; parameter of **&lt;Source&gt;.ExecuteSQLQuery**. 
	Example: 
	
	```wl
	MyQuery is Data Source
	```


3. Specify the value of parameters, via the following syntax: 
	
	```txt
	<Data source variable >.<Name of Parameter1> = xxx
	<Data source variable >.<Name of Parameter2> = xxx
	<Data source variable >.<Name of Parameter3> = xxx
	```

	Example: 
	
	```wl
	MyQuery.p_name = "Doe"
	```


4. Execute the SQL query with **&lt;Source&gt;.ExecuteSQLQuery**. Example: 
	
	```wl
	MyQuery.ExecuteSQLQuery("SELECT * FROM customer WHERE name={p_name}")
	```





**Remark**: The structure of the query parameters is reset each time the query is executed.


<a name="NOTE0_2B"></a>


### SQL injection: How to prevent it? 
<a name="sql_injection_how_prevent_ELTPARAGRAPHE001143"></a>

SQL injection is a hacking technique. It consists of injecting SQL code into the parameters of queries, forcing the execution of unwanted SQL code. 

To avoid SQL injection when executing queries via **&lt;Source&gt;.ExecuteSQLQuery**, you must use queries with parameters and you must not concatenate strings to build the query. 

Example: 


```wl
// Build the query by concatenation
// -> security failure
dsQuery is Data Source
dsQuery.ExecuteSQLQuery("SELECT * FROM customer WHERE name = '" + EDT_Name + "'")
```

```wl
// Using parameters in the query
// -> secure code
dsQuery is Data Source
sdQuery.p_name = EDT_Name
dsQuery.ExecuteSQLQuery("SELECT * FROM customer WHERE name={p_name}")
```


Using parameters does not allow to interpret the content as being SQL code. This technique prevents from injecting SQL code and it eliminates several risks of hacking.


<a name="NOTE0_3"></a>
![Native Connectors (Native Accesses)](https://doc.pcsoft.fr/ext/images/us/AN.png) 

### Joins
<a name="joins_ELTPARAGRAPHE001181"></a>

The joins such as (A join B on x=y) join C on y=z ... are not automatically supported by Native Accesses: in this case, the *hQueryWithoutCorrection* constant must be used to manage this type of join.

**Remark**: These joins are managed by the HFSQL engine.
<a name="NOTE0_4"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![User code (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png)![OLE DB](https://doc.pcsoft.fr/ext/images/us/OLEDB.png)![Native Connectors (Native Accesses)](https://doc.pcsoft.fr/ext/images/us/AN.png) 

### Why should the hQueryWithoutCorrection constant be used?
<a name="why_should_the_hquerywithoutcorrection_constant_used_ELTPARAGRAPHE001195"></a>

By default, WINDEV and WEBDEV interpret the SQL queries:

- built via a Native Access, 

- built on OLEDB and on ODBC via the OLE DB provider.




**In order for the query not to be interpreted, use the hQueryWithoutCorrection constant.**

| hQueryWithoutCorrection is not specified | hQueryWithoutCorrection is specified |
| --- | --- |
| The connection associated with the data files in the query is defined automatically. | The connection to use must be specified in **&lt;Source&gt;.ExecuteSQLQuery**. |
| All PC SOFT proprietary signs are replaced (e.g.: ']=' starts with) with their equivalent in standard SQL. | No replacement is performed. The standard SQL symbols must be used. |
| Format the dates and times according to the format used by the database.<br>For example, the dates are in 'YYYYMMDD' format in WINDEV and WEBDEV while in Access, the dates are in #YYYYDDMM# or #YYYYMMDD# format depending on the system language. | No formatting is performed. The format recognized by the database must be used. |
| Floats are formatted (the decimal separator can be '.' or ',') | No formatting is performed for the floats. |
| Depending on the database used, the alias names are replaced with the full names of the items in Where, Order by and Group by<br>For example, the JET engine (Access, dBase, etc.) accepts no alias name in the Where clause of a query | No replacement is performed. The full names of items must be used in the query code for Where, Order by and Group by. |


![Native Connectors (Native Accesses)](https://doc.pcsoft.fr/ext/images/us/AN.png) **Special case: Oracle and SQL Server**: If the query to be executed contains a script with ":param" (Oracle) or "@param" (SQL Server), you must use the *hQueryWithoutCorrection* + *hNoBind* constants in order for the query not to be interpreted.
<a name="NOTE0_5"></a>


### Condition on a composite key in an SQL query
<a name="condition_composite_key_sql_query_ELTPARAGRAPHE001249"></a>

To define a condition on a composite key in an SQL query, **the conditions must be specified for each component of the key**.

Do not attempt to directly assign a value to the composite key (indeed, the composite keys are stored as binary values).

**Example**: The composite key is made of LASTNAME and FIRSTNAME items (LASTNAMEFIRSTNAME item):


```sql
SELECT MyFile.MyItem, MyFile.MyItem1
FROM MyFile
WHERE FileName.LastName = "Smith" and FileName.FirstName = "Fred"
```

<a name="NOTE0_6"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png)![User code (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png)![External language](https://doc.pcsoft.fr/ext/images/us/LE.png)![HFSQL Classic](https://doc.pcsoft.fr/ext/images/us/HF.png)![HFSQL Client/Server](https://doc.pcsoft.fr/ext/images/us/HFCS.png)![Hyper File 5.5](https://doc.pcsoft.fr/ext/images/us/HF55.png) 

### Queries on HFSQL data files
<a name="queries_hfsql_data_files_ELTPARAGRAPHE001267"></a>

- To optimize the query execution time, use [&lt;Source&gt;.Optimize](../WDLang4/1000025042.md) or [HOptimizeQuery](../WDLang4/3044003.md) on the different data files included in the query.

- To optimize query processing time, use [&lt;Source&gt;.StatCalculate](../WDLang4/1000025064.md) or [&lt;Source&gt;.Index](../WDLang4/1000025060.md) on all data files included the query.

- **When running an INSERT query on HFSQL data files**, the following syntax allows you to find out the automatic identifier assigned to the new record: &lt;File name&gt;.&lt;Name of automatic identifier Item&gt;.

- If the data files used by the query are not found in the analysis, these files must be dynamically declared by [&lt;Source&gt;.DeclareExternal](../WDLang4/1000024214.md). Otherwise, an error such as **Unable to initialize the query. Unknown FILENAME** will occur. 
	Example:
	
	```wl
	// Connection parameters
	cntDatabase is Connection
	cntDatabase.Provider = hAccessHFClientServer
	cntDatabase.Server = "HYPERFILESQLSERVER:4900"
	cntDatabase.Database = "Database"
	cntDatabase.User = "admin"
	cntDatabase.Password = ""
	
	// Connect to the database
	IF cntDatabase.OpenConnection() = False THEN
		Error(HErrorInfo())
		RETURN
	END
	
	// Dynamic declaration of the files used in the query
	IF HDeclareExternal("CUSTOMER.FIC", "CUSTOMER", cntDatabase) = False THEN
		Error(HErrorInfo())
		RETURN	
	END
	
	// Execute query
	dsSQLQuery is Data Source
	IF NOT dsSQLQuery.ExecuteSQLQuery(cntDatabase, hQueryDefault,[
	SELECT * 
	FROM CUSTOMER
	]) THEN 
		Error("Runtime problem")
	ELSE
		Info("ok")
	END
	
	// Cancels the dynamic file declaration
	HCancelDeclaration("CUSTOMER")
	
	// Closes the connection to the database
	cntDatabase.CloseConnection()
	```





- **When running an INSERT/UPDATE/DELETE query on HFSQL data files**, the number of added, modified or deleted records is returned by the [H.NbRecModificationQuery](../WDLang4/3087002.md) variable.



<a name="NOTE0_7"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![User code (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png)![Native Connectors (Native Accesses)](https://doc.pcsoft.fr/ext/images/us/AN.png) 

### Optimization for running the same query several times (native Oracle and SQL Server accesses only)
<a name="optimization_for_running_the_same_query_several_times_native_oracle_and_sql_server_accesses_only_ELTPARAGRAPHE001321"></a>

To optimize the execution of a query run several times, you can:

1. Declare a data source. This data source will contain the result of the SQL query.

2. Declare the different parameters or variables of the query. 
	The parameters are string parameters by default. You can specify their type with the [Type](../Proprietes/2510131.md) property. 
	**Caution**: To specify the type of a query variable, you can also assign an existing variable of the desired type to this variable.

3. Prepare the query with [&lt;Source&gt;.PrepareSQLQuery](../WDLang4/1000025050.md).

4. Specify the value of different parameters to take into account and run the query with **&lt;Source&gt;.ExecuteSQLQuery**. Only the name of the data source that corresponds to the query must be specified.




This last step must be repeated as many times as necessary.

**Example**:


```wl
// Run the same query in Oracle
// Declare a data source
// This data source corresponds to the query. 
Insert1 is Data Source
// Declare one of the query parameters
// This parameter is an integer
Insert1.age = 0
i is int
// Prepare the query for multiple executions
Insert1.PrepareSQLQuery(MyConnection, ...
	hQueryWithoutCorrection, ...
	"INSERT INTO PERSONE VALUES (:lastname,:firstname,:age )")
// Loop for running the query
// Only some parameters are modified
FOR I = 1 TO 10
	Insert1.lastname = "LastName" + i
	Insert1.firstname = "FirstName" + i
	Insert1.age = i
	Insert1.ExecuteSQLQuery()
END
```

<a name="NOTE0_8"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![User code (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png)![Native Connectors (Native Accesses)](https://doc.pcsoft.fr/ext/images/us/AN.png) 

### Specifying and retrieving values when running a query (stored procedures)
<a name="specifying_and_retrieving_values_when_running_query_stored_procedures_ELTPARAGRAPHE001365"></a>

**Remark**: This note applies only to Native Oracle and SQL Server Connectors.

To specify and retrieve values when running a query, you must:

1. Declare a data source. This data source will contain the result of the SQL query.

2. Declare the different query variables (input and output variables). 
	The variables are string variables by default. You can specify their type with the [Type](../Proprietes/2510131.md) property. 
	**Caution**: To specify the type of a query variable, you can also assign an existing variable of the desired type to this variable.

3. Run the query with **&lt;Source&gt;.ExecuteSQLQuery**.



```wl
// Example for Oracle
// Declare the data source associated with the query
MyProc is Data Source
// Declare the variables
MyProc.n = 3
MyProc.str = "Example"
// Run the query and retrieve the result
MyProc.ExecuteSQLQuery(MyConnection, hQueryWithoutCorrection, ...
	"begin:Res:=sp_cut(:n,:str);end;")
// Display the result 
Info(MyProc.Res)
```


**Remarks**

- **&lt;Source&gt;.ExecuteSQLQuery** must be used with:

	- the connection name,

	- the *hQueryWithoutCorrection* constant.




- The variables declared in the query must be identical to the ones used in the code of stored procedure. Otherwise, a WLanguage error occurs.

- In the call to the stored procedure, you must use the syntax specific to the database used, including for the parameters.
	Therefore, for Oracle, the parameters are specified with the **:ParamName** syntax. Caution: The ":" character must be followed by at least one letter (the syntax:1 is not allowed). 
	In SQL Server, the parameters are specified via the following notation: **@ParamName**.
	The same parameter can be used several times. In this case, the corresponding variable will be reused.

- To execute a query without enabling the bind option, use the ***hNoBind*** constant in **&lt;Source&gt;.ExecuteSQLQuery**. 



<a name="NOTE0_9"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png)![User code (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png)![HFSQL Classic](https://doc.pcsoft.fr/ext/images/us/HF.png)![HFSQL Client/Server](https://doc.pcsoft.fr/ext/images/us/HFCS.png)![Hyper File 5.5](https://doc.pcsoft.fr/ext/images/us/HF55.png)![OLE DB](https://doc.pcsoft.fr/ext/images/us/OLEDB.png)![Native Connectors (Native Accesses)](https://doc.pcsoft.fr/ext/images/us/AN.png) 

### SQL queries (&lt;Source&gt;.ExecuteSQLQuery or execution of SQL queries created in the query editor)
<a name="sql_queries_docparampagetitleshort_execution_sql_queries_created_the_query_editor_ELTPARAGRAPHE001419"></a>

**When using the DELETE, INSERT or UPDATE SQL statements in SQL queries, no integrity check and no duplicate check are performed by default on an HFSQL database.**

**To perform an automatic integrity check**, simply specify the *hCheckIntegrity* constant. This constant is used to enable the management of integrity during the query execution. The query is not run if an integrity error occurs. Indeed, a transaction is started, the records are locked in write mode during the execution of the query and they are unlocked at the end of its execution, regardless of the result (failure or success). If the application is stopped during the query execution (power outage for example), the transaction will be canceled when the data files of query are used for the next time.
![HFSQL Client/Server](https://doc.pcsoft.fr/ext/images/us/HFCS.png) The integrity management is disabled if the *hCheckIntegrity* constant is not specified. However, an error occurs if an integrity management operation must be performed, but the user does not have the rights to disable integrity management ([&lt;Connection variable&gt;.ModifyDatabaseRights](../WDLang4/1000022707.md)).**To perform an automatic check for duplicates**, simply specify the *hCheckDuplicates* constant. This constant is used to enable the management of duplicates during the query execution. The query is not run if a duplicate error occurs. Indeed, a transaction is started, the records are locked in write mode during the execution of the query and they are unlocked at the end of its execution, regardless of the result (failure or success). If the application is stopped during the query execution (power outage for example), the transaction will be canceled when the data files of query are used for the next time.

Note: If a duplicates error occurs when running an UPDATE query, **HExecuteQuery** returns False and the process continues. The corresponding error is returned by [HErrorInfo](../WDLang4/3044071.md).

**Remark**: The default automatic assistance is not called for the duplicate errors. A simple duplicate error is generated. If an assistance was redefined by [&lt;Source&gt;.OnError](../WDLang4/1000025073.md), then this custom assistance is called.
![HFSQL Client/Server](https://doc.pcsoft.fr/ext/images/us/HFCS.png) The management of duplicates is disabled if the *hCheckDuplicates* constant is not specified. An error occurs if a duplicate management operation must be performed, but the user has no rights to disable the management of duplicates ([&lt;Connection variable&gt;.ModifyDatabaseRights](../WDLang4/1000022707.md)).**The UPDATE, DELETE and INSERT queries only exist during their execution**. No WLanguage function can be used on this type of query after its execution.
<a name="NOTE0_10"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png)![User code (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png)![External language](https://doc.pcsoft.fr/ext/images/us/LE.png)![HFSQL Classic](https://doc.pcsoft.fr/ext/images/us/HF.png)![OLE DB](https://doc.pcsoft.fr/ext/images/us/OLEDB.png)![Native Connectors (Native Accesses)](https://doc.pcsoft.fr/ext/images/us/AN.png) 

### Looping through queries ([&lt;Source&gt;.ReadFirst](../WDLang4/1000025022.md))
<a name="looping_through_queries_readfirstwdlang41000025022md_ELTPARAGRAPHE001465"></a>

- By default, [&lt;Source&gt;.ReadFirst](../WDLang4/1000025022.md) re-runs the query to refresh the result. It is recommended to use the *hNoRefresh* constant to avoid re-running the query. 

- **Looping through a query executed with the hQueryWithoutCorrection option**:
	To browse the records in the order returned by the database, there is no need to specify a browse item. Example:
	
	```wl
	MyQuery.ReadFirst(hNoRefresh)
	```

	If a browse item is specified, the query result is entirely retrieved and indexed. The iteration is performed on the specified item. The initial sort of the query (specified by ORDER BY) is ignored. The created index (in HFSQL format) is sensitive to the case, to the punctuation, to the accented characters and in ascending order.
	Example:
	
	```wl
	MyQuery.ReadFirst("MyItem", hNoRefresh)
	```

	The created index is used to perform searches on the query result.




<a name="XComponent"></a>

## Component:
wd280hf.dll
