


## &lt;Source&gt;.FilterStartsWith (Function)

***In french: &lt;Source&gt;.FiltreCommencePar***



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Defines and enables a "Start with" filter on a data file, a view or a query. The "Starts with" filter is used to select all the records that start with a specific set of characters.

After its execution, the most common cases are as follows:

- the filter cannot be created: **&lt;Source&gt;.FilterStartsWith** returns an empty string (""). [HError](../WDLang4/3044088.md) returns the error identifier.

- the filter is created: **&lt;Source&gt;.FilterStartsWith** returns the optimized search key that will be used to loop through the data file, view or query.



<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
MyKey is string
// Selects all the customers living in a city whose name starts with "Mon"
MyKey = Customer.FilterStartsWith(City, "Mon")
Customer.ReadFirst(MyKey)
WHILE NOT Customer.Out()
	Trace(Customer.CustomerName, Customer.City) 
	Customer.ReadNext(MyKey)
END
...
// Cancels the filter
Customer.DeactivateFilter()
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Syntax

`<Result> = <Source>.FilterStartWith(<Key item> , <Filter for key component 1> [, <Filter for key component 2> [... [, <Filter for key component N>]]])`
---

**`<Result>: (Character string)`**

Browse item. Corresponds to:

- the search key of data file if the filter is enabled

- an empty string if the filter cannot be implemented




**`<Source>: (Type corresponding to the specified source)`**

Name of the HFSQL data file, view or query used.

**`<Key item>: (Character string)`**

Name of key item onto which the filter will be applied. This item can be a simple key (string) or a composite key. 
Caution: this parameter must only contain letters, underlined characters or digits. Otherwise, the name must be enclosed in quotes.

**`<Filter for key component 1>: (Type of the first component of the search key)`**

Value sought for the first component (the 2nd respectively) of composite key. 
The type of this parameter must correspond to the type of key item component on which the filter is defined.

**`<Filter for key component 2>: (Type of the second component of the search key)`**

Value sought for the second component of composite key. 
The type of this parameter must correspond to the type of key item component on which the filter is defined.



**`<Filter for key component N>: (Type of the N component of the search key)`**

Value sought for component N of composite key . 
The type of this parameter must correspond to the type of key item component on which the filter is defined.



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks


### Automatic management of composite keys
<a name="automatic_management_composite_keys_ELTPARAGRAPHE000259"></a>

**&lt;Source&gt;.FilterStartsWith** automatically manages the filters on the simple keys and on the composite keys.

**Example 1:** Using **&lt;Source&gt;.FilterStartsWith** with a simple key:

```wl
Res = Customer.FilterStartsWith(Name, "A")
// Select all the customers whose name starts with A
```


**Example 2:** Using **&lt;Source&gt;.FilterStartsWith** with a key made of two items:

```wl
// Example where LastNameFirstname is a key made of: LastName + FirstName
// Select all customers whose last name is SMITH 
// and whose first name starts with a C
Res = Customer.FilterStartsWith(LastNameFirstName, "SMITH", "C")
```
This line of code is equivalent to:

```wl
Customer.Filter(LastNameFirstName, ...
	Customer.BuildKeyValue(LastNameFirstName, "SMITH", "C") + hMinVal, ...
	Customer.BuildKeyValue(LastNameFirstName, "SMITH", "C") + hMaxVal)
```


**Example 3:** Using **&lt;Source&gt;.FilterStartsWith** with a key made of three items:

```wl
// Example with CUSTINV a key made of: CustomerNumber+Date+Time
// All the invoices of a customer (sorted by date)
Res = Invoice.FilterStartsWith(CUSTINV, Customer.CustNum)
```
This line of code is equivalent to:

```wl
Invoice.Filter(CustInv, ...
	Invoice.BuildKeyValue(CustInv, Customer.CustNum) + hMinVal, ...
	Invoice.BuildKeyValue(CustInv, Customer.CustNum) + hMaxVal)
```


**Example 4:** Using **&lt;Source&gt;.FilterStartsWith** with a key made of three items:

```wl
// Example with CUSTINV a key made of: CustomerNumber+Date+Time
// All the invoices of a customer for today (sorted by date)
Res = Invoice.FilterStartsWith(CustInv, Customer.CustNum, Today())
```
This line of code is equivalent to:

```wl
Invoice.Filter(CustInv, ...
	Invoice.BuildKeyValue(CustInv, Customer.CustNum, Today()) + hMinVal, ...
	Invoice.BuildKeyValue(CustInv, Customer.CustNum, Today()) + hMaxVal)
```

<a name="NOTE0_2"></a>


### Filter and filtered iteration:
<a name="filter_and_filtered_iteration_ELTPARAGRAPHE000332"></a>

After the call to **&lt;Source&gt;.FilterStartsWith**, the iteration must be performed on the item returned by **&lt;Source&gt;.FilterStartsWith**. The filter will be ignored if another item is used to loop through the data file.

When a filter is defined and enabled on a data file (view or query), all records read correspond to the filter. If no other record corresponds to the filter during the iteration: 

- [&lt;Source&gt;.Out](../WDLang4/1000024605.md) returns <u><u><u><u>True</u></u></u></u>

- the current record corresponds to the last record read with the filter. 




For example:

| On a filtered data file, after the function: | the current record is: |
| --- | --- |
| **HReadFirst** | the first file record corresponding to the filter. |
| **HReadLast** | the last file record corresponding to the filter. |
| **HReadNext** (or **HForward**) | the next record (or the next Nth record) corresponding to the filter. |
| **HReadPrevious** (or **HBackward**) | the previous record (or the previous Nth record) corresponding to the filter. |


<a name="NOTE0_3"></a>


### Enabling/Disabling a filter
<a name="enablingdisabling_filter_ELTPARAGRAPHE000381"></a>

- [&lt;Source&gt;.DeactivateFilter](../WDLang4/1000024218.md) is used to disable a filter.

- [&lt;Source&gt;.ActivateFilter](../WDLang4/1000023969.md) is used to re-enable a filter.

- The filter is deleted when the data file (query or view) is closed ([&lt;Source&gt;.Close](../WDLang4/1000024958.md) for example).

- A single filter can exist at a given time on a data file (query or view). If **&lt;Source&gt;.FilterStartsWith** is used several times, only the last filter will be taken into account: the previous filters will be deleted. Similarly, if several filters are used (HFilter\* functions), only the last filter will be taken into account.



<a name="NOTE0_4"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![User code (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png)![Stored procedures](https://doc.pcsoft.fr/ext/images/us/PS.png)![OLE DB](https://doc.pcsoft.fr/ext/images/us/OLEDB.png)![Native Connectors (Native Accesses)](https://doc.pcsoft.fr/ext/images/us/AN.png) 

### Non-HFSQL data files
<a name="nonhfsql_data_files_ELTPARAGRAPHE000411"></a>

- The filter can be created from any item (key or non-key item).

- The filter is automatically enabled when **&lt;Source&gt;.FilterStartsWith** is executed.

- The changes made with **&lt;Source&gt;.FilterStartsWith** and [&lt;Source&gt;.DeactivateFilter](../WDLang4/1000024218.md) are only taken into account when the following functions are used [&lt;Source&gt;.ReadFirst](../WDLang4/1000025022.md)/[&lt;Source&gt;.First](../WDLang4/1000025048.md), [&lt;Source&gt;.ReadLast](../WDLang4/1000025020.md)/[&lt;Source&gt;.Last](../WDLang4/1000024217.md) or [&lt;Source&gt;.ReadSeekFirst](../WDLang4/1000025034.md)/[&lt;Source&gt;.ReadSeekLast](../WDLang4/1000025024.md).




<a name="XComponent"></a>

## Component:
wd280hf.dll
