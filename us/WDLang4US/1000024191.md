


## &lt;Source&gt;.Alias (Function)

***In french: &lt;Source&gt;.Alias***



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Creates a logical alias of a data file (or a query). The alias can be canceled using [HCancelAlias](../WDLang4/3044149.md).

**Reminder**: An alias allows you to work on different physical data files that have the same logical description. For a data file for example, the file description is performed in the analysis and **&lt;Source&gt;.Alias** is used to create the file alias.

**Remark**: This help page explains how to create an alias with **&lt;Source&gt;.Alias**. Starting with version 27, you can use [Alias](../WDLang4/1000021446.md) variables. This type of variable makes it easier to create and handle aliases. 


<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
// Data Source variable to handle the alias after its creation
OrderCheck is Data Source <description=Order>
// New feature of version 19: 
// <description=Order> is used to get the code completion 
// for the items in the Order data file

// Create an alias for the "Order" data file
IF Order.Alias(OrderCheck) = True THEN
	// "OrderCheck" can now be used in processes.
	// It behaves like the "Order" file described in the analysis.
	// By default, the name of the associated physical file corresponds to 
	// the name of the data source, therfore "OrderCheck" in this case
	// To handle the same physical file as the initial one, the name must be modified:
    OrderCheck.ChangeName(Order.PhysicalName)
    	// To use a file on another database or in another directory,
	// then use <Source>.ChangeConnection or <Source>.ChangeDir
ELSE
    Error("Failure creating the alias", HErrorInfo())
    RETURN
END
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Syntax

`<Result> = <Source>.Alias([<Alias>])`
---

**`<Result>: (Boolean)`**



- <u><u><u><u>True</u></u></u></u> if the alias was created,

- <u><u><u><u>False</u></u></u></u> if a problem occurs. [HError](../WDLang4/3044088.md) is used to identify the error.




**`<Source>: (Type corresponding to the specified source)`**

Logical name of data file (defined in the analysis) or name of query (created with the query editor). 

**`<Alias>: (Optional character string or data source)`**

Name of alias, used to define the alias structure. 
If this parameter is not specified, all the aliases created for &lt;Source&gt; are canceled. The corresponding data files are closed. 
To optimize your applications, it is recommended to use a variable of type [Data source](../WDLang4/1514053.md) (see example). 

![OLE DB](https://doc.pcsoft.fr/ext/images/us/OLEDB.png)![Native Connectors (Native Accesses)](https://doc.pcsoft.fr/ext/images/us/AN.png) The &lt;Alias&gt; file uses the connection parameters of &lt;Source&gt; (parameters defined in the analysis).



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks


### Physical data file associated with an alias
<a name="physical_data_file_associated_with_alias_ELTPARAGRAPHE000296"></a>

By default, the name specified for the alias is assigned to the physical data file corresponding to the alias. 

To associate the alias with a physical data file of different name, you must use [&lt;Source&gt;.ChangeConnection](../WDLang4/1000024199.md), [&lt;Source&gt;.ChangeName](../WDLang4/1000024201.md) and [&lt;Source&gt;.ChangeDir](../WDLang4/1000024202.md).
<a name="NOTE0_2"></a>


### Canceling the aliases of one or more data files
<a name="canceling_the_aliases_one_more_data_files_ELTPARAGRAPHE000323"></a>

The cancelation of an alias ([HCancelAlias](../WDLang4/3044149.md)) closes the associated data file.

To cancel the aliases of a data file, all you have to do is use **&lt;Source&gt;.Alias** without specifying the alias name.


<a name="NOTE0_3"></a>


### Alias and queries
<a name="alias_and_queries_ELTPARAGRAPHE000346"></a>

- **If the query was created through programming** ([&lt;Source&gt;.ExecuteSQLQuery](../WDLang4/1000024948.md)), all you have to do is create the alias query with this same function while specifying the alias name.

- **If the query was created by the report editor**, to create an alias, you must:

	- run the initial query. 

	- use **&lt;Source&gt;.Alias** and specify the query name as well as the alias name. 

	- use [&lt;Source&gt;.ExecuteQuery](../WDLang4/1000024944.md) with the alias query. The alias is initialized.


 For example:
	
	```wl
	QueryAlias is Data Source
	QRY_Query1.Alias(QueryAlias)
	QueryAlias.ExecuteQuery()
	```

	**Remark**: When creating the alias, the initial query must have been run.
	Example:
	
	```wl
	QueryAlias is Data Source 
	IF QRY_Query1.ExecuteQuery() = False THEN
		Error(HError(hErrCurrent)) 
	ELSE
		QRY_Query1.ReadFirst()
		Trace(QRY_Query1.City)
		QRY_Query1.Alias(QueryAlias)
		IF QueryAlias.ExecuteQuery() = False THEN
			Error(HError(hErrCurrent)) 
		ELSE
			FOR EACH AliasQuery
				Trace(QueryAlias.City)
			END
		END
	END
	```




<a name="NOTE0_4"></a>


### Handling the alias file and its items
<a name="handling_the_alias_file_and_its_items_ELTPARAGRAPHE000392"></a>

- To avoid the "Unknown identifier" error when using an alias file and its items, it is recommended to use a [Data source](../WDLang4/1514053.md) variable to declare the name of the alias before using **&lt;Source&gt;.Alias**.
	For example: 
	
	```wl
	Order2018 is Data Source
	Order.Alias(Order2018)
	```

	Caution: the **Extern** keyword can be used but it slows down the execution significantly.

- In [&lt;Source&gt;.ReadSeek](../WDLang4/1000025023.md), [&lt;Source&gt;.ReadFirst](../WDLang4/1000025022.md), [&lt;Source&gt;.ReadNext](../WDLang4/1000025035.md), ... the name of the alias item cannot be used directly: the name of the item must be enclosed in quotes. 
	For example:
	
	```wl
	srcAlias.ReadSeekFirst("KeyItem", "Sought Value")
	```


- The name of the data source must not be used in quotation marks, if [Automatic naming of data sources](../Editeurs/9500218.md) is enabled in the project description window.

- In the window or page editor, controls cannot be directly bound to the items of the alias files. You must:

	- assign a value to each control in the code.

	- use the [ControlAlias](../WDLang1/3025016.md) function or the [DataBinding](../Proprietes/2510060.md) property before calling: 

		- the [&lt;Window&gt;.FromFile](../WDLang4/1000021387.md) function.

		- the [&lt;Page&gt;.FromFile](../WDLang4/1000021389.md) function.




- The alias files can be handled by [&lt;Source&gt;.ListItem](../WDLang4/1000025017.md) and [&lt;Source&gt;.ListKey](../WDLang4/1000025012.md). The alias files can be re-indexed.

- By default, the **password of an alias file** is identical to the password of the initial data file. This password can be modified by [&lt;Source&gt;.Password](../WDLang4/1000025044.md). The name of the data file used in this function corresponds to the name of the alias.

- The automatic modification of the data files can be applied to the alias files.

- The locks performed on the data files or on the records are effective between the aliases of the same instance of an application.



<a name="NOTE0_5"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png)![User code (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png)![Stored procedures](https://doc.pcsoft.fr/ext/images/us/PS.png) 

### Changing the directory or modifying the connection
<a name="changing_the_directory_modifying_the_connection_ELTPARAGRAPHE000496"></a>

If [&lt;Source&gt;.ChangeDir](../WDLang4/1000024202.md) or [HConnect](../WDLang4/3044263.md) have been used with the initial data file, these changes are ignored for the &lt;Alias&gt; data file. 

Once an alias was created, we recommend that you use [&lt;Source&gt;.ChangeConnection](../WDLang4/1000024199.md) to define the connection parameters on the alias file.

<a name="XComponent"></a>

## Component:
wd280hf.dll
