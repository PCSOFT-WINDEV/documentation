
## Indirection operators

***In french: Opérateurs d'indirection***
				



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
The **{** and **}** operators are used to access a control, a variable, a class member or a file item by dynamically building the name of control, variable, class member or item.

The **{** and **}** operators present several benefits:

- Passing in parameter the name of a control, variable, class member or item.

- Building the name of a control, variable, class member or item by programming.






![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Indirections cannot be performed local variables. **Indirections can only be performed on global variables only**. An indirection on a local variable of a process will trigger an "unknown element" error.




<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
// Simple indirection 
{"NAME"} = CustName               // Is equivalent to NAME=CustName 
{"NAME"} = {"CU.CUSTNAME"}        // Is equivalent to NAME=CU.CUSTNAME 
{"CUSTWIN.NAME"} = CustName        // Is equivalent to CUSTWIN.NAME=CustName 
{"CUSTWIN"+".NAME"} = CustName     // Is equivalent to CUSTWIN.NAME=CustName 

// Typed indirection
{"Name",indControl} = CustName

// Indirection with a file name in a variable
nFileName is string = "Customer"
{nfileName + ".Name"} = "Smith"
HAdd({nFileName})
```




<a name="Example2"></a>





```wl
// Indirection in a procedure 
ControlName is string
ControlName = "EDT_EDIT1"			// EDT_EDIT1 is the name of the control 
// Call to a procedure used to make a control invisible 
Make_Invisible(ControlName)

PROCEDURE Make_Invisible(ControlN)
{ControlN}..State = Invisible
```




<a name="Example3"></a>



```wl
// Indirection with variable 
AliasName is string
AliasName = PreviousWin()
// NAME is the name of the control 
// CustName is the value to assign 
{AliasName+".NAME"} = CustName 
Abbrev is string
ItemName is string
{Abbrev+"."+ItemName} = CustName 
{Abbrev+"."+ItemName} = {ControlName}
```


<a name="Example4"></a>





```wl
// Declare a class with two members 
MyClass is Class
Member1 is string
Member2 is string
// Display the value of a member 
GLOBAL PROCEDURE DisplayMember(Number)
// Retrieve the value of the selected global member
Value is string = {"::Member"+Number}
Info(Value)
```



```wl
c is cWord
var is Variable Description
Def is Definition

Def = GetDefinition(c)
// Retrieves the 2nd variable of the class: version of Word
var = Def..Variable[2]

Trace(var..Name) // Displays the variable name
Trace("Version of Word: ",{c, var}) // Displays the Word version
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Syntax

### Simple indirection

`{ <Expression> }
`
---

**`<Expression>: (Character string)`**

Expression used to identify the control, the variable or the item to use. The element is sought:

- among the variables, 

- according to the number of dots found in the name:

	- 0 dot: search among the controls, then the HFSQL items.

	- 1 dot: search among the HFSQL items, the controls, then the query parameters.

	- greater than 1 dot: search among the controls.




- among the special elements kept for compatibility (_Tabx for example, ...).




A WLanguage error occurs if this expression corresponds to an empty string ("").


<a name="SYNTAX2"></a>

### Indirection on a class member

`{ <Expression> }
`
---

**`<Expression>: (Character string)`**

Expression used to identify the class member.

If the member is:

- global, use the following syntax: {" **::** MemberName"}

- not global, use the following syntax: {" **:** MemberName"}




A WLanguage error occurs if this expression corresponds to an empty string ("").  


<a name="SYNTAX3"></a>

### Indirection on a class member

`{ <Class> , <Member> }`
---

**`<Class>: (Character string)`**

Class instance

**`<Member>: (Character string or Variable Description variable)`**

Corresponds to: 

- the member name,

- a variable of type [Variable Description](../WDLang1/1000019560.md). 





<a name="SYNTAX4"></a>

### Indirection by specifying the element type (optimizes the execution speed)

`{ <Expression>, <Type> }
`
---

**`<Expression>: (Character string)`**

Expression used to identify the control, the variable or the item to use. A WLanguage error occurs if this expression corresponds to an empty string ("").

**`<Type>: (Constant)`**

Constant used to specify the type of sought element:


|   |   |
| --- | --- |
| *indControl* | Element sought among the controls and the groups. |
| *indConnection* | Element sought among the connections. |
| *indReport* | Element sought among the reports.<br><br> |
| *indWindow* | Element sought among the windows.<br><br> |
| *indFile* | Element sought among the files. |
| *indGPW* | Element sought among the elements of user groupware (used to retrieve the initial status of controls).<br><br> |
| *indLink* | Element sought among the links.<br><br> |
| *indQueryParameter* | Element sought among the parameters of queries. |
| *indItem* | Element sought among the items. |
| *indVariable* | Element sought among the variables.<br><br>![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) The element is only sought within the global variables. |
| *indPage* | Element sought among the pages.<br><br> |





<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks




### When should indirection be used?
<a name="when_should_indirection_used_ELTPARAGRAPHE000231"></a>

The indirection can be used for example:

- To access the value of an element (control, variable, item, ...):
	
	```wl
	{s_ControlName} = 10
	ControlValue = {s_ControlName}
	```


- To use a property:
	
	```wl
	{s_ControlName}..Height = 10
	```


- To programmatically handle a column of a Table control:
	
	```wl
	{s_ColumnName, indControl}[Row_Number] = 10
	```


- To handle a table column:
	
	```wl
	{s_TableName + "." +s_ColumnName, indControl}[Row_Number] = 10
	```




<a name="NOTE0_2"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png)![User code (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png) 

### Identifying the current object
<a name="identifying_the_current_object_ELTPARAGRAPHE000253"></a>

To identify the current object, use the following keywords:

- [MySelf](../Motscles/1511007.md): the current control

- [MyWindow](../Motscles/1511004.md): the current window

- [MyPage](../Motscles/1511017.md): the current page
	![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) This keyword is not available.

- [MyReport](../Motscles/1511010.md): the current report
	![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) This keyword is not available.

- [MySource](../Motscles/1511001.md): the current data source.

- [MyFile](../Motscles/1511019.md): the file that released the current trigger.



<a name="NOTE0_3"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png)![User code (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png) 

### Indirection on a structure
<a name="indirection_structure_ELTPARAGRAPHE000289"></a>

An indirection cannot contain an access to a structure or to a class instance. However, the following syntax can be used:


```txt
{<Structure Variable>}: member
```

<a name="NOTE0_4"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png)![User code (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png) 

### Using the indirection on the paths in an XML document
<a name="using_the_indirection_the_paths_xml_document_ELTPARAGRAPHE000300"></a>

The indirection operators can be used to build the path of an element in an XML document. 

Example: 


```wl
sXMLDoc is string = [
	<?xml version="1.0" encoding="UTF-8"?>
	<Document>
	<VersionInfo>
	<Version>21</Version>
	<Year>2016</Year>
	</VersionInfo>
	</Document>
]

xmlExample is xmlDocument = XMLOpen(sXMLDoc, fromString)

sParam is string = "WINDEV_Param"

// Direct reading of information ...
Trace(xmlExample.Document.VersionInfo.Version) 

// Reading with part of XML path found in a variable
// (idem if file item, window parameter, method, ...)
sParameter is string = "VersionInfo"
Trace ({"xmlExample.Document." + sParameter + ".Version"})
```

<a name="NOTE0_5"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png)![User code (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png) 

### Limitations
<a name="limitations_ELTPARAGRAPHE000309"></a>

The indirection operator allows you to find an element by its name.

Therefore, the following operations are not allowed in an indirection:

- access to a member of a class instance or structure instance other than the instance of current class (: and:: ).

- access to a subelement in a control or array ([] operator).

- access to a substring ([[]] operator).

- access to a property (.. operator).

- indirection ({ } operator).

- call to a procedure or to a method of Automation object.

- declaring a variable.




**Remarks**:

- The operators for accessing an element (: and [] and [[]] and .. ) can be used on the element obtained by indirection. For example:
	
	```txt
	{ <indirection> }: member
	
	{ <indirection> } [subscript]
	
	{ <indirection> } [[ subscript ]]
	
	{ <indirection> }..property
	```


- To call a procedure identified by its name, use [Execute](../WDLang1/3013041.md) or [ExecuteProcess](../WDLang1/3013031.md).

- To combine these operations dynamically, you have the ability to use the dynamic compilation (see [Compile](../WDLang1/3013015.md)).



<a name="NOTE0_6"></a>
![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) 

### Indirections in Java
<a name="indirections_java_ELTPARAGRAPHE000377"></a>

Indirections are not supported by local variables


- ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=Theindirections.gif) ***Unit examples (WINDEV)*** : **The indirections** <br>Handling indirections via the { and } operators.<br>The { and } operators are used to access a control or a file item by dynamically building the control name or the item name. <br>This program, powered by WINDEV, implements the indirection mechanism on the window controls to manage the "Undo-Redo" and "Cut-Copy-Paste" features in a generic way.
- ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=Theindirections.gif) ***Unit examples (WEBDEV)*** : **The indirections** <br>This example explains how to use the indirections on a variable.<br><br>You will notice that the indirections are available for the controls, the items, the links, etc. The principle is identical to the one presented in this example.
- ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=Theindirections.gif) ***Unit examples (WINDEV Mobile)*** : **The indirections** <br>Using the indirection { and } operators.<br>The { and } operators are used to access a control or a file item by dynamically building the control name or the item name. <br>This example implements the indirection mechanism on the window controls to manage the "Undo-Redo" and "Cut-Copy-Paste" features in a generic way.


