


## Array (Type of variable)

***In french: Tableau (Type de variable)***
				



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
An array is a structured type that is used to group a set of elements of the same type. Each element of the array can be accessed by its index.


<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
// Declares an array initialized with the names of days of the week
DayArray is array of strings = ["Monday", "Tuesday", "Wednesday", "Thursday", "Friday"]
// Handles the array
DayArray[2] // Returns "Tuesday"
```
<a name="Example2"></a>

```wl
// Declares an array of integers
ValueArray is array of int
// Populates the array of integers by adding values
Add(ValueArray, 5)
Add(ValueArray, 10)
Add(ValueArray, 15)
// Browses the array values
FOR EACH x OF ValueArray
	// x is successively equal to 5, 10, 15
	...
END
```
<a name="Example3"></a>

```wl
// Declares an array of integers
ValueArray is array <growth=1> of int
// Enlarges the array to insert the value 1: 10
ValueArray[1] = 10
// Enlarges the array to insert the value 2: 20
ValueArray[2] = 20
// Enlarges the array to insert the value 3: 30
ValueArray[3] = 30
// Browses the array values
FOR EACH x OF ValueArray
	// x is successively equal to 10, 20, 30
END
```
<a name="Example4"></a>

```wl
// Declares an array of integers
ValueArray is array <growth=N> of int
// Enlarges the array to insert the value 1: 10
ValueArray[1] = 10
// Enlarges the array to insert the value 5: 50
// The values 2, 3 and 4 are initialized to 0
ValueArray[5] = 50
// Browses the array values
FOR EACH x OF ValueArray
	// x is successively equal to 10, 0, 0, 0, 50
END
```


![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png) 
```wl
// Declare an Array of arrays
arrMyArray is array of strings
nArrIndex is Int

nArrIndex = Add(arrMyArray)
Add(arrMyArray[nArrIndex], "January")
Add(arrMyArray[nArrIndex], "300")
nArrIndex = Add(arrMyArray)
Add(arrMyArray[nArrIndex], "February")
Add(arrMyArray[nArrIndex], "600")

Trace(arrMyArray[1,1]) // Displays "January"
Trace(arrMyArray[1,2]) // Displays "300"
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Syntax

### Declaring a one-dimensional array

`
<Array name> is array [<growth>] [ [<Dimension 1>] ] <Type of array elements>`
---

Example:

arrString is array &lt;growth=N&gt; [10] strings

arrInt is array [5] ints



`<Array name> is array [<growth>] of [<Dimension>] <Type of array elements>`
---

**`<Array name>: ()`**

Name of the array variable to declare.

**`<growth>: ()`**

Optional mode for enlarging the array:

- nothing (by default) or "&lt;growth=0>": the array is not automatically enlarged. For example, a runtime error will occur if the array contains 5 elements and if the program accesses the element 6 or 100.

- "&lt;growth&gt;" or "&lt;growth=1>": the array is automatically enlarged by 1 element. For example, if the array contains 5 elements and if the program accesses the element 6, the array is automatically enlarged to handle the element 6 ; a runtime error will occur if the program accesses the element 100.

- "&lt;growth=N>": the array is automatically enlarged by the number of necessary elements. For example, if the array contains 5 elements and if the program accesses the element 6, the array is automatically enlarged to handle the element 6 ; if the program accesses the element 100, the array is automatically enlarged to handle the element 100. The intermediate elements are initialized with the default value for this type of array elements.


Remark: To enlarge an array with several dimensions, use [Dimension](../WDLang1/3013022.md). 

**`<Dimension>: ()`**

Optional dimension of the array. This parameter can:

- be unspecified or correspond to \* or 0: the array is initialized empty.

- correspond to a constant value or to an integer variable: the array is initialized with the specified number of elements. Each element is initialized with the default value of its type.




**`<Type of array elements>: ()`**

Type of the elements found in the array. See [the different types of variables](../Motscles/1514064.md). 
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png) Remark: The elements that make up the array can also be arrays, fixed arrays, associative arrays, queues, stacks or lists. 


<a name="SYNTAX2"></a>

### Declaring an array with several dimensions (up to 10)

`
 <Array name> is array of [ [<Dimension 1> [, <Dimension 2>]...[, <Dimension 10>]] ] <Type of array elements>`
---

Example:

arrString is array [10,20] strings

arrInt is array [3,5,2] ints



`<Array name> is array of [<Dimension 1> [by <Dimension 2>] ... [by <Dimension 10>]] <Type of array elements>
OR
 <Array name> is array of [<Dimension 1> [, <Dimension 2>]... [, <Dimension 10>]] <Type of array elements>`
---

**`<Array name>: ()`**

Name of the array variable to declare.

**`<Dimension 1>...<a name="SYNTAX3"></a>

### Declaring an array parameter

`One-dimensional array:
<Parameter name> is array of [<Dimension>] <Type of array elements>

Two-dimensional array
<Parameter name> is array of <Dimension 1> by <Dimension 2> <Type of array elements>

N-dimensional array
<Parameter name> is array of [<Dimension 1> [by <Dimension 2>] ... [ by <Dimension N>]] <Type of array elements>`
---

**`<Parameter name>: ()`**

Name of the array parameter to declare.

**`<Dimension n>: ()`**

&lt;Dimension&gt; may not be specified, it may correspond to \* , 0 or to a constant value.

**`<Type of array elements>: ()`**

Type of the elements found in the array. See [the different types of variables](../Motscles/1514064.md). 
Remark: The elements that make up the array can also be arrays, fixed arrays, associative arrays, queues, stacks or lists. 


<a name="SYNTAX4"></a>

### Referring to an array

`Referring to an element in a one-dimensional array:
<Array name>[Index1]
 
 Referring to an element in a two-dimensional array:
<Array name>[Index1, Index2]
OR
<Array name>[index1][index2]
 
 Referring to an element in a n-dimensional array:
<Array name>[Index1,...,IndexN]
OR
<Array name>[Index1]...[IndexN]`
---

**`<Array name>: ()`**

Name of the array to use.

**`<Index1>: ()`**

Index of the element for the 1st dimension.

**`<Index2>: ()`**

Index of the element for the 2nd dimension.

**`<IndexN>: ()`**

Index of the element for the Nth dimension (N &lt;= 10).





<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks


### Other types of arrays
<a name="other_types_arrays_ELTPARAGRAPHE000180"></a>

The **Array** keyword is used to define an array. Other types of "advanced" arrays are available:

- [Dynamic array](../Motscles/1514057.md): Array allocated upon request.

- [Fixed array](../Motscles/1514025.md): Fixed-size array (recommended for the Windows APIs).

- [Associative array](../Motscles/1514058.md): Array used to stored elements indexed on any type of information.



<a name="NOTE0_2"></a>


### Procedure: Declaring an array parameter
<a name="procedure_declaring_array_parameter_ELTPARAGRAPHE000197"></a>An array can be passed as parameter to a procedure. For example:


```wl
// Adds a customer into the array
PROCEDURE AddCustomer(arrCustomer is array of * CCustomer,
	sName is string,
	sInfo is string)
// Builds the new customer object
c is CCustomer(sName, sInfo)
// Adds the object into the array
Add(arrCustomer, c)
```

**Remarks**:

- The type of the elements found in the array passed as parameter must be the same as the one of the declaration.

- The number of dimensions in the array passed as parameter must be the same as the one of the declaration.

- The number of elements found in each dimension of the array passed as parameter must correspond to the declaration:

	- if the number of elements for the dimension is \*, no check is performed.

	- if the number of elements for the dimension is specified, the number of elements must be identical.




- No check is performed during the compilation: checks are performed at runtime.



<a name="NOTE0_3"></a>


### Passing an array as parameter to a procedure
<a name="passing_array_parameter_procedure_ELTPARAGRAPHE000216"></a>An array can be passed as parameter to a procedure. To do so, use the following syntax:


```txt
<Procedure name>(<Array name>)
```

For example:


```wl
SuppArray is array of 10 by 50 strings
// Call to the DisplayArray procedure
DisplayArray(SuppArray)
```

<a name="NOTE0_4"></a>


### WLanguage functions and arrays
<a name="wlanguage_functions_and_arrays_ELTPARAGRAPHE000229"></a>

Several WLanguage functions can be used to handle the arrays. You have the ability to perform sorts, searches, etc. For more details, see [Array management functions](../WDLang1/3075006.md).
<a name="NOTE0_5"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png) 

### Array of arrays, associative array, queue, stack, list
<a name="array_arrays_associative_array_queue_stack_list_ELTPARAGRAPHE000243"></a>The following syntaxes are supported: 


```txt
<variable> is array of arrays of int
<variable> is array of arrays of 5 int
<variable>  is array of 5 arrays of int
<variable> is array of 5 arrays of 5 int

<variable> is array of fixed arrays of 5 int
<variable> is array of 5 fixed arrays of 5 int

<variable> is array of associative arrays of int
<variable> is array of associative arrays (withDuplicates) of int
<variable> is array of associative arrays (withDuplicates,wlInt) of int
<variable> is array of 5 associative arrays of int
<variable> is array of 5 associative arrays (withDuplicates) of int
<variable> is array of 5 associative arrays (withDuplicates,wlInt) of int

<variable> is array of queues of int
<variable> is array of 5 queues of int

<variable> is array of stacks of int
<variable> is array of 5 stacks of int

<variable> is array of lists of int
<variable> is array of 5 lists of int
```

<a name="NOTE0_6"></a>


### Limitations: Elements of an array
<a name="limitations_elements_array_ELTPARAGRAPHE000252"></a>

- An array can be made of classes only if these classes have a constructor without parameter (or with optional parameters).

- An array cannot be made of composite variables.

- ![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) And array cannot be made of arrays.

- ![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) No array of classes or array of structures can be created.



<a name="NOTE0_7"></a>


### Arrays in the classes
<a name="arrays_the_classes_ELTPARAGRAPHE000266"></a>

When copying instances of classes, all the members of the class are copied into the new instance except for the arrays. Therefore, if the value of an array member is modified, this value is modified in all the instances.

To get independent arrays in all instances of classes, a local array must be declared as follows:


```wl
SystemClass is class
	aDefaultArray is local array of 1 int
END
```

<a name="NOTE0_8"></a>


### Adding the content of a structure to an array of structures without using a variable of the structure
<a name="adding_the_content_structure_array_structures_without_using_variable_the_structure_ELTPARAGRAPHE000466"></a>

If you are using an array of structures with few members, it may be easier to directly add a structure by using the [ ] operator (square brackets). 
For example, for the following structure:

```wl
// Structure to store a letter and its ASCII code
STKey is Structure
	sKey is string
	nAsciiCode is int
END
// Array of structures
arrKeys is array of STKey
```

In most cases, content is added by using a variable of a type declared in the structure:

```wl
// Temporary structure for the addition
stAKey is STKey
// Stores the letter A
stAKey.sKey = "A"
stAKey.nAsciiCode = Asc("A")
arrKeys.Add(stAKey)
```

With the [ ] operator, you get better legibility:

```wl
// Stores the letter A
arrKeys.Add(["A", Asc("A")])
```



- ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=Usingthearrays.gif) ***Unit examples (WINDEV)*** : **Using the arrays** <br>Using the array type in WLanguage.


