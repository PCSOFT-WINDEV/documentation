


## Combination (Type of variable)

***In french: Combinaison***
				



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
A combination is a set of options. A combination variable or a combination parameter can be assigned with none, one or more combination options. 




<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
// Declaration code
// Declare a combination
CombinationType is Combination
	CreateFile
	ReadFile
	WriteFile
	DeleteFile
END
```


<a name="Example2"></a>



```wl
// Declare a Combination variable
FileAction is CombinationType
// Assign the variable
FileAction = ReadFile + WriteFile
// FileAction = 0 // Disables all the options
...

// To test the value of the combination:
IF FileAction[WriteFile] THEN
	// The WriteFile option is enabled
ELSE
	// The WriteFile option is not enabled
END
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Syntax

### Declaring a combination

`<Combination name> is Combination
    <Combination options>
END
`
---

**`<Combination name>: ()`**

Name of combination to declare

**`<Combination options>: ()`**

Different options that can be taken by the combination. 

**`<END>: ()`**

End of declaration.  


<a name="SYNTAX2"></a>

### Declaring a Combination variable

`<Variable name> is <Combination name>
`
---

**`<Variable name>: ()`**

Name of Combination variable to declare.

**`<Combination name>: ()`**

Name of combination that was declared beforehand.   



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks




### Declaring the combinations
<a name="declaring_the_combinations_ELTPARAGRAPHE000080"></a>

A combination must be declared in:

- The project initialization code in order to be used in all the processes of the project (code of project, code of windows, code of controls, procedures, etc.).

- The declaration code of global variables of a window in order to be used in all the processes of the window (code of window, code of controls found in the window, local procedures, etc.).

- The opening code of a report in order to be used in all the processes of the report (code of the report, code of controls found in the report, local procedures, etc.).

- The declaration code of a class in order to be used in the class.

	- To access this combination from a method of the class, use the syntax "::&lt;Combination name&gt;".

	- To access this combination from a code external to the class, use the syntax "&lt;Class name&gt;::&lt;Combination name&gt;".




- The declaration code of sets of procedures in order to be used in all the procedures of the set.




**Remark**: A combination is always global:

- to a project,

- to a window,

- to a report,

- to a class.



<a name="NOTE0_2"></a>


### Enabling or disabling the options of a combination
<a name="enabling_disabling_the_options_combination_ELTPARAGRAPHE000103"></a>

Several syntaxes can be used to enable or disable the options of a combination. For example: 

- Declaring the combination used for the examples: 
	
	```wl
	CombinationType is Combination
		CreateFile
		ReadFile
		WriteFile
		DeleteFile
	END
	
	// Declare a Combination variable
	FileAction is CombinationType
	```


- Disabling all the options: 
	Syntax: 
	
	```txt
	<Combination variable> = 0
	```

	Example: 
	
	```wl
	FileAction = 0
	```


- Enabling one or more options: 
	Syntax: 
	
	```txt
	<Combination variable> = Option 1
	<Combination variable>[Option 1] = True
	
	<Combination variable> = Option 1 + Option 2
	<Combination variable>[Option 1 + Option 2] = True
	```

	Examples: 
	
	```wl
	FileAction = ReadFile  // Enables an option
	FileAction = ReadFile + WriteFile // Enables 2 options
	
	FileAction[ReadFile] = True  // Enables an option
	FileAction[ReadFile + WriteFile] = True // Enables 2 options
	```


- Disabling one or more options: 
	Syntax: 
	
	```txt
	<Combination variable>[Option 1] = False
	
	<Combination variable>[Option 1 + Option 2] = False
	```

	Examples: 
	
	```wl
	FileAction[ReadFile] = False  // Disables an option
	FileAction[ReadFile + WriteFile] = False // Disables 2 options
	```




<a name="NOTE0_3"></a>


### Reading the combination options
<a name="reading_the_combination_options_ELTPARAGRAPHE000141"></a>

Several syntaxes can be used to find out the combination options. For example: 

- Declaring the combination
	
	```wl
	CombinationType is Combination
		CreateFile
		ReadFile
		WriteFile
		DeleteFile
	END
	
	// Declare a Combination variable
	FileAction is CombinationType
	```


- Check whether all the options are disabled: 
	Syntax: 
	
	```txt
	IF <Combination variable> = 0
	```

	Examples: 
	
	```wl
	IF FileAction = 0 THEN...
	```


- Check whether an option is enabled:  
	Syntax: 
	
	```txt
	IF <Combination variable> = Option 1 ...
	IF <Combination variable>[Option 1] = True...
	```

	Examples:
	
	```wl
	IF FileAction = ReadFile THEN ...
	IF FileAction[ReadFile] = True THEN...
	```


- Check whether several options are enabled: 
	Syntax: 
	
	```txt
	IF <Combination variable> = Option 1 + Option 2 THEN ...
	IF <Combination variable>[Option 1 + Option 2] = True ...
	```

	Examples:
	
	```wl
	IF FileAction = ReadFile + WriteFile THEN ...
	IF FileAction[ReadFile + WriteFile] = True THEN...
	```


- Check whether at least one option is enabled: 
	Syntax: 
	
	```txt
	IF <Combination variable>[Option 1] OR <Combination variable>[Option 2] THEN...
	```

	Examples:
	
	```wl
	IF FileAction[ReadFile] OR FileAction[WriteFile] THEN...
	```




<a name="NOTE0_4"></a>


### Passing a combination as a parameter to a procedure
<a name="passing_combination_parameter_procedure_ELTPARAGRAPHE000188"></a>

A Combination variable can be passed as a parameter to a procedure. To do so, use the following syntax:

```txt
<Procedure name>(<Name of combination variable>)
```


For example:


```wl
// Procedure with a Combination parameter
PROCEDURE FileType(p is CombinationType)

// Calls with a Combination parameter
FileType(ReadFile + WriteFile)
FileType(FileAction)
```

<a name="NOTE0_5"></a>


### Assisted input
<a name="assisted_input_ELTPARAGRAPHE000201"></a>

The assisted input for a Combination parameter proposes the different combination options.
<a name="NOTE0_6"></a>


### Extension of combination
<a name="extension_combination_ELTPARAGRAPHE000208"></a>

A combination can extend the values of one or more other combinations by taking the options of this ones and by adding new options.

The following syntax is used: 


```txt
<Combination name> is Combination
    [Base Combination]
    <Combination additional options>
END
```


where: 

- &lt;Combination name&gt;: Name of combination that was declared beforehand. 

- Base Combination: Name of base combination to use.

- &lt;Combination additional options&gt;: Additional options that will be taken into account in the combination. 




Example: 


```wl
CombinationType is Combination
	CreateFile
	ReadFile
	WriteFile
	DeleteFile
END


AdvancedCombinationType is Combination
	[CombinationType]
	OtherAttributes
END
```





<a name="NOTE0_7"></a>


### Preset combinations
<a name="preset_combinations_ELTPARAGRAPHE000230"></a>

Several options can be combined to force a preset combination. The combined options must belong to the combination (or to base combinations)

The following syntax is used: 


```txt
<Combination name> is Combination
	<Option 1 of combination>
	<Option 2 of combination>
	<Option 3 of combination> = <Option 1 of combination> + <Option 2 of combination>
END
```


Example: 


```wl
CombinationType is Combination
	CreateFile
	ReadFile
	WriteFile
	DeleteFile
	Read_Write_File = ReadFile + WriteFile
END
```

<a name="NOTE0_8"></a>


### Mutual exclusions
<a name="mutual_exclusions_ELTPARAGRAPHE000245"></a>

You have the ability to describe mutual exclusions between the combination options. 

For example, a combination contains three options: Option1, Option2 and Option3. Option2 and Option3 are exclusive:

- Option1 and Option2 can be active.

- Option1 and Option3 can be active. 

- Option2 and Option3 cannot be enabled.

- Option1, Option2 and Option3 cannot be active.




The following syntax must be used: 


```txt
<Combination name> is Combination
	<Option 1 of combination>
	<Option 2 of combination>
	<Option 3 of combination>
	// Exclusions
	<Option 2 of combination> - <Option 3 of combination>
END
```


Remark: By combining the extensions and the exclusions, you have the ability to get for the same options: 

- a parameter of a function that imposes an exclusion, 

- a parameter of a function that imposes no exclusion or that imposes a different exclusion.



<a name="NOTE0_9"></a>


### Associated properties
<a name="associated_properties_ELTPARAGRAPHE000268"></a>

The following properties are associated with the Combination variables: 


|   |   |
| --- | --- |
| [Name](../Proprietes/2510082.md) | Used to find out the name of a Combination variable. |
| [Value](../Proprietes/2510130.md) | Used to find out the associated value of a Combination variable. |






<a name="NOTE0_10"></a>


### Available WLanguage functions
<a name="available_wlanguage_functions_ELTPARAGRAPHE000289"></a>

A combination can be stored in an HFSQL item or in another file format. Depending on your requirements, you can store the combination name ([Name](../Proprietes/2510082.md)) or value ([Value](../Proprietes/2510130.md)). 

The following WLanguage functions allow you to get the characteristics of a stored combination: 



|   |   |
| --- | --- |
| [CombinationCheckName](../WDLang1/1000020772.md) | Checks whether a combination of one of more options known by their names is valid. |
| [CombinationFromName](../WDLang1/1000020771.md) | Returns a combination of one or more options known by their names. |
| [CombinationFromValue](../WDLang1/1000020770.md) | Returns a combination of one or more options known by their associated values. |




<a name="NOTE0_11"></a>


### Limits
<a name="limits_ELTPARAGRAPHE000305"></a>

- Combinations are not available in dynamic compilation. 

- Combinations cannot be used outside the project. 





