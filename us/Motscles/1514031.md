


## Multiple assignments of arrays, structures and classes
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Overview
<a name="overview_ELTTEXTE000232"></a>
The multiple assignment of arrays consists in assigning a set of simple values (boolean, integer, string, real, numeric or currency) to the cells of an array.

This multiple assignment is also available for the structures and the classes.





<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Multiple assignments of arrays
<a name="multiple_assignments_arrays_ELTTEXTE000256"></a>


### Syntax
<a name="syntax_ELTPARAGRAPHE000053"></a>

- 1-dimensional array:
	
	```txt
	<1-dimensional array> = [ <Value 1>, <Value 2>, ...]
	```





- 2-dimensional array:
	
	```txt
	<2-dimensional array> = [ [ <Value 1 1>, <Value 1 2>, ...], 
	[ <Value 2 1>, <Value 2 2>, ...], 
	...
	]
	```





- 3-dimensional array:
	
	```txt
	<3-dimensional array> = [ [ [ <Value 1 1 1>, <Value 1 1 2>, ...], 
	[ <Value 1 2 1>, <Value 1 2 2>, ...], ... ],
	[ [ <Value 2 1 1>, <Value 2 1 2>;, ...], 
	[ <Value 2 2 1>, <Value 2 2 2>, ...], ... ],
	...
	]
	```




<a name="NOTE2_2"></a>


### Array of values
<a name="array_values_ELTPARAGRAPHE000070"></a>

Each set of values represents a dimension of the array of values. The sets of values can be nested to create an array of values with several dimensions.

The numbers of dimensions in the array of values must be consistent: each dimension must have the same number of sub-dimensions. However, a different number of elements can be found for each dimension.

Examples:


```txt
// Array of values not allowed: 
// The first value of the set has a sub-dimension
// The second value of the set has no sub-dimension
[[ <Value 1 1>, <Value 1 2> ], <Value 2> ]
```



```txt
// Array of values allowed: 
// All the values of the set have a sub-dimension
// Each sub-dimension has a different number of elements
[[ <Value 1 1>, [ <Value 2 1>, <Value 2 2>, <Value 2 3> ], 
		[<Value 3 1>, <Value 3 2> ]]
```


The dimensions of the array of values must correspond to the dimensions of the array to assign.

If the dimensions of the array to assign are not sufficient to store the elements found in the array of values, the array to assign is automatically resized.

For a fixed array, if the dimensions of the array to assign are not sufficient to store the elements found in the array of values, the array of values is truncated.

If the dimensions of the array to assign are greater than the dimensions of the array of values, the non-assigned elements keep their previous values.
<a name="NOTE2_3"></a>


### Examples
<a name="examples_ELTPARAGRAPHE000093"></a>


```wl
// Array of powers of 2
Power2 is array of 0 int
Power2 = [1, 2, 4, 8, 16, 32, 64, 128, 256]
```



```wl
// Array of numbers of days per month 
// Line 1: non-leap year
// Line 2: leap year
NumberDays is array of 2 by 12 int
NumberDays = [[31, 28, 31, 30, 31, 30, 31, 31, 30, 31, 30, 31],
	[31, 29, 31, 30, 31, 30, 31, 31, 30, 31, 30, 31]]
```


<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Multiple assignments of associative arrays
<a name="multiple_assignments_associative_arrays_ELTTEXTE000292"></a>


### Syntax
<a name="syntax_ELTPARAGRAPHE000104"></a>


```txt
<Associative array> = [ [ <Key1>, <Value1> ], 
[ <Key2>, <Value2> ],
...
]
```


The array of values must only contain 2 couples (Key, Value).

Each couple is assigned to the associative array by using the equivalence of the syntax: AssociativeArray[Key] = Value.

For an associative array without duplicates:

- If the key does not exist yet, the value is added.

- If the key already exists, the value is modified.




For an associative array with duplicates, all the values are added.
<a name="NOTE3_2"></a>


### Example
<a name="example_ELTPARAGRAPHE000128"></a>


```wl
// Associative array of acronyms of countries
Country is associative array of strings
Country = [["FR", "France"], ["US", "United States"], ["DE", "Germany"]]
```


<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Multiple assignments of structures and classes
<a name="multiple_assignments_structures_and_classes_ELTTEXTE000322"></a>


### Syntax
<a name="syntax_ELTPARAGRAPHE000137"></a>

- Structure:
	
	```txt
	<Structure Instance> = [ <Member 1>, <Member 2>, ...]
	```





- Class: 
	
	```txt
	<Class Instance> = [ <Member 1>, <Member 2>, ...]
	```




<a name="NOTE4_2"></a>


### Remarks
<a name="remarks_ELTPARAGRAPHE000150"></a>

- The members are assigned in the order of declaration.

- For the classes, the inheritances are ignored: the members of the class are directly assigned.

- Limitation: this syntax does not operate on the dynamic structures and classes.



<a name="NOTE4_3"></a>


### Example
<a name="example_ELTPARAGRAPHE000159"></a>


```wl
State is structure
	Name is string
	Number is int
	Prefecture is string
END
 
Allier is state = ["Allier", 3, "Moulins"]
```


<a name="NOTE5"></a>
<a name="NOTE5_1"></a>


## Nesting
<a name="nesting_ELTTEXTE000358"></a>
The syntaxes for multiple assignments can be used on arrays of structures, instances of structures containing arrays, ...


```wl
State is structure
	Name is string
	Number is int
	Prefecture is string
END
 
ArrState is array of 0 State
ArrState = [["Ain", 1, "Bourg-en-Bresse"],
			["Aisne", 2, "Laon"],
			["Allier", 3, "Moulins"]]

ArrStateName is associative array of State
ArrStateName = [["Ain", ["Ain", 1, "Bourg-en-Bresse"]],
			["Aisne", ["Aisne", 2, "Laon"]],
			["Allier", ["Allier", 3, "Moulins"]]]
```


<a name="NOTE6"></a>
<a name="NOTE6_1"></a>


## Hyphenation
<a name="hyphenation_ELTTEXTE000382"></a>
You have the ability to describe the arrays of values with line breaks without using the hyphenation syntax (... triple dot).

However, the hyphenation syntax must be used to go the next line after the first opening square bracket. For example:


```wl
State is structure
	Name is string
	Number is int
	Prefecture is string
END
 
Allier is state = [...
							"Allier", 3, "Moulins"]
```


A comma can also be found in front of a closing square bracket:


```wl
NumberDays = [[31, 28, 31, 30, 31, 30, 31, 31, 30, 31, 30, 31],
	[31, 29, 31, 30, 31, 30, 31, 31, 30, 31, 30, 31], 
	]
```



