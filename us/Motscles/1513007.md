
## Calling a procedure/a function

***In french: Appeler une procédure/une fonction***
				



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
The method for calling a procedure is the same, no matter whether it is a global procedure or a local procedure.

**Reminder**: 

- In WLanguage, there is no distinction between the procedures and the functions. The syntaxes for declaring and using procedures also apply to functions.

- For a multi-syntax procedure, the call to the proper syntax is resolved at runtime. For more details, see [Prototype overload](../Motscles/1514063.md).





<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
// Call to the Find procedure that returns a boolean
IF Find(Customer, CustNum, Number) THEN
	Info("Customer number found")
ELSE
	Info("Customer number not found")
END
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Syntax

### Calling a procedure that returns a parameter

`[<Returned value> = ] <Procedure name>([<Parameters>])
`
---

**`<Returned value>: ()`**

Value returned by the procedure (optional), only if the procedure returns a result.

The type of the return value of a procedure can be specified during its declaration. For more details, see [Declaring a procedure](../Motscles/1513008.md). 

**`<Procedure name>: ()`**

Name of the procedure to call.

**`<Parameters>: ()`**

Parameters passed to the procedure. Each parameter is separated by a comma. 
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) You have the ability to use named parameters. For more details, see "[Named parameters](#NOTE0_2)". 

For more details on how to declare the parameters of a procedure, see [The parameters of a procedure](../Motscles/1513001.md).  


<a name="SYNTAX2"></a>

### Calling a procedure that returns several parameters

`(<Value 1>, ..., <Value N>) = <Procedure name>([<Parameters>])
`
---

**`<Value 1 to N>: ()`**

Values returned by the procedure.

The type of the return values of a procedure can be specified during its declaration. For more details, see [Declaring a procedure](../Motscles/1513008.md). 

**`<Procedure name>: ()`**

Name of the procedure to call.

**`<Parameters>: ()`**

Parameters passed to the procedure. Each parameter is separated by a comma. 
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) You have the ability to use named parameters. For more details, see "[Named parameters](#NOTE0_2)". 

For more details on how to declare the parameters of a procedure, see [The parameters of a procedure](../Motscles/1513001.md). 



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks


### Special cases
<a name="special_cases_ELTPARAGRAPHE000108"></a>

- To make your programs more readable, the **PROCEDURE** keyword can be replaced with the **FUNCTION** keyword.

- If the procedure expects no parameter, you can also use the following syntax: 
	
	```txt
	[<Returned value> = ] <Procedure name>
	```


- To run a procedure of an opened window, use [ExecuteProcess](../WDLang1/3013031.md). Example: 
	
	```wl
	// Run the MyProc procedure found in WIN_MyWindow
	WinName is string = "WIN_MyWindow"
	ExecuteProcess(WinName + ".MyProc", trtProcedure)
	```




<a name="NOTE0_2"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) 

### Named parameters
<a name="named_parameters_ELTPARAGRAPHE000128"></a>

If a procedure includes parameters with default values, you have the ability to call the procedure by naming its parameters. Two syntaxes are possible: 

- Single-line named parameters, 

- Multiline named parameters. 




**1. Single-line named parameters**
The following syntax is used: 


```txt
ProcedureName(< <name of parameter1> >: <value1>, < <name of parameter2> >: <value2>, ...)
```


Let's see the different operating rules via an example: 

- Code of the procedure: 
	
	```wl
	PROCEDURE MyProcedure(p1, p2, p3 = 0, p4 = 0, p5 = 0)
	```


- Call to the procedure by using named parameters: 
	
	```wl
	MyProcedure(<p1>:1, <p2>:2)
	```


- The unspecified optional parameters will be assigned with their default value.
	
	```wl
	MyProcedure(<p1>:1, <p2>:2, <p4>:4)
	```


- The mandatory parameters can be passed without their name, by using the standard ordered parameters. The named parameters are necessarily found after the ordered parameters: 
	
	```wl
	MyProcedure(1, 2, <p4>:4)
	```

	
	```wl
	MyProcedure(1, <p4>:4, 2) // Triggers an error
	```


- All the mandatory parameters must be specified otherwise a compilation error will occur.
	
	```txt
	MyProcedure(<p1>:1) // error: p2 missing
	```

	
	```txt
	MyProcedure(<p1>:1, <p4>:4)// error: p2 missing
	```


- Each parameter must be specified once only otherwise a compilation error will occur. 
	
	```txt
	MyProcedure(<p2>:2, <p1>:1, <p2>:2) // error: p2 twice
	```


- The parameters can be specified in any order: the values of parameters are evaluated in the standard writing order (from left to right).
	
	```txt
	MyProcedure(f1(), f2(), <p5>:f3(), <p4>:f4()) // Execution order: f1, f2, f3, f4
	```





The rules for passing parameters are identical to the ones applied during a standard call. For more details, see [Passing parameters](../Motscles/1514077.md). 

**2. Multiline named parameters**
The following syntax is used: 


```txt
// Define the parameters
ProcedureName.<Name of parameter 1> = <Value 1>
...
ProcedureName.<Name of parameter N> = <Value N>
// Call the procedure
ProcedureName()
```


This syntax is available:

- for the global procedures.

- for the local procedures called from the element that owns the procedure.

- for the methods called from a method of the class or derived class.




Example: 

- Code of the procedure: 
	
	```wl
	PROCEDURE MyProcedure(p1, p2, p3 = 0, p4 = 0, p5 = 0)
	```


- Call code: 
	
	```wl
	// Define the parameters
	MyProcedure.p1 = 1
	MyProcedure.p2 = 2
	// Call the procedure
	MyProcedure()
	```





Remarks: 

- The parameters of the procedure are limited to the current process. Therefore, the call and the assignments must be found in the same process. A compilation error occurs if parameters are specified and if no call to the procedure is detected in the process.

- No check is performed regarding the presence of mandatory parameters. An error may occur at runtime if a mandatory parameter was not specified. 

- The parameters are reinitialized during the call so that no parameter can be re-used by mistake ; therefore, two calls cannot be performed with the same parameters.

- Caution: In this case, passing parameters is performed by value (and not by variable).



<a name="NOTE0_4"></a>


