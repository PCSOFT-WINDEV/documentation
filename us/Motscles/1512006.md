
## Comparison operators
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Overview
<a name="overview_ELTTEXTE000505"></a>
The comparison operators can be divided into several categories:


|   |   |
| --- | --- |
| **Equality** | <br><br>- Strict equality: =<br><br>- Flexible equality: ~=<br><br>- Very flexible equality: ~~ <br><br>- Starts with: [=<br><br><br> |
| **Comparison** | <br><br>- Not equal to: &lt;&gt;<br><br>- Less than or equal to: &lt;=<br><br>- Greater than or equal to: &gt;=<br><br>- Strictly less than: &lt;<br><br>- Strictly greater than: &gt;<br><br>- Starts with: [=<br><br><br><br><br>- Starts with: [~ (ignores the space characters found at the beginning of string and the case).<br>	<br><br>- Starts with: [~~ (ignores all the space characters, the punctuation and the case).<br>	<br><br>- Comparison to a list of values: IN<br><br>- Contains: [=]<br>	<br><br>- Contains: [~] (ignores the case and the space characters found at the beginning and end of string).<br>	<br><br>- Contains: [~~] (ignores all the space characters, the punctuation and the case)<br>	<br><br>- Ends with: =]<br>	<br><br>- Ends with: ~] (ignore the space characters at the end of string and the case)<br>	<br><br>- Ends with: ~~] (ignore the space characters, the punctuation and the case)<br>	<br><br><br> |
| **Comparison interval** | <br><br>- Strictly included between: &lt; .. &lt; (and conversely)<br>	Value1 &lt; x &lt; Value2<br><br>- Between: &lt; .. &lt;= AND &lt; .. = &lt; (and conversely)<br>	Value1 &lt;= x AND x &lt;= Value2<br><br>- Included between (including bounds): &lt;= &lt;= (and conversely)<br>	Value1 &lt;= x &lt;= Value2<br><br>- Included between (including bounds): x TO y <br><br><br> |

The comparison operators can be used with all the types of operands.

The result of a comparison expression is a boolean.



<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Details
<a name="details_ELTTEXTE000529"></a>


### Flexible equality and very flexible equality
<a name="flexible_equality_and_very_flexible_equality_ELTPARAGRAPHE000267"></a>

The flexible equality (~=) only applies to the character strings (except the fixed strings). This operator allows you to:

- make no difference between the uppercase characters and the lowercase characters, 

- ignore the space characters found before and after the string whose test must be run, 

- ignore the lowercase accented characters.




To ignore the space characters, the punctuation characters and the CR characters inside the strings, use the very flexible equality (~~).

**HFSQL equivalence**: To retrieve the equivalence to the very flexible equality when performing a search on a text key in an HFSQL data file, the following options must be configured when the item is described in the analysis:
<br>![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=Indexation%20des%20m%E9mos%20-%20HC%20N%B0002.gif)





```wl
"Smith" = "SMITH"                // Returns False
"Smith" ~= "SMITH"                // Returns True
" Smith" ~= "SMITH"               // Returns True
" Smith" ~= "Smith"              // Returns True
"C'est l'été" ~= "C'est l'ete"     // Returns True 
"I.R.S." ~~ "IRS"   // Returns True
```

<a name="NOTE2_2"></a>


### Equality and comparison: Real containing more than 6 different decimal places
<a name="equality_and_comparison_real_containing_more_than_6_different_decimal_places_ELTPARAGRAPHE000293"></a>

The test of equality between two real numbers is performed according to the first 6 decimal places. Indeed, the rounding errors caused by the internal coding of the reals require a specific test.

This test must be run by comparing the difference between the two values to test and a reference value. Depending on the type of your application, this value can be equal to 0.00001 or even less.

This rounding management is not specific to WINDEV, WEBDEV and WINDEV Mobile. It is common to all the programming languages that handle the reals in binary format.


```wl
Diff, R1, R2 are real
Diff = 0.00001
IF Abs(1-(R1/R2)) < Diff THEN
	// R1 and R2 are equal
ELSE
	// R1 and R2 are different
END
```

<a name="NOTE2_3"></a>


### Comparison intervals
<a name="comparison_intervals_ELTPARAGRAPHE000306"></a>

The comparison intervals are used to simplify the syntax of complex comparisons. 

Some examples of identical comparisons: 

- Example 1:  

	- IF x&gt;5 AND x&lt;10 THEN...

	- IF 5&lt;x&lt;10 THEN...




- Example 2: 

	- IF x&gt;=5 AND x&lt;=10 THEN...

	- IF x=5 TO 10 THEN ...
			![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Not supported.








```wl
MyArray is array of 5 strings
I is int
MyArray[1] = "Smith"
MyArray[2] = "Aida"
MyArray[3] = "Parapoline"
MyArray[4] = "Moulin"
MyArray[5] = "Clark"
FOR I = 1 TO 5
	IF "B" < MyArray[I] <= "M" THEN Trace(MyArray[I])
	// Displays Smith and Clark
END
```

<a name="NOTE2_4"></a>


### Comparison operators and UNICODE
<a name="comparison_operators_and_unicode_ELTPARAGRAPHE000330"></a>

The available operators are as follows:

- "=": Strict equality

- "&lt;&gt;": Difference

- "[=": Starts with

- "[~": Starts with while ignoring the space characters found at the beginning of the string and while ignoring the case.

- "[~~": Starts with while ignoring the space characters, the punctuation characters and  the case. 

- Contains: [=]

- Contains: [~]: Contains while ignoring the case. 

- Contains: [~~]: Contains while ignoring all the space characters, the punctuation characters and the case. 

- Ends with: =]

- Ends with: ~]: Ends with while ignoring the space characters found at the end of the string and while ignoring the case. 

- Ends with: ~~]: Ends with while ignoring the space characters, the punctuation and the case. 




- "&lt;", "&lt;=", "&gt;=", "&gt;": Strictly less than, less than or equal to, greater than or equal to, strictly greater than 




You have the ability to use ANSI strings, Unicode strings and buffers in the different parameters of the comparison.

The following conversion rule is used for the Ansi systems (Windows or Linux):

- If at least one of the strings is a buffer, all the strings are converted to buffers and the operation is performed with buffers,

- If the first condition is not met and there is at least one Unicode string, all the strings are converted to Unicode and the operation is performed in Unicode (the conversion is performed with the current character set, if necessary),

- Otherwise, the operation is performed in Ansi.




The following conversion rule is used for the Unicode systems (Windows CE):

- If at least one of the strings is a buffer, all the strings are converted to buffers and the operation is performed with buffers,

- Otherwise, the operation is performed in Unicode.




For more details on Unicode, see [WINDEV and the Unicode format](../WDLang1/3024038.md).
<a name="NOTE2_5"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png)![User code (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png) 

### Comparing instances of structures and instances of classes
<a name="comparing_instances_structures_and_instances_classes_ELTPARAGRAPHE000421"></a>

The dynamic structures are instantiated when they are allocated.

**You have the ability to compare instances of dynamic structures** (or instances of dynamic classes) via the "=" operator.

**Example**:


```wl
// Declare the structures
O is ST1
P is ST2

// Declare and instantiate the dynamic structures
P1 is dynamic structure ST1
P1 = O
P2 is dynamic structure ST2 
P2 = O

// Comparison 
IF P1 = P2 THEN...
```


The same operation can be performed on **the instances of classes** in order to find out whether two variables designate a single class instance.

The comparison between two different instances of a class can be performed by using buffers. 

**Example**:


```wl
Object1 is dynamic Class1
Object2 is dynamic Class2
BufferObject1, BufferObject2 are buffers

Object1 = new Class1
Object2 = new Class2

Serialize(Object1, BufferObject1, psdBinary)
Serialize(Object2, BufferObject2, psdBinary)

IF BufferObject1 = BufferObject2 THEN
	// True if the two instances have identical contents
ELSE
	// False if at least one member is different
END
```

<a name="NOTE2_6"></a>


### IN operator
<a name="operator_ELTPARAGRAPHE000442"></a>

The IN operator is used to compare a value to a list of values. Its evaluation returns a boolean. 
Example: 

```wl
MyValue is int
MyResult is boolean
MyValue = 10
MyResult = (MyValue IN(1,4,6,10))
```


The list of values can contain literal values or variables whose value will be evaluated before the comparison is performed. 
 Example: 

```wl
MyValue is int
MyOtherValue is int
MyResult is boolean
MyValue = 10
MyOtherValue = 15
MyResult = (MyValue IN(1,4,6,10,MyOtherValue))
```

![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) This feature is not available.


