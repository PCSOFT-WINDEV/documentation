


## Assignment operators
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Overview
<a name="overview_ELTTEXTE000278"></a>
Several assignment operators are available:

- [the simple assignment](#NOTE2_1): =

- [the assignment by copy](#NOTE3_1): &lt;=

- [the assignment by reference](#NOTE4_1): &lt;-






<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Simple assignment: =
<a name="simple_assignment_ELTTEXTE000302"></a>
The simple assignment is used between all the simple types (integers, reals, strings, etc.) to copy the value of a variable into another one.

Its operating mode changes when it is used on complex types. 


| Type of variables | Effect |
| --- | --- |
| Simple types (integer, real, string, etc.) | The value of the variable is copied. |
| Arrays | The destination array is a reference to the source array. |
| Associative arrays | The content of the array is copied. |
| Queue | The content of the queue is copied. |
| Stack | The content of the stack is copied. |
| List | The content of the list is copied. |
| Object = Dynamic object | The members of the dynamic object are copied to the members of the object. |
| Object = Object | The members of the source object are copied to the members of the destination object. |
| Dynamic object = Dynamic object | The destination dynamic object is a reference to the source dynamic object. |
| Dynamic object = Object | The destination dynamic object is a reference to the source object. |
| Structure = Dynamic structure | A copy of the structure is performed. Members with the same name are initialized with the existing values. Non-existing members are ignored. |
| Structure = Structure | A copy of the structure is performed. Members with the same name are initialized with the existing values. Non-existing members are ignored. |
| Dynamic structure = Dynamic structure | The destination dynamic structure is a reference to the source dynamic structure. |
| Dynamic structure = Structure | The destination dynamic structure is a reference to the source structure. |
| Advanced type = Advanced type | The advanced type variable is copied. Properties of the same name are initialized with the existing values. |
| Advanced type = Dynamic advanced type | The advanced type variable is copied. Properties of the same name are initialized with the existing values. |
| Dynamic advanced type = Advanced type | The destination dynamic advanced variable is a reference to the source advanced variable. |
| Dynamic advanced type = Dynamic advanced type | The destination dynamic advanced variable is a reference to the source dynamic advanced variable. |


<a name="NOTE2_2"></a>


### Multiple assignment
<a name="multiple_assignment_ELTPARAGRAPHE000066"></a>

The **'=**' operator can also be used to assign several variables in a single line of code.

The following syntax is used: 


```txt
( <Variable 1>, <Variable 2>, ... <Variable N> ) = ( <Value 1>, <Value 2>, ... <Value M> )
```


where: 

- N represents the number of declared variables.

- M represents the number of values to assign.




Remarks:

- The expressions to the right of the '=' sign are evaluated from left to right.

- Then, the assignment operations of variables on the left of the sign are performed from left to right.

- There is no dependencies between the expressions on the right and the ones on the left. On the other hand, there can be a dependency between the variables on the left.

- If the number of variables is less than the number of values (N less than M), a compilation error will occur.

- If the number of variables is greater than the number of values (N greater than or equal to M), only the first variables will be assigned.

- To assign the same value to all the variables, all you have to do is use the following syntax: 
	
	```txt
	( <Variable 1>, <Variable 2>, ... <Variable N> ) = <Value>
	```

	For example: 
	```wl
	(x,y,z) = 0
	```

	




Examples: 


```wl
(x, y, z) = (5, 8, 12)
// x is set to 5, y is set to 8, z is set to 12

(x, y, z) = (25, 38)
// x is set to 25, y is set to 38, z is not assigned


(x, , z) = (14, 87)
// x is set to 14, y is not assigned, z is set to 87

(t[Subscript1()], t[Subscript2()]) = (Calc1(), calc2())
// The execution order of procedures is as follows: 
// Calc1()
// Calc2()
// Subscript1()
// Subscript2()
// then t[Subscript1()] is assigned the value of Calc1()
// then t[Subscript2()] is assigned the value of Calc2()

(i, t[i]) = (1, "ABC")
// First assigns 1 to i
// Then assigns "ABC" in t to the subscript i
```


<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Assignment by copy: &lt;=
<a name="assignment_copy_ELTTEXTE000409"></a>
The assignment by copy always forces the copy of the source element into the destination element.

A new memory area is allocated to store the copied value. 


### Assignment by copy on the arrays
<a name="assignment_copy_the_arrays_ELTPARAGRAPHE000115"></a>

The &lt;= operator can be used on the arrays.
These arrays can have diferent types.
The assignment is using the implicit conversions of WLanguage (like the = operator would do =).

Example:


```wl
// Declare two arrays of same size and different types (integer and boolean)
arrIntegers is array of 3 by 2 int
arrBooleans is array of 3 by 2 booleans

// Fill the array of integers
arrIntegers[1,1] = -6
arrIntegers[1,2] = 9
arrIntegers[2,1] = 0

// Copy the array of integers into the array of booleans
arrBooleans <= arrIntegers

// Traces of array of booleans 
// (Reminder: all the numbers other than zero are "True" in boolean logic)
Trace(arrBooleeans[1,1]) // Displays 1
Trace(arrBooleeans[1,2]) // Displays 1
Trace(arrBooleeans[2,1]) // Displays 0
Trace(arrBooleeans[2,2]) // Displays 0
```



### Homonymic copy
<a name="homonymic_copy_ELTPARAGRAPHE000130"></a>

The homonymic copy mechanism uses the *&lt;=* operator to transfer the data from a structured element to another structured element. 

During the copy, the members with the same name are automatically copied from the source structured element to the destination structured element. The type conversions will be automatically performed.

This allows you to: 

- transfer the content of structure members to the items of an HFSQL file, 

- transfer the members of advanced type (Email for example) to the members of a class. 




The following structured types can be handled: 

- structures,

- classes,

- advanced types, 

- HFSQL files,

- queries, 

- data sources.






Example: Copying classes


```wl
// Declare the classes
Class_1 is class
	common is int
	member_1 is int
END

Class_2 is class
	common is int
	member_2 is int
END

// Assignment
o1 is Class_1
o1.common = 1
o1.member_1 = 1
	
o2 is Class_2
o2.common = 2
o2.member_2 = 2

// Copy
o1 <= o2

// Result
// o1.common = 2
// o1.member_1 = 1
```


Remarks: 

- The *&lt;=* operator can be replaced with the *=* operator when the operating mode presents no ambiguity.  

- For the structures and classes, each member can be followed by a "Mapping" attribute. This attribute is used to specify the name of the member that must be "mapped" to the structure member or to the class member. 
	
	Example:  
	
	```wl
	// Declare the classes
	Class_Simple_1 is class
		common is int
		member_1 is int
	END
	
	Class_Mapping_1 is class
		nCommon is int, Mapping = "common"
		member_1 is int, Mapping = ""
	END
	
	// Assignment
	o1 is Class_Mapping_1
	o1.nCommon = 1
	o1.member_1 = 1
		
	o2 is Class_Simple_1
	o2.common = 2
	o2.member_1 = 2
		
	o1 <= o2
	
	// Result
	// o1.ncommon = 2
	// o1.member_1 = 1
	```





- For advanced types with dynamic subelements (for example, the xmlNode type or the structured elements of a web service), the subelements have priority over the standard properties of the type. 
	Example:  
	
	```wl
	// Declare the classes
	Class_xmlNode is class 
		Name is string
		Tag1 is string
		Tag2 is string
	END
	
	// Assignment
	o1 is Class_xmlNode
	o1.Name = ""
	o1.Tag1 = ""
	o1.Tag2 = ""
		
	c1 is xmlNode
	c1..Name = "XML_node_with_Name_tag"
	c1.Name= "Name"
	c1.Tag1 = "Tag1"
	c1.Tag2 = "Tag2"
		
	o1 <= c1
	
	// Result
	// o1.Name = "Name"
	// o1.Tag1 = "Tag1"
	// o1.Tag2 = "Tag2"
	```


- Special case: homonymic copy between structures or classes: 

	- The "mapping" attribute is allowed for the members of the structure or source or destination class (but not for both). 

	- If a derived member has the same name as a member of the base class or a mapping with the same name, only the name of the derived class is taken into account. 







- Limit: Homonymic copy is not available: 

	- on .Net objects, 

	- on automation objects, 

	- if the source and the destination are advanced types with dynamic subelements. 







<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Assignment by taking reference: &lt;-
<a name="assignment_taking_reference_ELTTEXTE000445"></a>
The assignment by taking reference forces the destination element to reference the same data area as the source element.

This assignment cannot be used with the simple types (integer, reals, strings, etc.).

This type of assignment can be used to directly handle: 

- the appointments in an Organizer control. For more details, see [Programming an Organizer control](../WDChamp/1016326.md). 

- the appointments in a Scheduler control. For more details, see [Programming a Scheduler control](../WDChamp/1000019505.md). 

- the tasks in a Gantt Chart control. For more details, see [Programming a Gantt chart control](../WDChamp/1000030004.md). 




<a name="NOTE5"></a>
<a name="NOTE5_1"></a>


## Remarks
<a name="remarks_ELTTEXTE000469"></a>
The operators for taking reference (=, &lt;= et &lt;-) return "Null" if the assignment or the take of reference is invalid. You have the ability to write the following code:


```wl
Object1 is BaseClass1 object
Object2 is DerivedClass object

Object1 <- Object2

IF Object1 = Null THEN
	Trace("Object1 is not a DerivedClass or an ancestor class of DerivedClass.")
ELSE
	Trace("Object1 is a DerivedClass or an ancestor class of DerivedClass.")
END
```



