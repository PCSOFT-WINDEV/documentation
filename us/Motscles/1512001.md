
## Operators on character strings
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Use
<a name="use_ELTTEXTE000547"></a>
The character strings can be handled by specific WLanguage functions or by the +, [[ and ]] operators.

The operators on character strings are as follows:

- "** +**": To concatenate strings

- "** [**" and "** ]**": To optionally concatenate strings

- "** [[**" and "**]]**" (double opening brackets and double closing brackets): Operator for extracting a substring

- " **=**": Strict equality

- " **~=**": Flexible equality

- " **~~**": Very flexible equality

- " **[=**": Starts with

- " **[% %]**": Dynamically built string. For more details, see [String interpolation](../Motscles/1514044.md).





```wl
Text is string = "San Francisco"
Text[[5]]                // Returns "F"
Text[[5 to 10]]           // Returns "Franci"
Text[[5 to]]              // Returns "Francisco"
Text[[to 10]]              // Returns "San Franci"
Text[[10 on 4]]          // Returns "isco"
Text + " and New York"      // Returns "San Francisco and New York"
If Text [= "San" Then Trace(Text)
```




<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Notes
<a name="notes_ELTTEXTE000571"></a>


### The [ and ] operator
<a name="the_and_operator_ELTPARAGRAPHE000101"></a>

The [ and ] operator is used to optionally concatenate strings. The separator is not concatenated if the source string is an empty string ("") or if the source string ends with the separator.

For example:

- StartString += [EndString]
	EndString is concatenated to StartString only if StartString does not end with EndString.
	For example:
	
	```wl
	StartString = "San Francisco"
	EndString = "Francisco"
	StartString += [EndString]
	// StartString contains "San Francisco"
	```

	
	```wl
	StartString = "San "
	EndString = "Francisco"
	StartString += [EndString]
	// StartString contains "San Francisco"
	```


- StartString += [MiddleString] + EndString 
	MiddleString is concatenated to StartString only if StartString does not end with MiddleString and if EndString does not start with MiddleString. The separator is not concatenated if the source string is an empty string (""). 
	For example:
	
	```wl
	StartString = "C:\MyFiles\"
	MiddleString = "\"
	EndString = "MyFile.TXT"
	StartString += [MiddleString] + EndString
	// StartString contains "C:\MyFiles\MyFile.TXT"
	```

	
	```wl
	StartString = "C:\MyFiles"
	MiddleString = "\"
	EndString = "\MyFile.TXT"
	StartString += [MiddleString] + EndString
	// StartString contains "C:\MyFiles\MyFile.TXT"
	```

	
	```wl
	StartString = "C:\MyFiles"
	MiddleString = "\"
	EndString = "MyFile.TXT"
	StartString += [MiddleString] + EndString
	// StartString contains "C:\MyFiles\MyFile.TXT"
	```


- MyString = [StartString] + EndString 
	StartString is concatenated to EndString only if EndString does not start with StartString.
	For example:
	
	```wl
	StartString = "Type"
	EndString = "Type of programming"
	MyString = [StartString] + EndString
	// StartString contains "Type of programming"
	```

	
	```wl
	StartString = "Type"
	EndString = " of programming"
	MyString = [StartString] + EndString
	// StartString contains "Type of programming"
	```





**Examples:**

- Filling a string with elements separated by CR characters (Carriage Return). The separator is not concatenated if the source string is an empty string.
	
	```wl
	ResString is string
	FOR EACH Customer
		ResString += [CR] + Customer.Name
	END
	```


- Creating a file path without having to worry about the "\\" characters.
	
	```wl
	fOpen(DirectoryName + ["\"] + FileName)
	```





- ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=Theoptionalconcatenation.gif) ***Unit examples (WINDEV)*** : **The optional concatenation** <br>Using the optional string concatenation<br>The optional concatenation is used to add a character string at the the end of another string if it does not already exist.



<a name="NOTE2_2"></a>


### The [[ and ]] operator
<a name="the_and_operator_ELTPARAGRAPHE000180"></a>

The [[ and ]] operator is used to extract and replace a substring.



For example:

- STRINGTOEXTRACT[[&lt;Position&gt;]]

- STRINGTOEXTRACT[[&lt;Position&gt;]] = &lt;New string&gt;: Replaces the character found in the string by the new string. 
	

- STRINGTOEXTRACT[[&lt;Start&gt; to &lt;End&gt;]]

- STRINGTOEXTRACT[[&lt;Start&gt; to]]

- STRINGTOEXTRACT[[to &lt;End&gt;]]

- STRINGTOEXTRACT[[&lt;Start&gt; on &lt;Number&gt;]]




**Caution**: If you are using a procedure that handles strings with the [[]] operators, pay great attention to the syntax used. You may inadvertently modify character strings. 
For example, the following procedure can modify part of the string.


```wl
PROCEDURE P(sString)
IF sString then sString="5"
// -- Call to the procedure via the line:
p(sVar[[3 to]])
```


To avoid modifying the initial string, the parameter must be passed by value:

- by using brackets around the parameter in the call to the procedure, 

- by using the [Local keyword](../Motscles/1513001.md) in the header of the procedure.




 **Difference with WINDEV 7.5**: 

- WINDEV: The [[]] operator used with a negative value in second bound returns an empty string ("").

- WINDEV 7.5: The [[]] operator used with a negative value in second bound returns the full string.




**Example**: Parsing a character string, character by character: EDT_HTML and EDT_TEXT are 2 edit controls.


```wl
// Variables
sString is string = EDT_HTML
sCharact is string
i is int = 1
// Browse the HTML control
sCharact = sString[[i TO i]]
WHILE i <= Length(sString) 
	i++
	// Process on sCharact here 
	// Next characters
	sCharact = sString[[i TO i]]
END
```

<a name="NOTE2_3"></a>


### Flexible equality and very flexible equality
<a name="flexible_equality_and_very_flexible_equality_ELTPARAGRAPHE000342"></a>

The flexible equality (~=) and the very flexible equality (~~) operate on the character strings only (except for the fixed strings). These operators are used to:

- make no difference between the uppercase characters and the lowercase characters, 

- ignore the space characters found before and after the string whose test must be run, 

- ignore the lowercase accented characters, 

- ignore the space characters and the punctuation characters inside the strings **(very flexible equality only).**




**HFSQL equivalence**: To retrieve the equivalence to the very flexible equality when performing a search on a text key in an HFSQL file, the following options must be configured when the item is described in the analysis:<br>![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=Indexation%20des%20m%E9mos%20-%20HC%20N%B0002.gif)



<a name="NOTE2_4"></a>


### Operators on the character strings and UNICODE
<a name="operators_the_character_strings_and_unicode_ELTPARAGRAPHE000361"></a>

The available operators are as follows:

- "** +**": To concatenate strings

- "** [**" and " **]**": To optionally concatenate strings

- "** [[**" and " **]]**" (double opening brackets and double closing brackets): Operator for extracting a substring

- "** [=**": Starts with




**"+", "[" and "]" operators**
Two UNICODE strings can be concatenated. A UNICODE string and an ANSI string cannot be concatenated.
Remark: If the concatenation of two AINSI strings is assigned to a UNICODE string (and conversely), the conversion will be implicitly performed.

**"[[" and "]]" operator**
All the syntaxes of the [[]] operator are available for the Unicode strings.

- If the string passed as parameter is in ANSI format, the result returned by the [[]] operator will be in ANSI format.

- If the string passed as parameter is in Unicode format, the result returned by the [[]] operator will be in UNICODE format.

- The position parameters and the length parameters are expressed in number of characters.


**Remark**: If the result of the [[]] operator on an ANSI string is assigned to a UNICODE string (and conversely), the conversion will be implicitly performed. 
<a name="NOTE2_5"></a>


### Position in a character string
<a name="position_character_string_ELTPARAGRAPHE000464"></a>

- The position of the first character is set to 1 (and not to 0).					

- [Position](../WDLang1/3024027.md) returns the start position of a specified character string inside another character string.



<a name="NOTE2_6"></a>


### WLanguage functions
<a name="wlanguage_functions_ELTPARAGRAPHE000474"></a>

The character strings can also be handled by the functions:

- [Right](../WDLang1/3024016.md)

- [Left](../WDLang1/3024001.md)

- [Middle](../WDLang1/3024023.md)

- [for managing the character strings](../WDLang1/3024015.md)


**Remark**: in most cases, the [[]] operators are more efficient than the WLanguage functions.






- ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=____operators.gif) ***Unit examples (WINDEV)*** : **[[ ]] operators** <br>Using the [[ ]] operators and the different syntaxes.


