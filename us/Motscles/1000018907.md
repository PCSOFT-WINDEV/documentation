
## Procedure (Type of variable)

***In french: Procedure ***
				



<a name="description"></a>
The **Procedure** type is used to handle:  

- the procedures or methods found in the project. 

- the procedures or methods in an internal component.

- the procedures or methods in an external component. 




This type of variable is very useful, especially: 

- to optimize the time for starting the procedures. Using this type of variable allows you to replace the procedures run by [ExecuteProcess](../WDLang1/3013031.md). 

- to implement advanced algorithms (custom events, notifications, ...). 








This help page presents: 

- [Simple assignment of a Procedure variable](#NOTE0_1). 

- [Multiple assignment of a Procedure variable](#NOTE0_2). 

- [Runtime order of procedures](#NOTE0_5).

- [Passing Procedure parameters by reference or by value](#NOTE0_3).

- [Call to the procedures found in a Procedure variable](#NOTE0_4).  



<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
p is Procedure
// Assign the Procedure variable with the "MyProcedure" procedure found in the project
p = MyProcedure
// Run the procedure
p(5)
// Equivalent to MyProcedure(5)
```

<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks


### Assigning a Procedure variable
<a name="assigning_procedure_variable_ELTPARAGRAPHE000077"></a>

A **Procedure** variable can be assigned with: 

- **a procedure known in the project**. 
	The following syntax must be used: 
	
	```txt
	<Name of Procedure variable> = <Procedure>
	```

	For example: 
	
	```wl
	p is Procedure
	p = CheckExistence
	```

	Remarks: 

	- The &lt;Procedure&gt; parameter must correspond to a procedure found in the project. 

	- The &lt;Procedure&gt; parameter must not be enclosed in quotes. 

	- The &lt;Procedure&gt; parameter must not be followed by call parameters. 

	- The &lt;Procedure&gt; parameter must not correspond to the name of an internal procedure. 




- ![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) **a procedure dynamically compiled by [Compile](../WDLang1/3013015.md)**. 
	The following syntax must be used: 
	
	```txt
	<Name of Procedure variable> = Compile(<Procedure code>)
	
	or 
	
	<Name of Procedure variable> = 
				Compile(<Procedure Name>, <Procedure Code>)
	```

	For example: 
	
	```wl
	p is Procedure
	p = Compile("RESULT n1 + n2")
	```

	Remarks: 

	- If the name of the procedure is specified, the procedure can also be executed by [Execute](../WDLang1/3013041.md) and [ExecuteProcess](../WDLang1/3013031.md). 

	- By default, the procedure dynamically compiled is a global procedure. To define a local procedure, you muse use the syntax defining the name of the procedure in order to specify the requested element in the name of the procedure. 




- **a class method**. 
	The following syntax must be used: 
	
	```txt
	<Name of Procedure variable> = <Method>
	```

	Remarks: 

	- The &lt;Method&gt; parameter can correspond to a method of the current class or of a base class. 

	- If &lt;Method&gt; is an instance method, the following notation should be used:
			
		```txt
		<An_Object>.<Method>
		```










**Other possible syntaxes**

- You also have the ability to copy the procedures of a **Procedure** variable into another **Procedure** variable. The syntax is as follows: 
	
	```txt
	<Name of destination Procedure variable> = 
			<Name of Source Procedure Variable>
	```

	Remark: It is a copy of variable: any modification made to one of the variables will not be applied to the other one. 

- You also have the ability to find at runtime a procedure identified by its name and to assign it to a **Procedure** variable. In this case, the following syntax must be used: 
	
	```txt
	<Name of Procedure variable> = <Procedure name (with quotes)>
	```

	Remark: Using this syntax to assign a **Procedure** variable takes quite a long time because the name of the procedure is sought in all the procedures of the project.  

- **Assignment by reference (&lt;- operator)**
	The assignment by taking reference forces the destination element to reference the same data area as the source element.
	Therefore, a **Procedure** variable can be handled by another **Procedure** variable. To do so, we must retrieve a reference on the **Procedure** variable itself. Any future modification made to one of the variables will be applied to the other one.
	The following syntax must be used: 
	
	```txt
	<Name of destination Procedure variable> <- <Name of source Procedure variable>
	```




<a name="NOTE0_2"></a>


### Multiple assignment of a Procedure variable
<a name="multiple_assignment_procedure_variable_ELTPARAGRAPHE000165"></a>

Several procedures or methods can be assigned to a **Procedure** variable via the "+=" operator. The different procedures will be called one after another. 

For example: 


```wl
p is procedure
p += Proc1
p += Proc2
```


Remark: To delete a procedure from a **Procedure** variable, you have the ability to use the "-=" operator. 

For example: 


```wl
p is procedure
p += Proc1
p += Proc2

...

p -= Proc2
```

<a name="NOTE0_5"></a>


### Runtime order of procedures
<a name="runtime_order_procedures_ELTPARAGRAPHE000186"></a>

You have the ability to replace or insert a procedure before or after the other procedures in a **Procedure** element via [Before](../Proprietes/1000020916.md) and [After](../Proprietes/1000020917.md). 

Example: 


```wl
p is Procedure
p = MyProcedure
p.After = MyProcedureCalledAfter
p.Before = MyProcedureCalledBefore

// The order for calling the procedures will be:
// - MyProcedureCalledBefore
// - MyProcedure
// - MyProcedureCalledAfter
p()
```





<a name="NOTE0_3"></a>


### Passing Procedure parameters by reference or by value
<a name="passing_procedure_parameters_reference_value_ELTPARAGRAPHE000204"></a>

To handle a procedure as parameter of another procedure, you must use a typed parameter. The following syntax must be used: 

- to pass parameters by reference: 
	
	```txt
	PROCEDURE <ProcedureName>(MyProcedure is Procedure)
	```


- to pass parameters by value: 
	
	```txt
	PROCEDURE <ProcedureName>(LOCAL MyProcedure is Procedure)
	```





The possible calls to the &lt;ProcedureName&gt; procedure are: 

- **call while specifying a procedure known by the project**: the following syntax must be used: 
	
	```txt
	ProcedureName(<Project procedure>)
	```

	Remarks: 

	- The &lt;Project Procedure&gt; parameter must correspond to a procedure found in the project. 

	- The &lt;Project Procedure&gt; parameter must not be enclosed in quotes. 

	- The &lt;Project Procedure&gt; parameter must not be followed by shortcuts. 




- **call while specifying a class method**: the following syntax must be used: 
	
	```txt
	ProcedureName(<Method>)
	```

	Remarks: 

	- The &lt;Method&gt; parameter can correspond to a method of the current class or of a base class.

	- If &lt;Method&gt; is an instance method, the following notation should be used: &lt;An_Object&gt;.&lt;Method&gt;.




- **call with reference to a procedure variable**: the following syntax must be used: 
	
	```txt
	ProcedureName(<Name of Procedure variable>)
	```


- **call with search at runtime for a procedure identified by its name**: the following syntax must be used: 
	
	```txt
	ProcedureName(<Procedure name (with quotes)>)
	```




<a name="NOTE0_4"></a>


### Call to the procedures found in a Procedure variable
<a name="call_the_procedures_found_procedure_variable_ELTPARAGRAPHE000246"></a>

The call to the procedures found in a Procedure variable is performed by the standard syntax of procedures. This syntax is directly used on the Procedure variable with brackets containing 0, 1 or several parameters: 


```txt
<Name of Procedure variable> ([<Parameter 1> [, ...[, <Parameter N>]]])
```


Examples: 


```wl
p1 is Procedure
p1 = Proc1
p1()

p2 is Procedure
p2 = Proc2
p2(1,2)
```


If the Procedure variable contains several procedures, all the procedures are run according to the order of assignments. 


```wl
p3 is Procedure
p3 += Proc3_1
p3 += Proc3_2
p3("parameter")	// calls Proc3_1 and Proc3_2
```


**Managing the return value**

- If the Procedure variable contains a single procedure, the call returns the return value of the procedure.

- If the Procedure variable contains several procedures, the call returns the return value of the last procedure called.





- ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=TheProceduretype.gif) ***Unit examples (WINDEV)*** : **The Procedure type** <br>Using the Procedure type of WLanguage.<br>This Procedure type is used to easily manage a list of procedures.<br>You have the ability to dynamically add some or delete some (with the += and -= operators)


