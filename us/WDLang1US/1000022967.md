


## DocInsertTable (Function)

***In french: DocInsèreTableau***



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Inserts a table into a Word Processing document ir replaces the specified fragment with a new table. 


<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
// Inserts a 3x3 table at position 1
DocInsertTable(WP_Table, 1, 3, 3)
```

```wl
// Inserts a table containing 3 columns and 2 rows at cursor position
DocInsertTable(WP_Table, WP_Table.Cursor, 3, 2)
```

<a name="XSYNTAX"></a>

## Syntax
<a name="SYNTAX1"></a>

### Inserting a table into a a Word Processing document

`<Result> = DocInsertTable(<Document> , <Position> [, <Number of columns> [, <Number of rows>]])`
---

**`<Result>: (docFragment variable)`**

[docFragment](../WDLang1/1000022482.md) variable with the inserted fragment. 

**`<Document>: (Document variable or character string)`**

Document to use. This document corresponds to: 

- ![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) the name of a Word Processing control.

- a variable of type [Document](../WDLang1/1000022461.md). 




**`<Position>: (Integer)`**

Position where the table will be inserted. This position is expressed in number of characters. 

**`<Number of columns>: (Optional integer)`**

Number of columns in the table. This parameters corresponds to 1 by default. 

**`<Number of rows>: (Optional integer)`**

Number of rows in the table. This parameters corresponds to 1 by default.


<a name="SYNTAX2"></a>

### Replacing the existing fragment by a new table

`<Result> = DocInsertTable(<Fragment> [, <Number of columns> [, <Number of rows>]])`
---

**`<Result>: (docFragment variable)`**

[docFragment](../WDLang1/1000022482.md) variable with the inserted fragment. 

**`<Fragment>: (docFragment variable)`**

Name of the [docFragment](../WDLang1/1000022482.md) variable that corresponds to the fragment to handle. The current fragment content will be replaced with the created table. 

**`<Number of columns>: (Optional integer)`**

Number of columns in the table. This parameters corresponds to 1 by default. 

**`<Number of rows>: (Optional integer)`**

Number of rows in the table. This parameters corresponds to 1 by default.



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks


### Handling a Table through programming
<a name="handling_table_through_programming_ELTPARAGRAPHE000148"></a>

A table in a Word Processing document can be handled by the WLanguage functions for managing arrays. 

For example: 

- [Add](../WDLang1/1000017348.md) or [ArrayAdd](../WDLang1/3075003.md) to add rows or columns,

- [Delete](../WDLang1/1000017354.md) or [ArrayDelete](../WDLang1/3075008.md) to delete rows, columns, or the table itself. 




Example:


```wl
// Inserts a 3x3 table at position 1
DocInsertTable(WP_ExampleWP, 1, 3, 3)

// Define a fragment corresponding to the table
f is docFragment(WP_ExampleWP.Value, WP_ExampleWP.Cursor, 0)

let para <- f.Paragraph[1]
IF para.Table = Null THEN
	RETURN
END

doc is Document <- WP_ExampleWP.Value

// Adds a row to the table
nIndex is int = Add(para.Table.Rows)

// Input in cell 2,2
para.Table.Cells[2,2].Content.Text = "I am in cell 2,2"

// Deletes row 3
Delete(para.Table.Rows, 3)

// Deletes column 3
Delete(para.Table.Columns, 3)

// Deletes the entire table where the cursor is positioned
Delete(doc.Paragraph, para.ParagraphSubscript)
```


Example for creating a table in a document with the content of a Table control: 

```wl
// A window contains a Table control populated programmatically named TABLE_Demo
// and a Word Processing control named WP_Demo
// The following code adds a table into the Word Processing control with:
// - in first table row, the title of columns found in the Table control,
// - the content of Table control in the other table rows.

MyDoc is Document
cTable is Control <- TABLE_Demo
pCol is Control

FragmentStart is docFragment(MyDoc,1)
FragmentStart.Formatting.FontSize = 24
FragmentStart.Formatting.TextColor = DarkRed
FragmentStart.Text = "Table in WP with " + cTable.Caption + CR + CR

// Insert the table into the document in memory ...
DocInsertTable(MyDoc, 20, TableCount(cTable, toColumn), cTable.Occurrence + 1)

FOR EACH para OF MyDoc.Paragraph
	IF para.Table <> Null THEN
		// For all columns of the table control ...
		FOR nColumn = 1 _TO_ TableCount(cTable, toColumn)
			
			// First tab le line containing the title of columns
			pCol <- TableEnumColumn(cTable, nColumn)
			para.Table.Cells[1, nColumn].Content.Text = pCol.Caption
			
			// Fill all rows of this column
			FOR nRow = 1 _TO_ cTable.Occurrence
				para.Table.Cells[nRow+1, nColumn].Content.Text = pCol[nRow]
			END
		END
		BREAK
	END
END

// Document in memory assigned to the Word Processing control
WP_Demo = MyDoc
```


<a name="XComponent"></a>

## Component:
wd280mdl.dll
