


## Serialize (Function)

***In french: Sérialise***



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Transforms the following elements into a specific format:

- a structure (and its sub-elements).

- a class (and its sub-elements).

- an array (including the associative arrays).

- a queue.

- a stack.

- a list. 

- an advanced type of variable ([gglCalendar](../WDLang5/1000017418.md) for example).




The available formats are XML, binary and JSON. Then, the initial format can be retrieved by [Deserialize](../WDLang1/3013066.md).


<a name="Example1"></a>
<a name="sample_code"></a>

## Example
<a class="notetitle" target="_blank" href="$DOC$=3013077&name=serialize_function&product=WD">See additional examples</a>

```wl
// This example explains how to use the Serialize/Deserialize functions
// with an Array variable. 
// These functions can use all types of WLanguage variables.

MyArray is array of strings
bufResult is Buffer

// Adds elements into the array
Add(MyArray, "WINDEV")
Add(MyArray, "WEBDEV")
Add(MyArray, "WINDEV MOBILE")

// Serialize the array in the buffer in JSON
// => Save the array and its entire content in a JSON string
Serialize(MyArray, bufResult, psdJSON)

// Deserialize the JSON buffer
// => Rebuild the WLanguage array from the JSON string
MyRebuiltArray is array of strings
Deserialize(MyRebuiltArray, bufResult, psdJSON)
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Syntax

`Serialize(<Variable> , <Buffer> , <Parameters> [, <Root name>])`
---

**`<Variable>: (Type of variable)`**

Variable to serialize. This variable is a structure, a class, an array, a queue, a list or a stack.

**`<Buffer>: (Ansi character string or buffer)`**

Variable that contains the result of serialization.



**`<Parameters>: (Integer constant)`**

Type of serialization:


|   |   |
| --- | --- |
| *psdBinary* | Binary serialization.<br>![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Binary deserialization is not available. |
| *psdBinaryFormat16* | Binary serialization compatible with version 16.<br><br>This format must be used if an application in version 17 or later must share data with an application in version 16 or earlier. <br>**This format must not be used in Unicode mode.**<br><br>![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Binary deserialization is not available. |
| *psdJSON* | JSON serialization.<br><br>The encoding used corresponds to the JSON standard: <br><br>- 7-bit ASCII encoding, i.e. the first 128 characters, unaccented characters,<br><br>- JSON encoding for the other characters: "\\u" followed by the character code in 4-byte hexadecimal format. Example: for "é" (ASCII code 233, hexadecimal code E9), the encoding corresponds to "\\u00E9".<br><br><br><br><br>![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) JSON deserialization is not available. |
| *psdMinified* | **Caution**: This constant must be combined with the *psdJSON* constant. Otherwise, it will be ignored. <br><br>The encoding used corresponds to the JSON standard. This constant is used to generate a JSON by removing unnecessary spaces (carriage returns, spacing characters, etc.).<br><br>![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) This constant is not available. |
| *psdFormatting* | Serialization in JSON or XML format with carriage return and indent.<br><br>The encoding used corresponds to the JSON or XML standard. <br><br>Caution: This constant must be combined with the *psdJSON* or *psdXML* constant. <br><br>![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) The serialization in JSON format with formatting is not available. |
| *psdXML* | XML serialization with reference to sub-objects. This type of serialization allows you to use the XML format as storage and exchange modes between applications written in WLanguage. <br><br> |
| *psdXMLAggregated* | XML serialization with direct aggregation of sub-objects. This type of serialization allows you to easily generate an XML file in a standard format to perform exchanges with other systems.  <br><br>Remark: A deserialization in *psdXMLAggregated* mode is available, however the WLanguage elements (variants, arrays, derived classes, ...) will differ from the source ones.<br><br>![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) This serialization mode is not available. |



**`<Root name>: (Character string)`**

Name of the root of the generated XML. This parameter is taken into account in *psdXMLAggregated* mode only.
If this parameter is not specified, the name of the root is "DOCUMENT". 





<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks


### Deserialization
<a name="deserialization_ELTPARAGRAPHE000203"></a>

The deserialization of an array, queue, list or stack deletes the content from this element.

If there are additional members in the structure or in the class:

- if a structure or a class is deserialized: additional members keep their values from before the deserialization.

- if an array of structures or classes is deserialized: additional members take the default value of the member type.




If there are additional members in the serialized buffer, they are ignored during the deserialization.

To deserialize an untyped dynamic array, this array must be allocated beforehand.

To deserialize a class or a structure containing an untyped dynamic array, this array must be allocated beforehand.

The wd280xml.dll or wp280xml.dll library is necessesary to deserialize an XML document.

![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) In order for the serialization format to be compatible between the WINDEV applications and the WINDEV Java applications, the names of the following serialized elements must contain no special character and no accented character: 

- classes,

- arrays of classes,

- structures,

- arrays of structures.





<a name="NOTE0_2"></a>


### Change of behavior in version 10: generating a XML serialization for the Mobile devices
<a name="change_behavior_version_10_generating_xml_serialization_for_the_mobile_devices_ELTPARAGRAPHE000249"></a>

Until version 100050, the *psdXML* constant did not generate a valid XML file. The generated format caused problems on Mobile devices. This problem was fixed.

An XML file created with a version later than version 100050 cannot be read by a program compiled with an earlier version. To keep the operating mode of version 100050, all you have to do is use the ***psdXML_OldFormat*** constant.
<a name="NOTE0_3"></a>


### Speed
<a name="speed_ELTPARAGRAPHE000258"></a>

The binary serialization is faster than the XML serialization.
<a name="NOTE0_4"></a>


### Serializing the members of classes and structures
<a name="serializing_the_members_classes_and_structures_ELTPARAGRAPHE000267"></a>

By default, all the members of a class or structure are serialized. 

You have the ability to precisely manage the serialization of each member: 

- by specifying the member that will be serialized during the call to **Serialize**. 
	This operation can be performed on all types of serialization (XML, JSON, binary). 
	

- by changing the name of the member during the serialization with **Serialize**.  
	This operation can be performed during a binary serialization only. 
	




This management of serialization is performed by using the following syntax: 

- Serialization (or not) of a member : 
	
	```txt
	<Member name> is <Member type> [, Serialize = <True/False>] [, xmlAttribute]
	```


- Serialization and change of member name: 
	
	```txt
	<Member name> is <Member type> [, Serialize = <New name>] [, xmlAttribute]
	```





The "xmlAttribute" extension attribute is used to specify that the member is created as attribute (instead of tag). This attribute is taken into account during a *psdXMLAggregated* serialization only. 
![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) The "xmlAttribute" extension attribute is not available.

Example for a class: 


```wl
Cl is class
	// Serialized member
	SerializedMember is int 
	// Non-serialized member
	NonSerializedMember is string, Serialize = False  
	// Member renamed during the serialization
	RenamedMember is int, Serialize = "NewMemberName" 
END
```
Remark: To avoid serializing an array member, use the following syntax: 


```txt
t is array <Serialize = False> of int
t2 is array <Serialize = "<serialization name>"> of int
```
<a name="NOTE0_5"></a>


### Serialization name of classes
<a name="serialization_name_classes_ELTPARAGRAPHE000320"></a>

The "Serialize" extension attribute is available for the classes in order to specify the serialization name. 

The following syntax is used: 


```txt
<Class name> is Class [, Serialize = <New name>]
```


Example: 


```wl
CTest is Class, Serialize = "Test"
	m_sLastName is string, Serialize = "LastName"
	m_sFirstName is string, Serialize = "FirstName"
	m_sMiddleName is string, Serialize = "MiddleName"
END

sXML is Buffer
 myTest is CTest
 myTest.m_sLastName = "My last name"
 myTest.m_sFirstName = "My first name"
 myTest.m_sMiddleName = "My middle name"
Serialize(myTest, sXML, psdXMLAggregated)


// Content of sXML where the names in the serialization can be found:
// <?xml version="1.0" encoding="UTF-8"?>
// <Test>
//  <LastName>My last name</LastName>
//  <FirstName>My first name</FirstName>
//  <MiddleName>My middle name</MiddleName>
// </Test>
```


<a name="NOTE0_7"></a>


### Limitations
<a name="limitations_ELTPARAGRAPHE000338"></a>

- The fixed arrays and the associative arrays of local structures cannot be serialized.

- The global members of classes are not serialized.

- The XML serialization/deserialization of an array of structures with a string member containing "\\0" or Charact(0) returns the following error: "Wrong serialization format". In this case, a binary serialization/deserialization must be performed.

- You cannot serialize/deserialize an array of classes or structures if this class or structure contains a dynamic array without type. A dynamic array with defined type must be used in this class or in this structure.

- To serialize/deserialize a class, you must have a constructor with 0 parameter.

- stacks, queues, lists: 

	- The JSON serialization of stacks, queues and lists is not available. 

	- ![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) You cannot serialize/deserialize Queue, Stack or List variables.




- Only JSON serialization is available for [Record](../WDLang4/1000020962.md) variables. No deserialization is possible.  





- ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=TheSerialize_Deserializefunctions.gif) ***Unit examples (WEBDEV)*** : **The Serialize/Deserialize functions** <br>This example explains how to use the WLanguage functions Serialize and Deserialize.<br>The serialization consists in saving a variable, an object, a structure, an array or any other element in a buffer. Then, this buffer can be saved on disk or sent by socket. This allows for the persistence of objects.<br>The Deserialize function is used to rebuild an object, an array or a structure from a buffer.
- ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=TheSerialize_Deserializefunctions.gif) ***Unit examples (WINDEV)*** : **The Serialize/Deserialize functions** <br>Using the WLanguage Serialize and Deserialize functions<br>The serialization consists in saving a variable, an object, a structure, an array or any other element in a buffer. Then, this buffer can be saved on disk or sent by socket. This allows for the persistence of objects.<br>The Deserialize function is used to rebuild an object, an array or a structure from a buffer.

<a name="XComponent"></a>

## Business / UI classification:
Neutral code
## Component:
wd280vm.dll
