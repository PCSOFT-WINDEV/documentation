


## Add (Function)

***In french: Ajoute***



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Adds an element in last position:

- of a one-dimensional WLanguage array.

- of an advanced array property (array of gglCalendar events, array of Word Processing document, etc.).

- from a WLanguage list.




This function can also be used to concatenate two arrays or two lists.

**Remarks**: 

- This function is equivalent to [ArrayAdd](../WDLang1/3075003.md).





<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
MyArray is array of 2 strings
Add(MyArray, "WINDEV")
Add(MyArray, "WEBDEV")
Add(MyArray, "WINDEV and WEBDEV")
// Display the content of 3rd element (the first 2 elements are empty)
Trace(MyArray[3])  // Displays "WINDEV"
```


<a name="Example2"></a>

```wl
MyArray is array of 2 strings
MyArray[1] = "WINDEV"
MyArray[2] = "WEBDEV"
Add(MyArray, "WINDEV and WEBDEV")
// Display the content of 3rd element
Trace(MyArray[3]) // Displays "WINDEV and WEBDEV"
```


<a name="Example3"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) 
```wl
// Example to add an element into an XML file
NodeRSS is xmlNode,description="NEWS.rss.channel.item"
NodeRSS.title = DateToString(DateSys(), "DD/MM/YYYY") + " - " + ...
		TimeToSTring(TimeSys(), "HH:MM") + ": " + sTitle
NodeRSS.description = sComment		
NodeRSS.link = sHTTPLink		
NodeRSS.enclosure:type = "image/jpeg"
NodeRSS.enclosure:length = 150
NodeRSS.enclosure:url = sURL_Image

Add(MyXMLDoc.rss.channel, NodeRSS)
XMLSave(MyXMLDoc, gsPathXMLFile)
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Syntax

### Adding an element to an array, to an advanced array property or to a list

`<Result> = Add(<WLanguage variable> [, <Value>])`
---

**`<Result>: (Integer)`**

Index at which the specified element was added. If an error occurs, a fatal error is displayed.

**`<WLanguage variable>: (Array or List)`**

Name of variable to use. This variable can be:

- An Array variable. 

- A List variable.
	




**`<Value>: (Type of array elements, optional)`**

Value that will be added to the array or to the list. This parameter is mandatory for a list.

For an array, if this parameter is not specified, the array is enlarged with the default value of the type of the other array elements.  


<a name="SYNTAX2"></a>

### Concatenating arrays or lists

`<Result> = Add(<WLanguage variable> [, <Variable to concatenate>])`
---

**`<Result>: (Integer)`**

Index at which the specified element was added. If an error occurs, a fatal error is displayed.

**`<WLanguage variable>: (Array or List)`**

Name of variable to use. This variable can be:

- An Array variable. 

- A List variable.
	




**`<Variable to concatenate>: (Optional array or list)`**

Name of variable that will be added after the existing elements. This variable can be:

- An Array variable, to concatenate two arrays. 

- A List variable, to concatenate two lists.  
	






<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks


### Adding an element into a WLanguage array
<a name="adding_element_into_wlanguage_array_ELTPARAGRAPHE000189"></a>

When **Add** is called:

- the array is automatically enlarged to receive the new element.

- the element is converted (if necessary) into the type of the other array elements.




**Remark**: When declaring an array of N by M elements, this array contains N empty elements. For example, the array declared below contains 3 empty strings.


```wl
MyArray is array of 3 strings
```
When adding an element (**Add**), this element is automatically added after the elements already found in the array.

In our example, the added element will correspond to the 4th element.
<a name="NOTE0_2"></a>


### Adding an element into an advanced array property
<a name="adding_element_into_advanced_array_property_ELTPARAGRAPHE000212"></a>

When **Add** is called:

- the advanced variable must be created.

- the advanced type must have an enumerator of modifiable collection type.

- the advanced type is automatically enlarged to receive the new elements.

- the element is initialized with the value passed as parameter. If no value is passed in parameter, the element is initialized with the default value of the type of the array elements.



<a name="NOTE0_3"></a>


### Adding an element into a list
<a name="adding_element_into_list_ELTPARAGRAPHE000228"></a>

When **Add** is called:

- the list is automatically enlarged to receive the new element.

- the element is converted (if necessary) into the type of the other list elements.



<a name="NOTE0_4"></a>


### Adding an array into an array (concatenation)
<a name="adding_array_into_array_concatenation_ELTPARAGRAPHE000246"></a>

When **Add** is called:

- the array is automatically resized to include the new elements. The elements of &lt;Name of variable to concatenate&gt; are added at the end of &lt;Variable name&gt;.

- the two arrays must have the same type.

- the arrays must have the same dimension.

- the array dimensions (other than the first dimension) must be identical. 
	For example: 
	Correct code: 
	
	```wl
	t1 is array of * by 2 int = [[1,2],[3,4]]
	t2 is array of * by 2 int = [[5,6],[7,8],[9,10]]
	Add(t1, t2)
	```
Incorrect code: 
	```wl
	t1 is array of * by 3 int = [[1,2],[3,4]]
	t2 is array of * by 2 int = [[5,6],[7,8],[9,10]]
	Add(t1, t2)
	```




<a name="NOTE0_5"></a>


### Adding a list into a list (concatenation)
<a name="adding_list_into_list_concatenation_ELTPARAGRAPHE000277"></a>

When **Add** is called:

- the list is automatically enlarged to receive the new elements. The elements of &lt;Name of variable to concatenate&gt; are added at the end of &lt;Variable name&gt;.

- both lists must be of the same type.



<a name="NOTE0_5_1"></a>


### Adding the content of a structure to an array of structures without using a variable of the structure
<a name="adding_the_content_structure_array_structures_without_using_variable_the_structure_ELTPARAGRAPHE000295"></a>

If you are using an array of structures with few members, it may be easier to directly add a structure by using the [ ] operator (square brackets). For example, for the following structure:


```wl
// Structure to store a letter and its ASCII code
STKey is Structure
	sKey is string
	nAsciiCode is int
END
// Array of structures
arrKeys is array of STKey
```
In most cases, content is added by using a variable of a type declared in the structure:

```wl
// Temporary structure for the addition
stAKey is STKey
// Stores the letter A
stAKey.sKey = "A"
stAKey.nAsciiCode = Asc("A")
Add(arrKeys, stAKey)
```
With the [ ] operator, you get better legibility:

```wl
// Stores the letter A
Add(arrKeys, ["A", Asc("A")])
```

<a name="NOTE0_6"></a>


### Use conditions
<a name="use_conditions_ELTPARAGRAPHE000327"></a>

This function can be used with the structures. In this case, you must:

1. Declare a variable (same type as the structure). 

2. Initialize each member.

3. Pass the structure variable as parameter to **Add**.




This function cannot be used on:

- non-created arrays.

- fixed arrays.



<a name="NOTE0_7"></a>


### Miscellaneous
<a name="miscellaneous_ELTPARAGRAPHE000347"></a>



- To add an element at a given position, use [ArrayInsert](../WDLang1/3075009.md) or [Insert](../WDLang1/1000017351.md).

- To add an element into a sorted array (while respecting the sort), use [ArrayAddSorted](../WDLang1/1000017013.md).




<a name="XComponent"></a>

## Component:
wd280vm.dll
