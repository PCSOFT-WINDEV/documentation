
## WLanguage procedure called by fTrackDirectory



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
WLanguage procedure ("callback") called by [fTrackDirectory](../WDLang1/1000020523.md) when a change is made to the tracked directory. 

This can be a local, global, or internal.
<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
// Name of directory to track
sDirectoryName is string = "C:\Temp\MyDir"
// The fTrackDirectory_Callback procedure will be called 
// when a file or directory in the "C:\Temp\MyDir" directory is modified.  
IF fTrackDirectory(sDirectoryName, fTrackDirectory_Callback, ftCreateFile + ...
		ftModifyFile + ftDeleteFile + ftRename) THEN
	// Inform the user that the directory will be tracked
	Info("The " + sDirectoryName + " directory will be tracked.")
ELSE
	// Inform the user that the directory will not be tracked
	Info("The " + sDirectoryName + " directory will not be tracked.")
END
```



```wl
// Code of the procedure
PROCEDURE fTrackDirectory_Callback(sDirectoryName, sFileName, nAction, sOldFileName)
ExecuteMainThread(AddTable,sDirectoryName, sFileName, nAction, sOldFileName)
```



```wl
// Procedure that acts on the main thread
PROCEDURE AddTable(sDirectoryName, sFileName, nAction, sOldFileName)

sActionDesignation is string
// The designation of the action depends on nAction
SWITCH nAction
	CASE ftCreateFile: sActionDesignation = "Creating files"
	CASE ftDeleteFile: sActionDesignation = "Deleting files"
	CASE ftModifyFile: sActionDesignation = "Modifying files"
	CASE ftRename: sActionDesignation = "Renaming files"	
END
// Add a line containing the information about the modification
// into the TABLE_MODIFICATIONS table
TableAddLine(TABLE_MODIFICATIONS, sDirectoryName, sFileName, , ...
		sActionDesignation, sOldFileName)
```

<a name="XSYNTAX"></a>

## Syntax
<a name="SYNTAX1"></a>

`fTrackDirectory_Callback(<Name of tracked directory> , <Name of modified element> , <Action> , <Former name>)`
---

**`<Name of tracked directory>: (Character string)`**

Full name of the tracked directory. 

**`<Name of modified element>: (Character string)`**

Name of the modified file or directory.

**`<Action>: (Integer constant)`**

Action performed: 



|   |   |
| --- | --- |
| *ftCreateFile* | The &lt;Name of modified element&gt; file or directory was created in &lt;Directory&gt;. |
| *ftDeleteFile* | The &lt;Name of modified element&gt; file or directory was deleted from &lt;Directory&gt;. |
| *ftModifyFile* | The &lt;Name of modified element&gt; file or directory was modified in &lt;Directory&gt;. |
| *ftRename* | The &lt;Name of modified element&gt; file or directory was renamed in &lt;Directory&gt;. |



**`<Former name>: (Character string)`**

Former name of the modified file or directory if they were renamed.



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks


### Caution
<a name="caution_ELTPARAGRAPHE000061"></a>

This WLanguage procedure is run in a WLanguage thread.
Caution: The following processes cannot be run in the threads:

- opening windows with WLanguage functions such as [Open](../WDLang1/3038035.md), [Use](../WDLang1/3038044.md), [Close](../WDLang1/3038018.md), ... A specific management mode must be implemented if some windows must be handled in threads (rare case). For more details, see [Opening a window in a secondary thread](../WDLang1/3077015.md). 

- managing events.

- managing timers.




For more details on threads, see [Managing threads](../WDLang1/3077026.md).





<a name="XComponent"></a>

## Business / UI classification:
Neutral code
## Component:
wd280std.dll
