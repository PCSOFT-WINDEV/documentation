


## TableFormulaAdd (Function)

***In french: TableFormuleAjoute***



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Adds a calculated row to a Table or TreeView Table control by providing custom calculation procedures.
<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
TableFormulaDeleteAll(TABLE_MyTable)
nRow is int
nRow = TableFormulaAdd(TABLE_MyTable.COL_Num, "Positive mean", ProcInit, ProcAdd, ProcEnd)
// Change the background color of the row for custom calculation
COL_Num[nRow].BackgroundColor = LightRed

nCounter is int
INTERNAL PROCEDURE ProcInit() 
	nCounter = 0
	RESULT 0
END

INTERNAL PROCEDURE ProcAdd(Accumulator, ColValue) 
	// Ignores the negative numbers or NULL
	IF (ColValue <= 0) RESULT Accumulator
	nCounter++
	RESULT Accumulator + ColValue
END
INTERNAL PROCEDURE ProcEnd(Accumulator) 
	IF nCounter = 0 THEN RESULT 0
	// Calculate the mean
	RESULT Accumulator/nCounter
END
```


<a name="Example2"></a>

```wl
nRow2 is int
nRow2 = TableFormulaAdd(TABLE_TableControl.COL_Time, "Average duration", Null, ...
			AverageDuration_Iteration, AverageDuration_End)

INTERNAL PROCEDURE AverageDuration_Iteration(Accumulator, ColValue)
	RESULT Accumulator + [TAB] + ColValue
END

INTERNAL PROCEDURE AverageDuration_End(Accumulator)
	nSum is 8-byte int
	nNbValid is int
	FOR EACH STRING sValue OF Accumulator SEPARATED BY TAB
		IF ValidTime(sValue) THEN
			nSum += TimeToInteger(sValue)
			nNbValid++
		END
	END
	RESULT TimeToString(IntegerToTime(nSum/nNbValid), "HH:MM:SS")
END
```

<a name="XSYNTAX"></a>

## Syntax
<a name="SYNTAX1"></a>

`<Result> = TableFormulaAdd(<Column> , <Calculation caption> , <Initialization> , <Iteration> , <Ending>)`
---

**`<Result>: (Integer)`**

Index of the row containing the formula. 

**`<Column>: (Control name)`**

Name of column into which the formula will be added.
If this parameter corresponds to an empty string (""), the column to which the current process belongs will be used.

**`<Calculation caption>: (Character string)`**

Caption of the additional row where the calculation will be displayed. 
If this caption does not exist, the row will be created. 
If this caption exists but not for the specified column, the calculation is displayed in the existing row but for the specified column. 
If this caption exists for the specified column, a WLanguage error occurs. 

**`<Initialization>: (Procedure name or NULL)`**



- Name of the WLanguage procedure ("callback") called to initialize the formula. This procedure has the following format:
	
	```txt
	PROCEDURE <Procedure name> ()
	// your code
	
	RESULT <Initialization value Accumulator>
	```

	where &lt;Initialization value Accumulator&gt; is the return value for the first iteration of the calculation.

- NULL if the formula requires no initialization process. 




**`<Iteration>: (Procedure name or NULL)`**



- Name of the WLanguage procedure ("callback") called for each iteration of the formula (each row of the Table control). This procedure has read-only access to the columns of each row. This procedure has the following format:
	
	```txt
	PROCEDURE <Procedure name>(<Accumulator>, <Column value>)
	// Called for each row in the Table control
	// Your code
	
	RESULT <New value Accumulator>
	```

	where: 

	- &lt;Accumulator&gt; is the value coming from the previous calculation (initialization or previous iteration).

	- &lt;Column value&gt; is the value of the current column used to calculate this iteration.

	- &lt;New value Accumulator&gt; is the new value to return for the next iteration or at the end of calculation.




- NULL if the formula requires no iteration process. 




**`<Ending>: (Character string)`**



- Name of the WLanguage procedure ("callback") called to end the formula. This procedure has the following format:
	
	```txt
	PROCEDURE <Procedure name>  (<Accumulator>)
	// your code
	
	RESULT <End value Accumulator>
	```

	where: 

	- &lt;Accumulator&gt; is the value coming from the previous calculation (initialization or previous iteration). 

	- &lt;End value Accumulator&gt; is the return value that corresponds to the end value of the calculation.




- NULL if the formula requires no ending process. 






<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks


### Conditions for adding the row containing the custom calculation formula
<a name="conditions_for_adding_the_row_containing_the_custom_calculation_formula_ELTPARAGRAPHE000104"></a>

- If the calculation named &lt;Calculation caption&gt; does not exist in the Table control, a new calculation row is added below the Table control (after the existing calculations). 

- If a calculation named &lt;Calculation caption&gt; was already defined for another column, the calculation is displayed for the specified column in the existing calculation row.

- If a calculation named &lt;Calculation caption&gt; was already defined for the same column, a WLanguage error occurs.

- Only 5 custom calculation rows can be added. 




**Remark**: This function can be used: 

- on the columns of a Table or TreeView Table control based on a data file. 

- on the columns of a Table or TreeView Table control populated programmatically. 





### Recalculating data
<a name="recalculating_data_ELTPARAGRAPHE000119"></a>

The custom calculation rows are automatically recalculated as soon as the content of the Table control changes.

Tip: The iteration procedure is called for each row: we advise you not to perform slow calculations (avoid the accesses to the database for example).


### Customizing the calculation row
<a name="customizing_the_calculation_row_ELTPARAGRAPHE000128"></a>

The following syntax is used to customize the added row (caption, color, font, height, ...):

```txt
<Table control>[Row number].<Property> = <New value>
```
where &lt;Property&gt; can correspond to one of the properties that can be used on a column of a Table control. For more details, see [Programming custom calculations in Table controls](../WDChamp/1013331.md). 

<a name="XComponent"></a>

## Business / UI classification:
UI Code
## Component:
wd280obj.dll
