


## Management of Google calendars (prefix syntax)
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Overview
<a name="overview_ELTTEXTE000111"></a>
The Google Calendar service is an Internet application provided by Google that is used to manage a calendar on Internet.

WINDEV and WEBDEV allow you to create an application for synchronizing schedules with an existing application for example: using a meeting room, a vehicle, ...

These WLanguage functions also allow you to develop specific interfaces (suited for your business requirements, more user-friendly, etc.) and to add specific processes (prints, among others).

Examples of processes that can be implemented natively in WLanguage:

- Retrieving the detailed list of calendars (professional calendars, personal calendars, etc.).

- Retrieving the list of appointments from a calendar.

- Performing a search in the appointments of a calendar.

- Adding, modifying and deleting appointments.  




**Warning**: Before using a feature linked to Google services, we strongly recommend that you check the license for using this service. Some restrictions may apply. The content of the licenses may change over time. 

PC SOFT is in no case responsible for the way the native access functions are used. Make sure that you comply with the license of the service provider.

<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## How to manage a Google calendar?
<a name="how_manage_google_calendar_ELTTEXTE000152"></a>


### Managing a Google calendar
<a name="managing_google_calendar_ELTPARAGRAPHE000175"></a>

To manage a Google calendar:

1. Create a Google account if necessary. This account can be created via the following address:  [https://www.google.com/accounts/NewAccount?hl=fr](https://www.google.com/accounts/NewAccount?hl=fr)
	Caution: the address of this page may have been modified since the publication date of this page.
	The Google account is identified by an email address and the associated password. 

2. In the code of your application, create a variable of type [gglConnection](../WDLang5/1000018009.md). This variable contains the characteristics of the connection to your Google account.



<a name="NOTE2_2"></a>


### Creating a Google calendar
<a name="creating_google_calendar_ELTPARAGRAPHE000197"></a>

A Google calendar can be created via the Google interface or by programming with the WLanguage functions.

To create a Google calendar with the WLanguage functions:

1. Create a variable of type [gglCalendar](../WDLang5/1000017418.md).

2. Define the characteristics of the calendar via the [gglCalendar](../WDLang5/1000017418.md) properties.

3. Define (if necessary) the events linked to the calendar ([gglEvent](../WDLang5/1000017440.md) variable).

4. Validate the creation of the calendar with [&lt;gglConnection variable&gt;.Write](../WDLang5/1000021460.md).



<a name="NOTE2_3"></a>


### Remark
<a name="remark_ELTPARAGRAPHE000228"></a>

If you use a proxy to access Internet, the proxy must be configured ([Proxy](../WDLang3/3043002.md)) to use Google functions.

<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## How to retrieve a Google calendar and its elements?
<a name="how_retrieve_google_calendar_and_its_elements_ELTTEXTE000236"></a>


### 1st method: retrieving the list of calendars and retrieving their events
<a name="1st_method_retrieving_the_list_calendars_and_retrieving_their_events_ELTPARAGRAPHE000259"></a>

To retrieve a Google calendar from the list of calendars:

1. Declare an array of [gglCalendar](../WDLang5/1000017418.md) variables (to retrieve several calendars).

2. Use the [&lt;gglConnection variable&gt;.ListCalendar](../WDLang5/1000017747.md) function. This function is used to list the available calendars. The calendars found are assigned to the array of gglCalendar variables.

3. Use [&lt;gglConnection variable&gt;.FillCalendar](../WDLang5/1000017749.md) to retrieve the events. The events can be retrieved from a single calendar or from several calendars. The events to retrieve can be filtered (between two dates for example).




Example:

```wl
Cnt is gglConnection
...
arrCalendars is array of 0 gglCalendar 
arrCalendars = Cnt.ListCalendar() 
// First calendar
Calendar is gglCalendar = ArrCalendars[1]
// Retrieve the events between 01/01/2008 and 01/01/2009 included
Cnt.FillCalendar(Calendar, "20080101", "20090102")
// Browse the events of a calendar
Evt is gglEvent
FOR EACH Evt OF Calendar 
	Trace(Evt.Title)
END
```

<a name="NOTE3_2"></a>


### 2nd method: retrieving a specific calendar
<a name="2nd_method_retrieving_specific_calendar_ELTPARAGRAPHE000294"></a>

To retrieve a specific Google calendar as well as its events: 

1. Declare a variable of type [gglCalendar](../WDLang5/1000017418.md).

2. Use the [&lt;gglConnection variable&gt;.GetCalendar](../WDLang5/1000017748.md) function. This function is used to retrieve the Google calendar (and its events) corresponding to the specified title. 




Example:

```wl
Cnt is gglConnection
...

// Retrieve the calendar named "Work"
Calendar is gglCalendar = Cnt.GetCalendar("Work")
// Browse the events of the calendar
IF ErrorOccurred = False THEN
	Evt is gglEvent
	FOR EACH Evt OF Calendar
		Trace(Evt.Title)
	END
END
```


<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## How to add, modify or delete events in a Google calendar?
<a name="how_add_modify_delete_events_google_calendar_ELTTEXTE000318"></a>


### Principle
<a name="principle_ELTPARAGRAPHE000341"></a>

The principle for modifying the events is straightforward: the calendar is retrieved locally, the modifications are performed locally and the calendar is updated on the server.

Remark: For shared calendars, we advise you to regularly update the calendars on the server.
<a name="NOTE4_2"></a>


### Adding events to a calendar
<a name="adding_events_calendar_ELTPARAGRAPHE000353"></a>

To add events to a calendar:

1. Retrieve the requested calendar (and its events if necessary).

2. Declare a variable of type [gglEvent](../WDLang5/1000017440.md).

3. Define the characteristics of the event via the properties of the variable.

4. Use [&lt;gglConnection variable&gt;.Write](../WDLang5/1000021460.md) to update the calendar on the server.




Example:

```wl
Cnt is gglConnection
...

// Retrieve the calendar named "Work"
Calendar is gglCalendar = Cnt.GetCalendar("Work")

// Create an event
MyEvent is gglEvent
MyEvent.StartDate = "20081201085000"
MyEvent.EndDate = "20081201093000"
MyEvent.Title = "Appointment"
MyEvent.Content = "Appointment to discuss the November status report"

// Add the event into the calendar
Add(Calendar.Event, MyEvent)

// Update the calendar on the server
Cnt.Write(Calendar)
```

```wl
Cnt is gglConnection
...

arrCalendars is array of 0 gglCalendar 
arrCalendars = Cnt.ListCalendar() 

// Retrieve the future events of the first calendar
Cnt.FillCalendar(arrCalendars[1])

// Create an event 
MyEvent is gglEvent
MyEvent.StartDate = "20081201085000"
MyEvent.EndDate = "20081201093000"
MyEvent.Title = "Appointment"
MyEvent.Content = "Appointment to discuss the November status report"

// Add the event into the calendar
Add(arrCalendars[1].Event, MyEvent)

// Update the calendar on the server
Cnt.Write(arrCalendars[1])
```

<a name="NOTE4_3"></a>


### Modifying the events in a calendar:
<a name="modifying_the_events_calendar_ELTPARAGRAPHE000390"></a>

To modify the events in a calendar:

1. Retrieve the requested calendar and its events.

2. Find the event to modify.

3. Modify the characteristics of the event.

4. Validate the modifications with [&lt;gglConnection variable&gt;.Write](../WDLang5/1000021460.md).




**Remark**: You can make several changes before using [&lt;gglConnection variable&gt;.Write](../WDLang5/1000021460.md).

**Example**:

```wl
Cnt is gglConnection
...

// Retrieve the calendar named "Work"
Calendar is gglCalendar = Cnt.GetCalendar("Work")
// Modify the first event of the calendar
Calendar.Event[1].Title = "Meeting w/ boss"
Calendar.Event[1].EndDate = "200810131530"
// Actually update the changes on the server
Cnt.Write(Calendar)
```

<a name="NOTE4_4"></a>


### Deleting an event from a calendar
<a name="deleting_event_from_calendar_ELTPARAGRAPHE000425"></a>

To delete an event from a calendar:

1. Retrieve the requested calendar and its events.

2. Find the event to delete.

3. Delete the event.

4. Validate the modifications with [&lt;gglConnection variable&gt;.Write](../WDLang5/1000021460.md).


**Remark**: Several deletions can be performed before using [&lt;gglConnection variable&gt;.Write](../WDLang5/1000021460.md).

**Example**:

```wl
Cnt is gglConnection
...

// Retrieve the calendar named "Work"
Calendar is gglCalendar = Cnt.GetCalendar("Work")
// Delete the second event from the calendar
Delete(Calendar.Event, 2)
// Actually update the changes on the server
Cnt.Write(Calendar)
```



- ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=TheOrganizercontrol_displayingaGooglecalendar_.gif) ***Unit examples (WINDEV)*** : **The Organizer control (displaying a Google calendar)** <br>Retrieving events from a Google calendar via the gglXxx functions and displaying these events in an Organizer control.
- ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=WDOrganizer.gif) ***Complete examples (WINDEV)*** : **WD Organizer** <br>This example is used to synchronize some appointments between your Outlook, Lotus Notes and Google calendars.<br><br>Based on the Organizer control, you have the ability to create, modify, move or delete the appointments.<br>you also have the ability to classify the appointments by category and to link them to several external organizers.
- ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=WDSchedule.gif) ***Complete examples (WINDEV)*** : **WD Schedule** <br>This example presents the management of a graphic schedule.<br><br>The following topics are presented in this example:<br>1/ the scheduler control<br>2/ the Google Calendar functions


