
## Creating an RSS stream
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Overview
<a name="overview_ELTTEXTE000142"></a>
WINDEV, WEBDEV and WINDEV Mobile allow you to produce and/or consume an RSS stream (Rapid Simple Syndication).

An RSS stream is used to produce (make available) a data stream in standard format. This data stream can be read (consumed) by an application.

A reader of RSS stream is used to display one or more RSS streams. 



<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## How to?
<a name="how_ELTTEXTE000166"></a>


### Creating an RSS stream
<a name="creating_rss_stream_ELTPARAGRAPHE000044"></a>

To create an RSS stream:

1. Declare the [rssStream](../WDLang5/1000017795.md), [rssChannel](../WDLang5/1000017769.md) and [rssEntry](../WDLang5/1000017792.md) variables.

2. Describe the channel with the [rssChannel](../WDLang5/1000017769.md) properties.

3. Describe the entries with the [rssEntry](../WDLang5/1000017792.md) properties.

4. Add the channel then its entries into the stream.




**Remarks**: 

- A stream is made of channels. A channel is made of entries.

- We recommend that you use functions such as [Add](../WDLang1/1000017348.md), [Delete](../WDLang1/1000017354.md), … to handle the array of entries and the array of channels.



<a name="NOTE2_2"></a>


### Examples
<a name="examples_ELTPARAGRAPHE000082"></a>

Example for creating a RSS stream:


```wl
MyStream is rssStream
MyChannel is rssChannel
MyEntry is rssEntry
MyPath is string

// Characteristics of channel
MyChannel.Title = "My site"
MyChannel.Description = "Example of RSS 2.0 stream"
MyChannel.UpdateDate = DateSys() + TimeSys()
MyChannel.Link = "http://www.example.org"
// Characteristics of entries of channel 1
MyEntry.Author = "pcsoft@pcsoft.fr"
MyEntry.Description = "First news"
MyEntry.Title = "News #1"
// Adds the entry into the channel
Add(MyChannel.Entry, MyEntry)
// Adds the channel into the stream
Add(MyStream.Channel, MyChannel)

// Saves the stream in a file
MyPath = CompleteDir(fCurrentDir()) + "Rss.xml"
rssSave(MyStream, MyPath)

// Displays the XML file of stream
IF NOT ShellExecute(MyPath) THEN
	Error("ShellExecute('" + MyPath + "'). '" + ErrorInfo() + "'")
END
```


**Caution**: In this example, the additions performed by [Add](../WDLang1/1000017348.md) must be performed last to handle the requested element. You also have the ability to use the following code: 


```wl
MyStream is rssStream 
ChannelNum is int
EntryNum is int
MyPath is string

MyPath = CompleteDir(fCurrentDir()) + "Rss.xml"

// Describe and add the channel
ChannelNum = 1
ChannelNum = Add(MyStream.Channel)
MyStream.Channel[ChannelNum].Title = "My site"
MyStream.Channel[ChannelNum].Description = "Example of RSS 2.0 stream"
MyStream.Channel[ChannelNum].PublicationDate = Today()
MyStream.Channel[ChannelNum].Link = "http://www.example.org"

// Describe and add the entry 1
EntryNum = Add(MyStream.Channel[ChannelNum].Entry)
MyStream.Channel[ChannelNum].Entry[EntryNum].Title = "First news"
MyStream.Channel[ChannelNum].Entry[EntryNum].Description = "News #1"
MyStream.Channel[ChannelNum].Entry[EntryNum].Link = "http://www.example.org"
MyStream.Channel[ChannelNum].Entry[EntryNum].PublicationDate = Today()
EntryNum = Add(MyStream.Channel[ChannelNum].Entry)

// Describe and add the next entry
MyEntry is rssEntry dynamic = MyStream.Channel[ChannelNum].Entry[EntryNum]
MyEntry.Title = "Second news"
MyEntry.Description = "News #2"
MyEntry.Link = "http://www.example.org"
MyEntry.PublicationDate = Today()

// Saves the stream in a file
rssSave(MyStream, MyPath)

// Displays the XML file of stream
IF NOT ShellExecute(MyPath) THEN
	Error("ShellExecute('" + MyPath + "'). '" + ErrorInfo() + "'")
END
```



- ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=TheRSSfunctions.gif) ***Unit examples (WINDEV)*** : **The RSS functions** <br>Using the WLanguage RSS functions.<br>These functions are used to read and create a stream.<br>These functions can be handled by specific types of variables: rssStream, rssChannel,...


