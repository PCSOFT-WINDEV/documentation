
## Lesson 3.4. Conditional statements 


<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## In this lesson you will learn the following concepts
<a name="this_lesson_you_will_learn_the_following_concepts_ELTTEXTE000075"></a>


- The IF statement.

- The SWITCH statement.





|   |   |
| --- | --- |
| <br>![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=dur%E9e.png)<br> | <br>Estimated time: 20 mn |

| [Previous Lesson](../TutoWM/1410087589.md) | [Table of contents](../TutoWM/1410087586.md) | [Next Lesson](../TutoWM/1410087591.md) |
| --- | --- | --- |





<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Overview
<a name="overview_ELTTEXTE000131"></a>
WLanguage is a set of statements used to handle data. 

Conditional statements are used to compare variables and/or values in order to perform different processes. Several conditional statements are available:

- IF... THEN... ELSE... END

- SWITCH...



<a name="NOTE2_2"></a>


### Practical example
<a name="practical_example_ELTPARAGRAPHE000022"></a>

To complete the exercises in this lesson, we will work on the "WLanguage" project that we used in the previous lesson. The principle remains the same: the example code will be entered directly in the "Initialization" event of the project. 





- If necessary, open the "WLanguage" project that you created in the first lesson (see [A project to discover WLanguage](../TutoWM/1410087570.md) in lesson 3.2).








- To view WLanguage events of the project: 

	1. Right-click the "P" button next to the open element tabs. The context menu appears. 

	2. Select "Element code". 

	3. The code editor displays the different events associated with the project.







<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## The IF statement
<a name="the_statement_ELTTEXTE000199"></a>
This statement is used to execute one process or another depending on the result of an expression. If the expression evaluates to true, a process is executed. If it evaluates to false, another process can be executed.  

The IF statement can be used as follows: 

```txt
IF <Expression to evaluate> THEN
   Process to execute if the expression evaluates to true
ELSE
   Process to execute otherwise
END
```



- We will test this statement with a simple WLanguage code example: 

	1. If necessary, clear the "Initialization" event of the project and copy the following code. 
			This code selects a number at random and displays a message according to the value. 
			
		```wl
		RandomNumber is int
		// Resets the random number generator
		InitRandom()
		// Selects a number at random between 100 and 4000
		RandomNumber = Random(100, 4000)
		Trace("Random number value " + RandomNumber)
		// Check if this number is strictly greater than 2000
		IF RandomNumber > 2000 THEN
			Trace("Number greater than 2000")
		ELSE
			Trace("Number less than or equal to 2000")
		END
		```
In this code, the expression to be evaluated is "RandomNumber&gt;2000".

	2. Let's test this code: click
![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=ICO_GO_simu_PRJ_WM_GAF.jpg) in the quick access buttons. 

	3. If necessary, open the "Debugger trace" pane to see the result: on the "Home" tab, in the "Environment" group, expand "Panes", select "Panes", and then select "Debugger trace". 

	4. The "Debugger trace" pane will show the message that corresponds to your case (as the number is random, the message may change with each test!). 







**Remark**: The IF statement can also be written in a single line using the "? ... ELSE" statement (here, the statement spans over 2 lines because it doesn't fit in the page!): 

```txt
<Expression to evaluate>? <Process if expression evaluates to true> 
	ELSE <Process otherwise>
```
It is an abbreviated form of IF ... THEN ... ELSE: it takes up less space, but it may not be as easy to understand. 
In our example, the code becomes: 

```wl
// Single-line conditional statement
Trace(RandomNumber > 2000? "Number greater than 2000" ELSE "Number less than or equal to 2000")
```




- In an IF statement, you can also cumulate conditions using the AND and OR keywords. Here are some simple examples you can test right away. 

	- Compound condition with the AND keyword: Determines if two conditions are met. Both conditions are evaluated. 
			
		```wl
		// Compound condition (AND and OR keywords)
		Condition1 is boolean = False
		Condition2 is boolean = True
		// - AND keyword
		IF Condition1 AND Condition2 THEN
			Trace("Condition1 AND Condition2: <true>")
		ELSE
		 Trace("Condition1 AND Condition2: <false>")
		END
		```

			When you run the project test again, the "Debugger trace" pane will display: 
			
		```txt
		Condition1 AND Condition2: <false>
		```


	- Compound condition with the OR keyword: Tests if either of the conditions are met. Again, both conditions are evaluated. 
			
		```wl
		Condition1 is boolean = False
		Condition2 is boolean = True
		
		IF Condition1 OR Condition2 THEN
			Trace("Condition1 OR Condition2: <true>")
		ELSE
			Trace("Condition1 OR Condition2: <false>")
		END
		```

			When you run the project test again, the "Debugger trace" pane will display: 
			
		```txt
		Condition1 OR Condition2: <true>
		```

			|   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=note.png) | Note | The second condition can be ignored if the first condition evaluates to false: simply use the _AND_ keyword. For example: <br><br><pre><code>Condition1 is boolean = False<br>		Condition2 is boolean = True<br>		<br>		IF Condition1 _AND_ Condition2 THEN<br>			Trace("Condition1 _AND_ Condition2: <true>")<br>		ELSE<br>			Trace("Condition1 _AND_ Condition2: <false>")<br>		END</code></pre><br><br>When you run the project test again, the "Debugger trace" pane will display: <br><br><pre><code>Condition1 _AND_ Condition2: <false></code></pre><br><br><br>Similarly, using the _OR_ keyword, if the first part of the expression is true, the rest of the expression is not evaluated. |











For more details, see [IF statement](../Motscles/1510011.md).

<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## The SWITCH statement
<a name="the_switch_statement_ELTTEXTE000331"></a>
This statement is used to evaluate an expression and to execute a process for each possible expression value.

The SWITCH statement can be used as follows:

```txt
SWITCH <Expression>
CASE Value 1: 
	Process 1
CASE Value 2: 
	Process 2
...
CASE Value N: 
	Process N

OTHER CASE
	Process 
END
```



- We will write some WLanguage code to try this statement: 

	1. If necessary, clear the "Initialization" event of the project and copy the following code. This code retrieves the current date and displays a different message depending on its value. A specific message is displayed for the 1st and 15th of the month. In the other cases, the date of the corresponding day is displayed.
			
		```wl
		// By default, the Date variable is initialized with the current date
		CurrentDate is Date
		
		// Checks the day of the date
		SWITCH CurrentDate.Day
			// 1st day of the month
			CASE 1: Trace("Today is the first day of the month.")
			// 15th day of the month
			CASE 15: Trace("Today is the 15th day of the month.")
			// For all other days, display the date
			OTHER CASE: Trace("Today is: " + CurrentDate.ToString())
		END
		```

			|   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=note.png) | Note | Several functions and properties are available in WLanguage. Functions can take parameters and return results. Properties are directly applied to the controls or variables via the following syntax: <br><br><pre><code><Name of control or variable>.<Property name></code></pre><br><br><br>In our example, the [Day](../Proprietes/2512043.md) property is used on the CurrentDate variable to get the day of the date. |





	2. Let's test this code: click
![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=ICO_GO_simu_PRJ_WM_GAF.jpg) in the quick access buttons. 

	3. The "Debugger trace" pane will show the message that corresponds to your case (the message may vary depending on the actual date!). For example, if the test is run on November 28, 2021, the following message will appear: 
			
		```txt
		Today is: 28/11/2021
		```








**Remarks**:

- If the line "CASE 1:..." is executed, the other lines of code corresponding to the possible values are not executed. 

- Several values can be grouped in the same case. The different values are separated by a comma. For example, to test the month of a date, we can write the following WLanguage code: 
	
	```wl
	SWITCH CurrentDate.Month
		// Months 1, 2 and 3
		CASE 1, 2, 3: Trace("We are in the first quarter of the year.")
		// Months 4 to 6
		CASE 4 <= * <= 6: Trace("We are in the second quarter of the year.")
		// Months after the sixth month
		CASE > 6: Trace ("We are in the second half of the year.")
	END
	```

	For a project test run on November 28, 2021, the "Debugger trace" pane will display: 
	
	```txt
	Today is: 28/11/2021
	We are in the second half of the year.
	```





- In our examples, the process corresponding to an expression value is written in a single line of code, directly after the "CASE X:" statement. You can also execute multiple lines of code. In this case, the lines of code are located under the "CASE X:" statement. The following syntax must be used:
	
	```txt
	SWITCH <Expression>
	CASE Value 1: 
		Process 1 - Line of code 1
		Process 1 - Line of code 2
	CASE Value N: 
		Process N - Line of code 1
		Process N - Line of code 2
	OTHER CASE
		Process 
	END
	```





| [Previous Lesson](../TutoWM/1410087589.md) | [Table of contents](../TutoWM/1410087586.md) | [Next Lesson](../TutoWM/1410087591.md) |
| --- | --- | --- |




