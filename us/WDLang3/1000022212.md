
## OAuth2Parameters (Type of variable)

***In french: OAuth2Paramètres***
				



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
The **OAuth2Parameters** type is used to define the information required to authenticate on a Web service implementing the OAuth 2.0 standard. These characteristics can be defined and changed using different WLanguage properties. 

This type of variable must be passed as parameter to [AuthIdentify](../WDLang3/1000022219.md). In case of success, this function returns an [AuthToken](../WDLang3/1000022220.md) variable that can be used to perform authenticated queries on the Web service.

**Remark**: For more details on the declaration of this type of variable and the use of WLanguage properties, see [Declaring a variable](../Motscles/1514032.md). 


<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
// Example used to retrieve a token to perform a request on DropBox
OAuth2Params is OAuth2Parameters
OAuth2Params.ClientID = "01234567890123456789" 
OAuth2Params.ClientSecret = "98765432109876543210"
OAuth2Params.AuthURL = "https://www.dropbox.com/oauth2/authorize"
OAuth2Params.TokenURL = "https://api.dropboxapi.com/oauth2/token"
OAuth2Params.AdditionalParameters = "force_reapprove=false"
<COMPILE IF ConfigurationType<>Site>
	// If you are not using a website, you need to specify a localhost redirect URL
	OAuth2Params.RedirectionURL = "http://localhost:9874/"	
<END>

// Ask for authentication: opens the login window
MyToken is AuthToken = AuthIdentify(OAuth2Params)

// Request authenticated on a DropBox API
req is httpRequest
req.Method = httpPost
req.URL = "https://api.dropboxapi.com/2/files/list_folder"
req.AuthToken = MyToken // Authentication token
req.ContentType = "application/json"
vAPIParam is variant
vAPIParam.path = "/Homework/math"
vAPIParam.recursive = False
vAPIParam.include_media_info = False
vAPIParam.include_deleted = False
vAPIParam.include_has_explicit_shared_members = False
req.Content = VariantToJSON(vAPIParam)

HTTPresponse is httpResponse = HTTPSend(req)
let Data = JSONToVariant(HTTPresponse.Content)
// Use the incoming data...
```





<a name="NOTE0"></a>

## Remarks
<a name="NOTE0_1"></a>


### Properties specific to OAuth2Parameters variables
<a name="properties_specific_oauth2parameters_variables_ELTPARAGRAPHE000045"></a>

The following properties can be used to handle the information required to perform the authentication:

| Property name | Type used | Effect |
| --- | --- | --- |
| AdditionalParameters | Character string | Parameters of first authentication query. This string must be formatted like the URL parameters. |
| AuthURL | Character string | Authorization URL to be used (first URL of OAuth 2.0 specification). |
| ClientID | Character string | Client identifier supplied by the service when registering the application. |
| ClientSecret | Character string | Secret access code of application. This code is given by the service when saving the application. |
| GrantType | Constant | Grant type available. The possible values are: <br><br>- *gtClientCredentials*: Authentication without login window. The access authorization is given to the application (not to the user). The token provided to access the resources of the application is linked to the application itself. <br><br>- *gtAuthorizationCode* (Default value): The access authorization is given to the user. a login window appears to let users enter their username and password. The access token is linked to the user. <br><br><br> |
| RedirectionURL | Character string | Redirection URL to use during the authentication mechanism. <br><br>![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) For a Windows or Android application, this URL must be of the form "http://localhost:PortNumber". This value must be strictly identical to the one specified when declaring the application in the relevant Web service.<br><br><br><br> |
| ResponseType | Character string or constant | Type of response expected. The possible values are: <br><br>- *oauth2ResponseTypeCode* (or "Code"): The response is of type "Code". <br><br>- *oauth2ResponseTypeToken* (or "Token"): The response is of type "Token". <br><br><br><br><br>*oauth2ResponseTypeToken* is the default value. <br><br>Remark: For a "personal" authentication, the response type must be "Token". In the case of an authentication for an API or service (e.g. Google mail server), the response type must be "Code". <br><br> |
| Scope | Character string | Requested permissions. The possible values are specific to the Web service used. |
| TokenURL | Character string | URL for retrieving the access token to use (second URL of OAuth 2.0 standard). |


<a name="NOTE0_2"></a>


### Operating mode of OAuth 2.0 authentication 
<a name="operating_mode_oauth_20_authentication_ELTPARAGRAPHE000276"></a>

The steps of OAuth 2.0 authentication performed by [AuthIdentify](../WDLang3/1000022219.md) are as follows: 

- Running a first HTTP request to ask for an authorization (authorization URL specified in the [OAuth2Parameters](../WDLang3/1000022212.md) variable).

- Opening a window for user identification according to the OAuth 2.0 protocol. The identification interface is given by the service accessed.

- After identification, the server returns a first authorization code allowing you to ask the resources for an access token. This code is added as parameter of second URL (access token URL specified in the [OAuth2Parameters](../WDLang3/1000022212.md) variable).

- Running the second HTTP request to ask for the access token. The result is a JSON buffer that contains, among other things, the access token ("**access_token**") that will be used for the authenticated requests. The [AuthToken](../WDLang3/1000022220.md) variable contains the information found in this JSON buffer. This access token will be used by the calls to the APIs of Web service.




**To use the APIs of the Web service**, simply use the [HTTPSend](../WDLang3/1000021183.md) function with an [httpRequest](../WDLang3/1000021158.md) variable defining the query to be executed. 
The [AuthToken](../WDLang3/1000022220.md) variable will be assigned to the **AuthToken** property of the [httpRequest](../WDLang3/1000021158.md) variable (see example). 
In this case, the server will receive the HTTP "**Authorization**" header with a value in the following format: "Authorization: Bearer xxx_access_token_xxx". 

**Caution**: 

- If the server does not return the access token in the format of JSON code according to the OAuth2.0 standard, an error will occur and the token will not be retrieved. The server response can be retrieved via the **ServerResponse** property of the [AuthToken](../WDLang3/1000022220.md) variable.

- If the server does not support the HTTP "**Authorization**" header for transmitting the access token, this transmission must be done by the developer according to the format expected by the requested service. 
	The following example allows you to use the Web service of Facebook. In this case, the access token must be specified on the request URL.

	- ![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) Code sample for Facebook
			
		```wl
		// Example used to retrieve the name of the Facebook account
		MyToken is AuthToken
		MyTokenParam is OAuth2Parameters
		
		MyTokenParam.ClientID = "123456789012345"
		MyTokenParam.ClientSecret = "45g8jh5kll45579021qsg5444j"
		MyTokenParam.AuthURL = "https://www.facebook.com/dialog/oauth"
		MyTokenParam.TokenURL = "https://graph.facebook.com/v2.3/oauth/access_token"
		MyTokenParam.RedirectionURL = "http://localhost:9874/"
		MyTokenParam.Scope = "email"
		
		MyToken = AuthIdentify(MyTokenParam)
		IF MyToken <> Null THEN
			IF ErrorOccurred THEN
				Error(ErrorInfo())
			ELSE
				// Token specified on the request URL
				HTTPRequest("https://graph.facebook.com/me?access_token=" + MyToken.Value)
				vMyRes is Variant = JSONToVariant(HTTPGetResult(httpResult))
				// Retrieve the account name
				Trace(vMyRes.name)
			END
		END
		```









- ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=WDOAuth.gif) ***Training (WINDEV)*** : **WD OAuth** <br>OAuth allows you to act as user of an external platform without knowing the identifiers (user name and password) of this user.<br>Several external platforms (among which Google, Twitter, Facebook) propose APIs for which you have the ability to connect with the information belonging to one of their users. This identification is performed via OAuth. Therefore, all the requests performed on their services (API, ...) will require an access token identifying both the application (the "client") and the user.<br>The example proposes to connect to Google and Microsoft via the AuthConnect function and the OAuth2Parameter type.


