
## Reading an email
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Principle
<a name="principle_ELTTEXTE000207"></a>
This help page explains how to read an email from a WINDEV application and how to display its content in a WINDEV application.

The email is read by several WLanguage functions ([EmailReadFirst](../WDLang3/3032014.md), [EmailReadNext](../WDLang3/3032004.md), ...).

The content of the email can be retrieved: 

- in a variable of type [Email](../WDLang3/1000018713.md). The Email variables allow you to handle several emails simultaneously.

- via the [Email](../WDLang3/3032029.md) structure. The variables of this structure contain the informations regarding the email read. 




The variables of Email structure (as well as the properties of Email variables) correspond to the email characteristics.





<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Reading an email (Email variable): Steps to follow
<a name="reading_email_email_variable_steps_follow_ELTTEXTE000231"></a>
The steps for reading an email in WLangage are as follows:

1. Use a function for reading emails to browse all the emails of a messaging session, several syntaxes are available:

	- Perform a loop such as:
			
		```wl
		MySession is emailPOP3Session
		MyMessage is Email
		EmailReadFirst(MySession, MyMessage)
		WHILE NOT MyMessage.Out
		
			// Insert the message process
		
			EmailReadNext(MySession, MyMessage)
		END
		```


	- Retrieve all the messages in an array with [EmailGetAll](../WDLang3/1000018727.md):
			
		```wl
		MySession is emailPOP3Session
		MyMessages is array of Email = EmailGetAll(MySession)
		```

			**Remarks**:

		- When using the POP3 protocol, the incoming emails that can be read are the emails that have been received when the session was started. All the emails received once the session is started are not "visible". To access the new incoming emails, the session must be closed and re-started.

		- To follow the reading progress, use [EmailProgressBar](../WDLang3/3032017.md).




2. Read the content of the [Email](../WDLang3/1000018713.md) variable.
	Remark: If the email contains specific headers, they can be read via the [Email](../WDLang3/1000018713.md) variable.

3. If the **HTML** property is not empty: the message is in HTML format. It must be displayed in a browser (see the example below).
	For each attached file:

	- Save the file on disk ([EmailSaveAttachment](../WDLang3/3032020.md)).

	- Browse the HTML message and find the ***"cID:"+Identifier*** value of the attached file. Replace this value by the full path of attached file copied on disk. 
			Display the message in a browser.




4. If the **HTML** property is empty: the message is in text format.

5. Save the attached files on disk if necessary and display the message text.



<a name="NOTE2_2"></a>


### Example
<a name="example_ELTPARAGRAPHE000116"></a>


```wl
MyMessage is Email 
...
Temp_Dir is string = "C:\temp\"
CID is string
I is int

// For each attached file
Attachment is emailAttach
FOR EACH Attachment OF MyMessage.Attach
	// Copy the file into a temporary directory 
	EmailSaveAttachment(Attachment, Temp_Dir + Attachment.Name)
	// Retrieve the identifier of the attached file
	CID ="cid:" + Attachment.Identifier
	// Replace the references to the attached file 
	// by the real name of the file
	MyMessage.HTML = Replace(MyMessage.HTML, CID, ...
			"file:" + Temp_Dir + Attachment.Name)
END

// Display the HTML content in an HTML control
HTM_Display = MyMessage.HTML
```


<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Reading an email (Email structure): Steps to follow
<a name="reading_email_email_structure_steps_follow_ELTTEXTE000261"></a>
The steps for reading an email in WLangage are as follows:

1. Use a function for reading emails ([EmailReadFirst](../WDLang3/3032014.md), [EmailReadNext](../WDLang3/3032004.md), ...). To follow the reading progress, use [EmailProgressBar](../WDLang3/3032017.md).

2. Read the content of the email structure (see the example below).

3. If the Email.HTML variable is not empty: the message is in HTML format. It must be displayed in a browser.
	For each attached file (the Email.NbAttach variable is not empty):

	- Save the file on disk ([EmailSaveAttachment](../WDLang3/3032020.md)).

	- Browse the HTML message and find the ***"cID:"+Email.AttachIdentifier*** value of the attached file. Replace this value by the full path of attached file copied on disk.

	- Display the message in a browser.




4. If the Email.HTML variable is empty: the message is in text format.

5. Save the attached files on disk if necessary and display the message text.




**Remark**: When using the POP3 protocol, the incoming emails that can be read are the emails that have been received when the session was started. All the emails received once the session is started are not "visible". To access the new incoming emails, the session must be closed and re-started.
<a name="NOTE3_2"></a>


### Example
<a name="example_ELTPARAGRAPHE000156"></a>


```wl
Temp_Dir is string = "C:\temp\"
CID is string
I is int

// For each attached file
FOR I = 1 TO Email.NbAttach
	// Copy the file into a temporary directory 
	EmailSaveAttachment(Email.Attach[I], ...
			Temp_Dir + Email.Attach[I])
	// Retrieve the identifier of the attached file
	CID = "cid:" + Email.AttachIdentifier[I]
	// Replace the references to the attached file 
	// by the real name of the file
	Email.HTML = Replace(Email.HTML, CID, "file:" + ...
			Temp_Dir + Email.Attach[I])
END

// Display the HTML content in a Web browser
// Create a temporary file containing the HTML
FileName is string = Temp_Dir + "temp.htm"
hFile is int = fOpen(FileName, foCreate + foWrite)
fWrite(hFile, Email.HTML, Size(Email.HTML))
fClose(hFile)

// Supply the temporary file to the browser
// Web_Browser is an ActiveX control "Microsoft Web Browser"
Web_browser>>Navigate(FileName)
```



- ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=WDMail.gif) ***Complete examples (WINDEV)*** : **WD Mail** <br>This application is an email client developed in WINDEV. It is based on the Email objects.<br>This email client is used to retrieve and send emails by using the POP, IMAP and SMTP protocols.<br>You have the ability to apply filters to the incoming emails.<br><br>The application can also be used to manage several email accounts. The writing of an email is based on the HTML edit control.
- ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=ThePOP3Emailfunctions.gif) ***Unit examples (WINDEV)*** : **The POP3 Email functions** <br>Using the Email functions to manage the POP3 protocol.<br>This protocol is used to retrieve emails from a server.
- ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=WDJavaMail.gif) ***Training (WINDEV)*** : **WD JavaMail** <br>This example is a Java example used to read and send emails.


