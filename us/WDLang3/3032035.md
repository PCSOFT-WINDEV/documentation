
## Managing emails with "Simple MAPI"
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Overview of "Simple MAPI"
<a name="overview_simple_mapi_ELTTEXTE000204"></a>
**Simple MAPI** is used to simplify the management of the emails received at the hosting company. When an email is read, it is automatically loaded in the local message box and deleted from the server (at the hosting company).

All the characteristics required to manage the emails (POP3 protocol, SMTP protocol, remote access, etc.) are grouped in the "User Profile".
<br>![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=SMAPI.gif)


With the email functions of WLanguage, a WINDEV application or a WEBDEV site can directly handle the emails managed in an application that is using "Simple MAPI".

<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Principle
<a name="principle_ELTTEXTE000228"></a>
To send or read messages via **Simple MAPI**, you must:

1. Describe a user profile. This user profile must be created in the Microsoft application for email management (MS Exchange client for example).

2. From the WINDEV application (or from the WEBDEV site), connect to the application for email management (MS Exchange client for example) with [EmailStartSession](../WDLang3/3032028.md).

3. Send and read the messages.

4. Close the session to the application for email management (MS Exchange client for example) with [EmailCloseSession](../WDLang3/3032006.md).




<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Details of different steps
<a name="details_different_steps_ELTTEXTE000252"></a>


### Step 1: Creating a user profile
<a name="step_1_creating_user_profile_ELTPARAGRAPHE000042"></a>

The user profile is used to configure the application for email management (MS Exchange client for example).

The following elements are defined in the user profile:

- the SMTP protocol used, 

- the POP3 protocol used, 

- the different communication services used. To use the "email" functions of WLanguage, the user profile must use the ***Microsoft Exchange Server***.




**Remark**: A profile must be created for each different user or email account. The profile name will be used to start the email session with [EmailStartSession](../WDLang3/3032028.md).

**To create a profile:**

1. Open the control panel.

2. Double-click the "Email" option.

3. Click the "Display the profiles" button.

4. In the window named "Choosing a profile", click the "Add" button.

5. Give a name to the profile. This name will be used in the WINDEV programs.

6. Select "Add a new email account".

7. Select the "Microsoft Exchange Server" service.

8. Enter the name of Microsoft Exchange server.



<a name="NOTE3_2"></a>


### Step 2: Starting an email session
<a name="step_2_starting_email_session_ELTPARAGRAPHE000071"></a>

To start an email session, use [EmailStartSession](../WDLang3/3032028.md). This function must be the first "email" function used in your WINDEV application (or in your WEBDEV site).

[EmailStartSession](../WDLang3/3032028.md) returns the session identifier. This identifier will be used by all the WLanguage functions for email management.

**Example**: The following procedure is used to start an email session from a profile. If an error occurs, the *Email.Error* variable is used to identify the error.


```wl
Function StartSession(Profile)
// Start the session
SessionNum is int
SessionNum = EmailStartSession(Profile)
IF SessionNum = 0 THEN
	Error("The session cannot be started. Error: " + Email.Error)
END
RESULT SessionNum
```

<a name="NOTE3_3"></a>


### Step 3: Sending emails
<a name="step_3_sending_emails_ELTPARAGRAPHE000089"></a>

The following functions are use to send emails via **Simple MAPI**:

- [EmailSendMessage](../WDLang3/3032005.md): this function is used to place the message in the out-box of the application for email management (out-box of the MS Exchange client for example).

- [EmailUpdate](../WDLang3/3032036.md): this function is used to synchronize the email server and the application for email management: the new incoming emails are automatically transferred into the in-box, the emails found in the out-box are sent.  


For more details, see: [Preparing to send an email](../WDLang3/3032002.md).

**Example**: The following code sends all the emails in a Table control populated programmatically ("TABLE_TOSEND") via the MS Exchange Client. Each table row corresponds to an email.

For each email, the information in the Table control is transferred to the email structure, and the email is sent. Then, the email server is updated.


```wl
I is int
FOR I = 1 _TO_ TableCount(TABLE_TOSEND)
	// The email is sent to a single person
	Email.NbRecipient = 1
	Email.Recipient[1] = ExtractString(TABLE_TOSEND[I], 1)
	// Subject and message
	Email.Subject = ExtractString(TABLE_TOSEND[I], 2)
	Email.Message = ExtractString(TABLE_TOSEND[I], 3)
	// No attached file
	Email.NbAttach = 0
	// Send the message to MS Exchange
	EmailSendMessage(SessionNum, False)
END
// Send the messages from MS Exchange to the email server
IF NOT EmailUpdate(SessionNum) THEN
	Error("Problem with MS Exchange. Error:" + Email.Error)
END
```

<a name="NOTE3_4"></a>


### Step 3 bis: Reading the emails
<a name="step_3_bis_reading_the_emails_ELTPARAGRAPHE000114"></a>

Reading the emails via **Simple MAPI** is performed by:

- the [EmailUpdate](../WDLang3/3032036.md) function: this function is used to synchronize the email server and the software for email management: the new incoming emails are automatically transferred into the in-box, the emails found in the out-box are sent.

- the functions for reading emails ([EmailReadFirst](../WDLang3/3032014.md), [EmailReadNext](../WDLang3/3032004.md), etc.): these functions are used to initialize the email structure of WLanguage with the characteristics of the email currently read (sender, subject, etc.).


For more details, see: [Reading an email](../WDLang3/3032037.md).

**Example**: The following code is used to read the emails. Incoming emails are stored in a Table control populated programmatically ("TABLE_Messages"). The SessionNum variable corresponds to the identifier of the session. In this example, the incoming messages are deleted from the inbox and from the email server by [EmailDeleteMessage](../WDLang3/3032027.md).


```wl
// Receives the pending messages found on the email server
IF NOT EmailUpdate(SessionNum) THEN
	Error("Problem with MS Exchange. Error: " + Email.Error)
END

// Read the first unread message
IF NOT EmailReadFirst(SessionNum, "NOT READ") THEN
	Error("Error while reading the first message")
END

// Loop through unread messages and display them in a Table control populated programmatically
TableDeleteAll(TABLE_Messages)
WHILE NOT Email.Out
	// The reception date, the address of the sender and the message
	// are assigned to the table
	TableAdd("TABLE_Messages", Email.ReceiveDate + TAB +...
	Email.SenderAddress + TAB + Email.Message)

	// Delete the message
	IF NOT EmailDeleteMessage(SessionNum) THEN
		Error("Error during the deletion. The message was not deleted")
	END
	// Read the next unread message
	IF NOT EmailReadNext(SessionNum, "NOT READ") THEN
		Error("Error while reading the next message")
	END
END
```

<a name="NOTE3_5"></a>


### Step 4: Closing the email session
<a name="step_4_closing_the_email_session_ELTPARAGRAPHE000145"></a>

Once the management of incoming and outgoing emails is completed, the session is closed with [EmailCloseSession](../WDLang3/3032006.md). This function must be the last "email" function used.

**Example**: The following code is a procedure used to close an email session. In this code, the **SessionNum** variable corresponds to the session identifier returned by [EmailStartSession](../WDLang3/3032028.md).


```wl
PRODEDURE CloseSession(SessionNum)
// Close the session
EmailCloseSession(SessionNum)
```



