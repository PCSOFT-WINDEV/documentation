
## Day (Property)

***In french: Jour***
	



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
The **Day** property is used to:

- Get the day from a [Date](../Motscles/1514066.md) or [DateTime](../Motscles/1514070.md) variable, or get the number of days from a [Duration](../Motscles/1514069.md) variable.

- Change the day in a [Date](../Motscles/1514066.md) or [DateTime](../Motscles/1514070.md) variable, or change the number of days in a [Duration](../Motscles/1514069.md) variable.

- Get the day or the number of days from a Date item (in "Date", "Date and Time" or "Duration" format).
	

- Change the day or the number of days in a Date item (in "Date", "Date and Time" or "Duration" format).
	




**Remark**: The **Day** property is used to easily change dates (add a day, etc).

**Reminder**: Date items are used to manage:

- "Year - Month - Day" dates (YYYYMMDD format).

- "Year - Month - Day - Hours - Minutes - Seconds - Milliseconds" dates and times (YYYYMMDDHHmmSSCCC format).

- "Number of days - Number of hours - Number of minutes - Number of seconds - Number of milliseconds" durations (+DHHMMSSCCC format).





<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
// Example on a variable
StartDate is Date = "20011225"
// Add 5 days to the date
StartDate.Day += 5
// Modify the days
StartDate.Day = 10
```


<a name="Example2"></a>



```wl
// Example on an item
Work.StartDate = "20011225"
// Add 5 days to the date
Work.StartDate.Day += 5
// Modify the days
Work.StartDate.Day = 10
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Syntax

### Finding out the day in a Date, DateTime or Duration variable

`<Result> = <Date>.Day`
---

**`<Result>: (Integer)`**

Day on 2 digits.

**`<Date>: (Date or DateTime)`**

Name of the variable of type [Date](../Motscles/1514066.md), [DateTime](../Motscles/1514070.md) or [Duration](../Motscles/1514069.md) to be used.  


<a name="SYNTAX2"></a>

### Modifying the day in a Date, DateTime or Duration variable

`<Date>.Day = <New day>`
---

**`<Date>: (Date or DateTime)`**

Name of the variable of type [Date](../Motscles/1514066.md), [DateTime](../Motscles/1514070.md) or [Duration](../Motscles/1514069.md) to be used.

**`<New day>: (Integer or character string)`**

New day in digits (included between 1 and 31). Replaces the day in the specified date.  


<a name="SYNTAX3"></a>

### Finding out the day in a Date item

`<Result> = <Data file>.<Item>.Day`
---

**`<Result>: (Integer)`**

Day on 2 digits.

**`<Data file>: (Character string)`**

Name of the data file used. This name was defined in the data model editor or with the [File Description](../WDLang4/1514065.md) type.

**`<Item>: (Character string)`**

Name of the item used. This name was defined in the data model editor or with the [Item Description](../WDLang4/1514071.md) type.  


<a name="SYNTAX4"></a>

### Modifying the day in a Date item

`<Data file>.<Item>.Day = <New day>`
---

**`<Data file>: (Character string)`**

Name of the data file used. This name was defined in the data model editor or with the [File Description](../WDLang4/1514065.md) type.

**`<Item>: (Character string)`**

Name of the item used. This name was defined in the data model editor or with the [Item Description](../WDLang4/1514071.md) type.

**`<New day>: (Integer or character string)`**

New day in digits (included between 1 and 31). Replaces the day in the specified date.  



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks




### Range of dates
<a name="range_dates_ELTPARAGRAPHE000170"></a>

The [Date](../Motscles/1514066.md) and [DateTime](../Motscles/1514070.md) types are used to manage the dates from 01/01/0001 to 31/12/9999.
<a name="NOTE0_2"></a>




### Managing the days and months
<a name="managing_the_days_and_months_ELTPARAGRAPHE000184"></a>

**Case 1: Direct assignment**
During a direct assignment (for example, *MyDay.Day = n*), the day must be included between 1 and 31. A WLanguage error occurs if the specified day is incorrect.
The following syntax *MyDay.Day = MyDay.Day + 5* may generate an error at runtime. For example, the following lines of code trigger an error:


```wl
// Code triggering the error
MyDate is Date = "20201126"   // 11/26/2020 
MyDate.Day = MyDate.Day + 20   // Triggers a WLanguage error because the day is equal to 45
 
// Correct code
MyDate is Date = "20201126"   // 11/26/2020 
MyDate.Day += 20
```


```wl
// Code triggering the error
MyDate is Date = "20201126"   // 11/26/2020 
MyDate1 is Date 
MyDate1.Day = MyDate.Day + 20   // Triggers a WLanguage error because the day is equal to 45
 
// Correct code
MyDate1 = MyDate   // 11/26/2004 
MyDate1.Day += 20
```


** Case 2: Operations on the days**
When performing operations on the dates, the change of month is automatically managed. Therefore, if the number of days is greater than the number of days in the month, the number of days restarts from 1 and the number of the month is automatically modified. The year is also modified if necessary (month of December for instance).
For example:


```wl
StartDate is Date = "20201226"   // 12/26/2020
// Add 10 days to the date
StartDate.Day +=10    // StartDate is "20050105"
```

<a name="NOTE0_3"></a>




### Handling the durations
<a name="handling_the_durations_ELTPARAGRAPHE000208"></a>

A duration has no limit: the number of days can exceed 30 or 31 days.
<a name="NOTE0_4"></a>




### Operators available for the days
<a name="operators_available_for_the_days_ELTPARAGRAPHE000216"></a>

The following arithmetic operators can be used with the **Day** property:

- ++  and  --

- +=  and - =





```wl
StartDate is Date = "20201126"   
StartDate.Day++     // Add 1 day to the date
StartDate.Day+=5    // Add 5 days ot the date
StartDate.Day-=5    // Subtract 5 days to the date
```

<a name="NOTE0_5"></a>




### Calculating the last day of the month
<a name="calculating_the_last_day_the_month_ELTPARAGRAPHE000231"></a>

To get the last day of a month, simply assign 31 to the **Day** property of the date. The last day will be automatically calculated according to the specified month.


```wl
StartDate is Date = "20201126"   
StartDate.Day = 31    
// November does not have 31 days
// The day is automatically replaced with 30
```

<a name="NOTE0_6"></a>




### Calculating a 90-day end-of-month due date
<a name="calculating_90day_endofmonth_due_date_ELTPARAGRAPHE000243"></a>

The **Day** property can be used to calculate a 90-day end-of-month due date.


```wl
MyDate is Date = "20201126"   
MyDate.Day += 90   
MyDate.Day = 31
```

<a name="NOTE0_7"></a>




### Calculating the end of a 30-day period
<a name="calculating_the_end_30day_period_ELTPARAGRAPHE000255"></a>

A 30-day period corresponds to a one-month period from a given date. The [Month](../Proprietes/2512052.md) and **Day** properties can be used to easily calculate the end date of a 30-day period.


```wl
StartDate is Date = "20201126"   
EndDate is Date = StartDate
EndDate.Month++    
EndDate.Day--
```

<a name="NOTE0_8"></a>




### Calculations on dates
<a name="calculations_dates_ELTPARAGRAPHE000270"></a>

The date storage format allows you to store dates from 01/01/0001 to 12/31/9999.

[WLanguage functions](../WDLang1/3027029.md) and [WLanguage properties](../Proprietes/2514010.md) make accurate calculations on dates from January 1st, 1583.  


