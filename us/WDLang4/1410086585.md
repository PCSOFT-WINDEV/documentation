
## Managing HBase databases
			

<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Overview
<a name="overview_ELTTEXTE000293"></a>
HBase is a non-relational distributed database (NoSQL), based on columns and using a non-structured storage for large tables. 

HBase is a core component of Hadoop.

HBase has been conceived for processing large volumes of data (several million rows, several million columns per row) and non-standard data (each row can have a structure different from the next row).

<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## How is an HBase database organized?
<a name="how_hbase_database_organized_ELTTEXTE000317"></a>
In an HBase database, the data is stored in tables (files), in a denormalized way. 
Each table is divided into families of columns (items), that themselves contain columns. 
Each row (equivalent to a record in a traditional DBMS) is identified by a single "rowKey". 
For each row, in the same family of columns, the number and the name of columns are free.<br>![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=Table-HBase.gif)
"rowKey" is the only table key.
A cell is the intersection between a row and a column.
The history of the cell content is stored as well as its timestamp (date/time when it was written).
You have the ability to implement filters on an HBase table (a little bit like a filter on HFSQL).

<a name="NOTE3"></a>


## How to handle an HBase database in programming?
<a name="how_handle_hbase_database_programming_ELTTEXTE000341"></a>
WLanguage allows you to:

- [create tables](#CREATE_TABLE)

- use existing tables:

	- [retrieve the list of tables](#GET_LIST)

	- [retrieve the description of a table](#GET_TABLE_DESC)

	- [delete a table](#DEL_TABLE)




- use the table data:

	- [read data](#READ)

	- [write data](#WRITE)

	- [delete data](#DEL)

	- [browse the data according to a filter](#FILTER)







Caution: The HBase functions are available in edit and at run time from Windows Vista or Windows Server 2008.

<a name="CREATE_TABLE"></a>


### How to create an HBase table?
<a name="how_create_hbase_table_ELTPARAGRAPHE000068"></a>

To create a HBase table, you must: 

1. Define and initialize a [hbConnection](../WDLang4/1000021685.md) variable.

2. Define and initialize a [hbTableDescription](../WDLang4/1000021688.md) variable. This variable contains an array of [hbColumnDescription](../WDLang4/1000021694.md) variables describing the columns.

3. Use the [hbCreateTable](../WDLang4/1000021696.md) function.Example:



```wl
// Connection
Connection is hbConnection
Connection.Server = "MyServer"
// Describe the table
TableDesc is hbTableDescription
TableDesc.Name = "blog"
// Describe the "Post" column
ColDesc is hbColumnDescription
ColDesc.Name = "Post"
Add(TableDesc.Column, ColDesc)
// Create the table
hbCreateTable(Connection, TableDesc)
```

<a name="GET_LIST"></a>


### How to retrieve the list of tables found in an HBase database?
<a name="how_retrieve_the_list_tables_found_hbase_database_ELTPARAGRAPHE000093"></a>

To retrieve the list of tables found in a HBase database, you must: 

1. Define and initialize a [hbConnection](../WDLang4/1000021685.md) variable.

2. Use the [hbListTable](../WDLang4/1000021687.md) function.Example:



```wl
// Connection
Connection is hbConnection
Connection.Server = "MyServer"
// Display the list of tables
Trace(hbListTable(Connection))
```

<a name="GET_TABLE_DESC"></a>


### How to retrieve the description of an HBase database?
<a name="how_retrieve_the_description_hbase_database_ELTPARAGRAPHE000111"></a>

To retrieve the description of a HBase database, you must: 

1. Define and initialize a [hbConnection](../WDLang4/1000021685.md) variable.

2. Use the [hbGetTableDescription](../WDLang4/1000021695.md) function.

3. Retrieve the table description in a [hbTableDescription](../WDLang4/1000021688.md) variable. This variable contains an array of [hbColumnDescription](../WDLang4/1000021694.md) variables describing the columns.Example:



```wl
// Connection
Connection is hbConnection
Connection.Server = "MyServer"
// Retrieve the description of the table
TableDesc is hbTableDescription = hbGetTableDescription(Connection, "blog")
// Display the name of the table
Trace("table: " + TableDesc.Name)
// Display the description of columns
FOR EACH ColDesc OF TableDesc.Column
	Trace(TAB + "column: " + ColDesc.Name)
	Trace(TAB + TAB + "attribute: ")
	FOR EACH AttributeVal, AttributeName OF ColDesc.Attribute
		Trace(TAB + TAB + AttributeName + " = " + AttributeVal)
	END
END
```

<a name="DEL_TABLE"></a>


### How to delete an HBase table?
<a name="how_delete_hbase_table_ELTPARAGRAPHE000136"></a>

To delete a HBase table, you must: 

1. Define and initialize a [hbConnection](../WDLang4/1000021685.md) variable.

2. Use [hbDeleteTable](../WDLang4/1000021713.md) and specify the name of the table to delete.Example:



```wl
// Connection
Connection is hbConnection
Connection.Server = "MyServer"
// Delete the table
hbDeleteTable(Connection, "blog")
```

<a name="READ"></a>


### How to read the data found in an HBase database?
<a name="how_read_the_data_found_hbase_database_ELTPARAGRAPHE000154"></a>

To read the data found in a HBase database, you must: 

1. Define and initialize a [hbConnection](../WDLang4/1000021685.md) variable.

2. Define and initialize a [hbReading](../WDLang4/1000021701.md) variable. This variable contains an array of [hbColumn](../WDLang4/1000021698.md) variables describing the columns to retrieve.

3. Use the [hbRead](../WDLang4/1000021706.md) function.

4. Read data in the [hbCell](../WDLang4/1000021707.md) resultExample:



```wl
// Connection
Connection is hbConnection
Connection.Server = "MyServer"
// Description of data to read
Reading is hbReading
Reading.Row = "post1"
Reading.Version = 2
// Column description
Col is hbColumn
Col.Family = "Post"
Add(Reading.Column, Col)
// Read data
MyResult is array of hbCells = hbRead(Connection, "blog", Reading)
// Display the data read
FOR EACH Cell OF MyResult DO
	Trace("Column: " + Cell.Column.Qualifier + "; Value: " + ...
	Cell.Value + "; timestamp: " + Cell.Timestamp)
END
```

<a name="WRITE"></a>


### How to write data into an HBase database?
<a name="how_write_data_into_hbase_database_ELTPARAGRAPHE000183"></a>

To write data into a HBase database, you must: 

1. Define and initialize a [hbConnection](../WDLang4/1000021685.md) variable.

2. Define and initialize a [hbWriting](../WDLang4/1000021701.md) variable. This variable contains an array of [hbCell](../WDLang4/1000021707.md) variables describing the data to write.

3. Use the [hbWrite](../WDLang4/1000021712.md) function.Example:



```wl
// Connection
Connection is hbConnection
Connection.Server = "MyServer"
// Description of data to write
Writing is hbWriting
Writing.Row = "post1"
Cell is hbCell 
Cell.Column.Family = "post"
Cell.Column.Qualifier = "title"
Cell.Value = "This is my blog post."
Add(Writing.Cell, Cell)
// Write data
hbWrite(Connection, "blog", Writing)
```

<a name="DEL"></a>


### How to delete data from an HBase table?
<a name="how_delete_data_from_hbase_table_ELTPARAGRAPHE000208"></a>

To delete data from a HBase table, you must: 

1. Define and initialize a [hbConnection](../WDLang4/1000021685.md) variable.

2. Use [hbDelete](../WDLang4/1000021714.md) and specify the identifier of the row to delete.Example:



```wl
// Connection
Connection is hbConnection
Connection.Server = "MyServer"
// Delete the entire row
hbDelete(Connection, "blog", "post1")
```

<a name="FILTER"></a>


### How to perform a browse with filter in an HBase table?
<a name="how_perform_browse_with_filter_hbase_table_ELTPARAGRAPHE000226"></a>

To perform a browse with filter in a HBase database, you must: 

1. Define and initialize a [hbConnection](../WDLang4/1000021685.md) variable.

2. Define and initialize a [hbScanParameter](../WDLang4/1000021715.md) variable. This variable defines the filter to implement. 

3. Browse the filtered data (corresponding to the [hbScanParameter](../WDLang4/1000021715.md) variable) via the FOR EACH statement. The browse result is read in a [hbScanResult](../WDLang4/1000021724.md) variable. Example:



```wl
// Connection
Connection is hbConnection
Connection.Server = "MyServer"
// Filter description
MyScan is hbScanParameter
MyScan.Connection = Connection
MyScan.Table = "blog"
// Filters, for all the rows, the values of columns whose name starts with "Title"
MyScan.Filter = "{ ""type"": ""ColumnPrefixFilter"", ""value"": """ + ...
	Encode("Title", encodeBASE64) + """ }"
// Loops through and displays the filtered data
// MyResult is an hbScanResult variable
FOR EACH MyResult OF MyScan
	Trace("The column " + MyResult.Column.Family + ":" + ...
		MyResult.Column.Qualifier + ...
		" of the row " + MyResult.Row + " is set to " + MyResult.Value)
END
```



