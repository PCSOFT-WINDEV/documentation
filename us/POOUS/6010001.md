
## Constructor and destructor
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Overview
<a name="overview_ELTTEXTE000356"></a>
When creating a class in the code editor, the Constructor and Destructor methods are automatically created by default.

- **The Constructor method**, if defined by the developer, is automatically called when instantiating an object. It is used to perform the initialization steps for the object or related to the object (assigning members, checks, ...)

- **The Destructor method**, if it is defined by the developer, is automatically called when deleting the object (exit from the procedure in which the object was instantiated). It can be used to free resources for example...




The Constructor and Destructor methods cannot contain the following functions: [Event](../WDLang1/3015011.md), [Timer](../WDLang1/3015008.md), [Multitask](../WDLang1/3015004.md), [DnDEvent](../WDLang1/3030006.md), [DDEEvent](../WDLang1/3028009.md), [CallDLL32](../WDLang1/3014003.md), etc.



Remarks:

- The Constructor methods can be public, protected or private.

- You have the ability to create some multi-syntax constructors. For more details, see [Prototype overload](../Motscles/1514063.md).








<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Syntax of class constructor
<a name="syntax_class_constructor_ELTTEXTE000380"></a>
**Declaration of constructor**

```txt
PROCEDURE [<Access>] Constructor([<Parameters>])
```


**Details of syntax**

**&lt;Access&gt;**: Optional
3 access levels are available: 

- PUBLIC (by default): The constructor is accessible outside the class.

- PROTECTED: The constructor is accessible in the class and in the derived classes.

- PRIVATE: The constructor is accessible in the class only.


For more details, see [Notes about the Constructors](#NOTE3_0).

Remark: To modify the scope of the Constructor, you have the ability to use the popup menu on the method in the project explorer.

**&lt;Parameters&gt;**
Optional parameters of the constructor. If these parameters are specified, they must be passed during the [object declaration](../POO/6010002.md) or during [the dynamic instantiation](../POO/6010003.md).<a name="NOTE2_2"></a>
**Declaration of the object: the constructor is automatically called**

```txt
<ObjectName> is [object] <Class Name> ([<Parameters>])
```


**Details of syntax**

**&lt;ObjectName&gt;**
Name that identifies the instance of the class.**&lt;Class Name&gt;**
Name that identifies the class, defined when creating the class in the code editor.**&lt;Parameters&gt;**
Optional parameters of constructor.<a name="NOTE2_3"></a>
**Dynamic instantiation: the constructor is automatically called**

```txt
<ObjectName> is dynamic [object] <ClassName>
<ObjectName> = new <ClassName> ([<Parameters>])
```


**Details of syntax**

**&lt;ObjectName&gt;**
Name that identifies the instance of the class.**&lt;Class Name&gt;**
Name that identifies the class, defined when creating the class in the code editor.**&lt;Parameters&gt;**
Optional parameters of constructor.
<a name="NOTE3_0"></a>


## Note regarding the constructors
<a name="note_regarding_the_constructors_ELTTEXTE000416"></a>
**Scope and access of the Constructors**
The Constructor of a class can be public, protected or private.

Inheritance:

- A class with public Constructor can be inherited.

- A class with protected Constructor can be inherited.

- A class with private Constructor can be inherited.




Accessibility of private or protected Constructors:

- An object of the class cannot be instantiated in a project code.

- An array whose elements are instances of the class cannot be declared in a project code.

- A member of the type of the class in a composite variable, a structure or another class cannot be declared in a class code or in a project code.




**Important note:** Regardless the scope of the Constructor, dynamic objects can always be declared in a project code. If the instantiation (the allocation) of the objects cannot be performed, the declaration of dynamic objects is always possible.  Therefore, the following codes are always valid:


```wl
// In these two case, the instantiation is not performed during the declaration.
Object1 is dynamic Class1
Object2 is Class1 <-...
```


**To instantiate objects in the project code**, you have the ability to use a global method of the class.
Example with a private Constructor:


```wl
// Global method of ClassWithPrivateConstructor
GLOBAL PROCEDURE GlobalMethodAllocation(...)

o is ClassWithPrivateConstructor
RESULT o
// *****************************************

// Retrieve an object of the class from a project code:

// Solution 1:
o2 is dynamic ClassWithPrivateConstructor
o2 = ClassWithPrivateConstructor.GlobalMethodAllocation(...)

// Solution 2:
o3 is ClassWithPrivateConstructor <- CClassWithPrivateConstructor.GlobalMethodAllocation(...)
```


**Caution**: The assignment operator (=) behaves differently with the dynamic objects and with the non-dynamic objects. Using the o2 and o3 objects in the assignments will trigger a different behavior:


```wl
// Declaration of a new dynamic object.
o4 is dynamic ClassWithPrivateConstructor

// o2 is a reference to o4
o2 = o4

// The members of o4 are copied into o3
o3 = o4
```


<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Syntax of constructors for the base classes and members
<a name="syntax_constructors_for_the_base_classes_and_members_ELTTEXTE000440"></a>
**Running the constructor of a base class or member**
If a base class or a class type member has a constructor, this constructor is automatically called without parameter. You must:

- assign default values to the parameters of the constructor (or to the parameters of the base class or member)

- explicitly call the constructor by passing the parameters.



<a name="NOTE3_2"></a>
**Calling the constructor method to build a class**

```txt
Constructor <ClassName>(<Parameters>)
```


**Details of syntax**

**&lt;ClassName&gt;**
Name identifying the class.**&lt;Parameters&gt;**
Parameters of the constructor.<a name="NOTE3_3"></a>
**Calling the constructor to build a member**

```txt
Constructor <Member name> (<Parameters>)
```


**Details of syntax**

**&lt;Member name&gt;**
Name identifying the member of the class.**&lt;Parameters&gt;**
Parameters of the constructor.<a name="NOTE3_4"></a>
**Example**
The explicit constructors of the base class or member must be called in first statement of the constructor of the derived class.


```wl
//----Declare the BaseClass1 class
BaseClass1 is class
	BaseClass1Member is int
END
```

```wl
//---- Constructor of BaseClass1
PROCEDURE Constructor(Param1)
Info("Constructor of BaseClass1 => " + Param1)
```



```wl
//----Declare the BaseClass2 class
BaseClass2 is class
	BaseClass2Member is int
END
```

```wl
//---- Constructor of BaseClass2
PROCEDURE Constructor(Param1)
Info("Constructor of BaseClass2 => " + Param1)
```



```wl
//---- Declaration of DerivedClass
DerivedClass is class
	// Inheritance of BaseClass1 whose 
	// Constructor expects a parameter
	Inherits from ClassBase1
	// BaseClass2 member whose 
	// Constructor expects a parameter
	DerivedClassMember is BaseClass2
END
```

```wl
//----Constructor of DerivedClass
PROCEDURE Constructor()
// Explicit call to Constructor
Constructor BaseClass1(10) 
Constructor DerivedClassMember(20)
```


<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Notes
<a name="notes_ELTTEXTE000482"></a>
**Default value of members**
Each member of a class is set to zero if no constructor is associated with the class.

<a name="NOTE5"></a>
<a name="NOTE5_1"></a>


## Syntax of the Destructor method
<a name="syntax_the_destructor_method_ELTTEXTE000506"></a>
**Declaration of the method**

```txt
PROCEDURE Destructor <ClassName>()
```


**Details of syntax**

**&lt;ClassName&gt;**
Name identifying the class. The destructor accepts no parameter.<a name="NOTE5_2"></a>


![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) **Special case in Java/Android**
The concept of destructor does not exist in Java/Android. Therefore, the code found in the destructor of a class will never be run. 
An object is freed by the garbage collector when it is no longer referenced in the application. There is no way to foresee the moment when this operation occurs.
To force an object to be freed, you have the ability to assign it to NULL or to use the [Free](../Motscles/1511012.md) statement.


- ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=WDSimpleOOP.gif) ***Training (WINDEV)*** : **WD Simple OOP** <br>The "WD Simple OOP" example is an educational example about the OOP with WINDEV. This example presents the operating mode of:<br>- classes, <br>- inheritances,<br>- virtual procedures,<br>- UML diagrams,<br>- ...


