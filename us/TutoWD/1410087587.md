
## Lesson 2.3. Overview of available variables


<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## In this lesson you will learn the following concepts
<a name="this_lesson_you_will_learn_the_following_concepts_ELTTEXTE000549"></a>


- The Date, Time and Duration types.

- The Array type.

- The Structure type. 





|   |   |
| --- | --- |
| <br>![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=dur%E9e.png)<br> | <br>Estimated time: 1 h |

| [Previous Lesson](../TutoWD/1410087514.md) | [Table of contents](../TutoWD/1410087560.md) | [Next Lesson](../TutoWD/1410087515.md) |
| --- | --- | --- |





<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Overview
<a name="overview_ELTTEXTE000596"></a>
In addition to simple types (boolean, integer, real, etc.), WLanguage has various types of variables used to handle many specific cases. 

We will get an overview of these different types of variables with a small practical example for each one. For more details, don't hesitate to refer to the online help of each type. 



- If necessary, open the "WLanguage" project that you created in the first lesson (see [A project to discover WLanguage](../TutoWD/1410087578.md) in lesson 2.1).




<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Date, Time and Duration variables
<a name="date_time_and_duration_variables_ELTTEXTE000620"></a>
WLanguage has different types of variables to manage dates, times and durations.


### The Date type
<a name="the_date_type_ELTPARAGRAPHE000062"></a>

For dates, WLanguage has the **Date** type. You can use many functions and properties to manipulate dates.  

- We will use a small code example to manipulate dates: 

	1. Delete the code in the project initialization event (press Ctrl + A to select all and then "Delete").

	2. Type the following code: 
			
		```wl
		MyDate is Date
		```
This line of code declares a Date variable. By default, the variable is initialized with the current date. 

	3. Now, let's see how to assign a value to a date. 
		```wl
		MyDate = Yesterday()
		MyDate = "20210929"
		```
This code presents the different ways to initialize a variable of type Date. The variable can be initialized: 

		- via a WLanguage function. In this example, [Yesterday](../WDLang1/1000023786.md) is used. WLanguage has many functions to determine the first or last day of a month or year, ... 

		- via a string containing the date in "YYYYYMMDD" format. 




4. You can also change the date parameters using WLanguage properties. Type the following code: 
			
		```wl
		// Change date parameters
		MyDate.Year = 1985
		MyDate.Month = 10
		MyDate.Day = 26
		```


5. [&lt;Date&gt;.ToString](../WDLang1/1000023952.md) allows you to define the format used to display dates. For example, enter the following code: 
			
		```wl
		Trace(MyDate.ToString())
		Trace(MyDate.ToString("Dddd Mmmm DD YYYY"))
		Trace(MyDate.ToDayInAlpha())
		```


6. Let's test this code: click ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=ICO_GO_Projet_WD_GAF.jpg) in the quick access buttons. 

7. The "Debugger trace" pane contains: 
			
		```txt
		26/10/1985
		Saturday October 26 1985
		Saturday
		```


	- the date in a readable format.

	- the date in a readable format, using a display mask.

	- the day of the week that corresponds to the date.





### The Time type
<a name="the_time_type_ELTPARAGRAPHE000137"></a>



- Similarly, you can manipulate times using the **Time** type. 

	1. Delete the code in the project initialization event (press Ctrl + A to select all and then "Delete").

	2. Type the following code: 
			
		```wl
		// Declare a time
		MyTime is Time // By default, the time is set to the current time
		
		// Assign a time
		// - via a WLanguage function
		MyTime = Now()
		// - set in the code (e.g. 3:25 PM)
		MyTime = "1525"
		
		// Change time parameters
		MyTime.Hour = 12
		MyTime.Minute = 0
		MyTime.Second = 0
		
		// Display time in a readable format
		Trace(MyTime.ToString())
		// Display time in a readable format using a display mask
		Trace(MyTime.ToString("HH:mm:SS"))
		
		// Compare 2 times
		IF MyTime < Now() THEN
			Trace("The time is earlier than the current time")
		ELSE
			Trace("The time is later than the current time")
		END
		```
This code takes all the possibilities presented on dates, and applies them to times. 

	3. Let's test this code: click ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=ICO_GO_Projet_WD_GAF.jpg) in the quick access buttons. 

	4. The "Debugger trace" pane displays the desired information. 
			
		```txt
		12:00:00:00
		12:00:00
		The time is earlier than the current time
		```


		- the time in a readable format.

		- the time in a readable format, using a display mask.








### The Duration type
<a name="the_duration_type_ELTPARAGRAPHE000171"></a>



- The same type of code can be written for durations. 

	1. Delete the code in the project initialization event (press Ctrl + A to select all and then "Delete").

	2. Type the following code: 
			
		```wl
		// Declare a duration
		MyDuration is Duration
		
		// Assign a duration
		// - with a value in readable format
		MyDuration = 2 min 8 s
		// or 
		MyDuration = 128 s
		// - by specifying the duration in seconds, in minutes, ...
		MyDuration.InSeconds = 128
		
		// Display duration in a readable format
		Trace(MyDuration.ToString("MM:SS"))
		```


	3. Let's test this code: click ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=ICO_GO_Projet_WD_GAF.jpg) in the quick access buttons. 

	4. The "Debugger trace" pane displays the desired information. 
			
		```txt
		02:08
		```








For more details on Date, Time, and Duration variables, see the corresponding help pages: 

- [Date](../Motscles/1514066.md),

- [Time](../Motscles/1514068.md),

- [Duration](../Motscles/1514069.md). 




<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Array variables
<a name="array_variables_ELTTEXTE000662"></a>
Arrays are a widely used type of variable.

An array is a structured type that is used to group a set of elements of the same type. Each array element can be directly accessed by its index.

Specific WLanguage functions are used to handle the arrays and their elements. 
<a name="NOTE3_2"></a>


### Declaration
<a name="declaration_ELTPARAGRAPHE000228"></a>

An Array variable is declared as follows: 

```txt
<Array name> is array of <Type of array elements>
```


For example:

```wl
MyArrayOfStrings is array of strings
MyArrayOfIntegers is array of int
```

<a name="NOTE3_3"></a>


### Filling an array and accessing the elements
<a name="filling_array_and_accessing_the_elements_ELTPARAGRAPHE000243"></a>

During its declaration, the array is empty. The elements are added by [&lt;Array&gt;.Add](../WDLang1/1000024299.md) via the following syntax: 

```txt
<Array name>.Add(<Element value>)
```


To access the array elements, use the following syntax:

```txt
<Array name> [<Element index>]
```
|   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=avertissement.png) | Important | Array element indices start at 1. |







- We will use a small code example to add elements in an array: 

	1. Delete the code in the project initialization event (press Ctrl + A to select all and then "Delete").

	2. Type the following code: 
			
		```wl
		// Create an array of strings
		MyArray is array of strings
		
		// Add elements
		MyArray.Add("WINDEV")
		MyArray.Add("WEBDEV")
		MyArray.Add("WINDEV Mobile")
		
		// Display the content of the third element
		Trace("Value of element 3: [%MyArray[3]%]")
		```


	3. Let's test this code: click ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=ICO_GO_Projet_WD_GAF.jpg) in the quick access buttons. 

	4. If necessary, open the "Debugger trace" pane to see the result: on the "Home" tab, in the "Environment" group, expand "Panes", select "Panes", and then select "Debugger trace". 

	5. The "Debugger trace" pane contains the following information: 
			
		```txt
		Value of element 3: WINDEV Mobile
		```






|   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=astuce.png) | Tip | **Fast array initialization** <br><br>You can also use the following syntax to initialize an array: <br><br><pre><code>// Declare an array<br>DayArray is array of strings<br>// Initialization with <br>// the names of the days of the week<br>DayArray = ["Monday", "Tuesday", "Wednesday", "Thursday", <br>"Friday", "Saturday", "Sunday"]</code></pre><br> |




<a name="NOTE3_4"></a>


### Looping through an array
<a name="looping_through_array_ELTPARAGRAPHE000310"></a>

WLanguage offers two methods to loop through the elements of an array: 

- A standard FOR .. TO loop. 

- A FOR EACH loop.




- In a standard FOR .. TO .. loop, both the values of the start index (1) and end index must be specified. 

	1. For example, add the following code in the "Initialization" event of the project: 
			
		```wl
		FOR Index = 1 TO MyArray.Count
			Trace("Value of element [%Index%]: [%MyArray[Index]%]")
		END
		```

			In this code, the start index is 1 and the end index is given by the [Count](../Proprietes/2510097.md) property of the array. 

	2. Let's test this code: click ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=ICO_GO_Projet_WD_GAF.jpg) in the quick access buttons. 

	3. The "Debugger trace" pane contains the following information: 
			
		```txt
		Value of element 1: WINDEV
		Value of element 2: WEBDEV
		Value of element 3: WINDEV Mobile
		```








- WLanguage offers a much simpler and just as effective type of loop: the FOR EACH loop. We have already seen it on strings. This type of loop also applies to arrays. In this case, there is no need to specify the start or end index.  

	1. For example, replace the code in the FOR loop with the following code in the "Initialization" event of the project: 
			
		```wl
		FOR EACH ELEMENT AString, Index OF MyArray
			Trace("Value of element [%Index%]: [%AString%]")
		END
		```
In this case, we simply had to specify the name of the variable that corresponds to the array element and the variable that corresponds to the index. 

	2. Let's test this code: click ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=ICO_GO_Projet_WD_GAF.jpg) in the quick access buttons. 

	3. The "Debugger trace" pane contains the following information: 
			
		```txt
		Value of element 1: WINDEV
		Value of element 2: WEBDEV
		Value of element 3: WINDEV Mobile
		```








For more details on Array variables, see [Array](../Motscles/1514030.md). 
<a name="NOTE3_6"></a>


### The associative array
<a name="the_associative_array_ELTPARAGRAPHE000381"></a>

An associative array is an "advanced" type of array: it is used to group a set of elements of the same type. Each element of the array is indexed on any type of information (and not simply on a numeric index, as in the other types of arrays).

Associative arrays (with or without duplicates) can be manipulated in WLanguage.  



- Let's do a quick test. 

	1. Delete the code in the project initialization event (press Ctrl + A to select all and then "Delete").

	2. Type the following code: 
			
		```wl
		// Create an associative array of dates
		MyArray is associative array of Date
		
		// Add elements
		MyArray["Mark"] = "19820201"
		MyArray["Anne"] = "19840604"
		
		// Display contents of the element with key "Mark"
		Trace("Value of element with key ""Mark"": " + MyArray["Mark"])
		
		// Loop through the array
		FOR EACH ELEMENT Value, Key, Index OF MyArray
			Trace("Value of element [%Index%] with key [%Key%]: [%Value%]")
		END
		```


	3. Let's test this code: click ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=ICO_GO_Projet_WD_GAF.jpg) in the quick access buttons. 

	4. The "Debugger trace" pane contains the following information: 
			
		```txt
		Value of element with key "Mark": 19820201
		Value of element 1 with key Mark: 19820201
		Value of element 2 with key Anne: 19840604
		```








For more details on Associative array variables, see [Associative array](../Motscles/1514058.md). 

<a name="NOTE7"></a>
<a name="NOTE7_1"></a>


## Structure variables
<a name="structure_variables_ELTTEXTE000746"></a>


### The Structure type
<a name="the_structure_type_ELTPARAGRAPHE000426"></a>

A structure is a custom type of data. Structures contain different types of variables.

- We will use a small code example to manipulate Structures: 

	1. Delete the code in the project initialization event (press Ctrl + A to select all and then "Delete").

	2. Type the following code: 
			
		```wl
		FullName is Structure
			LastName is string
			FirstName is string
		END
		```
This code declares a structure composed of two variables, LastName and FirstName. Variables that are part of a structure are called "Members". To manipulate members, you must declare a variable of the type as the structure. 

	3. We are now going to: 

		- Declare a structure variable. Add the following WLanguage code: 
						
			```wl
			Contact is FullName
			```


		- Assign values to the members of the structure. Add the following WLanguage code: 
						
			```wl
			Contact.LastName = "POLO"
			Contact.FirstName = "MARCO"
			```





4. To read the contents of a member, simply use:
			
		```txt
		<Variable name>.<Member name>
		```
In our example, type the following code: 
			
		```wl
		TheLastName is string
		TheLastName = Contact.LastName
		// Displays the content of TheLastName
		Trace(TheLastName)
		// Displays Contact.LastName directly (without using a variable)
		Trace(Contact.LastName)
		```


5. Let's test this code: click ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=ICO_GO_Projet_WD_GAF.jpg) in the quick access buttons. 

6. The "Debugger trace" pane displays: 
			
		```txt
		POLO
		POLO
		```





As with the other types of variables, it is also possible to handle arrays of structures. 

- Let's complete our example to test the use of arrays of structures: 

	1. Type the following code after the code already entered: 
			
		```wl
		// Declare an array of structures
		MyContacts is array of FullName
		Add(MyContacts, Contact)
		Trace(MyContacts[1].LastName, MyContacts[1].FirstName)
		```
In this code, an array containing the FullName structure is created, then the previously created contact is added and finally the last name and the first name of the contact are displayed. 

	2. Let's test this code: click ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=ICO_GO_Projet_WD_GAF.jpg) in the quick access buttons. 

	3. The "Debugger trace" pane now displays: 
			
		```txt
		POLO
		POLO
		POLO MARCO
		```








For more details on Structures and Arrays of structures, see [Structure](../Motscles/1514008.md). 

<a name="NOTE8"></a>
<a name="NOTE8_1"></a>


## Conclusion
<a name="conclusion_ELTTEXTE000770"></a>
In this lesson, you have discovered some of the types of variables available in WLanguage: 

- the String type,

- the Date, Time and Duration types,

- the Array type,

- the Structure type. 




In the next lesson, we will see how to manipulate some of these variables using conditional statements available in WLanguage. 

| [Previous Lesson](../TutoWD/1410087514.md) | [Table of contents](../TutoWD/1410087560.md) | [Next Lesson](../TutoWD/1410087515.md) |
| --- | --- | --- |




