
## Lesson 7.8. FTP
<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## In this lesson you will learn the following concepts
<a name="this_lesson_you_will_learn_the_following_concepts_ELTTEXTE000171"></a>


- Overview of WINDEV's FTP functions.





|   |   |
| --- | --- |
| ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=dur%E9e.png)<br> | <br>Estimated time: 20 mn |

| [Previous Lesson](../TutoWD/1410087554.md) | [Table of contents](../TutoWD/1410087560.md) | [Next Lesson](../TutoWD/1410087557.md) |
| --- | --- | --- |





<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Overview
<a name="overview_ELTTEXTE000218"></a>
FTP (File Transfer Protocol) is a standard protocol for transferring files from one computer to another. One of the computers must be an FTP server.

Multiple WLanguage functions allow you to transfer files by using this protocol with a server. These functions start with "FTP".

WINDEV only includes FTP "client" functions. A standard FTP server is required. 

The unit example named "The FTP functions" shows how to handle files on an FTP server.



- Open the "The FTP functions" unit example. This example presents the main functionalities that can be used on an FTP server.
![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=P7_Exemple%20fonctions%20FTP%20-%20HC%20N%B0001.jpg)





<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Connecting to an FTP server
<a name="connecting_ftp_server_ELTTEXTE000242"></a>
[FTPConnect](../WDLang3/3040016.md) is used to connect to an FTP server. An FTP account (username and password) is required to access an FTP server.



- Open the WLanguage events associated with the "Connection" Button control in "WIN_FTP":

	1. Select the "Connection" Button control.

	2. Press F2 to open the associated WLanguage code:
			
		```wl
		// 1 - FTP address: corresponds to the FTP site 
		// to which the application must connect. Ex: 192.108.10.2
		sFTPAddress is string = EDT_FTP_Server 
		// 2 - Username: if this name is not specified, 
		// an "anonymous" connection will be used
		sUserName is string = EDT_User
		sUserPWD is string = EDT_Password
		// Connection to FTP server
		gnConnectionID = FTPConnect(sFTPAddress, sUserName, sUserPWD)		
		IF ErrorOccurred THEN
			// An error occurred during the connection
			Error("The connection to the FTP server: " + sFTPAddress + " failed", ErrorInfo())
			RETURN	
		END
		// The connection is established
		Info("Connection to the FTP server established", "Connection identifier: " + gnConnectionID)
		```








Remark: You can also specify the port number of the FTP server connection ("21" by default) as well as the connection mode ("True" for a "passive" connection, "False" for an "active" connection). 
For more details, see [Standard FTP functions](../WDLang3/3040004.md).

<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Sending a file
<a name="sending_file_ELTTEXTE000266"></a>
To send a file to an FTP server, use [FTPSend](../WDLang3/3040014.md).

Let's see an example of code that can be used:

```wl
// When connecting to the server with FTPConnect, we have
// retrieved the connection number in the gnConnectionID variable
// Transfer the "C:\MyDocuments\File.DOC" file to
// the "Temp" directory found on the server.
bResult is boolean = FTPSend(gnConnectionID, "C:\MyDocuments\File.DOC", "/Temp")
```
|   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=avertissement.png)<br> | Caution! | Pay close attention to the case (uppercase/lowercase characters) of the directory names on the server. Some FTP servers are UNIX machines and are "case sensitive", i.e. they are sensitive to the case of file and directory names.<br><br>For example, there is a directory named "MyDirectory" on the FTP server. If you try to access "mydirectory", an error such as "Path not found" will be returned by the FTP server because the case is incorrect. |





<a name="NOTE5"></a>
<a name="NOTE5_1"></a>


## Listing files on an FTP server
<a name="listing_files_ftp_server_ELTTEXTE000308"></a>
[FTPListFile](../WDLang3/3040018.md) lists the files on an FTP server. This function uses a "callback" procedure. The procedure is run for each file or directory found.



- Open the WLanguage events associated with the "List in a trace" Button control in "WIN_FTP":

	1. Select the "List in a trace" Button control.

	2. Press F2 to open the associated WLanguage code:
			
		```wl
		FTPListFile(gnConnectionID,"*.*", CallBackFTPListFile, ftpFile+ftpDirectory)
		
		// Check the function execution
		IF ErrorOccurred THEN
			Error("Error while browsing the files found on the FTP server", ErrorInfo())
			RETURN
		END
		```


	3. In the example, **CallBackFTPListFile** is an internal procedure that displays in a trace window the files found.
			
		```wl
		INTERNAL PROCEDURE CallBackFTPListFile(sFileName, nFileSize <useful>, ...
			sAttribute, sModifDate <useful>, sModifTime <useful>)
		
		// Is it a file or a directory
		IF sAttribute = "D" THEN
			// Directory or subdirectory
			Trace("Directory: " + sFileName)
		ELSE
			// File
			Trace("File: " + sFileName)
		END
		
		// Continue to loop through the files
		RETURN True
		```








<a name="NOTE6"></a>
<a name="NOTE6_1"></a>


## Retrieving a file
<a name="retrieving_file_ELTTEXTE000332"></a>
To get a file from an FTP server, call [FTPGet](../WDLang3/3040002.md).

Let's see an example of code that can be used:

```wl
// When connecting to the server with FTPConnect, we have
// retrieved the connection number in the gnConnectionID variable
// Download the "/Document/File.DOC" file found 
// on the FTP server to the "D:\Temp" directory on 
// the current computer
bRes is boolean = FTPGet(gnConnectionID, "C:\MyDocuments\File.DOC", "/Temp")
```


<a name="NOTE7"></a>
<a name="NOTE7_1"></a>


## Disconnecting from an FTP server
<a name="disconnecting_from_ftp_server_ELTTEXTE000356"></a>
To disconnect from an FTP server, use [FTPDisconnect](../WDLang3/3040011.md).



- Open the WLanguage events associated with the "Disconnection" Button control in "WIN_FTP":

	1. Select the "Disconnection" Button control.

	2. Press F2 to open the associated WLanguage code:
			
		```wl
		IF YesNo(No, "Do you want to close the connection to the FTP server?") = Yes THEN
			// Disconnect from the FTP server
			FTPDisconnect(gnConnectionID)
			IF ErrorOccurred THEN
				// Error while closing the connection
				Error("Unable to close the FTP connection", ErrorInfo())
				RETURN
			END
			
			// The connection is ended
			Info("The connection to the FTP server is over")
		END
		```








Other FTP functions are available but we won't go into details about them in this tutorial. They are mainly used to:

- create, delete and change directories on the FTP server,

- create, delete and change files on the FTP server,

- retrieve information about a directory and/or a file,

- ...




For more details, see [Standard FTP functions](../WDLang3/3040004.md).

| [Previous Lesson](../TutoWD/1410087554.md) | [Table of contents](../TutoWD/1410087560.md) | [Next Lesson](../TutoWD/1410087557.md) |
| --- | --- | --- |




