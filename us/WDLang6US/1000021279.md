
## PDFExtractPage (Function)

***In french: PDFExtraitPage***



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Extracts a page from a PDF file in a variable of type [Image](../WDLang1/1000019650.md).




<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
// Extracts page 3 from the text.pdf file
MyImage is Image = PDFExtractPage("text.pdf", 3)
```



```wl
EDT_NumPagePDF.BackgroundColor = White
IF EDT_NumPagePDF = 0 THEN
	Info("Specify a page page number to be extracted")
	EDT_NumPagePDF.BackgroundColor = PastelRed
	SetFocusAndReturnToUserInput(EDT_NumPagePDF)
	RETURN
END

sNewPath is string = fSelectDir(sOriginalPath, ...
	"Directory selector","Select the page extraction directory")

// Search in the DOC_Contact data file for the text contained in the Table control.
HReadSeek(DOC_Contact, DOCID_Contact, TABLE_Win_Document_List.COL_DOCID_Contact)
IF HFound() THEN
	HourGlass()

	MyPDFPath is string = sNewPath + "\" + ...
			DOC_Contact.DOCID_Contact + "." + DOC_Contact.Format

	// Document is a memo that contains the PDF file
	IF NOT HExtractMemo(DOC_Contact, Document, MyPDFPath) THEN
		Error(HErrorInfo())
	ELSE
		IF PDFNumberOFPages(MyPDFPath) < EDT_NumPagePDF THEN
			Error("The PDF document does not contain enough pages", ...
					"Process canceled")
			RETURN
		END

		MyPDFImage is Image = PDFExtractPage(MyPDFPath, ...
				EDT_NumPagePDF)
		// Delete original PDF
		fDelete(MyPDFPath)
		
		MyPNGImage is string = sNewPath + "\" + DOC_Contact.DOCID_Contact + ...
			 "_Page" + EDT_NumPagePDF + ".PNG"
		dSaveImagePNG(MyPDFImage, MyPNGImage)	
		ShellExecute(MyPNGImage)
	END
	HourGlass(False)
END
```

<a name="XSYNTAX"></a>

## Syntax
<a name="SYNTAX1"></a>

`<Result> = PDFExtractPage(<File path> , <Page number> [, <Width> [, <Height> [, <Password>]]])`
---

**`<Result>: (Image (image variable))`**

[Image](../WDLang1/1000019650.md) variable that contains the image of the extracted page. The returned image is a raster image (non vectorial).

Remark: If the PDF file does not exist or if the page does not exist, the [Image](../WDLang1/1000019650.md) variable returned will be invalid. The [ErrorOccurred](../WDLang1/3087001.md) variable will correspond to <u><u><u><u>True</u></u></u></u>.

**`<File path>: (Character string)`**

Path of the PDF file from which the page will be extracted. 

**`<Page number>: (Integer)`**

Number of the page to extract. 

**`<Width>: (Optional integer)`**

Width at which the page must be rasterized. 
If this parameter is not specified (or if this parameter is equal to 0), the height and width will be calculated from the page size (in millimeters), by considering a DPI set to 300.

**`<Height>: (Optional integer)`**

Height at which the page must be rasterized. 
If this parameter is not specified (or if this parameter is equal to 0), the height and width will be calculated from the page size (in millimeters), by considering a DPI set to 300.

**`<Password>: (Optional character string)`**

Password of the PDF document. 







<a name="XComponent"></a>

## Business / UI classification:
Business Logic
## Component:
wd280wdpdf.dll
