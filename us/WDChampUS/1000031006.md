


## Handling Dashboard controls programmatically (prefix syntax)
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Overview
<a name="overview_ELTTEXTE000091"></a>
To programmatically handle a Dashboard control, WINDEV, WEBDEV and WINDEV Mobile include [dashboard-specific functions](../WDLang1/1000022537.md). 

This help page explains how to programmatically handle Dashboard controls. 

![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png) Remark: Dashboard controls are available in Linux WEBDEV sites only. Dashboard controls are not available in WINDEV applications for Linux.



<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Handling Dashboard controls programmatically
<a name="handling_dashboard_controls_programmatically_ELTTEXTE000137"></a>


### Adding a widget
<a name="adding_widget_ELTPARAGRAPHE000160"></a>

Widgets can be added to Dashboard controls using [&lt;Dashboard&gt;.AddWidget](../WDLang1/1000022502.md). By default, a Widget is invisible. It can be displayed by using the [Visible](../Proprietes/2510138.md) property. If the widget is not visible, the user can make it visible via the context menu of the edit mode.

Examples: 

- Adding a simple widget (without parameters): 
	
	```wl
	// Adds a simple widget
	nIndex = DASH_Dashboard.AddWidget(IW_Widget_StockAlert, "Stock alert")
	```


- Adding a widget with parameters: 
	
	```wl
	nIndex is int
	
	// Adds a widget that expects parameters
	nIndex = DASH_Dashboard.AddWidget(IW_Widget_Clock, ...
		"Clock" + COMBO_AddWidget[COMBO_AddWidget].DisplayedValue, ...
		COMBO_AddWidget[COMBO_AddWidget].DisplayedValue)
	
	// Displays this widget upon request
	IF YesNo(Yes, "Do you want to display this widget?") = Yes THEN
		DASH_Dashboard[nIndex].Visible = True
	ELSE
	 	ToastDisplay("Widget added to the dashboard as ""hidden"". "+ ...
		"Switch to edit mode to add it", toastShort, vaMiddle, haCenter)
	END
	```



**Special case: adding a widget to the initial configuration**
By default, the Dashboard control displays the widgets as they have been defined in the window or page editor. 

To add widgets programmatically when the Dashboard control is loaded, use: 

- [&lt;Dashboard&gt;.AddWidget](../WDLang1/1000022502.md) to define the widget to add. 

- [&lt;Dashboard&gt;.InitialConfiguration](../WDLang1/1000022525.md) to configure the widget position in the initial configuration. 




Caution: In this case, these functions must be used in the initialization event of the Dashboard control. 

Examples: 

- Adding a simple widget (without parameters)
	
	```wl
	// -- Initialize the DASH_Dashboard control
	// - stock alert
	// Adds the widget
	nIndex = DASH_Dashboard.AddWidget(IW_Widget_StockAlert, "Stock alert")
	// Configures the widget (placed on the left)
	DASH_Dashboard.InitialConfiguration(nIndex, 5, 1)
	```


- Adding a widget with parameters
	
	```wl
	// -- Initialize the DASH_Dashboard control
	
	dDate is Date
	nIndex is int
	nX, nY are int
	
	// Adds information to the initial configuration:
	// - key numbers for the last 3 months
	nX = 1 ; nY = 3
	FOR i = 1 TO 3
		// Adds the widget
		nIndex = DASH_Dashboard.AddWidget(IW_Widget_KeyNumber, ...
			StringBuild("Key numbers for %1", ...
			dDate.ToString("Mmm YYYY")), dDate)
		// Configures the widget (placed on the left)
		DASH_Dashboard.InitialConfiguration(nIndex, nX, nY)
		// The next one will be lower
		nY++
		dDate.Month--
	END
	```



Remark: To refresh a widget with parameters, it is recommended to:  

- Add an optional event for refreshing the widget in the events of the internal window or page. 

- Enter the code used to refresh the widget. This code can be used for example to update the elements displayed in the internal window or page. 

- Refresh the widget with [&lt;Dashboard&gt;.Display](../WDLang1/1000022500.md).



<a name="NOTE2_2"></a>


### Running a procedure defined for an internal window or page (used as widget)
<a name="running_procedure_defined_for_internal_window_page_used_widget_ELTPARAGRAPHE000246"></a>

To run a procedure defined in the internal window or page used as Widget, use the following syntaxes: 

- Syntax that uses the name of the internal window or page: 
	
	```txt
	NameInternalWindow.ProcedureName(Param1, Param2, ...)
	```

	For example: 
	
	```wl
	IW_Widget_KeyNumber.NewDate(EDT_Date)
	```


- Syntax that uses a Control variable (if the same internal window or page is used several times in the Dashboard control): 
	
	```txt
	VariableName is Control <- DashboardName[WidgetIndex]
	VariableName.ProcedureName(Param1, Param2, ...)
	```

	For example: 
	
	```wl
	X is Control <-DASH_Dashboard[4]
	X.NewDate(EDT_Date)
	```





<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Handling a widget programmatically
<a name="handling_widget_programmatically_ELTTEXTE000266"></a>
To programmatically handle a widget, use one of the following syntaxes: 

- Syntax 1: Direct use of the Dashboard control:  
	
	```txt
	DashboardName[WidgetIndex]
	```

	For example: 
	
	```wl
	Trace(DASH_MyDashboard[1].Caption)
	```


- Syntax 2: Using a Control variable : 
	
	```txt
	VariableName is Control <- DashboardName[WidgetIndex]
	```

	For example: 
	
	```wl
	X is Control <-DASH_Dashboard[4]
	Trace(X.Caption)
	```





The properties that can be used on widgets are identical to those that can be used on internal windows or pages. For more details, see [Properties associated with the internal windows](../WDChamp/1013256.md) and [Properties associated with the internal pages](../WDChamp/1013282.md).

<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Dragging and dropping widgets onto a Dashboard control
<a name="dragging_and_dropping_widgets_onto_dashboard_control_ELTTEXTE000315"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) WINDEV allows you to drag and drop Image, List Box and Looper controls (among others) onto a Dashboard control. 

The different steps are as follows: 

1. **Declare that the Dashboard control is the drop target**.
	This declaration is made in the initialization code of the control via the [DndTarget](../Proprietes/2510016.md) property. 
	Example: 
	
	```wl
	// -- Initialize DASH_Dashboard
	// Allows drag-and-drop operations
	MySelf.DndTarget = dndAuto
	```


2. **Declare that the Dashboard control is the drag source**.
	In the initialization code of the source control, declare that the control is the drag source with the [DndSource](../Proprietes/2510014.md) property. 
	Example: 
	
	```wl
	// The image is the drag source
	MySelf.DndSource = dndProgram
	```


3. **Define the procedure to start the drag operation** (e.g. in the initialization code of the Source control).
	Example: 
	
	```wl
	// Defines the drag start procedure
	// This procedure uses DnDCacheDashElement 
	// to define the widget to drop
	DnDEvent(onDndBegin, MySelf, dndBeginDrag)
	```


4. **Type the code of the procedure**. 
	Example: 
	
	```wl
	PROCEDURE onDndBegin()
	
	// Define the widget to drop according to the source control
	SWITCH _DND.SourceControl
		// Indicates that the drop on the Dashboard control 
		// must add the "Clock" widget 
		// by using the "IW_Widget_Clock" internal window
		CASE IMG_Widget_Calendar.Name
			DnDCacheDashElement(IW_Widget_Calendar, "Clock")
			
		OTHER CASE
			Error("Only the IMG_Widget_Calendar image" + ...
				"is allowed for Drag and Drop")
	END
	```





<a name="NOTE7"></a>
<a name="NOTE7_1"></a>


## Properties specific to Dashboard controls
<a name="properties_specific_dashboard_controls_ELTTEXTE000376"></a>
Use the following properties to programmatically handle Dashboard controls.



|   |   |
| --- | --- |
| [CompactOption](../Proprietes/1000020608.md) | The **CompactOption** property is used to:<br><br>- find out whether the options found in a Check Box, Radio Button or Dashboard control are in compacted mode.<br><br>- switch the options found in a Check Box, Radio Button or Dashboard control to compacted mode.<br><br><br> |
| [ElementHeight](../Proprietes/1000020068.md) | The **ElementHeight** property is used to:<br><br>- Find out or modify the height of the elements in an Organization Chart control. <br><br>- Find out or modify the height of the cells in a Dashboard control.<br><br><br> |
| [ElementWidth](../Proprietes/1000020066.md) | The **ElementWidth** property is used to:<br><br>- Find out or modify the width of the elements in an Organization Chart control.<br><br>- Find out or modify the width of the cells in a Dashboard control.<br><br><br> |
| [MarginHeight](../Proprietes/1000021029.md) | The **MarginHeight** property is used to: <br><br>- Find out the vertical margin between the widgets found in a Dashboard control. <br><br>- Modify the vertical margin between the widgets found in a Dashboard control.<br><br><br> |
| [MarginWidth](../Proprietes/1000021028.md) | The **MarginWidth** property is used to: <br><br>- Find out the horizontal margin between the widgets found in a Dashboard control. <br><br>- Modify the horizontal margin between the widgets found in a Dashboard control.<br><br><br> |





For a complete list of WLanguage properties that can be used with Dashboard controls, see [Dashboard control properties](../WDChamp/1000031003.md).


