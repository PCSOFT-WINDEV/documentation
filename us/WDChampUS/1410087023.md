


## Handling a Word Processing control through programming
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Overview
<a name="overview_ELTTEXTE000337"></a>
A Word Processing control can be handled through programming. 

To handle the Word Processing control, WLanguage proposes: 

- a set of functions used to perform all the common operations (direct programming via the WLanguage DocXXX functions. For more details, see [Word Processing control functions](../WDLang1/1000022618.md). 

- an advanced programming via the different advanced types of WLanguage used to directly handle the content of the docx file displayed by the Word Processing control. For more details, see [Types associated with the Word Processing control](../WDLang1/1000022736.md). 




This help page explains how to handle a Word Processing control through programming.




## Handling the Word Processing control through programming
<a name="handling_the_word_processing_control_through_programming_ELTTEXTE000365"></a>


### Opening and creating a docx document in a Word Processing control
<a name="opening_and_creating_docx_document_word_processing_control_ELTPARAGRAPHE000033"></a>

To open an existing docx document through programming in a Word Processing control, you can: 

- assign the docx file to the Word Processing control. For example:
	
	```wl
	// Select a .docx file in the current directory
	sFileName is string
	sFileName = fSelect("", "presentation.docx", ...
		"Selecting DocX files", ...
		"Docx file (*.docx)" + TAB + "*.docx", "*.docx", fSelOpen)
	
	IF sFileName ~= "" THEN
		// No selected file
		RETURN
	END
	
	// Load the docx file
	WP_MyDocument = sFileName
	```


- use [DocOpen](../WDLang1/1000022622.md).
	 For example:
	
	```wl
	WP_MyDocument = DocOpen("C:\Users\test\Documents\file.docx")
	
	IF ErrorOccurred() THEN
		Error(ErrorInfo())
	RETURN
	```


- use a [Document](../WDLang1/1000022461.md) variable and assign it to the Word Processing control.
	For example:
	
	```wl
	MyDocument is Document
	MyDocument = "C:\temp\presentation.docx"
	WP_MyDocument = MyDocument
	```

	or: 
	
	```wl
	MyDocument is Document
	MyDocument = DocOpen("C:\Users\test\Documents\file.docx")
	IF ErrorOccurred() THEN
		Error(ErrorInfo())
	ELSE
		WP_MyDocument = MyDocument
	END
	```



Caution: only the files in docx format can be handled by the Word Processing control. 

**Tip**: To display an empty document in a Word Processing control, all you have to do is assign an empty string to the Word Processing control. 
Example: 

```wl
// Create a blank document
WP_MyDocument = ""
```



### Saving a docx document
<a name="saving_docx_document_ELTPARAGRAPHE000077"></a>

To save a docx document, all you have to do is use [DocSave](../WDLang1/1000022387.md). 

Example: 

```wl
// Choose the directory and the backup name 
sFileName is string = fSelect(CompleteDir(fExeDir()),"presentation.docx", ...
	"Selecting DocX files","Docx file (*.docx)" + TAB + "*.docx" , "*.docx", fselCreate)

// Save the file
DocSave(WP_NoName1, sFileName)

IF ErrorOccurred THEN
	Error(StringBuild("The %1 file was not saved.", sFileName), ErrorInfo())
	RETURN
END
```
Remarks: 

- The content of Word Processing control can also be: 

	- printed ([iPrintDoc](../WDLang5/1000022390.md)).

	- saved in PDF format ([DocToPDF](../WDLang1/1000022531.md)).




- [DocToText](../WDLang1/1000022389.md) is used to retrieve the document text. 

- [DocToImage](../WDLang1/1000022808.md) is used to convert a document page into image. 





### Performing a search in a document and modifying the search result
<a name="performing_search_document_and_modifying_the_search_result_ELTPARAGRAPHE000108"></a>

To perform a search in a Word Processing document, all you have to do is use [DocFind](../WDLang1/1000022388.md). 

Example: 

```wl
// Find "BEAUTIFUL" in the text 
// Selects the first one found to change its color
arrFragments is array of docFragments = DocFind(WP_NoName1, "BEAUTIFUL")

IF arrFragments.Count >= 1 THEN
	// Position the cursor at the beginning of the first word found
	WP_NoName1.Cursor = arrFragments[1].StartPosition
	// Calculate the selection length
	WP_NoName1.SelectionLength = ...
				arrFragments[1].EndPosition - arrFragments[1].StartPosition+1
	// Modify the text color
	arrFragments[1].Formatting.TextColor = PastelRed
	BREAK
END
```


In this code, the [docFragment](../WDLang1/1000022482.md) variable corresponds to the text section containing the searched word, as well as its position in the entire text. Then, you have the ability to handle the fragment found. 


### Handling the selection
<a name="handling_the_selection_ELTPARAGRAPHE000127"></a>

To handle the selection currently performed in a Word Processing control, you have the ability to retrieve this selection in a [docFragment](../WDLang1/1000022482.md) variable. 

The following code is used to change the color of selected text. 
Example: 

```wl
// Change the selection color
// Get the current selection
MySelection is docFragment(WP_MyDocument, WP_MyDocument.Cursor, ...
		WP_MyDocument.SelectionLength)
// Change the color
MySelection.Formatting.TextColor = PastelRed
```


The [docFragment](../WDLang1/1000022482.md) variable owns a specific constructor used to easily retrieve the selection. 


### Adding text into a document
<a name="adding_text_into_document_ELTPARAGRAPHE000148"></a>

Several methods can be used **to add text into an existing document**: 

- Inserting a fragment at the desired position. Example: 
	
	```wl
	// Insert a text at the end of document 
	frag2 is docFragment(WP_MyDoc, -1 , 0)
	frag2.Text += "End text"
	```


- Inserting a text from a given position with [DocInsert](../WDLang1/1000022732.md). 
	
	```wl
	DocInsert(WP_Document, WP_Document.Cursor, "My inserted text")
	```


- Adding a text at the end of document with [DocAdd](../WDLang1/1000022919.md). 




**To add text into an empty document**, you can: 

- use the direct assignment of control: 
	
	```txt
	<Word Processing control> = "Text to insert"
	```
Example: 
	
	```wl
	WP_MyDocument = "I am the first sentence of this document."
	```


- add the text with [DocAdd](../WDLang1/1000022919.md). 

- insert a fragment in first position. Example: 
	
	```wl
	// sInitialText contains the text to insert 
	frag is docFragment(WP_MyDoc, WP_MyDoc.Cursor, 0)
	frag.Text += sInitialText
	```


- insert a text from position 1 with [DocInsert](../WDLang1/1000022732.md). 
	
	```wl
	DocInsert(WP_Document, 1, "My inserted text")
	```






### Adding a page break into a document
<a name="adding_page_break_into_document_ELTPARAGRAPHE000201"></a>

The following code is used to add a page break into a document: 

```wl
// Add a page break 
DocAdd(WP_NoName, Charact(12))
```
You can also use the following constants to add the desired type of break to a document or fragment: 

- *docColumnBreak*: Adds a column break in a multicolumn section. If the section is not multicolumn, a page break is added. 

- *docLineBreak*: Adds a line break. 

- *docPageBreak*: Adds a page break. 

- *docParagraphBreak*: Adds a paragraph break. 




Example: 

```wl
// Add a page break 
DocAdd(WP_NoName1, docPageBreak + "Text on next page")
```



### Management of bookmarks
<a name="management_bookmarks_ELTPARAGRAPHE000236"></a>

You have the ability to manage bookmarks in a Word Processing document. For example: 

- Adding a bookmark: Adds a bookmark named MyBookmark with the selected text.
	
	```wl
	doc is document
	doc <- WP_MyDocument
	fragSelection is docFragment = WP_MyDocument.Selection.Fragment
	doc.Bookmark["MyBookmark"] = fragSelection
	```


- Deleting a bookmark: Deletes the bookmark named Bookmark1: 
	
	```wl
	doc is document
	doc <- WP_MyDocument
	Delete(doc.Bookmark, "Bookmark1")
	```


- Listing the bookmarks: Lists the document bookmarks: 
	
	```wl
	doc is document 
	doc <- WP_MyDocument
	FOR EACH uABookmark,BookmarkName of doc.Bookmark
		Trace(BookmarkName) // Bookmark name
		Trace(uABookmark.Text) // Bookmark text
	END
	```


- Inserting a text at the end of bookmark: 
	
	```wl
	doc is Document <- WP_MyDocument.Value
	// Find the bookmark position 
	fragmentBookmark is docFragment = doc.Bookmark["Bookmark 1"]	
	IF fragmentBookmark <> Null THEN
		// Insert text at the end of bookmark
		let nInsertionPosition = fragmentBookmark.EndPosition
		// Actual insertion
		DocInsert(WP_MyDocument, nInsertionPosition, "Text to insert at bookmark position")
	ELSE
		Erreur("'Bookmark 1' bookmark not found in the document")
	END
	```






### Sequencing reports and Word Processing documents
<a name="sequencing_reports_and_word_processing_documents_ELTPARAGRAPHE000263"></a>

You now have the ability to sequence reports and Word Processing documents. 

To do so, [iSequencingAddDoc](../WDLang5/1000023089.md) must be used in addition to the standard functions for creating sequences of reports. For example: 

```wl
MyDocument is Document = "c:\temp\generalconditions.docx"

// Print in the report viewer
iDestination(iViewer)
// Add reports into the sequence
iSequencingAdd(RPT_Report1)
iSequencingAdd(RPT_Report2, 3)
// Add the general conditions in document format
iSequencingAddDoc(MyDocument)
iSequencingPrint()
```
For more details, see [Sequencing reports](../WDChamp/9000128.md). 


### Properties associated with Word Processing controls
<a name="properties_associated_with_word_processing_controls_ELTPARAGRAPHE000280"></a>The following properties are specific to the Word Processing control. They are mainly used to handle the control characteristics: 



|   |   |
| --- | --- |
| [AutoCorrect](../Proprietes/1410088993.md) | The **AutoCorrect** property allows you to define a set of automatic corrections to be applied in a Word Processing control as the user types. |
| [AutomaticLink](../Proprietes/2513017.md) | The **AutomaticLink** property is used to: <br><br>- determine if the automatic link detection mode is enabled, <br><br>- enable or disable the automatic link detection mode.<br><br><br> |
| [DisplayBookmarks](../Proprietes/1000023694.md) | The **DisplayBookmarks** property is used to: <br><br>- identify the display mode of bookmarks in a Word Processing control. <br><br>- show or hide bookmarks in a Word Processing control.<br><br><br> |
| [DisplayFormattingMarks](../Proprietes/1000022634.md) | The **DisplayFormattingMarks** property is used to: <br><br>- Determine if formatting marks are shown or hidden in a Word Processing control. <br><br>- Show or hide formatting marks in a Word Processing control.<br><br><br> |
| [DisplayMode](../Proprietes/1000023378.md) | The **DisplayMode** property gets and changes: <br><br>- the display mode in a Word Processing control, <br><br>- the display mode in a PDF Reader control<br><br>- the display mode in an HTML Editor control, <br><br>- the display mode in a Kanban control<br><br><br> |
| [FilePath](../Proprietes/1000023104.md) | The **FilePath** property is used to identify: <br><br>- the name of the xlsx file associated with a Spreadsheet control.<br><br>- the name of the file associated with an Image Editor control. <br><br>- the name of the PDF file associated with a PDF Reader control. <br><br>- the name of the DOCX file associated with a Word Processing control. <br><br>- the name of the wddiag file associated with a Diagram Editor control. <br><br><br> |
| [FormattingMarksColor](../Proprietes/1000024632.md) | The **FormattingMarksColor** property is used to: <br><br>- get the color of the formatting marks in a Word Processing control. <br><br>- change the color of the formatting marks in a Word Processing control.<br><br><br> |
| [HTMLEdit](../Proprietes/1000023415.md) | The **HTMLEdit** property is used to: <br><br>- find out whether a Word Processing control is displayed in optimized mode for HTML editing, <br><br>- modify a Word Processing control to display it (or not) in optimized mode for HTML editing.<br><br><br> |
| [NumberAccessiblePages](../Proprietes/1000023477.md) | The **NumberAccessiblePages** property is used to get the number of pages currently loaded in a PDF Reader or Word Processing control. |
| [NumberDisplayedPage](../Proprietes/1000023479.md) | The **NumberDisplayedPage** property is used to identify and change the number of the page currently displayed in the PDF Reader or Word Processing control. |
| [NumberPage](../Proprietes/2510084.md) | The **NumberPage** property is used to get: <br><br>- the number of pages in a "multi-page" image file. This image is displayed in an Image control or in the background of a Chart control.<br><br>- the number of pages in a PDF file displayed in an Image control.<br><br>- the number of pages found in a PDF file displayed in a PDF Reader control.<br><br>- the number of pages found in a DOCX file displayed in a Word Processing control. <br><br><br> |
| [RuleVisible](../Proprietes/1000023076.md) | The **RuleVisible** property is used to: <br><br>- Find out whether the rulers are visible or invisible in a Word Processing control. <br><br>- Make the rulers visible or invisible in a Word Processing control.<br><br><br> |
| [Selection](../Proprietes/1000023033.md) | The **Selection** property is used to get the characteristics of the selection (or cursor): <br><br>- in a Word Processing control.<br>	Remark: This selection is in the section being edited in the control (body, header or footer). <br><br>- in a Spreadsheet control.<br><br>- in an HTML Editor control. <br><br>- in a Diagram Editor control.<br><br><br> |
| [SelectionLength](../Proprietes/1000022522.md) | The **SelectionLength** property is used to get and change the length of the selection made in a Word Processing control. |
| [ToolbarVisible](../Proprietes/1000021333.md) | The **ToolbarVisible** property is used to: <br><br>- know if the toolbar or the ribbon is displayed in a control.  <br><br>- show or hide the toolbar or the ribbon in a control.<br><br><br> |





For a complete list of WLanguage properties that can be used with a Word Processing control, see [Properties associated with Word Processing controls](../WDChamp/1410087022.md).


- ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=TheWordProcessingcontrol.gif) ***Unit examples (WINDEV)*** : **The Word Processing control** <br>This example explains how to handle a Word Processing control by programming. You have the ability to add text and to format it by programming.


