
## DateTimeDifference (Function)

***In french: DateHeureDifférence***



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Calculates the difference between two dates and times.

**Remarks**: 

- You can also use the '-' (minus) operator to calculate the difference between two dates (see Remarks). 

- To display the result of this function in a Duration variable, use [StringToDuration](../WDLang1/3027002.md).





<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
// Time elapsed since 01/01/1998 at 12:15
Diff is string
Diff = DateTimeDifference("199801011215", SysDateTime())
// Display time elapsed
Info("Time elapsed: " + CR + ...
	Left(Diff, 8) + "days" + CR + ...
	Middle(Diff, 9, 2) + "hours" + CR + ...
	Middle(Diff, 11, 2) + "minutes" + CR + ...
	Middle(Diff, 13, 2) + "seconds" + CR + ...
	Middle(Diff, 15, 2) + "hundredths of a second")
```


<a name="Example2"></a>

```wl
// To fill a duration from the result of DateTimeDifference
Diff is string = DateTimeDifference("199801011215", SysDateTime())
// convert the difference into a duration variable
Duration1 is Duration = StringToDuration(Diff, durationCenti)

// --------------------------------------------
// Other possible solution 
// DateTime1 is DateTime = "199801011215"
// DateTime2 is DateTime = DateTimeSys()
// Duration1 is Duration = DateTime2 - DateTime1
// ----------------------------------------------

// Display time elapsed
Info("Time elapsed: " + Duration1.Day + "days" + CR + ...
	Duration1.Hour + "hours" + CR + ...
	Duration1.Minute + "minutes" + CR + ...
	Duration1.Second + "seconds" + CR + ...
	Duration1.Millisecond + "thousandths of a second")
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Syntax

`<Result> = DateTimeDifference(<Start date/time> , <End date/time>)`
---

**`<Result>: (Character string)`**

Numbers of days, hours, minutes and seconds elapsed between two dates in +/-DDDDDDHHMMSSCC format where:

- DDDDDD is the number of days elapsed between the two specified "Date - Time",

- HH is the number of hours,

- MM is the number of minutes,

- SS is the number of seconds,

- CC is the number of hundredths of a second.


This result comes from the following operation: &lt;End date/time&gt; - &lt;Start date/time&gt;. 

This result contains the '-' sign if the &lt;Start date/time&gt; is later than the &lt;End date/time&gt;.

**Caution**: To assign this result to a Duration variable, use [StringToDuration](../WDLang1/3027002.md) with the *durationCenti* constant.

**`<Start date/time>: (Character string or DateTime variable)`**

Start date/time in the following format:

- YYYYMMDDHHmmSSCC

- YYYYMMDDHHmmSS

- YYYYMMDDHHmm

- YYYYMMDDHH




**`<End date/time>: (Character string or DateTime variable)`**

End date/time in the following format:

- YYYYMMDDHHmmSSCC

- YYYYMMDDHHmmSS

- YYYYMMDDHHmm

- YYYYMMDDHH  






<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks


### Validity of the dates
<a name="validity_the_dates_ELTPARAGRAPHE000100"></a>

The validity of the dates and times passed as parameters is checked. A message is displayed if the date or time is invalid. A "Date - Time" is considered invalid if:

- The date is invalid. You can check the validity of a date using [DateValid](../WDLang1/3027003.md).

- The time is invalid. You can check the validity of a time using [TimeValid](../WDLang1/3027021.md).




This function cannot be used to calculate the difference between two dates before October 14, 1582 (the change from the Julian to the Gregorian calendar will not be taken into account).  

The date storage format allows you to store dates from 01/01/0001 to 12/31/9999.

[WLanguage functions](../WDLang1/3027029.md) and [WLanguage properties](../Proprietes/2514010.md) make accurate calculations on dates from January 1st, 1583.


### Calculating the difference between two dates with the operators
<a name="calculating_the_difference_between_two_dates_with_the_operators_ELTPARAGRAPHE000118"></a>

The **DateTimeDifference** function can be replaced by the '-' operator. You can also assign the result directly to a variable of type Duration, which allows you to use a negative duration. 

Examples: 

```wl
interval_duration is Duration
IF date_time_1 > date_time_2 THEN
	interval_duration = date_time_1 - date_time_2
ELSE
	interval_duration = date_time_2 - date_time_1
END
```

```wl
interval_duration is Duration = date_time_1 - date_time_2
IF interval_duration < 0 THEN
	interval_duration = - interval_duration
END
```


<a name="XComponent"></a>

## Business / UI classification:
Neutral code
## Component:
wd280std.dll
