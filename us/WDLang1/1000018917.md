
## fMemOpen (Function)

***In french: fMemOuvre***



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Opens a memory zone shared between several applications.

Then, the content of memory area can be handled by [fSeek](../WDLang1/3036039.md), [fRead](../WDLang1/3036048.md) and [fWrite](../WDLang1/3036014.md).
<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
// Create/Open a memory area shared between applications (1st application)
arrZoneID is associative array of int
Zone1ID is int
Zone1ID = fMemOpen("MySharedZone", 1024, shareGlobal, ...
		foReadWrite, ModificationProcedure)
arrZoneID["MySharedZone"] = Zone1ID
...
// Create/Open a memory area shared 
// between applications (2nd application) - 
// no callback
Zone2ID is int
Zone2ID = fMemOpen("MySharedZone", 1024, shareGlobal)
// Write into the shared memory area
fWrite(Zone2ID, "Hello, I am a WINDEV application!")
...
// The writing performed by the 2nd application triggers 
// the call to the procedure in the 1st application
```

```wl
// -------------------------
// Procedure called whenever the shared memory area is modified
PROCEDURE ModificationProcedure(NameNotifZone is string)
ModifiedString is string
ModifiedString = fRead(arrZoneID[NameNotifZone], 1024)
Trace(StringBuild("The %1 string was written into the %2 zone", ModifiedString, NameNotifZone))
```


<a name="Example2"></a>


![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png) 
```wl
llMemHdl is int
sFileName is string = fTempPath() + ["/"] + "ipc0"
fSaveText(sFileName, "")
sNameMemZone is string = Charact(3) + sFileName
llMemHdl = fMemOpen(sNameMemZone, 1024, shareGlobal)

// Equivalent code in C
// char * szZoneName = "/tmp/ipc0"
// int nSize;
// int nMem;
// key_t nKey;
// int nAccess = 0666; 
// Opening in read/write

// nSize = 1024;
// nKey = ftok(szZoneName, 3);
// nMem = shmget(nKey, nSize, nAccess);
```

<a name="XSYNTAX"></a>

## Syntax
<a name="SYNTAX1"></a>

`<Result> = fMemOpen(<Memory area> , <Size> [, <Share> [, <Opening mode> [, <WLanguage procedure>]]])`
---

**`<Result>: (Integer)`**



- Identifier of the memory area.
	This identifier can be handled by the fxxx functions like an identifier of external file returned by [fOpen](../WDLang1/3036036.md).

- -1 if an error occurred. 




**`<Memory area>: (Character string)`**

Name of the shared memory area that must be created or opened. This name must be the same in the different applications that access the zone.
The zone is automatically created if it does not exist.
![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png) The name of the memory area must have the following format: 

```txt
Charact(<Val>) + <Existing Unix file name>
```

where &lt;Val&gt; is an integer from 1 to 127 that corresponds to the second parameter of the "C" ftok() API (if the exchange must be performed with a process written in C). 

**`<Size>: (Integer)`**

Minimum size of the shared memory zone in bytes.

- If the zone already exists: 

	- if &lt;Size&gt; is equal to 0: this parameter is ignored and the size of the zone is returned by [fSize](../WDLang1/3036055.md).

	- if &lt;Size&gt; is different from 0: 

		- if &lt;Size&gt; is too big, fMemOpen returns -1. 

		- the existing memory area is opened with a size at least equal to the parameter (the actual size is defined by the operating system and it is returned by [fSize](../WDLang1/3036055.md)).




- If the zone does not exist, it is created with a size at least equal to the parameter (the actual size is defined by the operating system and it is returned by [fSize](../WDLang1/3036055.md)).




**`<Share>: (Optional Integer constant)`**

Mode for sharing the memory area between the user sessions:


|   |   |
| --- | --- |
| *shareGlobal* | The zone is shared between all the applications of all the computer sessions. |
| *shareUser*<br>(Default value) | The zone is shared between all the applications of the user session. |



**`<Opening mode>: (Optional Integer constant)`**

Constants used to define the access mode to the shared memory zone.


|   |   |
| --- | --- |
| *foRead* | The zone is opened in "read only". You will only have the ability to read this zone.<br><br>If this constant is used, to change the opening mode of the memory area, you must close the memory area ([fClose](../WDLang1/3036027.md)) and re-open it with a different opening mode. |
| *foReadWrite*<br>(Default value) | The zone is opened in "read/write". You will be able to read and modify this zone. |
| *foUnicode* | Open a file in UNICODE format. (For more details, see the Notes) |
| *foWrite* | The zone is opened in "write only". You will only have the ability to write into this zone. |



**`<WLanguage procedure>: (Procedure name)`**

Name of the WLanguage procedure ("callback" procedure) that will be automatically called if the memory zone is modified by another WLanguage application.
This procedure has the following format:

```txt
PROCEDURE <Procedure name>(<Zone name>)
```

The parameter of this procedure is optional. It must be used when a unique callback is used to receive the notifications of several shared memory areas.
The &lt;Zone name&gt; parameter will be prefixed by the "Global\\" string if the zone was created with the  *shareGlobal* constant and by the  "Local\\" string if the zone was created with the *shareUser* constant.



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks




### Managing the share mode
<a name="managing_the_share_mode_ELTPARAGRAPHE000139"></a>

The share mode differs according to the versions of the operating systems:

- Windows Vista and later: the &lt;Share&gt; parameter of **fMemOpen** is supported. The services and the users are located in a different space. To share a memory area between a service and an application in a user session, the *shareGlobal* constant must be used.

- In Terminal Server: the &lt;Share&gt; parameter of **fMemOpen** is supported. Each session opened on the Terminal Server has a different memory space. The applications started in the same TSE session can share a memory area created with the *shareUser* constant. Several applications located in different sessions can share a memory zone created by the *shareGlobal* constant.

- Windows: The administrator rights are required to access a *Global* memory zone in write mode or in read mode.



<a name="NOTE0_2"></a>
![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png) 

### Correspondence between the name provided to fMemOpen and the opening in C
<a name="correspondence_between_the_name_provided_fmemopen_and_the_opening_ELTPARAGRAPHE000156"></a>

The two following code examples present the opening of a memory zone (named "myzone") in WLanguage and inC.


```wl
// Code in WLanguage
ZoneID is int
ZoneID = fMemOpen("myzone", 1024, shareGlobal)
```



```txt
// Equivalent code in C
char * szZoneName = "myzone";
int nSize;
int nMem;
key_t nKey;
int nAccess = 0666;	// Open in read/write

nSize = 1024;
nKey = ftok(szZoneName+sizeof(char),(int) szZoneName[0]);
nMem = shmget(nKey, nSize , nAccess);
```





<a name="NOTE0_3"></a>


### Managing the memory areas in UNICODE format
<a name="managing_the_memory_areas_unicode_format_ELTPARAGRAPHE000168"></a>

**fMemOpen** is used to read and write memory areas using UTF-16 or Little Endian Unicode. At opening, the "current Unicode" mark (FFFE) is automatically read. At creation, it is automatically added.

**Remark**: The mechanism for automatic callback of a WLanguage procedure whenever the memory zone is modified by another application operates between WLanguage applications only.
<a name="NOTE0_4"></a>


### Positioning in the shared memory area
<a name="positioning_the_shared_memory_area_ELTPARAGRAPHE000179"></a>

When opening a shared memory area, the current position corresponds to the first byte of the zone. 

This position can be modified by [fSeek](../WDLang1/3036039.md).
<a name="NOTE0_5"></a>


### Closing the shared memory area
<a name="closing_the_shared_memory_area_ELTPARAGRAPHE000191"></a>

When the application does no longer need to access the shared memory area, the access can be closed by [fClose](../WDLang1/3036027.md). The zone is effectively destroyed once the last application accessing it has closed the zone. If no explicit call to [fClose](../WDLang1/3036027.md) is found in the code of the application, the closing will be automatically performed when the application ends.

<a name="XComponent"></a>

## Component:
wd280std.dll
