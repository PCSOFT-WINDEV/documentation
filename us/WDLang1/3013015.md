


## Compile (Function)

***In french: Compile***



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Dynamically compiles a procedure whose source code is supplied. The created and compiled procedure is a procedure global to the project.

Remark: [ExecuteCode](../WDLang1/1000019783.md) and [EvaluateExpression](../WDLang1/1000019782.md) also allow you to use a code that is dynamically generated. 


<a name="sample_code"></a>

## Example
<a class="notetitle" target="_blank" href="$DOC$=1000003013015&name=compile_function&product=WD">See additional examples</a>
<a name="Example1"></a>

```wl
// Syntaxe 1: Compilation dynamique d'une procédure globale
// Source: champ dans lequel est saisi le code de procédure

sRésultat est une chaîne 
sRésultatCompile est une chaîne 
sRésultatCompile = Compile("Proc_dynamique", MonCodeSource)
SELON sRésultatCompile
	CAS "": sRésultat = ExécuteTraitement("Proc_dynamique", trtProcédure)
	CAS "ERR": sRésultat = "Compilation impossible." + RC + ErreurInfo()
	AUTRE CAS: sRésultat = sRésultatCompile
FIN
RENVOYER sRésultat
```
<a name="Example2"></a>

```wl
// Syntaxe 2: Compilation dynamique d'une procédure globale
// Utilisation du type Procédure
// Source: champ dans lequel est saisi le code de procédure

Formule est une Procédure = Compile("Proc_dynamique", MonCodeSource)
SI ErreurDétectée = Faux ALORS
	ExécuteTraitement("Proc_Dynamique", trtProcédure)
	// Autre possibilité pour exécuter la procédure: Formule()
SINON
	Info(ErreurInfo())
FIN
```
<a name="Example3"></a>

```wl
// Syntaxe 3: Compilation dynamique d'une procédure globale
// Utilisation directe du type Procédure
// Source: champ dans lequel est saisi le code de procédure

Formule est une Procédure = Compile(MonCodeSource)
SI ErreurDétectée = Faux ALORS
	Formule()
SINON
	Info(ErreurInfo())
FIN
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Syntax

### Dynamic compilation of a global procedure

`<Result> = Compile(<Procedure name> , <Source code>)`
---

**`<Result>: (Character string)`**

Compilation result:

- Empty string ("") if the compilation was successful. The procedure can be run using [ExecuteProcess](../WDLang1/3013031.md) and [Execute](../WDLang1/3013041.md) with the *trtProcedure* constant.

- ERR if a fatal error occurred: wdxxxcpl.dll not found, incorrect &lt;Source code&gt;, no current project, global procedure already created, etc. [ErrorInfo](../WDLang1/3013008.md) returns the details of the error.

- An error message if a compilation error was detected. This message corresponds to the caption of the error.




**`<Procedure name>: (Character string)`**

Name of dynamic global procedure to create.

To create a local procedure, all you have to do is specify the element name. For example: "WindowName.ProcedureName". 

Caution: If **Compile** is called several times with the same procedure name, the last created procedure is automatically overwritten.

**`<Source code>: (Character string)`**

Source code in WLanguage of the procedure to compile dynamically. 

- If this code contains quotes, they must be doubled (see the example).

- This code may contain the call and declaration code of an internal procedure.





<a name="SYNTAX2"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png)![User code (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png) 
### Dynamic compilation of a global procedure associated with a Procedure variable 

`<Result> = Compile(<Procedure name> , <Source code>)`
---

**`<Result>: (Procedure variable)`**

Name of the [Procedure](../Motscles/1000018907.md) variable that points to the compiled procedure. 

The procedure can be run: 

- with the [ExecuteProcess](../WDLang1/3013031.md) function.

- with the [Execute](../WDLang1/3013041.md) function. 

- via a call to the [Procedure](../Motscles/1000018907.md) variable. 




If a compilation error occurs, the [ErrorOccurred](../WDLang1/3087001.md) variable is set to <u><u><u><u>True</u></u></u></u> and [ErrorInfo](../WDLang1/3013008.md) returns the error details.

**`<Procedure name>: (Character string)`**

Name of dynamic global procedure to create.

To create a local procedure, all you have to do is specify the element name. For example: "WindowName.ProcedureName". 

If **Compile** is called several times with the same procedure name, the last created procedure is automatically overwritten.  

**`<Source code>: (Character string)`**

Source code in WLanguage of the procedure to compile dynamically. 

- If this code contains quotes, they must be doubled (see the example).

- This code may contain the call and declaration code of an internal procedure.





<a name="SYNTAX3"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png)![User code (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png) 
### Dynamic compilation of anonymous procedure

`<Result> = Compile(<Source code>)`
---

**`<Result>: (Procedure variable)`**

Name of the [Procedure](../Motscles/1000018907.md) variable that points to the compiled procedure. 

The procedure can be run by a call to the [Procedure](../Motscles/1000018907.md) variable. 

If a compilation error occurs, the [ErrorOccurred](../WDLang1/3087001.md) variable is set to <u><u><u><u>True</u></u></u></u> and [ErrorInfo](../WDLang1/3013008.md) returns the error details.

The procedure is compiled as a global procedure. To compile a procedure associated with a window, you must name the procedure (syntax 2). 

**`<Source code>: (Character string)`**

Source code in WLanguage of the procedure to compile dynamically. 

- If this code contains quotes, they must be doubled.

- This code may contain the call and declaration code of an internal procedure.






<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks


### Using the dynamic procedure
<a name="using_the_dynamic_procedure_ELTPARAGRAPHE000192"></a>

- **If you are using the first syntax**, to use your dynamic procedures, all you have to do is run the procedure via [ExecuteProcess](../WDLang1/3013031.md) or [Execute](../WDLang1/3013041.md).
	Another solution (not recommended): before calling **Compile**, declare the name of the procedure to the WLanguage compiler using the [EXTERN](../Motscles/1511013.md) keyword and use the procedure directly.

- **If you are using the second syntax**, to use your dynamic procedures, simply start the procedure: 

	- using [ExecuteProcess](../WDLang1/3013031.md) or [Execute](../WDLang1/3013041.md).

	- via a direct call to the [Procedure](../Motscles/1000018907.md) variable. 




- **If you are using the third syntax**, to use your dynamic procedures, all you have to do is run the procedure via a direct call to the [Procedure](../Motscles/1000018907.md) variable. A procedure that is dynamically compiled can take parameters. Example: 





```wl
MonCodeSource est une chaîne = [
PROCÉDURE AdditionneXetY(aa, bb)
RENVOYER aa + bb
]
MaProc est une procédure = Compile(MonCodeSource)
Trace(MaProc(2, 3))
```

<a name="NOTE0_2"></a>


### Dynamic code
<a name="dynamic_code_ELTPARAGRAPHE000236"></a>

**Structures** can be used in dynamic code.

The **constants cannot be used** in the dynamic code (defined by the [CONSTANT](../Motscles/1514012.md) keyword).

When using constants in a code, all the occurrences of the constants are replaced with their value during the compilation in the editor but the correspondence between the name of constants and their value is not "embedded" in the application. Therefore, the dynamic compilation cannot use the constants.

Let's see two alternatives:

- 1st solution: Use variables instead of constants. 
	The code: 
	
	```wl
	CONSTANTE
		CST_Nom = 1
	FIN
	```

	becomes for example
	
	```wl
	CST_Nom est un entier = 1
	```


- 2nd solution: In the string containing the code that must be compiled dynamically, replace the name of the constant by its value: 
	
	```wl
	sCode est une chaîne
	sCode = [
	Info(CST_Nom)
	]
	
	// Remplace le nom de la constante par sa valeur
	sCode = Remplace(sCode, "CST_Nom", CST_Nom, MotComplet + SansCasse)
	
	// Il est ensuite possible de compiler le code
	SI Compile("ProcDyn", sCode) <> "" ALORS
		Erreur("Erreur de compilation de la procédure dynamique: ", ...
			ErreurInfo())
	SINON
		// Puis de l'exécuter
		QUAND EXCEPTION DANS 
			ExécuteTraitement("ProcDyn", trtProcédure)
		FAIRE
			Erreur("Erreur d'exécution de la procédure dynamique: ", ...
				ExceptionInfo())
		FIN
	FIN
	```




<a name="NOTE0_3"></a>


### Parameters of a query
<a name="parameters_query_ELTPARAGRAPHE000267"></a>

If the source code used for the dynamic compilation runs a query with parameters ([HExecuteQuery](../WDLang4/3044080.md)), the parameters expected by the query must necessarily be specified in [HExecuteQuery](../WDLang4/3044080.md).

For example, you must use:


```wl
HExécuteRequête("MaRequête", hRequêteDéfaut, "Dupont")
```
instead of:


```wl
MaRequête.NomClient = "Dupont"
HExécuteRequête("MaRequête", hRequêteDéfaut)
```

<a name="NOTE0_4"></a>


### Deploying of an application that uses Compile
<a name="deploying_application_that_uses_docparampagetitleshort_ELTPARAGRAPHE000287"></a>

When deploying an application that uses**Compile** (when creating the executable or deploying the site), you must specify all the libraries of the WINDEV/WEBDEV/WINDEV Mobile framework used by the code that is dynamically compiled. Indeed, this code being compiled dynamically, WINDEV, WEBDEV and WINDEV Mobile do not detect the framework libraries used.
<a name="NOTE0_5"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png)![User code (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png) 

### Limitations
<a name="limitations_ELTPARAGRAPHE000299"></a>

- If a window (page) in the project uses a local Procedure with the name , calls to  made from a WLanguage event in the window (page) will always execute the local Procedure.

- If a global Procedure  already exists, dynamically compiling an Procedure  will cause an error. 

- By default, overloading the WLanguage functions is ignored during the dynamic compilation. 
	For example, if [Trace](../WDLang1/3013050.md) has been overloaded, the WLanguage function (not the overloaded function) will be called in a code that is dynamically compiled. To force the use of the overloaded function during the dynamic compilation, the name of the function must be preceded by the "Extern" keyword.

- The enumerations and the combinations are not available in dynamic compilation.




- ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=Dynamiccompilation.gif) ***Unit examples (WINDEV)*** : **Dynamic compilation** <br>Dynamic compilation of WLanguage code (stored in string format), execution of the dynamically generated procedure and process of possible runtime errors.<br>The dynamic compilation is very useful when mathematical calculations are proposed to the end user for example.

<a name="XComponent"></a>

## Component:
wd280vm.dll
