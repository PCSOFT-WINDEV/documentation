


## ExecuteCode (Function)

***In french: ExecuteCode***



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Runs the WLanguage code found in a character string.


<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
sText is string = "Text"
// Displays the text in a window
ExecuteCode("Info(sText)")
```


<a name="Example2"></a>

```wl
// Declare the variables.
sCodeToRun is string
xOperationResult is numeric

// WLanguage code to run. 
// This code returns the result of an operation %1.
sCodeToRun = [
	MyCalculation is numeric
	MyCalculation = %1
	RETURN MyCalculation
	]

// WLanguage code to run. 
// Replace %1 by the operation displayed 
// in the EDT_EXPRESSION edit control.
sCodeToRun = StringBuild(sCodeToRun, EDT_EXPRESSION)

// Run the code and retrieve the result.
xOperationResult = ExecuteCode(sCodeToRun)

// Displays the result of the operation in an Info message box
Info("The result of your operation is: " + xOperationresult)
```

<a name="XSYNTAX"></a>

## Syntax
<a name="SYNTAX1"></a>

`<Result> = ExecuteCode(<Code>)`
---

**`<Result>: (Any type)`**



- Result of the code if it contains a RETURN statement,

- Nothing otherwise. In this case, an error message may be displayed when the result is assigned in a variable for example. 




**`<Code>: (Character string)`**

WLanguage code to run. 
Remark: This code may contain the call and declaration code of an internal procedure.



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks


### Local variables
<a name="local_variables_ELTPARAGRAPHE000049"></a>

The local variables of the current process can be directly used in the code to run.

**If the code is compiled without error**, the code is run directly. 

A fatal error is triggered **if the code is not compiled**.
<a name="NOTE0_2"></a>


### Compiling the code
<a name="compiling_the_code_ELTPARAGRAPHE000060"></a>

The code is recompiled each time **ExecuteCode** is called.

To avoid the compilation step, you have the ability to use [Compile](../WDLang1/3013015.md) and [Execute](../WDLang1/3013041.md).


### Dynamic code
<a name="dynamic_code_ELTPARAGRAPHE000075"></a>

The **constants cannot be used** in the dynamic code (defined by the [CONSTANT](../Motscles/1514012.md) keyword).

When using constants in a code, all the occurrences of the constants are replaced with their value during the compilation in the editor but the correspondence between the name of constants and their value is not "embedded" in the application. Therefore, the dynamic compilation cannot use the constants.

Let's see two alternatives:

1 - Use variables instead of constants


```wl
CONSTANT
	CST_Name = 1
END
```
becomes for example


```wl
CST_Name is int = 1
```
or

2 - In the string containing the code that must be compiled dynamically, replace the name of the constant by its value:


```wl
sCode is string
// In the string containing the code that will be compiled dynamically
// instead of leaving the name of the constant:
sCode=[
Info(CST_Name)
]
// Replace the name of the constant by its value
sCode = Replace(sCode, "CST_Name", CST_Name, WholeWord + IgnoreCase)
// The code can be compiled now
IF Compile("DynProc", sCode) <> "" THEN
	Error("Error while compiling the dynamic procedure: ", ...
		ErrorInfo())
ELSE
	// Then it can be run
	WHEN EXCEPTION IN 
		ExecuteProcess("DynProc", trtProcedure)
	DO
		Error("Error while running the dynamic procedure: ", ...
		ExceptionInfo())
	END
END
```


<a name="XComponent"></a>

## Component:
wd280vm.dll
