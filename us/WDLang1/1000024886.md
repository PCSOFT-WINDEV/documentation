
## InputMask (Type of variable)

***In french: MasqueSaisie***
				



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
The **InputMask** type is used to define all the advanced characteristics of a custom input mask. The characteristics of this custom input mask can be defined and changed using different WLanguage properties. 

**Remark**: For more details on the declaration of this type of variable and the use of WLanguage properties, see [Declaring a variable](../Motscles/1514032.md).


<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
m_sMask is string = "99 99 99 99 99"
CustomMask is InputMask
CustomMask.FormatDuringInput = FormatDuringInput
CustomMask.ValidateDuringInput = ValidateDuringInput
CustomMask.ValidateDuringExit = ValidateDuringExit
CustomMask.FormatDuringExit = FormatDuringExit
CustomMask.FormatDuringEntry = FormatDuringEntry
CustomMask.FormatDuringAssignment = FormatDuringAssignment
CustomMask.CheckIfValidInput = CheckIfValidInput

EDT_Phone.InputMask = CustomMask

INTERNAL PROCEDURE FormatDuringInput(LOCAL sTextBefore is string, 
	LOCAL nCursorBefore is int, ...
	LOCAL nEndCursorBefore is int, ...
	sTextAfter is string, ...
	nCursorAfter is int, ...
	nEndCursorAfter int)
	IF Length(sTextBefore) > Length(sTextAfter) THEN RESULT 
	//No action if deleting
	// if at the end
	IF nEndCursorAfter = Length(sTextAfter) +1 THEN
		// include " " if needed at this location
		IF Middle(m_sMask, nEndCursorAfter, 1) = " " THEN
			sTextAfter += " "
			nEndCursorAfter ++
			nCursorAfter = nEndCursorAfter
		END
	END
END	

INTERNAL PROCEDURE ValidateDuringInput(sText is string): boolean
	RESULT MatchRegularExpression(sText , "[0-9 ]*")
END

INTERNAL PROCEDURE CheckIfValidInput(sText is string): boolean
	RESULT ValidateDuringInput(sText) _AND_ Length(sText) >= 3
END

INTERNAL PROCEDURE FormatDuringAssignment(sText_INOUT is string) 
		FormatStringToPhoneNumber(sText_INOUT)
END

INTERNAL PROCEDURE ValidateDuringExit(sText is string): boolean
	IF Length(sText) < 3 THEN
		ToastDisplay("3 characters minimum")
		RESULT False
	END
	RETURN True
END

INTERNAL PROCEDURE FormatDuringExit(sText_INOUT is string) 
	FormatStringToPhoneNumber(sText_INOUT)
END

INTERNAL PROCEDURE FormatDuringEntry(sText_INOUT is string) 
	FormatStringToPhoneNumber(sText_INOUT)
END

INTERNAL PROCEDURE FormatStringToPhoneNumber(sText_INOUT is string)
	// group numbers like in the mask
	sText_INOUT = Replace(sTexte_INOUT," ","")
	sResult is string
	nPosSrc is int = 1
	FOR I = 1 _TO_ Length(m_sMask)
		// depending on the character in the mask
		SWITCH Middle( m_sMask, I, 1) 
			CASE " "	
				sResult += " "
			OTHER CASE
				// keep the entered character
				sResult += Middle(sText_INOUT, nPosSrc, 1) 
				nPosSrc++
		END
	END
	sText_INOUT = sResult
END
```





<a name="NOTE0"></a>

## Remarks
<a name="NOTE0_1"></a>


### Properties specific to the description of InputMask variables
<a name="properties_specific_the_description_inputmask_variables_ELTPARAGRAPHE000046"></a>

The following properties can be used to handle a custom input mask:

| Property name | Type used | Effect |
| --- | --- | --- |
| CheckIfValidInput | WLanguage procedure | Name of the WLanguage procedure called by [InvalidInputDetect](../WDLang1/1000026180.md) and [InvalidInputListControl](../WDLang1/1000026244.md) to determine if the text entered is valid.<br><br>This procedure has the following format: <br><br><pre><code>PROCEDURE <Procedure name>(<Text> string)</code></pre><br>where &lt;Text&gt; corresponds to the text to handle. <br><br>If this property is not specified, no check is performed. |
| FormatDuringAssignment | WLanguage procedure | Name of the WLanguage procedure that transforms the text assigned programmatically into text to be displayed.<br><br>This procedure has the following format: <br><br><pre><code>PROCEDURE <Procedure name>(<Text> string)</code></pre><br>where &lt;Text&gt; corresponds to the text to handle. <br><br>If this property is not specified, no transformation is performed. |
| FormatDuringEntry | WLanguage procedure | Name of the WLanguage procedure that transforms the displayed text into input text.<br><br>This procedure has the following format: <br><br><pre><code>PROCEDURE <Procedure name>(<Text> string)</code></pre><br>where &lt;Text&gt; corresponds to the text to handle. <br><br>If this property is not specified, no transformation is performed. |
| FormatDuringExit | WLanguage procedure | Name of the WLanguage procedure that transforms the input text into displayed text. <br><br>This procedure has the following format: <br><br><pre><code>PROCEDURE <Procedure name>(<Text> string)</code></pre><br>where &lt;Text&gt; corresponds to the text to handle. <br><br>If this property is not specified, no transformation is performed. |
| FormatDuringInput | WLanguage procedure | Name of the WLanguage procedure that transforms the text during input.<br><br>This procedure has the following format: <br><br><pre><code>PROCEDURE <Procedure name>(LOCAL<Text before> string, <br>LOCAL <Cursor before> int, <br>LOCAL <End Cursor before> int, <br><Text after> string, <Cursor after> int, <br><End Cursor after> int)</code></pre><br>where: <br><br>- &lt;Text before&gt; corresponds to the text before input.<br><br>- &lt;Cursor before&gt; corresponds to the position of the cursor before input.<br><br>- &lt;End cursor before&gt; corresponds to the end position of the cursor before input. <br><br>- &lt;Text after&gt; corresponds to the text after input.<br><br>- &lt;Cursor after&gt; corresponds to the position of the cursor after input.<br><br>- &lt;End cursor after&gt; corresponds to the end position of the cursor after input. <br><br><br><br><br>If this property is not specified, no transformation is performed. |
| ValidateDuringExit | WLanguage procedure | Name of the WLanguage procedure that checks the input (once all the characters have been entered). This procedure returns <u><u><u><u>False</u></u></u></u> if the text entered contains invalid characters. <br><br>This procedure will be called when leaving the control only if blocking input validation is enabled. For more details, see [Required or invalid input in WINDEV](../WDChamp/9500154.md).<br><br>This procedure has the following format: <br><br><pre><code>PROCEDURE <Procedure name>(<Text> string)</code></pre><br>where &lt;Text&gt; corresponds to the text to handle. <br><br>If this property is not specified, no transformation is performed. |
| ValidateDuringInput | WLanguage procedure | Name of the WLanguage procedure that checks the current input. This procedure returns <u><u><u><u>False</u></u></u></u> if the text being entered contains invalid characters. <br><br>This procedure has the following format: <br><br><pre><code>PROCEDURE <Procedure name>(<Text> string)</code></pre><br>where &lt;Text&gt; corresponds to the text to handle. <br><br>If this property is not specified, no transformation is performed. |


<a name="NOTE0_2"></a>


### Use
<a name="use_ELTPARAGRAPHE000183"></a>

- An **InputMask** variable can be assigned to the [InputMask](../Proprietes/2510033.md) property to control each event linked to the management of a mask in an edit control.

- [MaskPhoneNumber](../WDLang1/1000024741.md) and [MaskZipCode](../WDLang1/1000024742.md) handle **InputMask** variables. 





