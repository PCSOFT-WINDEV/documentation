


## Input (Function)

***In french: Saisie***



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Displays a message allowing the user to type an information.<br>![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=fonctions_dialogue%20-%20HC%20N%B0008.gif)
**Remark**: Several syntaxes are available for this function: 

- Simplified syntax, entered in the code editor directly. 

- Full syntax with the question entered via a wizard. The code editor proposes a [full wizard](../WDLang1/3021020.md) ("&lt;Wizard&gt;" option proposed by the [assisted input](../Editeurs/2013001.md)). This wizard is used to define the different elements displayed in the message and to automatically generate the corresponding code. The caption of the buttons is proposed according to the text of the question.

- Direct syntax, typed in the code editor directly. 



<a name="Example1"></a>
<a name="sample_code"></a>

## Example


```wl
// Example of simplified input 
NbCopies is int
ReturnValue is int
ReturnValue = Input("How many copies do you want to print?", NbCopies)
SWITCH ReturnValue 
	CASE 0: Info("You canceled. The default number is set to 1.")
	CASE 1: Info("You requested " + NbCopies)
END
```
<a name="Example2"></a>

```wl
// Simplified input while using an input mask 
PhoneNum is string
// Use a specific text input mask
Input.InputMask = maskPhoneFrance
SWITCH Input("What is your phone number?", PhoneNum)
	// OK
	CASE 1: EDT_PhoneNum = PhoneNum 
	// Cancel
	CASE 0: EDT_PhoneNum = "No phone"
END
```
<a name="Example3"></a>

```wl
Comments is string
Comments = RepeatString(CR, 10)
// Comments typed by the user in a multiline control 
// Input limited to 140 characters
Input.InputMask = "regexp:.{0,140}"
SWITCH Input("Type your comments.", Comments)
	// OK
	CASE 1: EDT_Comment = Comments
	// Cancel
	CASE 0: EDT_Comment = "No comment"
END
```
<a name="Example4"></a>

```wl
DisplayInfo is boolean = False
// Ask the user whether he really wants to close the application 
// A checkmark ("Don't display this window anymore") allows the user
// not to display this question anymore
Input.OptionalCaption = "Don't display this window anymore"
IF DisplayInfo = False THEN
	SWITCH Input("Do you want to close the application?", DisplayInfo)
		// Close
		CASE 1: Close()
	END
END
// Remember to store the value of DisplayInfo for the next time
```
<a name="Example5"></a>

```wl
// Direct syntax 
PhoneNum is string
If Input("What is your phone number?", PhoneNum, ...
		["Validate", "No phone", "Cancel"], 3, 3, dlgIconQuestion) = 1 THEN
	Info(PhoneNum)
END
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Syntax

### Simplified syntax (without using the wizard)

`<Result> = Input(<Question> , <Variable to enter>)`
---

**`<Result>: (Integer)`**

Identifies the answer selected by the user: 

- 0: the "Cancel" button was clicked by the user.

- 1: the "OK" button was clicked by the user.




**`<Question>: (Character string)`**

Question that will be asked to the user. This question can use [StringBuild](../WDLang1/3024006.md).

**`<Variable to enter>: (Any type)`**

Variable in which the user must type the answer. 

- This variable must have been declared before the call to **Input**. 

- The type of variable defines the type of control where users enter the answer. For Time or Date variables, the input mask is automatically deduced from the variable. 
	![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) A specific input mask can be specified (especially for the Text variables) via the **Input.InputMask** variable (see Notes).

- If the variable is initialized with a value, this value corresponds to the default value.

- After **Input** is called, this variable contains the user's answer (regardless of the button clicked).





<a name="SYNTAX2"></a>

### Full syntax with the question entered via a wizard

`<Result> = Input(<Question> , <Variable to enter> [, <Parameter 1> [... [, <Parameter N>]]])`
---

**`<Result>: (Integer)`**

Identifies the answer selected by the user (value from 1 to the number of buttons found in the edit window). 

This answer depends on the number of buttons in the question description window. The different values corresponding to the different answers are automatically included in comments in the code editor when selecting the message.  

**`<Question>: (Character string)`**

Question that will be asked to the user. This question can contain parameters identified by %1, %2, ...

**`<Variable to enter>: (Any type)`**

Variable in which the user must type the answer. 

- This variable must have been declared before the call to **Input**. 

- The type of variable defines the type of control where users enter the answer. For Time or Date variables, the input mask is automatically deduced from the variable. 
	![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) A specific input mask can be specified (especially for the Text variables) via the **Input.InputMask** variable (see Notes).

- If the variable is initialized with a value, this value corresponds to the default value.

- After **Input** is called, this variable contains the user's answer (regardless of the button clicked).




**`<Parameter 1>: (Character string)`**

If the question contains configurable elements (identified by %1, %2, ...), this parameter is used to give the desired value. Therefore, &lt;Parameter 1&gt; will replace %1.



**`<Parameter N>: (Character string)`**

If the selected message contains elements with parameters (identified by %1, %2, ...), this parameter is used to give the desired value. Therefore, &lt;Parameter 2&gt; will replace %2.


<a name="SYNTAX3"></a>

### Direct syntax

`<Result> = Input(<Question> , <Variable to enter> , <Captions of buttons> [, <Default button> [, <Cancellation button> [, <Icon>]]])`
---

**`<Result>: (Integer)`**

Identifies the answer selected by the user. This answer depends on the selected message.

**`<Question>: (Character string)`**

Question asked to the user.

**`<Variable to enter>: (Any type)`**

Variable in which the user must type the answer. 

- This variable must have been declared before the call to **Input**. 

- The type of variable defines the type of control where users enter the answer. For Time or Date variables, the input mask is automatically deduced from the variable. 
	![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) A specific input mask can be specified (especially for the Text variables) via the **Input.InputMask** variable (see Notes).

- If the variable is initialized with a value, this value corresponds to the default value.

- After **Input** is called, this variable contains the user's answer (regardless of the button clicked).




**`<Captions of buttons>: (Array)`**

Name of the Array variable containing the captions of the buttons.

**`<Default button>: (Integer)`**

Index of the button selected by default. This parameter is set to 1 by default.

**`<Cancellation button>: (Integer)`**

Index of the cancellation button. By default, this parameter corresponds to the index of the last button.

**`<Icon>: (Character string or Integer constant)`**

Displayed icon. This parameter can correspond to: 

- the path of file corresponding to the displayed icon. 

- one of the following constants: 
	


|   |   |
| --- | --- |
| *dlgIconError* | Icon representing an error. |
| *dlgIconInfo* | Icon representing an information. |
| *dlgIconQuestion*<br>(Default value) | Icon representing a question. |








<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Remarks


### Characteristics of dialog box and edit control
<a name="characteristics_dialog_box_and_edit_control_ELTPARAGRAPHE000166"></a>



- The title of the dialog box corresponds to the title of the current window (or page).
	

- To modify or define the title of dialog box, use [NextTitle](../WDLang1/3038043.md).




- The skin template of current project is automatically applied to the dialog box. 
	![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) The skin template of the application is not applied to the input window. 

- ![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) To modify the characteristics of the edit control (e.g. to modify the alignment or to replace the characters typed in the control with asterisks), display the question edit wizard (![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=IconeSaisie.GIF) in the syntax of **Input**) and click ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=Saise_Exemple%201.GIF).



<a name="NOTE0_2"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) 

### Managing input masks
<a name="managing_input_masks_ELTPARAGRAPHE000190"></a>

By default, the input mask is automatically defined from the project information.

However, the **Input.InputMask** variable allows you to modify the input mask.

This variable can take one of the following values:  


|   |   |
| --- | --- |
| *maskAAlphaNum* | Letter, then letters + digits |
| *maskAAlphaNumUpper* | Uppercase letter, then uppercase letters + digits |
| *maskAlpha* | Letters |
| *maskAlphaNum* | Letters + digits |
| *maskAlphaNumUpper* | Letters in uppercase characters + digits |
| *maskAlphaUpper* | Letters in uppercase characters |
| *maskEmail* | Email address |
| *maskFileName* | File name and path |
| *maskFileNoFolder* | File name (without its directories) |
| *maskFileSize* | Numeric mask for the file and disk sizes |
| *maskFUpper* | First letter in uppercase character |
| *maskINSEE* | INSEE number |
| *maskINSEEKey* | INSEE number + key |
| *maskLower* | All in lowercase characters |
| *maskNone* | No input mask |
| *maskNum* | Digits |
| *maskNumPlus* | Digits, '+', ' ', '.', '-', ',' |
| *maskPatronymic* | Letters + Digits + Space + Quote + Dash |
| *maskPatronymicUpper* | Caps + Digits + Space + Quote + Dash |
| *maskPhone* | Phone number |
| *maskPhoneFrance* | Phone number in French format |
| *maskUpper* | All in uppercase characters |


You also have the assign a regular expression as input mask. For more details, see the help on the [2510033](../Proprietes/2510033.md) property. 
<a name="NOTE0_3"></a>


### Typing multiline text
<a name="typing_multiline_text_ELTPARAGRAPHE000295"></a>

The multiline input will be allowed in the associated control if &lt;Variable to Enter&gt; is initialized with a character string containing CR characters.
<a name="NOTE0_4"></a>


### Input performed via a check box
<a name="input_performed_via_check_box_ELTPARAGRAPHE000302"></a>

The input is performed in a check box if the variable is a boolean. The caption of the check box is defined by the ***Input.OptionalCaption*** variable.
<a name="NOTE0_5"></a>


### Message database
<a name="message_database_ELTPARAGRAPHE000309"></a>

All the new messages are automatically added to the message database. 

By default, the database of messages is found in the "Personal\\Messages" directory of WINDEV, WEBDEV and WINDEV Mobile. This directory can be modified in the options of WINDEV/WEBDEV/WINDEV Mobile: 

- On the "Home" tab, in the "Environment" group, expand "Options" and select "General options of WINDEV/WEBDEV/WINDEV Mobile". 

- Display the "Directory" tab.




For more details, see [question edit wizard](../WDLang1/3021020.md).
<a name="NOTE0_6"></a>
![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) 

### Limitations
<a name="limitations_ELTPARAGRAPHE000340"></a>

- ![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) The skin template of the application is not applied to the input window. 

- The variable to input cannot be initialized with CR characters. The edit control will be a single-line edit control.
	

- ![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) If the variable is a Boolean, the input will be performed in an edit control (and not in a check box like in WINDEV).

- ![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) The input mask is not supported.



<a name="NOTE0_7"></a>


### Example of full input
<a name="example_full_input_ELTPARAGRAPHE000379"></a>

1. Typing the question in the editor: <br>![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=3&name=Saisie_Exemple.gif&type=thumb)


2. Code automatically generated (only the "Recipient" variable was entered in the code editor): 



```wl
Recipient is string
//1: Create a follow-up
//0: Don't create a follow-up
//1: OK 
SWITCH Input("Specify a recipient.", Recipient)
	// Validate the recipient
	CASE 1
		
	// Display the list
	CASE 2
		
	// Cancel
	CASE 3
		
END
```

<a name="NOTE0_8"></a>

<a name="XComponent"></a>

## Business / UI classification:
UI Code
## Component:
wd280obj.dll
