
## Lección 7.8. FTP
<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Esta lección abarcará los siguientes temas
<a name="esta_leccion_abarcara_los_siguientes_temas_ELTTEXTE000173"></a>


- Presentación de las funciones FTP de WINDEV.





|   |   |
| --- | --- |
| <br>![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=dur%E9e.png)<br> | <br>Tiempo estimado: 20 min |

| [Lección anterior](../TutoWD/1410087554.md) | [Tabla de contenido](../TutoWD/1410087560.md) | [Siguiente lección](../TutoWD/1410087557.md) |
| --- | --- | --- |





<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Presentación
<a name="presentacion_ELTTEXTE000220"></a>
FTP (File Transfer Protocol) es un protocolo estándar para transferir archivos de una máquina a otra. Una de las máquinas debe ser un servidor FTP.

Varias funciones WLanguage permiten transferir archivos usando este protocolo con un servidor. Estas funciones comienzan con "FTP".

WINDEV solo incluye funciones "cliente" FTP. Se requiere un servidor FTP estándar. 

El ejemplo unitario "The FTP functions" muestra cómo manipular archivos en un servidor FTP.



- Abra el ejemplo unitario "The FTP functions". Este ejemplo presenta las principales funcionalidades que se pueden utilizar en un servidor FTP.<br>![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=P7_Exemple%20fonctions%20FTP%20-%20HC%20N%B0001.jpg)





<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Conexión a un servidor FTP
<a name="conexion_servidor_ftp_ELTTEXTE000244"></a>
La función [FTPConnect](../WDLang3/3040016.md) permite conectarse a un servidor FTP. Se necesita una cuenta FTP (nombre de usuario y contraseña) para acceder a un servidor FTP.



- Abra los eventos WLanguage del botón "Connection" de la ventana "WIN_FTP":

	1. Seleccione el control Botón "Connection".

	2. Pulse F2 para abrir el código WLanguage asociado:
			
		```wl
		// 1 - FTP address: corresponds to the FTP site 
		// to which the application must connect. Ex: 192.108.10.2
		sFTPAddress is string = EDT_FTP_Server 
		// 2 - Username: if this name is not specified, 
		// an "anonymous" connection will be used
		sUserName is string = EDT_User
		sUserPWD is string = EDT_Password
		// Connection to FTP server
		gnConnectionID = FTPConnect(sFTPAddress, sUserName, sUserPWD)		
		IF ErrorOccurred THEN
			// An error occurred during the connection
			Error("The connection to the FTP server: " + sFTPAddress + " failed", ErrorInfo())
			RETURN	
		END
		// The connection is established
		Info("Connection to the FTP server established", "Connection identifier: " + gnConnectionID)
		```








Observación: También es posible especificar el número de puerto de la conexión al servidor FTP ("21" de forma predeterminada), así como el modo de conexión ("True" para una conexión "pasiva", "False" para una conexión "activa"). 
Para obtener más información, consulte [Funciones FTP estándar](../WDLang3/3040004.md).

<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Enviar un archivo
<a name="enviar_archivo_ELTTEXTE000268"></a>
Para enviar un archivo a un servidor FTP, utilice la función [FTPSend](../WDLang3/3040014.md).

Veamos un ejemplo de código que se puede utilizar:

```wl
// When connecting to the server with FTPConnect, we have
// retrieved the connection number in the gnConnectionID variable
// Transfer the "C:\MyDocuments\File.DOC" file to
// the "Temp" directory found on the server.
bResult is boolean = FTPSend(gnConnectionID, "C:\MyDocuments\File.DOC", "/Temp")
```
|   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=avertissement.png) | ¡Atención! | Preste mucha atención a las mayúsculas y minúsculas de los nombres de los directorios en el servidor. Algunos servidores FTP son máquinas UNIX "case sensitive", es decir, que distinguen las mayúsculas y minúsculas de los nombres de los archivos y directorios.<br><br>Por ejemplo, hay un directorio llamado "MiDirectorio" en el servidor FTP. Si intenta acceder a "mydirectory", el servidor FTP le devolverá un error como "Ruta no encontrada" porque las mayúsculas y minúsculas no son correctas. |





<a name="NOTE5"></a>
<a name="NOTE5_1"></a>


## Lista de archivos en un servidor FTP
<a name="lista_archivos_servidor_ftp_ELTTEXTE000310"></a>
La función [FTPListFile](../WDLang3/3040018.md) hace una lista de los archivos que se encuentran en un servidor FTP. Esta función utiliza un procedimiento de tipo "callback". El procedimiento se ejecuta por cada archivo o directorio encontrado.



- Abra los eventos WLanguage del botón "List in a trace" de la ventana "WIN_FTP":

	1. Seleccione el control Botón "List in a trace".

	2. Pulse F2 para abrir el código WLanguage asociado:
			
		```wl
		FTPListFile(gnConnectionID,"*.*", CallBackFTPListFile, ftpFile+ftpDirectory)
		
		// Check the function execution
		IF ErrorOccurred THEN
			Error("Error while browsing the files found on the FTP server", ErrorInfo())
			RETURN
		END
		```


	3. En el ejemplo, **CallBackFTPListFile** es un procedimiento interno que muestra los archivos encontrados en una ventana de salida.
			
		```wl
		INTERNAL PROCEDURE CallBackFTPListFile(sFileName, nFileSize <useful>, ...
			sAttribute, sModifDate <useful>, sModifTime <useful>)
		
		// Is it a file or a directory
		IF sAttribute = "D" THEN
			// Directory or subdirectory
			Trace("Directory: " + sFileName)
		ELSE
			// File
			Trace("File: " + sFileName)
		END
		
		// Continue to loop through the files
		RETURN True
		```








<a name="NOTE6"></a>
<a name="NOTE6_1"></a>


## Transferir un archivo
<a name="transferir_archivo_ELTTEXTE000334"></a>
Para transferir un archivo de un servidor FTP a su equipo, llame a la función [FTPGet](../WDLang3/3040002.md).

Veamos un ejemplo de código que se puede utilizar:

```wl
// When connecting to the server with FTPConnect, we have
// retrieved the connection number in the gnConnectionID variable
// Download the "/Document/File.DOC" file found 
// on the FTP server to the "D:\Temp" directory on 
// the current computer
bRes is boolean = FTPGet(gnConnectionID, "C:\MyDocuments\File.DOC", "/Temp")
```


<a name="NOTE7"></a>
<a name="NOTE7_1"></a>


## Desconectarse de un servidor FTP
<a name="desconectarse_servidor_ftp_ELTTEXTE000358"></a>
Para desconectarse de un servidor FTP, utilice la función [FTPDisconnect](../WDLang3/3040011.md).



- Abra los eventos WLanguage del botón "Disconnection" de la ventana "WIN_FTP":

	1. Seleccione el control Botón "Disconnection".

	2. Pulse F2 para abrir el código WLanguage asociado:
			
		```wl
		IF YesNo(No, "Do you want to close the connection to the FTP server?") = Yes THEN
			// Disconnect from the FTP server
			FTPDisconnect(gnConnectionID)
			IF ErrorOccurred THEN
				// Error while closing the connection
				Error("Unable to close the FTP connection", ErrorInfo())
				RETURN
			END
			
			// The connection is ended
			Info("The connection to the FTP server is over")
		END
		```








Existen otras funciones FTP pero no las presentaremos en detalle en este tutorial. Estas permiten principalmente:

- crear, eliminar y cambiar directorios en el servidor FTP,

- crear, eliminar y cambiar archivos en el servidor FTP,

- obtener información sobre un directorio y/o un archivo,

- ...




Para obtener más información, consulte [Funciones FTP estándar](../WDLang3/3040004.md).

| [Lección anterior](../TutoWD/1410087554.md) | [Tabla de contenido](../TutoWD/1410087560.md) | [Siguiente lección](../TutoWD/1410087557.md) |
| --- | --- | --- |




