
## Lección 2.2. Variables


<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Esta lección abarcará los siguientes temas
<a name="esta_leccion_abarcara_los_siguientes_temas_ELTTEXTE000898"></a>


- ¿Qué es una variable?

- Tipos de variables.

- Alcance de variables.

- El tipo String en detalle.





|   |   |
| --- | --- |
| <br>![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=dur%E9e.png)<br> | <br>Tiempo estimado: 1 h |

| [Lección anterior](../TutoWD/1410087578.md) | [Tabla de contenido](../TutoWD/1410087560.md) | [Siguiente lección](../TutoWD/1410087587.md) |
| --- | --- | --- |





<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Ejemplo práctico
<a name="ejemplo_practico_ELTTEXTE000945"></a>
Para seguir las etapas de esta lección, utilizaremos el proyecto "WLanguage" que creamos en la lección anterior.




- Si es necesario, abra el proyecto "WLanguage" de la lección anterior (véase [Un proyecto para descubrir WLanguage](../TutoWD/1410087578.md) en la lección 2.1).






- Vamos a escribir el código directamente en el código de inicialización del proyecto. Como este es el primer código que se ejecuta al probar el proyecto, podemos hacer pruebas de forma muy sencilla, sin ningún tipo de interfaz. Para ver el resultado de nuestras operaciones, utilizaremos la función [Trace](../WDLang1/3013050.md), que nos permite escribir la información deseada en una ventana de salida así como en el panel de depuración (vamos a utilizar la segunda opción). 




- Para ver los eventos WLanguage del proyecto: 

	1. Haga clic derecho en la "P" que se encuentra junto a las pestañas de elementos abiertos. El menú contextual se abre. 

	2. Seleccione "Código del elemento". 

	3. El editor de código muestra los diferentes eventos asociados al proyecto. 






<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## ¿Qué es una variable?
<a name="¿que_una_variable_ELTTEXTE000963"></a>
En lenguaje de programación, las variables permiten almacenar datos. Estas son secciones de memoria que contienen cadenas, números, etc.

Las variables permiten realizar cálculos, comparaciones o simplemente almacenar información para utilizarla más adelante.<br>![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=Declaration27.gif)
Una variable está representada por: 

- un nombre: Nombre dado a la variable para poder manipularla en el código. 

- un tipo: Naturaleza del dato almacenado en la variable (ver [Tipos de variables](../TutoWD/1410087514.md)).

- un valor: Información almacenada en la variable.

- un alcance: Límite del uso de la variable en el programa (ver [Alcance de variables](../TutoWD/1410087514.md)). El alcance se define dependiendo de la ubicación en la que se declara la variable. 


|   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=note.png) | Nota | En las primeras lecciones, vimos que podemos utilizar un estilo de código. <br>Este estilo de código se aplica **automáticamente** a los elementos del proyecto: controles, ventanas, páginas, reportes, etc.. <br><br>Al crear el proyecto, en la etapa "Estilo de código", puede seleccionar la opción "Agregar prefijos automáticamente a las nuevas variables". Si selecciona esta opción, se añadirá un prefijo automáticamente para identificar el tipo de variable. <br>Así, la siguiente declaración: <br><br><pre><code>Price is currency</code></pre><br> pasará a ser: <br><pre><code>cyPrice is currency</code></pre><br>donde "cy" es el prefijo de las variables de tipo "Currency".<br><br>Esta opción puede cambiarse en la pestaña "Opciones" de la ventana de descripción del proyecto, opción "Agregar prefijos automáticamente". |




<a name="NOTE3_2"></a>


### Declarar una variable
<a name="declarar_una_variable_ELTPARAGRAPHE000143"></a>

Antes de utilizar una variable, es necesario declararla (es decir, crearla).

- Ejemplo de declaración simple:
	
	```wl
	Price is currency
	```


	- **Price** representa el nombre de la variable. 

	- **is** permite declarar la variable. WLanguage utiliza un lenguaje común y legible. 

	- **currency** corresponde al tipo de variable. 




- Ejemplo de declaración múltiple:
	
	```wl
	LastName, FirstName are strings
	```


	- **LastName, FirstName** representan los nombres de las variables.

	- **are** permite declarar un conjunto de variables.

	- **strings** representa el tipo de variable. 





|   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=note.png) | Nota | Dependiendo de dónde se declare la variable, ésta puede ser global al elemento (veremos este concepto más adelante en el párrafo "Alcance de variables" de esta lección). <br><br>Si activa la opción para añadir prefijos a las variables automáticamente, se agregará la letra 'g' (de global) antes del nombre de la variable. Por ejemplo, la declaración de la variable **LastName** pasa a ser la siguiente: <br><br><pre><code>gsLastName is string</code></pre><br>No utilizaremos prefijos automáticos en el resto de este tutorial. <br><br>Veremos los diferentes tipos de variables (locales y globales) en los siguientes párrafos. |







- Vamos a escribir estas declaraciones en el editor de código. En el evento "Inicialización" del proyecto "WLanguage", escriba el siguiente código: 
	
	```wl
	Price is currency
	LastName, FirstName are strings
	```

	Aquí se puede observar la coloración específica de los diferentes elementos de la declaración: se utiliza un color específico para el nombre de la variable, y otro para su tipo. Estos colores permiten identificar rápidamente los diferentes elementos del código. 



<a name="NOTE3_3"></a>


### Asignación y uso
<a name="asignacion_uso_ELTPARAGRAPHE000188"></a>

Al declarar una variable, puede asignarle o darle un valor. El operador **=** permite asignar un valor. 



- Vamos a hacer una prueba con este operador. En el evento "Inicialización" del proyecto "WLanguage", agregue el siguiente código: 
	
	```wl
	// Assign a value to a currency variable
	Price = 1256.67
	// Display the contents of the variable
	Trace(Price)
	// Assign a value to a string variable
	LastName = "Doe"
	// Display the contents of the variable
	Trace(LastName)
	```

	En este código, se asigna un valor a las variables **Price** y **LastName**. Para leer y manipular el contenido de la variable, basta con utilizar su nombre. En este ejemplo, el contenido de las variables se muestra en el panel "Traza del depurador" gracias a la función [Trace](../WDLang1/3013050.md).






- Probemos nuestro código: 

	1. Pruebe el proyecto haciendo clic en ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=ICO_GO_Projet_WD_GAF.jpg) en los botones de acceso rápido.

	2. El proyecto se ejecuta y el editor de código reaparece. 

	3. Si es necesario, abra el panel "Traza del depurador" para ver el resultado: en la pestaña "Inicio", en el grupo "Entorno", despliegue "Paneles", seleccione "Paneles" y, a continuación, haga clic en "Traza del depurador". 

	4. El panel "Traza del depurador" tiene el contenido de las variables:
			
		```txt
		1256.67
		Doe
		```








<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Tipos de variables
<a name="tipos_variables_ELTTEXTE001035"></a>
El tipo de variable indica el tipo de información que se almacenará en la variable. Los tipos más comunes son:

- boolean (True o False),

- string ("Doe"),

- integer (1234),

- currency (12,32),

- real (7,766666),

- etc.


|   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=avertissement.png) | Importante | Utilice el tipo que corresponde a la información que debe almacenarse. Esto permite optimizar la memoria y evitará errores de cálculo o de procesos al utilizar variables en funciones WLanguage. |




La mayoría de estos tipos de variables se utilizarán en este tutorial. Además, veremos los tipos Array, Date, Time, Structure, etc. 

También existen variables avanzadas. Los tipos avanzados agrupan todas las características del elemento manipulado en una sola variable. Estos tipos avanzados se pueden utilizar para manipular documentos XML, correos electrónicos, archivos XLS y más. Utilizaremos este tipo de variable más adelante en este tutorial.

Para obtener más información, consulte la ayuda sobre el tipo deseado (ver [Tipos de variables](../Motscles/1514013.md)).

<a name="NOTE5"></a>
<a name="NOTE5_1"></a>


## Alcance de variables
<a name="alcance_variables_ELTTEXTE001077"></a>
Las variables se pueden declarar en cualquier parte del código. Sin embargo, dependiendo de dónde se declaren, las variables no puede utilizarse para realizar procesos o cálculos. Esto se denomina alcance de las variables.

Hay dos tipos de alcance disponibles:

- Global.

- Local.



<a name="NOTE5_2"></a>


### Alcance global
<a name="alcance_global_ELTPARAGRAPHE000293"></a>

Global significa que la variable es visible desde cualquier parte del código. La variable es visible fuera del evento (o proceso) donde fue declarada. Existen varios niveles:

- Proyecto y conjunto de procedimientos,

- Ventana, ventana móvil, página, reporte, 

- Control. 




Una variable declarada **a nivel del proyecto** tiene la mayor visibilidad en el programa. En este caso, la variable se declara en el evento de inicialización del proyecto. La variable es visible en todo el proyecto, eventos y procesos del programa. Sin embargo, no se recomienda declarar demasiadas variables con este alcance: la memoria que ocupa la variable siempre se reserva, incluso si la variable no se utiliza. No se recomienda utilizar un gran número de variables globales en la arquitectura de un programa. Para pasar variables a un evento o proceso, se recomienda utilizar parámetros (para obtener más información, consulte ["Parámetros y resultado de un procedimiento"](../TutoWD/1410087517.md)).

Una variable declarada **a nivel de un conjunto de procedimientos** tiene la misma visibilidad que una variable declarada a nivel del proyecto. La ventaja de declarar una variable a nivel de un Conjunto es la de agrupar (o clasificar) las variables por temas para que el evento de inicialización del proyecto sea más fácil de leer.

Una variable declarada **a nivel de una ventana, ventana móvil, página o reporte** limita el alcance de la variable a los eventos o procesos de ese elemento y sus controles. Esto permite encapsular y limitar su uso.

Una variable global declarada **a nivel de un control** limita el alcance de la variable a los eventos de ese control. Esto permite agrupar las variables por control para que el evento de inicialización de la ventana sea más fácil de leer.|   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=note.png) | Nota | De forma predeterminada, las variables globales aparecen en azul en el editor de código. Para identificar las variables globales y su alcance, se recomienda seguir un estándar. |




<a name="NOTE5_3"></a>


### Alcance local
<a name="alcance_local_ELTPARAGRAPHE000320"></a>

Local significa que la variable tiene una visibilidad limitada en el código. La variable solo es visible en el proceso en el que se declaró. Esto permite restringir el uso de la variable al proceso o evento.
<a name="NOTE5_4"></a>


### Diagrama del alcance
<a name="diagrama_del_alcance_ELTPARAGRAPHE000329"></a>
<br>![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=Projet%20Variables%20et%20Collections%20-%204-.gif)
De forma predeterminada, las variables son globales cuando se declaran: 

- en el evento "Inicialización" del proyecto (o en el evento "Declaración" del conjunto de procedimientos). La variable es global al proyecto.

- en el evento "Declaraciones globales" de la ventana, página o reporte. La variable es global al elemento (ventana, página o reporte) en el que se declaró.




En todos los demás casos, una variable es local al proceso o evento en el que se declara.

**Atención**: Para declarar una variable global a un control, utilice la palabra clave "GLOBAL" al declararla en el evento "Inicialización" del control. 

<a name="NOTE6"></a>
<a name="NOTE6_1"></a>


## Operaciones simples con variables
<a name="operaciones_simples_con_variables_ELTTEXTE001137"></a>
Se pueden utilizar varios operadores matemáticos para realizar cálculos con las variables:

- + para realizar una suma.

- - para realizar una resta.

- \* para realizar una multiplicación.

- / para realizar una división.




También es posible utilizar otros operadores para realizar cálculos:

- ++ para incrementar 1 unidad (suma 1 a la variable).

- - - para disminuir 1 unidad (resta 1 a la variable).

- += para asignar valores a una variable agregando un valor.

- - = para asignar valores a una variable restando un valor.






- Para tener una visión general de las operaciones que se pueden realizar, haremos una pequeña prueba: 

	1. Elimine el código en el evento "Inicialización" del proyecto (presione Ctrl + A para seleccionar todo y "Supr" para eliminar). 

	2. Escriba el siguiente código: 
			
		```wl
		// Declaration of variables
		Counter is int
		V1 is int
		Res is numeric
		
		// Assignment
		Counter = 10
		V1 = 3
		
		// Use of operators
		Counter = Counter + 3   // Counter is set to 13
		Trace(Counter)
		Counter ++ // Counter is set to 14
		Trace(Counter)
		Counter -= 8 // Counter is set to 6
		Trace(Counter)
		Counter = Counter * V1 // Counter is set to 18
		Trace(Counter)
		Res = Counter / 5 // Res is set to 3.6
		Trace(Res)
		```
Este código permite realizar diferentes operaciones y muestra el resultado en la ventana de salida. 









- Probemos nuestro código: 

	1. Pruebe el proyecto haciendo clic en ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=ICO_GO_Projet_WD_GAF.jpg) en los botones de acceso rápido. 

	2. Si es necesario, abra el panel "Traza del depurador" para ver el resultado: en la pestaña "Inicio", en el grupo "Entorno", despliegue "Paneles", seleccione "Paneles" y, a continuación, haga clic en "Traza del depurador". 

	3. El panel "Traza del depurador" contiene el valor de la variable Counter para cada operación:
			
		```txt
		13
		14
		6
		18
		3.6
		```








Los operadores de comparación también están disponibles:

- &lt; menor que

- &gt; mayor que

- &lt;= menor que o igual a

- &gt;= mayor que o igual a

- &lt;&gt; diferente de

- = igual a 




Existen otros tipos de operadores. Para obtener más información sobre los operadores, consulte [Lista de operadores](../Motscles/1512010.md).
<a name="NOTE6_2"></a>


### Consejos
<a name="consejos_ELTPARAGRAPHE000425"></a>



- Se recomienda utilizar nombres largos para las variables (y evitar nombres como i, j, k, etc.). Cuando vuelva a leer el programa, podrá recordar fácilmente el propósito de la variable.

- WLanguage acepta todos los caracteres Unicode (incluidos los acentos) para los nombres de las variables. Esto permite una mejor legibilidad. Atención: algunos caracteres no están permitidos: espacios, signos de igual, puntos, comas, etc.

- Es muy importante dar el tipo adecuado a la variable de acuerdo a su uso. Por ejemplo, para almacenar varios dígitos, puede que sea necesario:

	- utilizar una variable numérica si se utilizará para hacer cálculos.

	- utilizar una variable de cadena si debe utilizarse para almacenar dígitos sin realizar cálculos (para almacenar el número de seguridad social, por ejemplo).







<a name="NOTE7"></a>
<a name="NOTE7_1"></a>


## Detalles de tipo variable: Variables de tipo String
<a name="detalles_tipo_variable_variables_tipo_string_ELTTEXTE001167"></a>
Las variables de tipo String son uno de los tipos de variables más utilizados.

Veamos algunas de las funcionalidades disponibles para este tipo de variable.
<a name="NOTE7_2"></a>


### Tipo String
<a name="tipo_string_ELTPARAGRAPHE000450"></a>

El tipo String permite almacenar y manipular caracteres y cadenas de caracteres.

Ya hemos visto cómo inicializar una variable de tipo String:

```wl
LastName is string
// Assign a value to a string variable
LastName = "Doe"
```
|   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=note.png) | Nota | En WLanguage, el carácter las comillas dobles (") se utilizan para delimitar una cadena de caracteres. En el ejemplo anterior, se utilizaron comillas dobles para asignar el valor Doe a la variable LastName. |







No hay necesidad de declarar la longitud de la cadena: la longitud se adapta automáticamente cuando se utiliza la variable. |   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=astuce.png) | Consejo | Para inicializar una variable de tipo String con un texto en varias líneas, utilice la siguiente sintaxis: <br><br><pre><code><Variable name> = "<br><Text of line 1><br><Text of line 2><br>"</code></pre><br>Por ejemplo:<br><br><pre><code>MyString is string<br>MyString = "<br>Example of<br>multiline string<br>"</code></pre><br> |



También puede asignar el contenido de un control que manipula cadenas a una variable de tipo String. El siguiente código permite asignar el contenido de un control Campo de entrada a una variable de tipo String: 

```wl
LastName is string
// Assign a value to a string variable 
// the content of EDT_LastName edit control
LastName = EDT_LastName
```

<a name="NOTE7_4"></a>


### Construir una cadena
<a name="construir_una_cadena_ELTPARAGRAPHE000481"></a>

Como hemos visto anteriormente, es muy fácil declarar y asignar un valor a una cadena. Por ejemplo: 

```wl
MyBuiltString is string
ProductName is string = "WINDEV"
```


Sin embargo, en muchos casos una cadena puede estar compuesta por elementos que pueden variar, como por ejemplo, el nombre de un producto. En este caso, existen varios métodos para construir cadenas. Vamos a probar estos métodos y ver sus ventajas e inconvenientes. 



- Volvamos a nuestro ejemplo de "WLanguage": 

	1. Elimine el código en el evento "Inicialización" del proyecto (presione Ctrl + A para seleccionar todo y "Supr" para eliminar). 

	2. En el evento "Inicialización" del proyecto "WLanguage", escriba el siguiente código:  
			
		```wl
		MyBuiltString is string
		ProductName is string = "WINDEV"
		
		// Concatenation
		MyBuiltString = ProductName + " is my go-to development tool" + "!"
		Trace("Concatenation: " + MyBuiltString)
		
		// StringBuild
		MyBuiltString = StringBuild("%1 is my go-to development tool!", ProductName)
		Trace("StringBuild: " + MyBuiltString)
		
		// Direct input of the variable ([% %] syntax)
		MyBuiltString = "[%ProductName%] is my go-to development tool!"
		Trace("Direct input: " + MyBuiltString)
		```

			En este código, la variable MyBuiltString se construye utilizando 3 métodos diferentes: 

		- **Método 1: concatenación simple usando el operador '+'**. Las cadenas se agregan una después de la otra. Este es el método más común. Sin embargo, tiene serias limitaciones si el mensaje se va a mostrar al usuario en una aplicación multilingüe. Dependiendo de la sintaxis del idioma utilizado, podría ser necesario ubicar la variable en una posición diferente, lo que no sería posible en este caso. 

		- **Método 2: Uso de la función [StringBuild](../WDLang1/3024006.md)**. Esta función se utiliza para escribir un mensaje completo. La parte variable se identifica con '%1', '%2', etc. El valor de estos identificadores corresponde a los parámetros que siguen a la función. Este método es mucho más simple para una traducción, pero sigue siendo difícil de leer (el contenido de los %x son parámetros de la función). 

		- **Método 3: Uso directo de la variable**. Este método combina todas las ventajas: simplicidad para la traducción y la legibilidad. Simplemente escriba el nombre de la variable deseada entre las etiquetas [% y %].  


 Para cada método, la función [Trace](../WDLang1/3013050.md) muestra el resultado en el panel "Traza del depurador". 







- Probemos nuestro código: 

	1. Pruebe el proyecto haciendo clic en ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=ICO_GO_Projet_WD_GAF.jpg) en los botones de acceso rápido. 
			 El proyecto se ejecuta y el editor de código reaparece. 

	2. Si es necesario, abra el panel "Traza del depurador" para ver el resultado: en la pestaña "Inicio", en el grupo "Entorno", despliegue "Paneles", seleccione "Paneles" y, a continuación, haga clic en "Traza del depurador". 

	3. El panel "Traza del depurador" contiene los anteriores ejemplos de concatenación:
			
		```txt
		Concatenation: WINDEV is my go-to development tool!
		StringBuild: WINDEV is my go-to development tool!
		Direct input: WINDEV is my go-to development tool!
		```






|   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=note.png) | Nota | El método utilizado para concatenar cadenas puede mejorar la legibilidad y facilitar la traducción, pero también puede repercutir en el rendimiento. Por lo tanto, el primer método tarda aproximadamente el doble que cualquiera de los otros dos métodos. |




<a name="NOTE7_5"></a>


### Operaciones con cadenas
<a name="operaciones_con_cadenas_ELTPARAGRAPHE000587"></a>

WLanguage ofrece un gran número de operadores y funciones para extraer y manipular cadenas. 



- Para hacernos una idea de la potencia y sencillez de WLanguage para manipular cadenas, haremos una prueba rápida en la que vamos a recorrer una cadena y extraer subcadenas. 

	1. Elimine el código en el evento "Inicialización" del proyecto (presione Ctrl + A para seleccionar todo y "Supr" para eliminar). 

	2. Escriba el siguiente código: 
			
		```wl
		ProductList is string = "
		WINDEV
		WEBDEV
		WINDEV Mobile
		"
		
		FOR EACH STRING AProduct OF ProductList SEPARATED BY CR
			Trace(AProduct)
		END
		Product is string = ProductList.ExtractString(2, CR)
		Trace(Product)
		```


	3. Este código permite recorrer una cadena multilínea y extraer un elemento de ella. Analicemos este código: 

		- ProductList es una cadena multilínea. 

		- La instrucción "FOR EACH STRING" permite recorrer los diferentes elementos de la cadena. Estos elementos están separados por un retorno de carro (CR).
						|   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=note.png) | Nota | En WLanguage, no tiene que preocuparse por las mayúsculas y minúsculas cuando escriba en el editor de código: las mayúsculas y minúsculas correspondientes se aplicarán automáticamente a medida que escriba. |





		- En nuestro caso, la función [&lt;Cadena&gt;.ExtractString](../WDLang1/1000024876.md) extrae el segundo elemento de la cadena ("WEBDEV"). 




4. Pruebe el proyecto haciendo clic en ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=ICO_GO_Projet_WD_GAF.jpg) en los botones de acceso rápido. 

5. El panel "Traza del depurador" contiene los siguientes elementos:
			
		```txt
		WINDEV
		WEBDEV
		WINDEV Mobile
		WEBDEV
		```





- También es posible extraer partes de una cadena utilizando los operadores [ y ]. Estos operadores permiten extraer la parte deseada del texto, especificando la posición del carácter inicial y/o final. 

	1. Elimine el código en el evento "Inicialización" del proyecto (presione Ctrl + A para seleccionar todo y "Supr" para eliminar). 

	2. Introduzca el siguiente código:
			
		```wl
		Text is string = "San Francisco"
		// Display the fifth character in the trace: "F"
		Trace(Text[5])
		// Display characters 5 to 10 in the trace: "Franci"
		Trace(Text[5 TO 10])
		// Display characters from the fifth position in the trace: "Francisco"
		Trace(Text[5 TO])	
		// Display first 10 characters in the trace: "San Franci"
		Trace(Text[TO 10])
		// Display 3 characters from the 10th character in the trace: "isc"
		Trace(Text[10 ON 3])
		```


	3. Pruebe el proyecto haciendo clic en ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=ICO_GO_Projet_WD_GAF.jpg) en los botones de acceso rápido. 

	4. El panel "Traza del depurador" contiene la siguiente información: 
			
		```txt
		F
		Franci
		Francisco
		San Franci
		isc
		```








- WLanguage también permite definir el formato de las cadenas con la función [&lt;Cadena&gt;.Format](../WDLang1/1000024877.md). Esto permite especificar que una cadena se muestre sin acentos, en mayúsculas o minúsculas, etc. Veamos un ejemplo rápido: 

	1. Elimine el código en el evento "Inicialización" del proyecto (presione Ctrl + A para seleccionar todo y "Supr" para eliminar). 

	2. Introduzca el siguiente código:
			
		```wl
		InitialText is string = "WINDEV is my go-to development tool!"
		
		// Remove accents from string and set to lowercase
		InitialText = InitialText.Format(ccIgnoreAccent + ccLowCase)
		Trace(InitialText)
		// Remove spaces from string and set to uppercase
		InitialText = InitialText.Format(ccUpCase + ccIgnoreInsideSpace)
		Trace(InitialText)
		```


	3. Pruebe el proyecto haciendo clic en ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=ICO_GO_Projet_WD_GAF.jpg) en los botones de acceso rápido. 

	4. El panel "Traza del depurador" contiene la siguiente información: 
			
		```txt
		windev is my go-to development tool!
		WINDEVISMYGO-TODEVELOPMENTTOOL!
		```








Para obtener más información sobre las cadenas, consulte [Manipular cadenas de caracteres](../WDLang1/3024020.md) y [Funciones de gestión de cadenas de caracteres](../WDLang1/3024015.md).
<a name="NOTE7_6"></a>


### Buscar una cadena
<a name="buscar_una_cadena_ELTPARAGRAPHE000718"></a>

La búsqueda es una de las operaciones más comunes que se realiza en las cadenas. En algún momento, todo el mundo ha tratado de saber si una palabra está en una cadena, cuál es su posición, etc. WLanguage facilita esta operación. 



- Vamos a buscar una cadena dentro de otra:

	1. Elimine el código en el evento "Inicialización" del proyecto (presione Ctrl + A para seleccionar todo y "Supr" para eliminar).

	2. Introduzca el siguiente código WLanguage:
			
		```wl
		SearchString is string = "WINDEV"
		
		OriginalText is string = "
		Development tools:
		- WINDEV
		- WEBDEV
		- WINDEV Mobile
		"
		
		Pos is int
		Pos = Position(OriginalText, SearchString)
		IF Pos = 0 THEN
			Trace("[%SearchString%] was not found in the text")
		ELSE
			Trace("[%SearchString%] was found at position [%Pos%]")
		END
		```

			Analicemos este código WLanguage:

		- Se declaran varias variables: 

			- una variable de tipo String que corresponde a la cadena a buscar. 

			- una variable de tipo String que corresponde al texto en el que se realizará la búsqueda. 

			- una variable de tipo Integer que corresponde a la posición de la cadena buscada.




- La función [Position](../WDLang1/3024027.md) permite buscar una cadena dentro de otra. En nuestro ejemplo, buscamos el contenido de la variable SearchString en el contenido de OriginalText.

- Si la función [Position](../WDLang1/3024027.md) devuelve 0, significa que no se encontró la cadena buscada. 

- La función [Trace](../WDLang1/3013050.md) muestra un mensaje indicando si se encontró la cadena, así como su posición. 

3. Pruebe el proyecto haciendo clic en ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=ICO_GO_Projet_WD_GAF.jpg) en los botones de acceso rápido. 

4. El panel "Traza del depurador" contiene la siguiente información: 
			
		```txt
		WINDEV was found at position 23
		```




<a name="NOTE7_7"></a>


### Comparar dos cadenas
<a name="comparar_dos_cadenas_ELTPARAGRAPHE000805"></a>

Las comparaciones son otra operación común que se realiza en las cadenas: por ejemplo, para saber si dos cadenas son iguales. 

- Vamos a comparar dos cadenas:

	1. Elimine el código en el evento "Inicialización" del proyecto (presione Ctrl + A para seleccionar todo y "Supr" para eliminar).

	2. Introduzca el siguiente código WLanguage:
			
		```wl
		// Strings to compare
		String1 is string = "DEVELOPMENT"
		String2 is string = "DEV"
		
		// The 2 strings are compared with the ~= (flexible equality) operator
		IF String1 ~= String2 THEN
			Trace("The 2 strings are equal")
		ELSE
			Trace("The 2 strings are different")
		END
		```

			Analicemos este código WLanguage:

		- Se declaran dos variables de tipo String. Estas contienen las cadenas a comparar.

		- La comparación se realiza con el operador ~= (igualdad flexible). Este operador permite realizar una comparación sin distinguir mayúsculas y minúsculas, espacios antes y después de la cadena, y acentos.

		- Finalmente, se muestra el resultado de la comparación. 




3. Pruebe el proyecto haciendo clic en ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=ICO_GO_Projet_WD_GAF.jpg) en los botones de acceso rápido. 

4. El panel "Traza del depurador" contiene la siguiente información: 
			
		```txt
		The 2 strings are different
		```


5. Vuelva al código y cambie la declaración de String2: 
			
		```wl
		String2 is string = "Development"
		```


6. Pruebe el proyecto haciendo clic en ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=ICO_GO_Projet_WD_GAF.jpg) en los botones de acceso rápido. 

7. El panel "Traza del depurador" contiene la siguiente información: 
			
		```txt
		The 2 strings are equal
		```
Aunque las cadenas no contengan las mismas mayúsculas y minúsculas, son iguales. 




Observación: Existen diferentes [operadores de comparación](../Motscles/1512006.md). 

| [Lección anterior](../TutoWD/1410087578.md) | [Tabla de contenido](../TutoWD/1410087560.md) | [Siguiente lección](../TutoWD/1410087587.md) |
| --- | --- | --- |




