
## Elementos de una clase: Enumeración y manejo a través de la programación
			

<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Presentación
<a name="presentacion_ELTTEXTE000210"></a>
Varios tipos de variables de WLanguage se utilizan para enumerar y manipular dinámicamente los elementos de una clase. Un desarrollador que esté familiarizado con la Programación Orientada a Objetos (POO) puede crear métodos personalizados que se adapten a los elementos que se encuentran en un sistema [class](../POO/6010006.md).

Tienes la habilidad de enumerar:

- la [métodos](../POO/6010006.md) (o procedimientos) de una clase.

- la [propiedades](../POO/6010006.md) de una clase.

- la [miembros](../POO/6010006.md) (o variables) de una clase.




Durante esta enumeración, usted tendrá la habilidad de recuperar: 

- el nombre del elemento.

- el tipo de elemento.




Por ejemplo:

- capacidad de recuperar el nombre de un método para llamarlo utilizando [ExecuteProcess](../WDLang1/3013031.md).

- capacidad de recuperar el nombre y el tipo de una variable para averiguar o asignar su valor mediante el uso de la [operadores indirectos](../Motscles/1512005.md) { }.










## ¿Cómo proceder?
<a name="¿como_proceder_ELTTEXTE000234"></a>
Los diferentes pasos para enumerar y manejar un objeto son los siguientes: 

1. Recuperación de la Definition del objeto a manipular a través de [GetDefinition](../WDLang1/1000019333.md). 
	Ejemplo: 
	
	```wl
	O is cWord    // Instantiation of the CWord class
	Def is Definition // Declaration of a Definition variable
	
	Def = GetDefinition(O)  // Retrieve the definition of the O object in the Def variable
	
	Trace(Def..name)  // Display the name of the object 
	Trace(Def.DefinitionFamily)
	Trace(Def.Type) // Display the type of the object
	```


2. Recuperación de la lista de métodos. Se utiliza un bucle "FOR EACH" para leer la propiedad **Procedimiento** del Definition Variable. Esta Property corresponde a una array que contiene todos los procedimientos encontrados en el objeto descrito. 
	Ejemplo: 
	
	```wl
	// Declaration of the variable that will contain the description of a procedure
	Proc is Procedure Description  
	
	// List of procedures (or methods) of the Def object
	FOR EACH Proc of Def.Procedure
		Trace (Proc.Name)  // Displays the name of the procedure found
	END
	```

	Puede acceder a otras propiedades utilizando el nombre **Property** Property en el objeto. Para obtener más información, consulte [Procedure Description](../WDLang1/1000019317.md).

3. Recuperación de la lista de propiedades. Se utiliza un bucle FOR EACH para leer el **Property** Property. Esta Property corresponde a una array que contiene todas las propiedades encontradas en el objeto descrito.
	Ejemplo: 
	
	```wl
	// Declaration of a variable that will contain the description of a property
	Prop is Property Description  
	
	// List of properties of the Def object
	FOR EACH Prop of Def.Property  
		Trace (Prop.Name)  // Displays the name of the property found
	END
	```

	Puede acceder a otras propiedades utilizando el nombre **Property** Property en el objeto. Para obtener más información, consulte [Property Description](../WDLang1/1000019324.md).
	También puede utilizar [PropertyExist](../WDLang1/1000020472.md) para determinar si existe un Property específico en una clase. 

4. Recuperación de la lista de variables. Un bucle FOR EACH lee la propiedad **Variable**. Esta Property corresponde a una array que contiene todas las variables encontradas en el objeto descrito.
	Ejemplo:
	
	```wl
	// Declaration of the variable that will contain the description of a variable
	Prop is Variable Description  
	
	// List of variables of the Def object
	FOR EACH Var of Def.Variable  
		// Displays the name and type of Variable found
		Trace (Var.Name, Var.Declaration.Type, Var.Definition.Name)
	END
	```

	Puede acceder a otras propiedades utilizando el nombre **Property** Property en el objeto. Para obtener más información, consulte [Variable Description](../WDLang1/1000019560.md).








## Operaciones realizadas mediante ExecuteProcess y mediante los operadores indirectos
<a name="operaciones_realizadas_mediante_executeprocess_mediante_los_operadores_indirectos_ELTTEXTE000258"></a>


### Utilización de ExecuteProcess
<a name="utilizacion_executeprocess_ELTPARAGRAPHE000111"></a>

[ExecuteProcess](../WDLang1/3013031.md) se utiliza para llamar al código de un event associated con un objeto.

Por ejemplo, el siguiente código provoca la llamada del código de clic del botón Validar:


```wl
ExecuteProcess(ValidateButton, trtClick)
```


Para ejecutar un método de clase, basta con utilizar una variable de tipo "Procedimiento description" con [ExecuteProcess](../WDLang1/3013031.md).

Por ejemplo:


```wl
c is BalloonTip
p is Procedure Description
Def is Definition

Def = GetDefinition(c)
// Retrieves the first procedure of the BalloonTip object
p = Def.Procedure[1]  

// Runs the procedure represented by p of the c object
ExecuteProcess(c, p)
```



### Utilizando los operadores indirectos { ... }
<a name="utilizando_los_operadores_indirectos_ELTPARAGRAPHE000131"></a>

Los operadores indirectos { } se utilizan para manipular el contenido de un Variable por direccionamiento indirecto: el nombre del objeto a tratar se encuentra en un Variable. 

La sintaxis es la siguiente: 


```txt
{Variable_containing_Object_name, VariableType}
```


donde VariableType es una constante del lenguaje que comienza con indXXX.

Ejemplo:



```wl
// Fills the "EDT_CITY" control with "Montpellier"
ControlName is string
ControlName = "EDT_CITY"
{ControlName, IndControl} = "Montpellier"
```


Para que manipular sea miembro de un método de clase, todo lo que tienes que hacer es pasar { }: 

- la instancia de la clase

- una variable description Variable o el nombre del miembro. 




Por ejemplo:



```wl
c is cWord
var is Variable Description
Def is Definition

Def = GetDefinition(c)
// Retrieves the 2nd variable of the class: version of Word
var = Def.Variable[2] 

Trace(var.Name) // Displays the variable name
Trace("Version of Word: ",{c, var})  // Displays the version of Word
```








## Ejemplo
<a name="ejemplo_ELTTEXTE000288"></a>
El siguiente ejemplo de código se utiliza para enumerar todos los procedimientos, propiedades y variables que se encuentran en una clase. En este ejemplo, el **cPalabra** proviene del ejemplo "WD Controlling Word" (suministrado con WINDEV).



```wl
o is cWord
def is Definition
def = GetDefinition(o)

Trace(def.Nom)
Trace(def.DefinitionFamily)
Trace(def.Type)
	
Proc is Procedure Description
Prop is Property Description
Var is Variable Description
	
Trace("--------------- Procedures --------------------")
FOR EACH Proc of def.Procedure
	Trace(Proc.Global, Proc.Inherited, Proc.Name, Proc.Virtual)		
END
	
Trace("--------------- Properties --------------------")
FOR EACH Prop OF def.Property
	Trace(Prop.Declaration, Prop.Definition, Prop.Write, ...
		Prop.Global, Prop.Inherited, Prop.Read, Prop.Name, Prop.Virtual)
END
	
Trace("--------------- Variables --------------------")
FOR EACH Var OF def.Variable
	Trace(Var.Name, Var.Definition.Type, Var.Definition.Name)
	SWITCH Var.Definition.Type
		CASE 1, 4, 8, 19
			// Display the value of the variable via indirection
			Trace({o,Var})  
		CASE 34 // array
			Trace("Dimension: ", {o,Var.Nom}..Count)
		CASE 36 // structure
			v is Variable Description
			Trace("Structure " + Var.Definition.Name)
			Trace("........................................................")
			FOR EACH v OF Var.Definition.Variable
				Trace(v.Name, v.Definition.Type, v.Definition.Name)
			END
			Trace("........................................................")
		OTHER CASE
				
	END
	Trace("======================================================================")
END
```



