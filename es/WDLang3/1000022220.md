
## AuthToken (Tipo de Variable)

*** ***
				



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
El tipo **AuthToken** contiene las características de un Token utilizado para acceder a un servicio Web. Este acceso Token fue solicitado de antemano: 

- mediante la función [AuthIdentify](../WDLang3/1000022219.md).  

- por una petición HTTP. En este caso, la solicitud devuelve el Token en formato JSON. 


Las características de este acceso Token pueden definirse y modificarse mediante diferentes propiedades de WLanguage. 

**Observación**: Para obtener más información sobre la declaración de este tipo de variable y el uso de propiedades WLanguage, consulte [Declarar una variable](../Motscles/1514032.md).


<a name="Example1"></a>
<a name="sample_code"></a>

## Ejemplo


```wl
// Example used to retrieve a token to perform a request on DropBox
OAuth2Params is OAuth2Parameters
OAuth2Params.ClientID = "01234567890123456789" 
OAuth2Params.ClientSecret = "98765432109876543210"
OAuth2Params.AuthURL = "https://www.dropbox.com/oauth2/authorize"
OAuth2Params.TokenURL = "https://api.dropboxapi.com/oauth2/token"
OAuth2Params.AdditionalParameters = "force_reapprove=false"
<COMPILE IF ConfigurationType<>Site>
	// If you are not using a website, you need to specify a localhost redirect URL
	OAuth2Params.RedirectionURL = "http://localhost:9874/"	
<END>

// Ask for authentication: opens the login window
MyToken is AuthToken = AuthIdentify(OAuth2Params)

// Request authenticated on a DropBox API
req is httpRequest
req.Method = httpPost
req.URL = "https://api.dropboxapi.com/2/files/list_folder"
req.AuthToken = MyToken // Authentication token
req.ContentType = "application/json"
vAPIParam is variant
vAPIParam.path = "/Homework/math"
vAPIParam.recursive = False
vAPIParam.include_media_info = False
vAPIParam.include_deleted = False
vAPIParam.include_has_explicit_shared_members = False
req.Content = VariantToJSON(vAPIParam)

HTTPresponse is httpResponse = HTTPSend(req)
let Data = JSONToVariant(HTTPresponse.Content)
// Use the incoming data...
```
<a name="Example2"></a>



```wl
// Retrieves the token (in JSON) via an HTTP request. The AuthIdentify function is not used
// Define the request
httpReq is httpRequest
httpReq.Method = httpPost
httpReq.URL = PAYPAL_TOKEN
httpReq.User = PAYPAL_APP_ID
httpReq.Password = PAYPAL_SECRET
httpReq.Content = "grant_type=client_credentials"
httpReq.ContentType = "application/x-www-form-urlencoded"

// Execute query
httpRep is httpResponse = HTTPSend(httpReq)

// Retrieve the token
If httpRep.StatusCode = 200 THEN
	// Declares the parameters, required to refresh the token
	oAuth2Param is OAuth2Parameters
	oAuth2Param.ClientID = PAYPAL_APP_ID
	oAuth2Param.ClientSecret = PAYPAL_SECRET
	oAuth2Param.URLAuth = PAYPAL_ACCESS_BASEURL
	oAuth2Param.Scope = PAYPAL_SCOPES
	oAuth2Param.URLToken = PAYPAL_TOKEN
	
	// Initializes the token with JSON
	MyToken is AuthToken(oAuth2Param, httpRep.Content)
	gMyToken <= MyToken
END
```




## Sintaxis
<a name="SYNTAX1"></a>

### Declarando un AuthToken Variable

`MyVariable is AuthToken`
---

En este caso, se utiliza [AuthIdentify](../WDLang3/1000022219.md) para recuperar los parámetros de Token. 






<a name="SYNTAX2"></a>

### Declarar y describir un AuthToken Variable (sin usar la función AuthIdentify)

`MyVariable is AuthToken(<OAuth2 parameter> , <Token>)`
---

**`<OAuth2 parameter>: (OAuthParámetros Variable)`**

Nombre de [OAuth2Parameters](../WDLang3/1000022212.md) Variable que contiene la información necesaria para autenticarse en un servicio que implementa el estándar OAuth 2.0.

**`<Token>: (Cadena de caracteres)`**

Cuerda en formato JSON o UTF8 que contiene el Token. Corresponde al Token devuelto por el servicio. 







<a name="NOTE0"></a>

## Observaciones
<a name="NOTE0_1"></a>


### Propiedades específicas de las variables AuthToken
<a name="propiedades_especificas_las_variables_authtoken_ELTPARAGRAPHE000073"></a>

Las siguientes propiedades pueden ser usadas para manipular a Token para acceder a un servicio web:

| Nombre de la propiedad | Tipo utilizado | Efecto |
| --- | --- | --- |
| ExpirationDate | FechaHora | Fecha y hora de caducidad de Token. <br><br> |
| refrescar | Cadena de caracteres | Valor devuelto por el servidor para determinar si la Token puede ser refrescada. <br><br>Si no se especifica esta Property, no se puede utilizar [AuthRefreshToken](../WDLang3/1000026168.md) para actualización el Token: tendrá que solicitar una nueva Token.. |
| ServerResponse | Buffer | Valor devuelto por el servidor durante la solicitud hecha por el acceso Token. <br><br>**Esta propiedad es de solo lectura**. |
| válido | booleano | Validez del acceso Token: <br><br>- <u><u><u><u>True</u></u></u></u> si el token de acceso es válido. <br><br>- <u><u><u><u>False</u></u></u></u> en caso contrario.<br><br><br>**Esta propiedad es de solo lectura**. |
| Valor | Cadena de caracteres | Acceso a Token. <br><br>Valor rellenado automáticamente al utilizar [AuthIdentify](../WDLang3/1000022219.md). <br><br><br><br>Este valor se puede utilizar para enviar solicitudes autenticadas al servicio web correspondiente. |


<a name="NOTE0_2"></a>


### Modo de funcionamiento de la autenticación OAuth 2.0
<a name="modo_funcionamiento_autenticacion_oauth_20_ELTPARAGRAPHE000292"></a>

Las etapas de la autenticación OAuth 2.0 que realiza la función [AuthIdentify](../WDLang3/1000022219.md) son las siguientes: 

- Ejecución de una primera solicitud HTTP para pedir una autorización (URL de autorización especificada en la variable de tipo [OAuth2Parameters](../WDLang3/1000022212.md)).

- Apertura de una ventana de identificación de usuarios según el protocolo OAuth 2.0. La interfaz de identificación corresponde al servicio al que se accede.

- Después de la identificación, el servidor devuelve un primer código de autorización que permite solicitar un token de acceso a los recursos. Este código se añade en el parámetro de la segunda URL (URL del token de acceso especificado en la variable de tipo [OAuth2Parameters](../WDLang3/1000022212.md)).

- Ejecución de la segunda solicitud HTTP para solicitar el token de acceso. El resultado es un buffer JSON que contiene, entre otras cosas, el token de acceso ("**access_token**") que se utilizará para las solicitudes autenticadas. La variable de tipo [AuthToken](../WDLang3/1000022220.md) contiene la información que se encuentra en el buffer JSON. Este token de acceso será utilizado por las llamadas a las API del servicio Web.




**Para utilizar las API del servicio web**, basta con utilizar la función [HTTPSend](../WDLang3/1000021183.md) con un [httpRequest](../WDLang3/1000021158.md) Variable definiendo la consulta a ejecutar. 
El [AuthToken](../WDLang3/1000022220.md) Variable se asignará al **AuthToken** Property del [httpRequest](../WDLang3/1000021158.md) Variable (ver ejemplo). 
En este caso, el servidor recibirá el encabezado HTTP "**Authorization**" con un valor en el siguiente formato: "Autorización: Bearer xxx_access_token_xxx". 

**Atención**: 

- Si el servidor no devuelve el token de acceso en formato de código JSON según el estándar OAuth2.0, se producirá un error y no se obtendrá el token. La respuesta del servidor puede recuperarse a través del **ServerResponse** Property del [AuthToken](../WDLang3/1000022220.md) Variable.

- Si el servidor no soporta el encabezado HTTP "**Authorization**" para transmitir el token de acceso, el desarrollador debe realizar la transmisión según el formato esperado por el servicio solicitado. 
	El siguiente ejemplo permite utilizar el servicio Web de Facebook. En este caso, el token de acceso debe especificarse en la URL de la solicitud.

	- ![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) Ejemplo de código para Facebook
			
		```wl
		// Example used to retrieve the name of the Facebook account
		MyToken is AuthToken
		MyTokenParam is OAuth2Parameters
		
		MyTokenParam.ClientID = "123456789012345"
		MyTokenParam.ClientSecret = "45g8jh5kll45579021qsg5444j"
		MyTokenParam.AuthURL = "https://www.facebook.com/dialog/oauth"
		MyTokenParam.TokenURL = "https://graph.facebook.com/v2.3/oauth/access_token"
		MyTokenParam.RedirectionURL = "http://localhost:9874/"
		MyTokenParam.Scope = "email"
		
		MyToken = AuthIdentify(MyTokenParam)
		IF MyToken <> Null THEN
			IF ErrorOccurred THEN
				Error(ErrorInfo())
			ELSE
				// Token specified on the request URL
				HTTPRequest("https://graph.facebook.com/me?access_token=" + MyToken.Value)
				vMyRes is Variant = JSONToVariant(HTTPGetResult(httpResult))
				// Retrieve the account name
				Trace(vMyRes.name)
			END
		END
		```







<a name="NOTE0_3"></a>


### Utilización de las variables AuthToken
<a name="utilizacion_las_variables_authtoken_ELTPARAGRAPHE000156"></a>

Las variables **AuthToken** se pueden utilizar en las funciones: 

- sintaxis estándar: 
	


|   |   |
| --- | --- |
| [AuthIdentify](../WDLang3/1000022219.md) | Realiza una autenticación utilizando el protocolo OAuth 2.0 en cualquier Webservice. |
| [OpenIDReadIdentity](../WDLang3/1000023573.md) | Recupera la identidad de un usuario contenido en una [AuthToken](../WDLang3/1000022220.md) Variable. |

- sintaxis con prefijo: 
	


|   |   |
| --- | --- |
| [&lt;AuthToken Variable&gt;.OpenIDReadIdentity](../WDLang3/1000023629.md) | Recupera la identidad de un usuario contenido en una [AuthToken](../WDLang3/1000022220.md) Variable. |





