
## OAuth2Parameters (Tipo de Variable)

*** ***
				



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
El tipo **OAuth2Parameters** permite definir la información necesaria para autenticarse en un Webservice implementado el estándar OAuth 2.0. Estas características pueden definirse y modificarse mediante diferentes propiedades de WLanguage. 

Este tipo de variable se debe pasar como parámetro a la función [AuthIdentify](../WDLang3/1000022219.md). En caso de éxito, esta función devuelve una [AuthToken](../WDLang3/1000022220.md) Variable que puede utilizarse para realizar consultas autenticadas en el servicio web.

**Observación**: Para obtener más información sobre la declaración de este tipo de variable y el uso de propiedades WLanguage, consulte [Declarar una variable](../Motscles/1514032.md). 


<a name="Example1"></a>
<a name="sample_code"></a>

## Ejemplo


```wl
// Example used to retrieve a token to perform a request on DropBox
OAuth2Params is OAuth2Parameters
OAuth2Params.ClientID = "01234567890123456789" 
OAuth2Params.ClientSecret = "98765432109876543210"
OAuth2Params.AuthURL = "https://www.dropbox.com/oauth2/authorize"
OAuth2Params.TokenURL = "https://api.dropboxapi.com/oauth2/token"
OAuth2Params.AdditionalParameters = "force_reapprove=false"
<COMPILE IF ConfigurationType<>Site>
	// If you are not using a website, you need to specify a localhost redirect URL
	OAuth2Params.RedirectionURL = "http://localhost:9874/"	
<END>

// Ask for authentication: opens the login window
MyToken is AuthToken = AuthIdentify(OAuth2Params)

// Request authenticated on a DropBox API
req is httpRequest
req.Method = httpPost
req.URL = "https://api.dropboxapi.com/2/files/list_folder"
req.AuthToken = MyToken // Authentication token
req.ContentType = "application/json"
vAPIParam is variant
vAPIParam.path = "/Homework/math"
vAPIParam.recursive = False
vAPIParam.include_media_info = False
vAPIParam.include_deleted = False
vAPIParam.include_has_explicit_shared_members = False
req.Content = VariantToJSON(vAPIParam)

HTTPresponse is httpResponse = HTTPSend(req)
let Data = JSONToVariant(HTTPresponse.Content)
// Use the incoming data...
```





<a name="NOTE0"></a>

## Observaciones
<a name="NOTE0_1"></a>


### Propiedades específicas de las variables OAuth2Parameters
<a name="propiedades_especificas_las_variables_oauth2parameters_ELTPARAGRAPHE000045"></a>

Las siguientes propiedades pueden utilizarse para manipular la información necesaria para realizar la autenticación:

| Nombre de la propiedad | Tipo utilizado | Efecto |
| --- | --- | --- |
| AdditionalParameters | Cadena de caracteres | Parámetros de la primera consulta de autenticación. Esta cadena debe estar formateada como los parámetros de la URL. |
| AuthURL | Cadena de caracteres | URL de autorización a utilizar (primera URL del estándar OAuth 2.0). |
| campo de aplicación | Cadena de caracteres | Permisos solicitados. Los posibles valores son específicos del servicio web utilizado. |
| ClientID | Cadena de caracteres | Identificador del cliente proporcionado por el servicio al registrar la aplicación. |
| ClientSecret | Cadena de caracteres | Código de acceso secreto de la aplicación. Este código viene dado por el servicio al guardar la aplicación. |
| GrantType | Constante | Tipo de subvención disponible. Los valores posibles son: <br><br>- *gtClientCredentials*: Autenticación sin ventana de inicio de sesión. La autorización de acceso se otorga a la aplicación (no al usuario). El Token proporcionado para acceder a los recursos de la aplicación es relacionado a la propia aplicación. <br><br>- *gtAuthorizationCode* (valor Default): La autorización de acceso se concede al usuario. aparece una ventana de inicio de sesión para que los usuarios introduzcan su nombre de usuario y contraseña. El acceso Token es relacionado para el usuario. <br><br><br> |
| RedirectionURL | Cadena de caracteres | URL de redirección a utilizar durante el mecanismo de autenticación. <br><br>![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) Para una aplicación Windows o Android, esta URL debe tener la forma "http://localhost:PortNumber". Este valor debe ser estrictamente idéntico al especificado al declarar la aplicación en el servicio web correspondiente.<br><br><br><br> |
| ResponseType | Cadena de caracteres o constante | Tipo de respuesta esperada. Los valores posibles son: <br><br>- *oauth2ResponseTypeCode* (o "Código"): La respuesta es de tipo "Código". <br><br>- *oauth2ResponseTypeToken* (o "Token"): La respuesta es del tipo "Token". <br><br><br><br><br>*oauth2ResponseTypeToken* es el valor de Default. <br><br>Observación: Para una autentificación "personal", el tipo de respuesta debe ser "Token". En el caso de una autenticación para un API o servicio (por ejemplo, el servidor de correo de Google), el tipo de respuesta debe ser "Código". <br><br> |
| TokenURL | Cadena de caracteres | URL para obtener el token de acceso a utilizar (segunda URL del estándar OAuth 2.0). |


<a name="NOTE0_2"></a>


### Modo de funcionamiento de la autenticación OAuth 2.0
<a name="modo_funcionamiento_autenticacion_oauth_20_ELTPARAGRAPHE000276"></a>

Las etapas de la autenticación OAuth 2.0 que realiza la función [AuthIdentify](../WDLang3/1000022219.md) son las siguientes: 

- Ejecución de una primera solicitud HTTP para pedir una autorización (URL de autorización especificada en la variable de tipo [OAuth2Parameters](../WDLang3/1000022212.md)).

- Apertura de una ventana de identificación de usuarios según el protocolo OAuth 2.0. La interfaz de identificación corresponde al servicio al que se accede.

- Después de la identificación, el servidor devuelve un primer código de autorización que permite solicitar un token de acceso a los recursos. Este código se añade en el parámetro de la segunda URL (URL del token de acceso especificado en la variable de tipo [OAuth2Parameters](../WDLang3/1000022212.md)).

- Ejecución de la segunda solicitud HTTP para solicitar el token de acceso. El resultado es un buffer JSON que contiene, entre otras cosas, el token de acceso ("**access_token**") que se utilizará para las solicitudes autenticadas. La variable de tipo [AuthToken](../WDLang3/1000022220.md) contiene la información que se encuentra en el buffer JSON. Este token de acceso será utilizado por las llamadas a las API del servicio Web.




**Para utilizar las API del servicio web**, basta con utilizar la función [HTTPSend](../WDLang3/1000021183.md) con un [httpRequest](../WDLang3/1000021158.md) Variable definiendo la consulta a ejecutar. 
El [AuthToken](../WDLang3/1000022220.md) Variable se asignará al **AuthToken** Property del [httpRequest](../WDLang3/1000021158.md) Variable (ver ejemplo). 
En este caso, el servidor recibirá el encabezado HTTP "**Authorization**" con un valor en el siguiente formato: "Autorización: Bearer xxx_access_token_xxx". 

**Atención**: 

- Si el servidor no devuelve el token de acceso en formato de código JSON según el estándar OAuth2.0, se producirá un error y no se obtendrá el token. La respuesta del servidor puede recuperarse a través del **ServerResponse** Property del [AuthToken](../WDLang3/1000022220.md) Variable.

- Si el servidor no soporta el encabezado HTTP "**Authorization**" para transmitir el token de acceso, el desarrollador debe realizar la transmisión según el formato esperado por el servicio solicitado. 
	El siguiente ejemplo permite utilizar el servicio Web de Facebook. En este caso, el token de acceso debe especificarse en la URL de la solicitud.

	- ![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) Ejemplo de código para Facebook
			
		```wl
		// Example used to retrieve the name of the Facebook account
		MyToken is AuthToken
		MyTokenParam is OAuth2Parameters
		
		MyTokenParam.ClientID = "123456789012345"
		MyTokenParam.ClientSecret = "45g8jh5kll45579021qsg5444j"
		MyTokenParam.AuthURL = "https://www.facebook.com/dialog/oauth"
		MyTokenParam.TokenURL = "https://graph.facebook.com/v2.3/oauth/access_token"
		MyTokenParam.RedirectionURL = "http://localhost:9874/"
		MyTokenParam.Scope = "email"
		
		MyToken = AuthIdentify(MyTokenParam)
		IF MyToken <> Null THEN
			IF ErrorOccurred THEN
				Error(ErrorInfo())
			ELSE
				// Token specified on the request URL
				HTTPRequest("https://graph.facebook.com/me?access_token=" + MyToken.Value)
				vMyRes is Variant = JSONToVariant(HTTPGetResult(httpResult))
				// Retrieve the account name
				Trace(vMyRes.name)
			END
		END
		```











