
## AuthIdentify (Función)



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Realiza una autenticación utilizando: 

- el protocolo OAuth 2.0 en cualquier servicio Web.

- ![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png) el protocolo OpenID en cualquier servicio Web.







<a name="Example1"></a>
<a name="sample_code"></a>

## Ejemplo


```wl
// Example used to retrieve a token to perform a request on DropBox
OAuth2Params is OAuth2Parameters
OAuth2Params.ClientID = "01234567890123456789" 
OAuth2Params.ClientSecret = "98765432109876543210"
OAuth2Params.AuthURL = "https://www.dropbox.com/oauth2/authorize"
OAuth2Params.TokenURL = "https://api.dropboxapi.com/oauth2/token"
OAuth2Params.AdditionalParameters = "force_reapprove=false"
<COMPILE IF ConfigurationType<>Site>
	// If you are not using a website, you need to specify a localhost redirect URL
	OAuth2Params.RedirectionURL = "http://localhost:9874/"	
<END>

// Ask for authentication: opens the login window
MyToken is AuthToken = AuthIdentify(OAuth2Params)

// Request authenticated on a DropBox API
req is httpRequest
req.Method = httpPost
req.URL = "https://api.dropboxapi.com/2/files/list_folder"
req.AuthToken = MyToken // Authentication token
req.ContentType = "application/json"
vAPIParam is variant
vAPIParam.path = "/Homework/math"
vAPIParam.recursive = False
vAPIParam.include_media_info = False
vAPIParam.include_deleted = False
vAPIParam.include_has_explicit_shared_members = False
req.Content = VariantToJSON(vAPIParam)

HTTPresponse is httpResponse = HTTPSend(req)
let Data = JSONToVariant(HTTPresponse.Content)
// Use the incoming data...
```

<a name="XSYNTAX"></a>

## Sintaxis
<a name="SYNTAX1"></a>
<a name="SYNTAX2"></a>
![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png) 
### Autenticación mediante el protocolo OpenID

`<Result> = AuthIdentify(<Authentication parameters>)`
---

**`<Result>: (AuthToken Variable)`**

Variable de tipo [AuthToken](../WDLang3/1000022220.md) que corresponde al token que contiene la información de acceso para las siguientes solicitudes autenticadas.  

**`<Authentication parameters>: (OpenIDParameters Variable)`**

Nombre de la variable de tipo [OpenIDParameters](../WDLang3/1000023561.md) que contiene los parámetros que describen los elementos necesarios para obtener el token de acceso.


<a name="SYNTAX3"></a>

### Autenticación mediante el protocolo OAuth 2.0 (sintaxis asíncrona)

`AuthIdentify(<Authentication parameters> , <WLanguage procedure>)`
---

**`<Authentication parameters>: (OAuth2Parameters Variable)`**

Nombre de la variable de tipo [OAuth2Parameters](../WDLang3/1000022212.md) que contiene los parámetros que describen los elementos necesarios para obtener el token de acceso.

**`<WLanguage procedure>: (Nombre de Procedure)`**

Nombre de la WLanguage Procedure ("callback") llamada durante la autenticación. Para obtener más información sobre este procedimiento, consulte [Parámetros del procedimiento utilizado por la función AuthIdentify](../WDLang3/1000025107.md). 


<a name="SYNTAX4"></a>
![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png) 
### Autenticación mediante el protocolo OpenID (sintaxis asíncrona)

`AuthIdentify(<Authentication parameters> , <WLanguage procedure>)`
---

**`<Authentication parameters>: (OpenIDParameters Variable)`**

Nombre de la variable de tipo [OpenIDParameters](../WDLang3/1000023561.md) que contiene los parámetros que describen los elementos necesarios para obtener el token de acceso.

**`<WLanguage procedure>: (Nombre de Procedure)`**

Nombre de la WLanguage Procedure ("callback") llamada durante la autenticación. Para obtener más información sobre este procedimiento, consulte [Parámetros del procedimiento utilizado por la función AuthIdentify](../WDLang3/1000025107.md).



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Observaciones


### Modo de funcionamiento de la autenticación OAuth 2.0
<a name="modo_funcionamiento_autenticacion_oauth_20_ELTPARAGRAPHE000119"></a>

Las etapas de la autenticación OAuth 2.0 que realiza la función [AuthIdentify](../WDLang3/1000022219.md) son las siguientes: 

- Ejecución de una primera solicitud HTTP para pedir una autorización (URL de autorización especificada en la variable de tipo [OAuth2Parameters](../WDLang3/1000022212.md)).

- Apertura de una ventana de identificación de usuarios según el protocolo OAuth 2.0. La interfaz de identificación corresponde al servicio al que se accede.

- Después de la identificación, el servidor devuelve un primer código de autorización que permite solicitar un token de acceso a los recursos. Este código se añade en el parámetro de la segunda URL (URL del token de acceso especificado en la variable de tipo [OAuth2Parameters](../WDLang3/1000022212.md)).

- Ejecución de la segunda solicitud HTTP para solicitar el token de acceso. El resultado es un buffer JSON que contiene, entre otras cosas, el token de acceso ("**access_token**") que se utilizará para las solicitudes autenticadas. La variable de tipo [AuthToken](../WDLang3/1000022220.md) contiene la información que se encuentra en el buffer JSON. Este token de acceso será utilizado por las llamadas a las API del servicio Web.




**Para utilizar las API del servicio web**, basta con utilizar la función [HTTPSend](../WDLang3/1000021183.md) con un [httpRequest](../WDLang3/1000021158.md) Variable definiendo la consulta a ejecutar. 
El [AuthToken](../WDLang3/1000022220.md) Variable se asignará al **AuthToken** Property del [httpRequest](../WDLang3/1000021158.md) Variable (ver ejemplo). 
En este caso, el servidor recibirá el encabezado HTTP "**Authorization**" con un valor en el siguiente formato: "Autorización: Bearer xxx_access_token_xxx". 

**Atención**: 

- Si el servidor no devuelve el token de acceso en formato de código JSON según el estándar OAuth2.0, se producirá un error y no se obtendrá el token. La respuesta del servidor puede recuperarse a través del **ServerResponse** Property del [AuthToken](../WDLang3/1000022220.md) Variable.

- Si el servidor no soporta el encabezado HTTP "**Authorization**" para transmitir el token de acceso, el desarrollador debe realizar la transmisión según el formato esperado por el servicio solicitado. 
	El siguiente ejemplo permite utilizar el servicio Web de Facebook. En este caso, el token de acceso debe especificarse en la URL de la solicitud.

	- ![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) Ejemplo de código para Facebook
			
		```wl
		// Example used to retrieve the name of the Facebook account
		MyToken is AuthToken
		MyTokenParam is OAuth2Parameters
		
		MyTokenParam.ClientID = "123456789012345"
		MyTokenParam.ClientSecret = "45g8jh5kll45579021qsg5444j"
		MyTokenParam.AuthURL = "https://www.facebook.com/dialog/oauth"
		MyTokenParam.TokenURL = "https://graph.facebook.com/v2.3/oauth/access_token"
		MyTokenParam.RedirectionURL = "http://localhost:9874/"
		MyTokenParam.Scope = "email"
		
		MyToken = AuthIdentify(MyTokenParam)
		IF MyToken <> Null THEN
			IF ErrorOccurred THEN
				Error(ErrorInfo())
			ELSE
				// Token specified on the request URL
				HTTPRequest("https://graph.facebook.com/me?access_token=" + MyToken.Value)
				vMyRes is Variant = JSONToVariant(HTTPGetResult(httpResult))
				// Retrieve the account name
				Trace(vMyRes.name)
			END
		END
		```











### Modo de funcionamiento de la autenticación OpenID
<a name="modo_funcionamiento_autenticacion_openid_ELTPARAGRAPHE000183"></a>

La función [AuthIdentify](../WDLang3/1000022219.md) realiza las siguientes etapas de autenticación OpenID: 

- Ejecución de una primera solicitud HTTP para pedir una autorización (URL de autorización especificada en la variable de tipo [OpenIDParameters](../WDLang3/1000023561.md)).

- Apertura de una ventana de identificación de usuarios según el protocolo OpenID. La interfaz de identificación corresponde al servicio al que se accede.

- Después de la identificación, ejecución de una segunda solicitud HTTP para solicitar el token de acceso. El resultado es un buffer JSON que contiene, entre otras cosas, el token de acceso ("**access_token**") que se utilizará para las solicitudes autenticadas. La variable de tipo [AuthToken](../WDLang3/1000022220.md) contiene la información que se encuentra en el buffer JSON. Este token de acceso será utilizado por las llamadas a las API del servicio Web. 




Para identificar al usuario, basta con utilizar [OpenIDReadIdentity](../WDLang3/1000023573.md) y especificar el [AuthToken](../WDLang3/1000022220.md) Variable. 

**Atención**: Si el servidor no devuelve el token de acceso en formato de código JSON según el estándar OAuth2.0, se producirá un error y no se obtendrá el token. La respuesta del servidor puede recuperarse a través del **ServerResponse** Property del [AuthToken](../WDLang3/1000022220.md) Variable.
<a name="NOTE0_5"></a>





<a name="XComponent"></a>

## Clasificación Lógica de negocio / UI:
Lógica de negocio
## Componente:
wd280ggl.dll
