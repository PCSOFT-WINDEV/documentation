
## Protocolo SOCKS5
			

<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Presentación
<a name="presentacion_ELTTEXTE000150"></a>
SOCKS5 es un protocolo de red que tiene como objetivo estandarizar el uso de proxies para las aplicaciones. Tras una breve fase de transacción entre la aplicación y el servidor proxy SOCKS5, la aplicación puede intercambiar datos sin problemas a través de un socket.

<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## ¿Cómo utilizar el protocolo SOCKS5 con WINDEV, WEBDEV y WINDEV Mobile?
<a name="¿como_utilizar_protocolo_socks5_con_windev_webdev_windev_mobile_ELTTEXTE000174"></a>
Diferentes tipos de funciones permiten configurar el uso de un proxy a través del protocolo SOCKS5: 

- [La función Proxy](#NOTE2_2). 

- [La función FTPProxy con la constante *ftpProxySOCKS5*](#NOTE2_3). 

- [La función SocketProxy](#NOTE2_4). 



<a name="NOTE2_2"></a>


### Función Proxy
<a name="funcion_proxy_ELTPARAGRAPHE000031"></a>

Para utilizar el protocolo SOCKS5 con la función [Proxy](../WDLang3/3043002.md), agregue "socks5://" como prefijo al nombre del servidor proxy y utilice la función con la sintaxis estándar (pasando el puerto y los identificadores).

ATENCIÓN: 

- No se puede utilizar un proxy SOCKS5 si HTTP está configurado para utilizar WinInet (modo que utiliza Internet Explorer). Este modo puede configurarse con la constante *httpParameterMode* de la función [HTTPParameter](../WDLang3/1000018985.md).

- Al llamar a las funciones [HTTPRequest](../WDLang3/3043007.md) y [HTTPSendForm](../WDLang3/1000017190.md) con un proxy que utiliza el protocolo SOCKS5 se forzará el uso del comando cURL. 
	Por lo tanto, se recomienda utilizar una variable de tipo [httpRequest](../WDLang3/1000021158.md) con un proxy SOCKS5.




Ejemplo: 


```wl
// Do not use WinInet to access the SOCKS5 proxy
HTTPParameter(httpParameterMode, 1)
// The following functions will use the SOCKS5 proxy 
// at address 172.12.2.79:1080 with the identifiers USER:PWD 
Proxy("socks5://172.12.2.79", 1080, "USER", "PWD")
req is httpRequest
req.URL = "https://www.google.com"
req is httpResponse = req.Send()
IF ErrorOccurred THEN
	Error(ErrorInfo(errFullDetails))
ELSE
	Info("OK")
END
Proxy("")
```

<a name="NOTE2_3"></a>


### FTPProxy
<a name="ftpproxy_ELTPARAGRAPHE000066"></a>

Para utilizar el protocolo SOCKS5 con la función [FTPProxy](../WDLang3/1000019366.md), utilice la constante *ftpProxySOCKS5* para especificar el tipo de proxy a utilizar.

ATENCIÓN: 

- No se puede utilizar un proxy SOCKS5 si el protocolo FTP está configurado para utilizar WinInet (modo que utiliza Internet Explorer). Este modo puede configurarse con la constante *httpParameterMode* de la función [FTPParameter](../WDLang3/1000020947.md). Si se llama a la función [FTPProxy](../WDLang3/1000019366.md), se generará un error fatal.

- Los proxies SOCKS5 solo pueden utilizarse con FTP y SFTP. Si se configura un proxy SOCKS5, al llamar a la función [FTPConnect](../WDLang3/3040016.md) en un servidor a través de FTPES o FTPIS se generará un error fatal.




Ejemplo: 


```wl
// Do not use WinInet to access the SOCKS5 proxy
FTPParameter(ftpParameterMode, 1)
// FTP functions will use the SOCKS5 proxy 
// at address 172.12.2.79:1080 with the identifiers USER:PWD 
FTPProxy(ftpProxySOCKS5, "172.12.2.79",1080, "USER", "PWD")
// Connect to ftp://test.rebex.net/
nFTPID is int = FTPConnect("ftp://test.rebex.net/", "demo", "password", 21)
IF nFTPID = -1 THEN
	Error(ErrorInfo(errFullDetails))
ELSE
	Info("OK")
END
FTPDisconnect(nFTPID)
// Remove proxy
FTPProxy(ftpProxyNone)
```

<a name="NOTE2_4"></a>


### SocketProxy
<a name="socketproxy_ELTPARAGRAPHE000096"></a>

La función [SocketProxy](../WDLang3/1410088761.md) permite especificar si los sockets TCP y los WebSockets deben pasar por un proxy SOCKS5 para ejecutar las solicitudes. 


```wl
// SOCKS5 proxy info
SocketProxy("socks5://172.17.2.79",1080,"USER","PWD")

// Connect to the server through the proxy
IF NOT SocketConnect("tcp_through_socks5", 4242, "172.19.5.80") THEN
	Error(ErrorInfo(errFullDetails))
ELSE
	// Write and read operations (as usual)
	Info("OK")
	SocketWrite("tcp_through_socks5", "echo")
	s is Buffer = SocketRead("tcp_through_socks5")
	Trace(BufferToHexa(s))
	SocketClose("tcp_through_socks5")
END

// Remove proxy
SocketProxy("")
```



