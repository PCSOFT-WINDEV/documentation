


## Input( Función)



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Muestra un mensaje que permite al usuario escribir una información.<br>![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=fonctions_dialogue%20-%20HC%20N%B0008.gif)
**Observación**: Existen varias sintaxis disponibles para esta función: 

- Sintaxis simplificada, introducida directamente en el editor de código. 

- Sintaxis completa con la pregunta introducida a través de una asistente. El editor de código propone una opción [completo asistente](../WDLang1/3021020.md) ("" opción propuesta por el [entrada predictiva](../Editeurs/2013001.md)). Esta asistente se utiliza para definir los diferentes elementos que aparecen en el mensaje y para generar automáticamente el código correspondiente. El título de los botones se propone según el texto de la pregunta.

- Sintaxis directa, tecleada directamente en el editor de código. 


<a name="Example1"></a>
<a name="sample_code"></a>

## Ejemplo


```wl
// Example of simplified input 
NbCopies is int
ReturnValue is int
ReturnValue = Input("How many copies do you want to print?", NbCopies)
SWITCH ReturnValue 
	CASE 0: Info("You canceled. The default number is set to 1.")
	CASE 1: Info("You requested " + NbCopies)
END
```
<a name="Example2"></a>

```wl
// Simplified input while using an input mask 
PhoneNum is string
// Use a specific text input mask
Input.InputMask = maskPhoneFrance
SWITCH Input("What is your phone number?", PhoneNum)
	// OK
	CASE 1: EDT_PhoneNum = PhoneNum 
	// Cancel
	CASE 0: EDT_PhoneNum = "No phone"
END
```
<a name="Example3"></a>

```wl
Comments is string
Comments = RepeatString(CR, 10)
// Comments typed by the user in a multiline control 
// Input limited to 140 characters
Input.InputMask = "regexp:.{0,140}"
SWITCH Input("Type your comments.", Comments)
	// OK
	CASE 1: EDT_Comment = Comments
	// Cancel
	CASE 0: EDT_Comment = "No comment"
END
```
<a name="Example4"></a>

```wl
DisplayInfo is boolean = False
// Ask the user whether he really wants to close the application 
// A checkmark ("Don't display this window anymore") allows the user
// not to display this question anymore
Input.OptionalCaption = "Don't display this window anymore"
IF DisplayInfo = False THEN
	SWITCH Input("Do you want to close the application?", DisplayInfo)
		// Close
		CASE 1: Close()
	END
END
// Remember to store the value of DisplayInfo for the next time
```
<a name="Example5"></a>

```wl
// Direct syntax 
PhoneNum is string
If Input("What is your phone number?", PhoneNum, ...
		["Validate", "No phone", "Cancel"], 3, 3, dlgIconQuestion) = 1 THEN
	Info(PhoneNum)
END
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Sintaxis

### Sintaxis simplificada (sin usar la asistente)

`<Result> = Input(<Question> , <Variable to enter>)`
---

**`<Result>: (Integro)`**

Identifica la respuesta seleccionada por el usuario: 

- 0: el usuario ha pulsado el botón "Cancel".

- 1: el usuario ha pulsado el botón "OK".




**`<Question>: (Cadena de caracteres)`**

Pregunta que se le hará al usuario. Esta pregunta puede utilizar [StringBuild](../WDLang1/3024006.md).

**`<Variable to enter>: (Cualquier tipo)`**

Variable en la que el usuario debe escribir la respuesta. 

- Esta variable debe haber sido declarada antes de llamar a la función **Input**. 

- El tipo de Variable define el tipo de control donde los usuarios introducen la respuesta. En el caso de los Variable s de hora o fecha, la máscara de entrada se deduce automáticamente de los Variable. 
	![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Se puede especificar una máscara de entrada (especialmente para las variables de tipo Texto) con la variable **InputMask** (ver Notas).

- Si el Variable está inicializado con un valor, este valor corresponde al valor de Default.

- Después de llamar a la función **Input**, esta variable contiene la respuesta del usuario (independientemente del botón utilizado).





<a name="SYNTAX2"></a>

### Sintaxis completa con la pregunta introducida a través de una asistente

`<Result> = Input(<Question> , <Variable to enter> [, <Parameter 1> [... [, <Parameter N>]]])`
---

**`<Result>: (Integro)`**

Identifica la respuesta seleccionada por el usuario (valor de 1 al número de botones que se encuentran en la ventana de edición).. 

Esta respuesta depende del número de botones en la ventana de la pregunta description. Los diferentes valores correspondientes a las diferentes respuestas se incluyen automáticamente en los comentarios en el editor de código al seleccionar el mensaje..  

**`<Question>: (Cadena de caracteres)`**

Pregunta que se le hará al usuario. Esta pregunta puede contener parámetros identificados por %1, %2, ....

**`<Variable to enter>: (Cualquier tipo)`**

Variable en la que el usuario debe escribir la respuesta. 

- Esta variable debe haber sido declarada antes de llamar a la función **Input**. 

- El tipo de Variable define el tipo de control donde los usuarios introducen la respuesta. En el caso de los Variable s de hora o fecha, la máscara de entrada se deduce automáticamente de los Variable. 
	![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Se puede especificar una máscara de entrada (especialmente para las variables de tipo Texto) con la variable **InputMask** (ver Notas).

- Si el Variable está inicializado con un valor, este valor corresponde al valor de Default.

- Después de llamar a la función **Input**, esta variable contiene la respuesta del usuario (independientemente del botón utilizado).




**`<Parameter 1>: (Cadena de caracteres)`**

Si la pregunta contiene elementos configurables (identificados por %1, %2, ...), este parámetro se utiliza para dar el valor deseado. Así, &lt;Parámetro 1&gt; reemplazará %1.



**`<Parameter N>: (Cadena de caracteres)`**

Si el mensaje seleccionado contiene elementos con parámetros (identificados por %1, %2, ...), este parámetro se utiliza para dar el valor deseado. Así, &lt;Parámetro 2&gt; reemplazará %2.


<a name="SYNTAX3"></a>

### Sintaxis directa

`<Result> = Input(<Question> , <Variable to enter> , <Captions of buttons> [, <Default button> [, <Cancellation button> [, <Icon>]]])`
---

**`<Result>: (Integro)`**

Identifica la respuesta seleccionada por el usuario. Esta respuesta depende del mensaje seleccionado.

**`<Question>: (Cadena de caracteres)`**

Pregunta hecha al usuario.

**`<Variable to enter>: (Cualquier tipo)`**

Variable en la que el usuario debe escribir la respuesta. 

- Esta variable debe haber sido declarada antes de llamar a la función **Input**. 

- El tipo de Variable define el tipo de control donde los usuarios introducen la respuesta. En el caso de los Variable s de hora o fecha, la máscara de entrada se deduce automáticamente de los Variable. 
	![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Se puede especificar una máscara de entrada (especialmente para las variables de tipo Texto) con la variable **InputMask** (ver Notas).

- Si el Variable está inicializado con un valor, este valor corresponde al valor de Default.

- Después de llamar a la función **Input**, esta variable contiene la respuesta del usuario (independientemente del botón utilizado).




**`<Captions of buttons>: (Array)`**

Nombre de la Array Variable que contiene las leyendas de los botones.

**`<Default button>: (Integro)`**

Índice del botón seleccionado por Default. El valor predeterminado de este parámetro es 1.

**`<Cancellation button>: (Integro)`**

Índice del botón de cancelación. En Default, este parámetro corresponde al índice del último botón.

**`<Icon>: (Cadena de caracteres o constante entera)`**

Mostrar Icon. Este parámetro puede corresponder a: 

- el ruta de archivo correspondiente al Icon mostrado. 

- una de las siguientes constantes: 
	


|   |   |
| --- | --- |
| *dlgIconError* | Icon representando un error. |
| *dlgIconInfo* | Icon representando una información. |
| *dlgIconQuestion*<br>(Valor predeterminado) | Icon representando una pregunta. |








<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Observaciones


### Características de la ventana de diálogo y de control Campo de entrada
<a name="caracteristicas_ventana_dialogo_control_campo_entrada_ELTPARAGRAPHE000168"></a>



- El título del cuadro de diálogo corresponde al título de la ventana (o página) actual.
	

- Para modificar o definir el título del cuadro de diálogo, utilice la función [NextTitle](../WDLang1/3038043.md).




- La plantilla del proyecto actual se aplica automáticamente al cuadro de diálogo. 
	![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) El plantilla de la aplicación no se aplica a la ventana de entrada.. 

- ![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Para modificar las características del control Campo de entrada (por ejemplo, modificar la reemplazar los caracteres introducidos en el control por asteriscos), abra el asistente de edición de preguntas (ícono ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=IconeSaisie.GIF) en la sintaxis de la función **Input**) y haga clic en el ícono ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=Saise_Exemple%201.GIF).



<a name="NOTE0_2"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) 

### Gestión de máscaras de entrada
<a name="gestion_mascaras_entrada_ELTPARAGRAPHE000192"></a>

Por Default, la máscara de entrada se define automáticamente a partir de la información del proyecto.

Sin embargo, puede modificar la máscara de entrada con la variable **Input.InputMask**.

Esta Variable puede tomar uno de los siguientes valores:  


|   |   |
| --- | --- |
| *maskAAlphaNum* | Letra, luego letras + dígitos |
| *maskAAlphaNumUpper* | Letra mayúscula, luego letras mayúsculas + dígitos |
| *maskAlpha* | Letras |
| *maskAlphaNum* | Letras + dígitos |
| *maskAlphaNumUpper* | Letras en mayúsculas + dígitos |
| *maskAlphaUpper* | Letras en mayúsculas |
| *maskEmail* | Envía un correo electrónico a Address |
| *maskFileName* | Nombre de archivo y ruta |
| *maskFileNoFolder* | Nombre del archivo (sin sus directorios) |
| *maskFileSize* | Máscara numérica para los tamaños de archivo y disco |
| *maskFUpper* | Primera letra en mayúsculas |
| *maskINSEE* | Número INSEE |
| *maskINSEEKey* | Número INSEE + tecla |
| *maskLower* | Todo en minúsculas |
| *maskNone* | Sin máscara de entrada |
| *maskNum* | Dígitos |
| *maskNumPlus* | Dígitos, '+', ' ', '.', '-', ',' |
| *maskPatronymic* | Letras + Dígitos + Espacio + Cotización + Guión |
| *maskPatronymicUpper* | Mayúsculas + Dígitos + Espacio + Cuota + Guión |
| *maskPhone* | Número de teléfono |
| *maskPhoneFrance* | Número Phone en formato francés |
| *maskUpper* | Todo en mayúsculas |


También tiene la opción de asignar una expresión regular como máscara de entrada.. Para obtener más información, consulte la ayuda sobre la propiedad [2510033](../Proprietes/2510033.md). 
<a name="NOTE0_3"></a>


### Escribir texto multilínea
<a name="escribir_texto_multilinea_ELTPARAGRAPHE000297"></a>

La entrada multilínea se permitirá en el control asociado si  se inicializa con una cadena de caracteres que contenga caracteres CR.<a name="NOTE0_4"></a>


### Entrada mediante una casilla de verificación
<a name="entrada_mediante_una_casilla_verificacion_ELTPARAGRAPHE000306"></a>

La entrada se realiza en una casilla de verificación si el Variable es un booleano. El título de la casilla de verificación está definido por la variable ***Input.OptionalCaption***.
<a name="NOTE0_5"></a>


### Base de datos de mensajes
<a name="base_datos_mensajes_ELTPARAGRAPHE000313"></a>

Todos los mensajes nuevos se añaden automáticamente a la base de datos de mensajes.. 

Por Default, la base de datos de mensajes se encuentra en el directorio "Personal\\Messages" de WINDEV, WEBDEV y WINDEV Mobile. Este directorio puede ser modificado en las opciones de WINDEV/WEBDEV/WINDEV Mobile: 

- En la pestaña "Inicio", en el grupo "Entorno", despliegue "Opciones" y seleccione "Opciones generales de WINDEV/WEBDEV/WINDEV Mobile". 

- Acceda a la pestaña "Directorio".




Para obtener más información, consulte [pregunta editar asistente](../WDLang1/3021020.md).
<a name="NOTE0_6"></a>
![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) 

### Limitaciones
<a name="limitaciones_ELTPARAGRAPHE000344"></a>

- ![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) El plantilla de la aplicación no se aplica a la ventana de entrada.. 

- El Variable a introducir no puede ser inicializado con caracteres CR. La control Campo de entrada será una control Campo de entrada de una sola línea..
	

- ![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Si la Variable es booleana, la entrada se realizará en una control Campo de entrada (y no en una casilla de verificación como en WINDEV).

- ![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) La máscara de entrada no es compatible.



<a name="NOTE0_7"></a>


### Ejemplo de entrada completa
<a name="ejemplo_entrada_completa_ELTPARAGRAPHE000383"></a>

1. Escribiendo la pregunta en el editor: <br>![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=Saisie_Exemple.gif&type=thumb)


2. Código generado automáticamente (sólo se ha introducido el "Destinatario" Variable en el editor de código): 



```wl
Recipient is string
//1: Create a follow-up
//0: Don't create a follow-up
//1: OK 
SWITCH Input("Specify a recipient.", Recipient)
	// Validate the recipient
	CASE 1
		
	// Display the list
	CASE 2
		
	// Cancel
	CASE 3
		
END
```

<a name="NOTE0_8"></a>

<a name="XComponent"></a>

## Clasificación Lógica de negocio / UI:
Código UI
## Componente:
wd280obj.dll
