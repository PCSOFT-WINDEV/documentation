
## Gestión de tareas paralelas
			

<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Presentación
<a name="presentacion_ELTTEXTE000336"></a>
Los ordenadores son cada vez más potentes. Los ordenadores tienen potentes procesadores con varios núcleos. 

Para mejorar el rendimiento de las aplicaciones y trabajar con el máximo número de núcleos del ordenador, ahora tiene la posibilidad de dividir los procesos en una lista de subprocesos (llamados tareas) y ejecutarlos en paralelo en lugar de secuencialmente. 

Una tarea es una Procedure a ejecutar que puede esperar parámetros y que puede devolver un resultado. Estas tareas serán ejecutadas por el equipo en uno o más hilos según la disponibilidad de el equipo.
Una tarea puede dividirse en varias subtareas.

**¿Cuál es el beneficio de las tareas paralelas?**
Las tareas paralelas son useful a: 

- **acelera el tiempo de aplicación de Process mediante el paralelismo**: varios procesos se ejecutan en paralelo en lugar de ejecutarse secuencialmente: se mejora la velocidad de la aplicación. 
	***Un ejemplo sencillo***: iniciar un cálculo estadístico para enviar y recibir correos electrónicos en cada correo electrónico Address de la base de datos. Si el cálculo estadístico de un correo electrónico Address toma un segundo y si la base de datos contiene 200 000 correos electrónicos Address es, el cálculo toma más de dos días. 
	Para ir más rápido, tienes la capacidad de iniciar una tarea paralela por cada correo electrónico que Address encontró. 
	Este ejemplo se presenta en [Ejemplo: Acelerar los procesos](#NOTE4_1).

- **mejorar la reactividad de la aplicación**: varios procesos largos y de bloqueo se ejecutan en paralelo en lugar de ejecutarse secuencialmente: el usuario no se siente atrapado. 
	***Un ejemplo sencillo***: Un control Tabla muestra una lista de contactos cuya foto se carga desde un INTERNET Address. Para cada contacto, la aplicación realiza una solicitud de INTERNET (que desencadena una desaceleración). 
	Para que el control Tabla se llene sin ser bloqueado, el proceso de visualización de fila inicia una tarea paralela que se usa para iniciar la solicitud de INTERNET y para actualización la GUI si es necesario. 
	Este ejemplo se presenta en [Ejemplo: Mejorar la reactividad de la aplicación](#NOTE5_1).






<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Principio
<a name="principio_ELTTEXTE000360"></a>
Para gestionar las tareas paralelas, WLanguage propone: 

- una variable de tipo [ParallelTask](../WDLang1/1000021220.md). Este tipo de Variable se utiliza para manipular una tarea paralela. Este tipo de Variable no puede ser usado para modificar las características de una tarea paralela. 

- funciones de gestión de tareas ([ParallelTask\*](../WDLang1/1000021300.md)).




Observación: También tiene la posibilidad de utilizar el [Descripción de ParallelTask](../WDLang1/1000021586.md) Variable. Este tipo de Variable sólo puede ser usado para describir una tarea paralela. Una vez definida la tarea paralela, sus características no pueden modificarse.  

<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## ¿Cómo proceder?
<a name="¿como_proceder_ELTTEXTE000384"></a>


### Principio
<a name="principio_ELTPARAGRAPHE000082"></a>

Para implementar una gestión de tareas paralelas, debe: 

1. Crear y ejecutar una tarea paralela. 

2. Esperar la ejecución de la tarea y recuperar el valor de retorno. 

3. Gestionar (si es necesario) la secuencia de tareas paralelas. 

4. Manejar (si es necesario) los controles de una tarea paralela..   



<a name="NOTE3_2"></a>


### Crear y ejecutar una tarea paralela
<a name="crear_ejecutar_una_tarea_paralela_ELTPARAGRAPHE000094"></a>

Una tarea paralela debe estar asociada a una [ParallelTask](../WDLang1/1000021220.md) Variable. 

Se pueden utilizar varios métodos para declarar un [ParallelTask](../WDLang1/1000021220.md) Variable: 

- Declaración simple. La tarea paralela se describe durante su ejecución con [ParallelTaskExecute](../WDLang1/1000021211.md): 
	
	```wl
	// Declares a variable to handle a parallel task
	t is ParallelTask
	// Run and describe the parallel task
	t = ParallelTaskExecute(Proc, ("First parameter", 2))
	```


- Declaración y description de la tarea paralela. A continuación, la tarea paralela es ejecutada por [ParallelTaskExecute](../WDLang1/1000021211.md). 
	
	```wl
	// Builds a parallel task 
	t is ParallelTask(Proc, ("First parameter", 2))
	// Triggers the execution of parallel task
	ParallelTaskExecute(t)
	```





Observación: Al describir la tarea paralela, tiene la capacidad de especificar: 

- el Procedure para dirigir. 

- los parámetros esperados por la Procedure.

- el modo de tiempo de ejecución de la tarea paralela: gestión de los contextos HFSQL e interacciones con el principal Thread. 



<a name="NOTE3_2b"></a>


### La palabra clave de MyParallelTask
<a name="palabra_clave_myparalleltask_ELTPARAGRAPHE000128"></a>

**MyParallelTask** se utiliza para gestionar la tarea paralela actual y descubrir sus propiedades. Esto le permite acceder a la información sobre la tarea paralela actual en el código ejecutado por una tarea paralela. Las propiedades accesibles son las de las variables de [ParallelTask](../WDLang1/1000021220.md): 


| Nombre de la propiedad | Tipo utilizado | Efecto |
| --- | --- | --- |
| Cancelado | booleano | <br><br>- <u><u><u><u>True</u></u></u></u> si se cancela la tarea,<br><br>- <u><u><u><u>False</u></u></u></u> en caso contrario. <br><br><br><br><br>Esta propiedad es de solo lectura. |
| Concluido | booleano | <br><br>- <u><u><u><u>True</u></u></u></u> si se completó la tarea,<br><br>- <u><u><u><u>False</u></u></u></u> en caso contrario. <br><br><br>Esta propiedad es de solo lectura. |
| Estado | Constante de tipo Integer | Status de la tarea: <br><br>- *ptsCanceled*: la tarea paralela se cancela ([ParallelTaskCancel](../WDLang1/1000021278.md)). <br><br>- *ptsCancellationRequested*: se ha realizado una solicitud de cancelación en la tarea paralela ([ParallelTaskRequestCancellation](../WDLang1/1000021213.md)). <br><br>- *ptsWaitingExecution*: la tarea paralela está a la espera de ser ejecutada. <br><br>- *ptsWaitingPrevious*: La tarea paralela espera la ejecución de una tarea paralela anterior.. <br><br>- *ptsExecutionInProgress*: la tarea paralela se está ejecutando actualmente. <br><br>- *ptsNotScheduled*: la tarea paralela no está programada. <br><br>- *ptsCompleted*: la tarea paralela ha terminado. <br><br><br><br><br>Esta propiedad es de solo lectura. |
| Identificador | Integro | Identificador de tareas. Este identificador puede ser usado para propósitos de depuración, por ejemplo. <br><br>Esta propiedad es de solo lectura. |
| ReturnedValue | Valor devuelto por la tarea. | Atención: <br><br>- Si la tarea aún está en curso, la **ReturnedValue** Property espera el final de la tarea<br><br>- Si la tarea se completa sin error fatal, la Property devuelve los valores de retorno de la Procedure de la tarea.<br><br><br><br><br>Esta propiedad es de solo lectura. |


<a name="NOTE3_3"></a>


### Esperar la ejecución de la tarea y recuperar el valor de retorno
<a name="esperar_ejecucion_tarea_recuperar_valor_retorno_ELTPARAGRAPHE000139"></a>

Se pueden iniciar varias tareas paralelas al mismo tiempo. Tienes la capacidad de esperar la ejecución de una o más tareas paralelas antes de ejecutar un Process: 


|   |   |
| --- | --- |
| [ParallelTaskWait](../WDLang1/1000021212.md) | Espera la ejecución de una tarea paralela. |
| [ParallelTaskWaitAll](../WDLang1/1000021297.md) | Espera al final de la ejecución de todas las tareas paralelas que se encuentran en una array. |





La propiedad **ValeurRenvoyée** de la variable [ParallelTask](../WDLang1/1000021220.md) obtiene el valor devuelto por el Procedure ejecutado por la tarea paralela. 
Atención: Este valor sólo está disponible si la tarea paralela ha finalizado.. Si la tarea está en progreso, la llamada a este Property se bloquea hasta el final de la tarea. 
<a name="NOTE3_4"></a>


### Gestión de la secuencia de tareas paralelas
<a name="gestion_secuencia_tareas_paralelas_ELTPARAGRAPHE000154"></a>

Se pueden iniciar varias tareas paralelas al mismo tiempo. Tiene la capacidad de definir la secuencia de tareas paralelas: una tarea puede esperar al final de la ejecución de una o más tareas antes de ejecutarse. Las siguientes funciones se utilizan para definir una tarea de continuación: 


|   |   |
| --- | --- |
| [ParallelTaskExecuteAfter](../WDLang1/1000021214.md) | Indica una tarea paralela de continuación que se ejecutará cuando se termine una de las tareas paralelas especificadas. |
| [ParallelTaskExecuteAfterAll](../WDLang1/1000021217.md) | Indica una tarea paralela de continuación que se ejecutará cuando todas las tareas de un array de tareas paralelas hayan finalizado. |
| [ParallelTaskExecuteAfterOne](../WDLang1/1000021216.md) | Indica una tarea paralela de continuación que se ejecutará una vez finalizada la primera tarea de un array de tareas paralelas. |





Observación: En una tarea de continuación, puede: 

- gestionar la tarea anterior completada con la palabra clave ***MyPreviousParallelTask*** ([ParallelTaskExecuteAfter](../WDLang1/1000021214.md) y [ParallelTaskExecuteAfterOne](../WDLang1/1000021216.md)). 

- gestionar las tareas anteriores realizadas con la palabra clave ***MyPreviousParallelTasks*** ([ParallelTaskExecuteAfterAll](../WDLang1/1000021217.md)). 



<a name="NOTE3_5"></a>


### Manejo de los controles desde una tarea paralela
<a name="manejo_los_controles_desde_una_tarea_paralela_ELTPARAGRAPHE000176"></a>

No puede actuar en la interfase desde una tarea paralela. Por lo tanto, no puedes asignar un control, llenar una tabla o control Looper. 

Sólo una tarea definida por la constante *ptoMainThread* puede ser ejecutada en el Thread principal y puede actualizar los controles si es necesario. 

Observación: También puede utilizar [ExecuteMainThread](../WDLang1/1000019862.md) para ejecutar una visualización específica Procedure desde la tarea paralela. 

Estos dos métodos se utilizan en el ejemplo [Mejorar la reactividad de la aplicación](#NOTE5_1) presentado en este Page. 

<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Ejemplo: Acelerar los procesos
<a name="ejemplo_acelerar_los_procesos_ELTTEXTE000555"></a>


### Acelerar los procesos: cálculo estadístico
<a name="acelerar_los_procesos_calculo_estadistico_ELTPARAGRAPHE000197"></a>

Una aplicación utiliza el procedimiento **CalculateStatEmailAddress** para realizar cálculos estadísticos sobre las operaciones de envío y recepción de cada dirección de correo electrónico del archivo CUSTOMER. Este Procedure toma como parámetro el correo electrónico Address y calcula todas las estadísticas de este Address. 

Si el cálculo estadístico en un correo electrónico Address toma un segundo y si la base de datos contiene 200 000 correos electrónicos Address es, el cálculo toma más de dos días (200 000 segundos). 

Para ir más rápido, tienes la capacidad de iniciar una tarea paralela por cada correo electrónico que Address encontró. 

Ejemplo de código: 

- Código inicial (antes de utilizar tareas paralelas): 
	
	```wl
	nAddressesInError is int
	
	Hourglass(True)
	ChronoStart()
	// Browses the list of customers
	FOR EACH Customer 	
		// Starts the statistical calculation on the email address
		IF CalculateStatEmailAddress(Customer.Email, 1) = False THEN
			nAddressesInError++
		END	
	END
	HourGlass(False)
	STC_Result_1 = StringBuild("Result: %1 addresses in error", nAddressesInError)
	Info("Process completed", DurationToString(ChronoEnd(), "MMm SSs CCC"))
	```


- Code thai está usando las tareas paralelas: 
	
	```wl
	nAddressesInError is int
	arrTasks is array of ParallelTasks
	ATask is ParallelTask
	
	Hourglass(True)
	ChronoStart()
	// Browses the list of customers
	FOR EACH Customer 
		// Starts the statistical calculation on the email address via a parallel task
		ATask = ParallelTaskExecute(CalculateStatEmailAddress, ...
				(Customer.Email, 1), ptoLightCopyHFSQLContext)
		// Stores this task in an array 
		Add(arrTasks, ATask)
	END
	
	// Waits for the end of task execution
	ParallelTaskWaitAll(arrTasks)
	HourGlass(False)
	
	// Browses the tasks
	FOR EACH ATask OF arrTasks
		IF ATask..ReturnedValue = False THEN
			nAddressesInError++
		END
	END
	STC_Result_2 = StringBuild("Result: %1 addresses in error", nAddressesInError)
	Info("Process completed", DurationToString(ChronoEnd(), "MMm SSs CCC"))
	```





<a name="NOTE5"></a>
<a name="NOTE5_1"></a>


## Ejemplo: Mejorar la reactividad de la aplicación
<a name="ejemplo_mejorar_reactividad_aplicacion_ELTTEXTE000579"></a>


### Mejorar la reactividad de la aplicación
<a name="mejorar_reactividad_aplicacion_ELTPARAGRAPHE000219"></a>

Un control Tabla muestra una lista de contactos cuya foto se carga desde un INTERNET Address. Para cada contacto, la aplicación realiza una solicitud de INTERNET (que desencadena una desaceleración). 

Para mejorar la reactividad de la aplicación y obtener una interfaz gráfica de usuario fluida, se inicia una tarea paralela en el Process para mostrar un fila de control Tabla. Esta tarea paralela: 

- recibe el identificador de Contact en el prameter. 

- realiza la solicitud de INTERNET a get el Image. 

- recupera el Image. 

- llama una función para actualizar el control Tabla. 




Veamos dos códigos diferentes para este ejemplo: estos dos códigos presentan dos métodos diferentes para actualizar la GUI: 

- el ejemplo 1 actualiza la GUI [mediante una tarea de continuación](#NOTE5_2). 

- el ejemplo 2 actualiza la GUI [a través de un Procedure ejecutado en el Thread principal](#NOTE5_3). 



<a name="NOTE5_2"></a>


### Actualización de la GUI mediante una tarea de continuación
<a name="actualizacion_gui_mediante_una_tarea_continuacion_ELTPARAGRAPHE000244"></a>

Al no ser posible la actualización de la interfaz desde una tarea paralela ejecutada en el Thread principal, se implementa una tarea de continuación específica para la visualización mediante [ParallelTaskExecuteAfter](../WDLang1/1000021214.md). Uno de los parámetros pasados a la tarea de continuación corresponde al valor devuelto por la tarea paralela principal.. Para especificar este parámetro, todo lo que tiene que hacer es utilizar la función ***ReturnedValuePreviousTask*** descriptor. 

Ejemplo de código: 

- Código para mostrar un tabla fila: 
	
	```wl
	// If the photo is not filled yet
	IF COL_Photo ~= "" THEN
		// Positions the hourglass image while waiting for the photo to be retrieved from "Internet"
		COL_Photo = IMG_Hourglass
		// Starts retrieving the photo in a parallel task
		MyTaskFindImage is ParallelTask = ParallelTaskExecute(FindImage, ...
			(COL_CustomerNum), ptoLightCopyHFSQLContext)
		// Starts displaying the image in a continuation task 
		// that interacts with the interface
		ParallelTaskExecuteAfter(MyTaskFindImage, DisplayImage, ...
				(COL_CustomerNum, ReturnedValuePreviousTask), ptoMainThread)
	END
	```


- Código de "FindImage" Procedure: Este Process se usa para recuperar el Image. 
	
	```wl
	PROCEDURE FindImage(LOCAL nCustomerID is 8-byte int)
	
	// Retrieve the photo
	Result1 is boolean = HTTPRequest("http://Linkedin.com/photos/id=" + ID)
	IF Result1 = True THEN
		bufPhoto is Buffer = HTTPGetResult()
		bufPhoto = fLoadBuffer(fExeDir() + fSep() + "Photos\" + ID + ".jpg")
	END
	
	RESULT bufPhoto
	```


- Código de "DisplayImage" Procedure: Este Procedure se usa para mostrar el Image en el tabla. 
	
	```wl
	PROCEDURE DisplayImage(nCustomerID is 8-byte int, sPhotoPath is string)
	// Finds the customer in the table
	nIndex is int = TableSearch("WIN_MENU.TABLE_Customer.COL_CustomerNum", nCustomerID)
	IF nIndex > 0 THEN
		// Displays the customer photo
		WIN_Menu.TABLE_Customer.COL_Photo[nIndex] = sPhotoPath
	END
	```




<a name="NOTE5_3"></a>


### Actualizando el GUI a través de una corrida de Procedure en el Thread principal
<a name="actualizando_gui_traves_una_corrida_procedure_thread_principal_ELTPARAGRAPHE000266"></a>

No se puede acceder a los controles de la ventana desde una tarea paralela. Para mostrar la imagen, el programa **DisplayImage** Procedure es ejecutado por [ExecuteMainThread](../WDLang1/1000019862.md). Esta función obliga a la ejecución de la Procedure en la thread principal. 

También tienes la capacidad de indicar al **DisplayImage** Procedure que siempre se ejecutará en el Thread principal. Lo único que tienes que hacer es pulsar el botón ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=Proc_auto_Ico.gif) en la barra del editor de código y marcar "Ejecutar en el Thread principal". 

Ejemplo de código: 

- Código para mostrar un tabla fila: 
	
	```wl
	// If the photo is not filled yet
	IF COL_Photo ~= "" THEN
		// Positions the hourglass image while waiting for the photo to be retrieved from "Internet"
		COL_Photo  = IMG_Hourglass
		// Starts retrieving the photo in a parallel task
		MyTaskFindImage is ParallelTask = ParallelTaskExecute(FindImage, ...
			(COL_CustomerNum), ptoLightCopyHFSQLContext)
	END
	```


- Código de "FindImage" Procedure: Este Process se usa para recuperar el Image.
	
	```wl
	PROCEDURE FindImage(LOCAL nCustomerID is 8-byte int)
	
	// Retrieve the photo
	Result1 is boolean = HTTPRequest("http://Linkedin.com/photos/id=" + ID)
	If Result1 = True THEN
		bufPhoto is Buffer = HTTPGetResult()
		bufPhoto = fLoadBuffer(fExeDir() + fSep() + "Photos\"+ ID + ".jpg")
	END
	
	// Calls the procedure to display the image
	ExecuteMainThread(DisplayImage, nCustomerID, bufPhoto)
	```


- Código de "DisplayImage" Procedure: Este Procedure se usa para mostrar el Image en el tabla. 
	
	```wl
	PROCEDURE DisplayImage(nCustomerID is 8-byte int, sPhotoPath is string)
	// Finds the customer in the table
	nIndex is int = TableSearch("WIN_MENU.TABLE_Customer.COL_CustomerNum", nCustomerID)
	IF nIndex > 0 THEN
		// Displays the customer photo
		WIN_Menu.TABLE_Customer.COL_Photo[nIndex] = sPhotoPath
	END
	```






