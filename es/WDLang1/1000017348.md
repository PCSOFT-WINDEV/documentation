


## Add (Función)



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Agrega un elemento en la última posición:

- de un array WLanguage unidimensional.

- de una propiedad array de tipo avanzado (array de eventos de gglCalendar, array de documentos de tipo procesador de texto, etc.).

- de una lista WLanguage.




Esta función también permite concatenar dos arrays o dos listas.

**Observaciones**: 

- Esta función es equivalente a la función [ArrayAdd](../WDLang1/3075003.md).





<a name="Example1"></a>
<a name="sample_code"></a>

## Ejemplo


```wl
MyArray is array of 2 strings
Add(MyArray, "WINDEV")
Add(MyArray, "WEBDEV")
Add(MyArray, "WINDEV and WEBDEV")
// Display the content of 3rd element (the first 2 elements are empty)
Trace(MyArray[3])  // Displays "WINDEV"
```


<a name="Example2"></a>

```wl
MyArray is array of 2 strings
MyArray[1] = "WINDEV"
MyArray[2] = "WEBDEV"
Add(MyArray, "WINDEV and WEBDEV")
// Display the content of 3rd element
Trace(MyArray[3]) // Displays "WINDEV and WEBDEV"
```


<a name="Example3"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) 
```wl
// Example to add an element into an XML file
NodeRSS is xmlNode,description="NEWS.rss.channel.item"
NodeRSS.title = DateToString(DateSys(), "DD/MM/YYYY") + " - " + ...
		TimeToSTring(TimeSys(), "HH:MM") + ": " + sTitle
NodeRSS.description = sComment		
NodeRSS.link = sHTTPLink		
NodeRSS.enclosure:type = "image/jpeg"
NodeRSS.enclosure:length = 150
NodeRSS.enclosure:url = sURL_Image

Add(MyXMLDoc.rss.channel, NodeRSS)
XMLSave(MyXMLDoc, gsPathXMLFile)
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Sintaxis

### Agregar un elemento a un array, a una propiedad array de un tipo avanzado o a una lista

`<Result> = Add(<WLanguage variable> [, <Value>])`
---

**`<Result>: (Integro)`**

Índice en el que se agregó el elemento especificado. Si se produce un error, se muestra un error fatal.

**`<WLanguage variable>: (Array o Lista)`**

Nombre de la variable que se utilizará. Esta variable puede ser:

- Una variable de tipo Array. 

- Una variable de tipo List.
	




**`<Value>: (Tipo de elementos del array, opcional)`**

Valor que se agregará al array o a la lista. Este parámetro es obligatorio para una lista.

Para un array, si no se especifica este parámetro, el array se amplía con el valor predeterminado del tipo de los otros elementos del array..  


<a name="SYNTAX2"></a>

### Concatenar arrays o listas

`<Result> = Add(<WLanguage variable> [, <Variable to concatenate>])`
---

**`<Result>: (Integro)`**

Índice en el que se agregó el elemento especificado. Si se produce un error, se muestra un error fatal.

**`<WLanguage variable>: (Array o Lista)`**

Nombre de la variable que se utilizará. Esta variable puede ser:

- Una variable de tipo Array. 

- Una variable de tipo List.
	




**`<Variable to concatenate>: (Array o lista opcional)`**

Nombre de la variable que se agregará después de los elementos que ya existen. Esta variable puede ser:

- Una variable de tipo Array, para concatenar dos arrays. 

- Una variable de tipo List, para concatenar dos listas.  
	






<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Observaciones


### Agregar un elemento a un array WLanguage
<a name="agregar_elemento_array_wlanguage_ELTPARAGRAPHE000189"></a>

Cuando se llama a la función **Add**:

- el array se amplía automáticamente para recibir el nuevo elemento.

- si es necesario, el elemento se convierte en el tipo de los otros elementos del array.




**Observación**: Cuando se declara un array de N por M elementos, este array contiene N elementos vacíos. Por ejemplo, el array que se declara a continuación contiene 3 cadenas vacías.


```wl
MyArray is array of 3 strings
```
Cuando se agrega un elemento (función **Add**), este elemento se agrega automáticamente después de los elementos que ya existen en el array.

En nuestro ejemplo, el elemento agregado corresponderá al 4º elemento.
<a name="NOTE0_2"></a>


### Agregar un elemento a una propiedad array de tipo avanzado
<a name="agregar_elemento_una_propiedad_array_tipo_avanzado_ELTPARAGRAPHE000212"></a>

Cuando se llama a la función **Add**:

- se debe crear la variable de tipo avanzado.

- el tipo avanzado debe tener un enumerador de tipo de colección modificable.

- el tipo avanzado se amplía automáticamente para recibir los nuevos elementos.

- el elemento se inicializa con el valor pasado como parámetro. Si no se pasa ningún valor en el parámetro, el elemento se inicializa con el valor Default del tipo de los elementos array.



<a name="NOTE0_3"></a>


### Agregar un elemento a una lista
<a name="agregar_elemento_una_lista_ELTPARAGRAPHE000228"></a>

Cuando se llama a la función **Add**:

- la lista se amplía automáticamente para recibir el nuevo elemento.

- si es necesario, el elemento se convierte en el tipo de los otros elementos de la lista.



<a name="NOTE0_4"></a>


### Agregar un array a otro array (concatenación)
<a name="agregar_array_otro_array_concatenacion_ELTPARAGRAPHE000246"></a>

Cuando se llama a la función **Add**:

- el array se amplía automáticamente para recibir los nuevos elementos. Los elementos del &lt;Nombre de la variable por concatenar&gt; se agregan al final de &lt;Nombre de la variable&gt;.

- los dos arrays deben ser del mismo tipo.

- los arrays deben tener la misma dimensión.

- las dimensiones de los arrays (excepto la primera dimensión) deben ser idénticas. 
	Por ejemplo: 
	Código correcto: 
	
	```wl
	t1 is array of * by 2 int = [[1,2],[3,4]]
	t2 is array of * by 2 int = [[5,6],[7,8],[9,10]]
	Add(t1, t2)
	```
Código incorrecto: 
	```wl
	t1 is array of * by 3 int = [[1,2],[3,4]]
	t2 is array of * by 2 int = [[5,6],[7,8],[9,10]]
	Add(t1, t2)
	```




<a name="NOTE0_5"></a>


### Agregar una lista a otra lista (concatenación)
<a name="agregar_una_lista_otra_lista_concatenacion_ELTPARAGRAPHE000277"></a>

Cuando se llama a la función **Add**:

- la lista se amplía automáticamente para recibir los nuevos elementos. Los elementos del &lt;Nombre de la variable por concatenar&gt; se agregan al final de &lt;Nombre de la variable&gt;.

- ambas listas deben ser del mismo tipo.



<a name="NOTE0_5_1"></a>


### Agregar el contenido de una estructura a un array de estructuras sin utilizar una variable de la estructura
<a name="agregar_contenido_una_estructura_array_estructuras_sin_utilizar_una_variable_estructura_ELTPARAGRAPHE000295"></a>

Si utiliza un array de estructuras con pocos miembros, puede ser más fácil agregar directamente una estructura utilizando el operador [ ] (corchetes). Por ejemplo, para la siguiente estructura:


```wl
// Structure to store a letter and its ASCII code
STKey is Structure
	sKey is string
	nAsciiCode is int
END
// Array of structures
arrKeys is array of STKey
```
En la mayoría de los casos, el contenido se agrega utilizando una variable de un tipo declarado en la estructura:

```wl
// Temporary structure for the addition
stAKey is STKey
// Stores the letter A
stAKey.sKey = "A"
stAKey.nAsciiCode = Asc("A")
Add(arrKeys, stAKey)
```
El operador [ ] le permite obtener una mejor legibilidad:

```wl
// Stores the letter A
Add(arrKeys, ["A", Asc("A")])
```

<a name="NOTE0_6"></a>


### Condiciones de uso
<a name="condiciones_uso_ELTPARAGRAPHE000327"></a>

Esta función se puede utilizar con las estructuras. En este caso, se debe:

1. Declarar una variable del mismo tipo que la estructura. 

2. Inicializar cada miembro.

3. Pasar la variable estructura como parámetro a la función **Add**.




Esta función no puede utilizarse:

- arrays no creados.

- arrays fijos.



<a name="NOTE0_7"></a>


### Varios
<a name="varios_ELTPARAGRAPHE000347"></a>



- Para agregar un elemento en una posición determinada, utilice la función [ArrayInsert](../WDLang1/3075009.md) o [Insertar](../WDLang1/1000017351.md).

- Para agregar un elemento a un array ordenado (respetando el orden), utilice la función [ArrayAddSorted](../WDLang1/1000017013.md).




<a name="XComponent"></a>

## Componente:
wd280vm.dll
