
## fTrackDirectory( Función)



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Detecta las modificaciones realizadas en el contenido de un directorio. Sólo se detectan las modificaciones realizadas en los archivos encontrados en el directorio. Si se realiza un cambio, se ejecuta una Procedure específica en un thread. 
<a name="Example1"></a>
<a name="sample_code"></a>

## Ejemplo


```wl
// Name of directory to track
sDirectoryName is string = "C:\Temp\MyDir"
// The fTrackDirectory_Callback procedure will be called 
// when a file or directory in the "C:\Temp\MyDir" directory is modified.  
IF fTrackDirectory(sDirectoryName, fTrackDirectory_Callback, ftCreateFile + ...
		ftModifyFile + ftDeleteFile + ftRename) THEN
	// Inform the user that the directory will be tracked
	Info("The " + sDirectoryName + " directory will be tracked.")
ELSE
	// Inform the user that the directory will not be tracked
	Info("The " + sDirectoryName + " directory will not be tracked.")
END
```



```wl
// Code of the procedure
PROCEDURE fTrackDirectory_Callback(sDirectoryName, sFileName, nAction, sOldFileName)
ExecuteMainThread(AddTable,sDirectoryName, sFileName, nAction, sOldFileName)
```



```wl
// Procedure that acts on the main thread
PROCEDURE AddTable(sDirectoryName, sFileName, nAction, sOldFileName)

sActionDesignation is string
// The designation of the action depends on nAction
SWITCH nAction
	CASE ftCreateFile: sActionDesignation = "Creating files"
	CASE ftDeleteFile: sActionDesignation = "Deleting files"
	CASE ftModifyFile: sActionDesignation = "Modifying files"
	CASE ftRename: sActionDesignation = "Renaming files"	
END
// Add a line containing the information about the modification
// into the TABLE_MODIFICATIONS table
TableAddLine(TABLE_MODIFICATIONS, sDirectoryName, sFileName, , ...
		sActionDesignation, sOldFileName)
```

<a name="XSYNTAX"></a>

## Sintaxis
<a name="SYNTAX1"></a>

`<Result> = fTrackDirectory(<Directory to track> , <WLanguage procedure> [, <Modifications to notify> [, <Subdirectory>]])`
---

**`<Result>: (booleano)`**



- <u><u><u><u>True</u></u></u></u> si se activó el seguimiento del directorio,

- <u><u><u><u>False</u></u></u></u> en caso contrario. Para obtener más información sobre el error, utilice la función [ErrorInfo](../WDLang1/3013008.md).




**`<Directory to track>: (Cadena de caracteres)`**

Nombre completo del directorio a rastrear.

**`<WLanguage procedure>: (Nombre de Procedure)`**

Nombre del WLanguage Procedure ("callback") llamado cuando se realiza un cambio en el directorio especificado.

Para obtener más información sobre este procedimiento, consulte [Parámetros del procedimiento utilizado por la función fTrackDirectory](../WDLang1/1000025350.md).

**`<Modifications to notify>: (Constante o combinación de constantes de tipo Integer (opcional))`**

Modificaciones realizadas en el contenido del directorio a rastrear y para las cuales el Procedure debe ser ejecutado: 


|   |   |
| --- | --- |
| *ftAll* | Se hará un seguimiento de todas las acciones posibles. Corresponde a *ftCreateFile* + *ftModifyFile* + *Rename* + *ftDeleteFile*. |
| *ftCreateFile* | Creación de un fichero o de un directorio. |
| *ftDeleteFile* | Eliminar un archivo o un directorio. |
| *ftModifyFile* | Modificar un fichero o un directorio. |
| *ftRename* | Renombrar un archivo o un directorio. |



**`<Subdirectory>: (booleano)`**



- <u><u><u><u>True</u></u></u></u> (valor predeterminado) para administrar los subdirectorios. 

- <u><u><u><u>False</u></u></u></u> en caso contrario. 






<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Observaciones


### Casos especiales
<a name="casos_especiales_ELTPARAGRAPHE000090"></a>

- **fTrackDirectory** sólo controla el contenido del directorio. Se ignoran los cambios de nombre o ubicación del directorio.  

- Para dejar de rastrear el directorio, utilice [fTrackStop](../WDLang1/1000020575.md) y [fTrackStopTodos](../WDLang1/1000020576.md).

- Para hacer un seguimiento de las modificaciones realizadas en un fichero, utilice [fTrackFile](../WDLang1/1000020522.md). 

- Limitación: Sólo 5 directorios pueden ser rastreados al mismo tiempo. 



<a name="NOTE0_2"></a>


### Procedimiento WLanguage
<a name="procedimiento_wlanguage_ELTPARAGRAPHE000111"></a>

La WLanguage Procedure se ejecuta en una WLanguage thread.
Atención: Los siguientes procesos no se pueden ejecutar en los hilos:

- abrir ventanas con funciones WLanguage como [entablar](../WDLang1/3038035.md), [aprovechamiento](../WDLang1/3038044.md), [Cerrar](../WDLang1/3038018.md), etc., ... Se debe implementar un modo de gestión específico si algunas ventanas deben ser manejadas en hilos (caso raro). Para obtener más información, consulte [Abrir una ventana en una thread secundaria](../WDLang1/3077015.md). 

- gestionar eventos.

- gestionar timers.




Para más detalles sobre los hilos, véase [Gestión de hilos](../WDLang1/3077026.md).





<a name="XComponent"></a>

## Componente:
wd280std.dll
