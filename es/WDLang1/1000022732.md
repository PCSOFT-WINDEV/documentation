


## DocInsert (Función)



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Inserta un objeto en un procesador de textos Document o sustituye el contenido del fragmento actual. Los objetos que se pueden utilizar son: 

- an element,

- un Document,

- un párrafo,

- un mensaje de texto,

- una imagen, etc, 

- un fragmento, 

- una tabla de contenidos.





<a name="Example1"></a>
<a name="sample_code"></a>

## Ejemplo


```wl
// Inserts an image cursor location
// Select the image file
sFile is string 
sFile = fImageSelect(fCurrentDir(), "", ...
	"Insert an image","JPEG (*.JPG)" + TAB + "*.jpg" + CR + ...
	"PNG (*.PNG)" + TAB + "*.png" + CR + "All files (*.*)" + TAB + "*.*")
	
IF sFile = "" THEN
	RETURN
END

// The image file is transferred into an Image variable
img is Image
img = sFile

// Declare an Image element (that will be inserted into the document)
elt is docElement
elt.Type = typeDocElementImage
elt.Image.BufferImage = img
elt.Image.Wrapping = docWrappingAlignedOnText


// Calculate the width and height of the image found in the element
// The image size is expressed in pixels in the Image variable. 
// This size is converted into mm in the element (unit used in a document)
elt.Image.Width = img.Width * 25.4 / 96.0
elt.Image.Height = img.Height * 25.4 / 96.0
// Image position
elt.Image.X = 0
elt.Image.Y = 0
	
DocInsert(WP_MyDocument, WP_MyDocument.Cursor, elt)
```

```wl
doc is Document <- WP_MyDocument..Value
// Find the bookmark position 
fragmentBookmark is docFragment = doc..Bookmark["Bookmark 1"]	
IF fragmentBookmark <> Null THEN
	// Insert text at the end of bookmark
	let nInsertionPosition = fragmentBookmark..EndPosition
	// Actual insertion
	DocInsert(WP_MyDocument, nInsertionPosition, "Text to insert at bookmark position")
ELSE
	Erreur("'Bookmark 1' bookmark not found in the document")
END
```

<a name="XSYNTAX"></a>

## Sintaxis
<a name="SYNTAX1"></a>

### Insertar un objeto en una posición

`<Result> = DocInsert(<Document> , <Position> , <Element>)`
---

**`<Result>: (Variable de tipo DocFragment)`**

Variable de tipo [docFragment](../WDLang1/1000022482.md) que contiene el fragmento insertado. 

**`<Document>: (Documento Variable o cadena de caracteres)`**

Document para usar. Este Document corresponde a: 

- ![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) el nombre de un control Procesador de texto.

- una variable de tipo [Document](../WDLang1/1000022461.md).




**`<Position>: (Integro)`**

Posición en la que se insertará el elemento. Esta posición se expresa en número de caracteres. 

**`<Element>: (Tipo correspondiente al elemento a insertar)`**

Elemento a insertar: 

- aspecto: Nombre de la [docElement](../WDLang1/1000022484.md) Variable que corresponde al elemento a insertar. 

- Document: Nombre de la variable [Document](../WDLang1/1000022461.md) que corresponde al Document a insertar. 

- Párrafo: Nombre de la [docParagraph](../WDLang1/1000022483.md) Variable que corresponde al párrafo a insertar. 

- Texto en formato de cadena de caracteres: Texto a insertar.
	Puede utilizar las siguientes constantes en el texto de inserción para gestionar las diferentes pausas posibles: 
	


|   |   |
| --- | --- |
| *docColumnBreak* | Añade un salto de columna en una sección multicolumna. Si la sección no es multicolumna, se añade un salto de página. |
| *docLineBreak* | Añade una ruptura de Line. |
| *docPageBreak* | Añade un salto de página. |
| *docParagraphBreak* | Añade una ruptura de párrafo a. |



- Image: Nombre de la [Image](../WDLang1/1000019650.md) Variable que se insertará en la posición especificada.

- Fragment: Nombre de la [docFragment](../WDLang1/1000022482.md) Variable que se insertará en la posición especificada.  





<a name="SYNTAX2"></a>

### Sustitución de un fragmento por un objeto (texto, Document, fragmento, párrafo, ...)

`<Result> = DocInsert(<Fragment> , <Object to use>)`
---

**`<Result>: (Variable de tipo DocFragment)`**

Variable de tipo [docFragment](../WDLang1/1000022482.md) que contiene el fragmento insertado. 

**`<Fragment>: (Variable de tipo DocFragment)`**

Nombre de la variable de tipo [docFragment](../WDLang1/1000022482.md) que se utilizará. El contenido de este fragmento será sustituido por el objeto especificado. 

**`<Object to use>: (Variable correspondiente al tipo de objeto)`**

Objeto que sustituye al contenido del fragmento. Este objeto puede corresponder a: 

- una cadena de caracteres. 
	Puede utilizar las siguientes constantes en el texto para gestionar las diferentes pausas posibles: 
	


|   |   |
| --- | --- |
| *docColumnBreak* | Añade un salto de columna en una sección multicolumna. Si la sección no es multicolumna, se añade un salto de página. |
| *docLineBreak* | Añade una ruptura de Line. |
| *docPageBreak* | Añade un salto de página. |
| *docParagraphBreak* | Añade una ruptura de párrafo a. |






- el nombre de una variable de tipo [docElement](../WDLang1/1000022484.md). 

- el nombre de una variable de tipo [Document](../WDLang1/1000022461.md). 

- el nombre de una variable de tipo [docParagraph](../WDLang1/1000022483.md). 

- el nombre de una variable de tipo [Image](../WDLang1/1000019650.md).

- el nombre de una variable de tipo [docFragment](../WDLang1/1000022482.md). 

- el nombre de una variable de tipo [docTableOfContents](../WDLang1/1000024893.md).






<a name="XComponent"></a>

## Clasificación Lógica de negocio / UI:
Código UI
## Componente:
wd280mdl.dll
