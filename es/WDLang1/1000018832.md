
## Método COMCall (Función)



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Ejecuta un método de una interfaz de un objeto COM instanciado por [COMCreateInstance](../WDLang1/1000018831.md).
<a name="Example1"></a>
<a name="sample_code"></a>

## Ejemplo


```wl
// Initialize a Unicode string containing 260 characters
WallPaper is Unicode string
WallPaper = AnsiToUnicode(RepeatString(" ", 260))

// Declare a COMObject variable
ActiveDesktop is COMObject

// Retrieve the interface for handling the desktop
ActiveDesktop = COMCreateInstance("75048700-EF1F-11D0-9888-006097DEACF9", ...
		"F490EB00-1240-11D1-9888-006097DEACF9")
IF ErrorOccurred = False THEN
	// Retrieve the image of the desktop wallpaper
	COMCallMethod(ActiveDesktop, 4, &WallPaper, 260, 1)

	Info("The image of the desktop is: " + UnicodeToAnsi(WallPaper))
END
```

<a name="XSYNTAX"></a>

## Sintaxis
<a name="SYNTAX1"></a>

`<Result> = COMCallMethod(<COM Object> , <Method number> [, <Parameter 1> [... [, <Parameter N>]]])`
---

**`<Result>: (Un entero de 4 bytes en 32 bits, un entero de 8 bytes en 64 bits)`**

Resultado de la ejecución del método. Este resultado puede ser un código de error. 
La clase de este resultado depende de la ejecución del método. Para más detalles, consulte la documentación sobre el método. 
El resultado del método no se recuperará si su tamaño excede el tamaño del entero definido para la plataforma.

**`<COM Object>: (COMObject Variable)`**

Interfaz del objeto COM devuelto por [COMCreateInstance](../WDLang1/1000018831.md).

**`<Method number>: (Integro)`**

Número de orden del método en el tabla de métodos virtuales. 

**ATENCIÓN**:

- el número de orden tiene en cuenta las clases base

- el número de orden empieza por 0.




**`<Parameter 1>: (El tipo correspondiente al parámetro)`**

Primer parámetro que se pasará al método. Estos parámetros y los parámetros esperados por el método deben ser del mismo tipo. Se puede utilizar: 

- los tipos "simples" (véase las Notas),

- las estructuras (véase las Notas),

- un nombre de WLanguage Procedure. Esta Procedure se llamará por el método (ver las Notas).






**`<Parameter N>: (El tipo correspondiente al parámetro)`**

Nth parámetro que se pasará al método. Estos parámetros y los parámetros esperados por el método deben ser del mismo tipo. Se puede utilizar: 

- los tipos "simples" (véase las Notas),

- las estructuras (véase las Notas),

- un nombre de WLanguage Procedure. Esta Procedure se llamará por el método (ver las Notas).










<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Observaciones




### Controles realizados
<a name="controles_realizados_ELTPARAGRAPHE000064"></a>

**Método COMCall** está protegida contra los "fallos de protección general" en el método denominado. Sin embargo, un error WLanguage se activa si se produce este tipo de error.
<a name="NOTE0_2"></a>




### Gestión de errores
<a name="gestion_errores_ELTPARAGRAPHE000074"></a>

Al ejecutar los métodos, si el resultado devuelto corresponde a un error, utilice la función [ErrorInfo](../WDLang1/3013008.md) (con la constante *errSystemCode* o *errSystemMessage*) para obtener los detalles del error.
<a name="NOTE0_3"></a>




### Parámetros que se deben pasar al método del objeto COM
<a name="parametros_que_deben_pasar_metodo_del_objeto_com_ELTPARAGRAPHE000085"></a>

Estos parámetros y los parámetros esperados por el método deben tener el mismo tipo.

Los tipos disponibles son los siguientes:

- Los tipos "simples" (entero, real y booleano). El uso de otro tipo de WLanguage desencadena un error de WLanguage. 
	Si el método a ejecutar espera un puntero de Windows o manipular, utilice un [system integer](../Motscles/1514049.md). 
	Si el método a ejecutar espera una Address, utilice [el & operador](../Motscles/1512002.md).

- Los tipos de "cadena. 

- Los tipos de estructura.

- Un nombre de WLanguage Procedure. Esta Procedure será llamada por el método del objeto COM (ver el párrafo siguiente). 
	Los parámetros dependen de la ejecución del método. Consulte la documentación sobre este método para obtener más detalles.



<a name="NOTE0_4"></a>




### Pasar una cadena a un método de objeto COM o recuperar una cadena después de la llamada a un método de objeto COM
<a name="pasar_una_cadena_metodo_objeto_com_recuperar_una_cadena_despues_llamada_metodo_objeto_com_ELTPARAGRAPHE000112"></a>

1. En el parámetro de entrada, utilice el tipo de cadena. Por ejemplo:
	
	```txt
	sString is string 
	COMCallMethod(<Object>, <Method number>, sString)
	```


2. En el parámetro de salida, el lenguaje C no puede gestionar fácilmente las cadenas dinámicas. Por lo tanto, debes hacerlo:

	- definir un tamaño máximo, con el tipo "String on. Por ejemplo:
			
		```txt
		sString is string on 100
		COMCallMethod(<Object>, <Method number>, sString) 
		// in Method in C: 
		// strncpy(PointerInC, "Test", 100)
		```


	- recuperar las direcciones de las cadenas en C (sin embargo, en este caso, la sección de código en C debe "preservar" las cadenas devueltas), luego transferir la cadena a una cadena Variable con [StringRetrieve](../WDLang1/3024005.md). Por ejemplo:
			
		```txt
		nStringAddress is system int
		COMCallMethod(<Object>, <Method number>, &nStringAddress)
		sString is string
		sString = StringRetrieve(nStringAddress, srASCIIZAddress)
		// in Method in C: *PointerInC = "Test"
		```





3. En el valor de retorno, recupera las direcciones de las cadenas en C (sin embargo, en este caso, la sección de código en C debe "preservar" las cadenas devueltas), luego transfiere la cadena en una cadena Variable con [StringRetrieve](../WDLang1/3024005.md). Por ejemplo:
	
	```txt
	nStringAddress is system int
	nStringAddress = COMCallMethod(<Object>, <Method number>)
	sString is string
	sString = StringRetrieve(nStringAddress, srASCIIZAddress)
	// in Method in C: Return PointerInC
	```




<a name="NOTE0_5"></a>




### Pasar una estructura que contiene una cadena a un método de objeto COM
<a name="pasar_una_estructura_que_contiene_una_cadena_metodo_objeto_com_ELTPARAGRAPHE000139"></a>

1. En la entrada se debe utilizar el siguiente código: 
	
	```txt
	Struct is structure
		sString is string
	END
	AStruct is Struct
	COMCallMethod(<Object>, <Method number>, &AStruct)
	```


2. En salida, el lenguaje C no gestiona fácilmente las cadenas dinámicas. Por lo tanto, debes hacerlo:

	- definir un tamaño máximo y realizar una copy en la memoria de WLanguage. Por ejemplo:
			
		```txt
		sString is string on 100
		Struct is structure
			aString is int
		END
		AStruct is Struct
		AStruct:aString = &sString
		COMCallMethod(<Object>, <Method number>, &AStruct) 
		// in Method in C: 
		// strncpy(StructInC->PointerInC, "Test", 100)
		```


	- recuperar la Address de cuerdas en Do (sin embargo, en este caso, la sección de código en Do debe "preservar" las cuerdas devueltas). Por ejemplo:
			
		```txt
		sString is string
		Struct is structure
			aString is int
		END
		AStruct is Struct
		COMCallMethod(<Object>, <Method number>, &AStruct)
		sString = StringRetrieve(AStruct:aString, srASCIIZAddress)
		// in Method in C: StructInC->PointerInC = "Test"
		```







<a name="NOTE0_6"></a>




### Procedure llamó en CallBack
<a name="procedure_llamo_callback_ELTPARAGRAPHE000157"></a>

Algunos métodos esperan la dirección de una "Callback" Procedure como parámetro: esta Procedure será llamada por el método del objeto COM.  

Para usar una llamada de Procedure en WLanguage:

**1.** Crea la llamada Procedure en tu proyecto
Para recuperar los parámetros, debe describir exactamente los parámetros esperados por la función "CallBack".. De lo contrario, pueden producirse "fallos generales de protección"..

```txt
PROCEDURE <Procedure name> (<Param1> is <Type1>,<Param2> is <Type2>)
```


**Observaciones**:

- Los tipos deben corresponder a los descritos en la documentación del objeto COM.

- Los parámetros deben necesariamente ser pasados por valor. Para recuperar un parámetro de reference:

	1. Utilice un número entero.

	2. Con [Transferencia](../WDLang1/3014015.md), recupera o asigna el valor real.







**2.** Modificar la llamada al método en consecuencia. Utilice la siguiente sintaxis:

```txt
COMCallMethod(<Object>, <Method number>, &<Name of CallBack procedure>)
```

<a name="NOTE0_7"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Código de Usuario (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png) 

### Varios
<a name="varios_ELTPARAGRAPHE000189"></a>

- **Método COMCall** no bloquea otros hilos.

- Si el método llamado modifica los parámetros regionales del sistema, se restauran los parámetros regionales anteriores.




[APIParameter](../WDLang1/3014026.md) se utiliza para configurar el comportamiento de default de esta función.





<a name="XComponent"></a>

## Componente:
wd280vm.dll
