
## InputMask (Tipo de variable)

*** ***
				



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
El tipo **InputMask** permite definir las características avanzadas de una máscara de entrada personalizada. Las características de esta máscara de entrada personalizada se pueden definir y cambiar mediante diferentes propiedades WLanguage. 

**Observación**: Para obtener más información sobre la declaración de este tipo de variable y el uso de propiedades WLanguage, consulte [Declarar una variable](../Motscles/1514032.md).


<a name="Example1"></a>
<a name="sample_code"></a>

## Ejemplo


```wl
m_sMask is string = "99 99 99 99 99"
CustomMask is InputMask
CustomMask.FormatDuringInput = FormatDuringInput
CustomMask.ValidateDuringInput = ValidateDuringInput
CustomMask.ValidateDuringExit = ValidateDuringExit
CustomMask.FormatDuringExit = FormatDuringExit
CustomMask.FormatDuringEntry = FormatDuringEntry
CustomMask.FormatDuringAssignment = FormatDuringAssignment
CustomMask.CheckIfValidInput = CheckIfValidInput

EDT_Phone.InputMask = CustomMask

INTERNAL PROCEDURE FormatDuringInput(LOCAL sTextBefore is string, 
	LOCAL nCursorBefore is int, ...
	LOCAL nEndCursorBefore is int, ...
	sTextAfter is string, ...
	nCursorAfter is int, ...
	nEndCursorAfter int)
	IF Length(sTextBefore) > Length(sTextAfter) THEN RESULT 
	//No action if deleting
	// if at the end
	IF nEndCursorAfter = Length(sTextAfter) +1 THEN
		// include " " if needed at this location
		IF Middle(m_sMask, nEndCursorAfter, 1) = " " THEN
			sTextAfter += " "
			nEndCursorAfter ++
			nCursorAfter = nEndCursorAfter
		END
	END
END	

INTERNAL PROCEDURE ValidateDuringInput(sText is string): boolean
	RESULT MatchRegularExpression(sText , "[0-9 ]*")
END

INTERNAL PROCEDURE CheckIfValidInput(sText is string): boolean
	RESULT ValidateDuringInput(sText) _AND_ Length(sText) >= 3
END

INTERNAL PROCEDURE FormatDuringAssignment(sText_INOUT is string) 
		FormatStringToPhoneNumber(sText_INOUT)
END

INTERNAL PROCEDURE ValidateDuringExit(sText is string): boolean
	IF Length(sText) < 3 THEN
		ToastDisplay("3 characters minimum")
		RESULT False
	END
	RETURN True
END

INTERNAL PROCEDURE FormatDuringExit(sText_INOUT is string) 
	FormatStringToPhoneNumber(sText_INOUT)
END

INTERNAL PROCEDURE FormatDuringEntry(sText_INOUT is string) 
	FormatStringToPhoneNumber(sText_INOUT)
END

INTERNAL PROCEDURE FormatStringToPhoneNumber(sText_INOUT is string)
	// group numbers like in the mask
	sText_INOUT = Replace(sTexte_INOUT," ","")
	sResult is string
	nPosSrc is int = 1
	FOR I = 1 _TO_ Length(m_sMask)
		// depending on the character in the mask
		SWITCH Middle( m_sMask, I, 1) 
			CASE " "	
				sResult += " "
			OTHER CASE
				// keep the entered character
				sResult += Middle(sText_INOUT, nPosSrc, 1) 
				nPosSrc++
		END
	END
	sText_INOUT = sResult
END
```





<a name="NOTE0"></a>

## Observaciones
<a name="NOTE0_1"></a>


### Propiedades específicas de la descripción de variables de tipo InputMask
<a name="propiedades_especificas_descripcion_variables_tipo_inputmask_ELTPARAGRAPHE000046"></a>

Las siguientes propiedades pueden utilizarse para manipular una máscara de entrada personalizada:

| Nombre de la propiedad | Tipo utilizado | Efecto |
| --- | --- | --- |
| CheckIfValidInput | Procedimiento WLanguage | Nombre del procedimiento WLanguage llamado por las funciones [InvalidInputDetect](../WDLang1/1000026180.md) y [InvalidInputListControl](../WDLang1/1000026244.md) para determinar si el texto introducido es válido.<br><br>Este procedimiento tiene el siguiente formato: <br><br><pre><code>PROCEDURE <Procedure name>(<Text> string)</code></pre><br>donde &lt;Texto&gt; corresponde al texto a manipular. <br><br>Si no se especifica esta propiedad, no se realiza ninguna verificación. |
| FormatDuringAssignment | Procedimiento WLanguage | Nombre del procedimiento WLanguage que convierte el texto asignado mediante programación en el texto que se mostrará.<br><br>Este procedimiento tiene el siguiente formato: <br><br><pre><code>PROCEDURE <Procedure name>(<Text> string)</code></pre><br>donde &lt;Texto&gt; corresponde al texto a manipular. <br><br>Si no se especifica esta propiedad, no se realiza ninguna transformación. |
| FormatDuringEntry | Procedimiento WLanguage | Nombre del procedimiento WLanguage que transforma el texto que se muestra al entrar en edición.<br><br>Este procedimiento tiene el siguiente formato: <br><br><pre><code>PROCEDURE <Procedure name>(<Text> string)</code></pre><br>donde &lt;Texto&gt; corresponde al texto a manipular. <br><br>Si no se especifica esta propiedad, no se realiza ninguna transformación. |
| FormatDuringExit | Procedimiento WLanguage | Nombre del procedimiento WLanguage que transforma el texto al salir de la edición. <br><br>Este procedimiento tiene el siguiente formato: <br><br><pre><code>PROCEDURE <Procedure name>(<Text> string)</code></pre><br>donde &lt;Texto&gt; corresponde al texto a manipular. <br><br>Si no se especifica esta propiedad, no se realiza ninguna transformación. |
| FormatDuringInput | Procedimiento WLanguage | Nombre del procedimiento WLanguage que transforma el texto durante la entrada.<br><br>Este procedimiento tiene el siguiente formato: <br><br><pre><code>PROCEDURE <Procedure name>(LOCAL<Text before> string, <br>LOCAL <Cursor before> int, <br>LOCAL <End Cursor before> int, <br><Text after> string, <Cursor after> int, <br><End Cursor after> int)</code></pre><br>donde: <br><br>- &lt;Texto antes&gt; corresponde al texto antes de la entrada.<br><br>- &lt;Cursor antes&gt; corresponde a la posición del cursor antes de la entrada.<br><br>- &lt;Fin cursor antes&gt; corresponde a la posición final del cursor antes de la entrada. <br><br>- &lt;Texto después&gt; corresponde al texto después de la entrada.<br><br>- &lt;Cursor después&gt; corresponde a la posición del cursor después de la entrada.<br><br>- &lt;Fin cursor después&gt; corresponde a la posición final del cursor después de la entrada. <br><br><br><br><br>Si no se especifica esta propiedad, no se realiza ninguna transformación. |
| ValidateDuringExit | Procedimiento WLanguage | Nombre del procedimiento WLanguage que verifica la entrada (una vez que se han introducido todos los caracteres). Este procedimiento devuelve <u><u><u><u>False</u></u></u></u> si el texto introducido contiene caracteres no válidos. <br><br>Se llamará a este procedimiento al salir del control solo si la validación de la entrada está en modo bloqueante. Para obtener más información, consulte [Entrada obligatoria o no válida en WINDEV](../WDChamp/9500154.md).<br><br>Este procedimiento tiene el siguiente formato: <br><br><pre><code>PROCEDURE <Procedure name>(<Text> string)</code></pre><br>donde &lt;Texto&gt; corresponde al texto a manipular. <br><br>Si no se especifica esta propiedad, no se realiza ninguna transformación. |
| ValidateDuringInput | Procedimiento WLanguage | Nombre del procedimiento WLanguage que verifica la entrada en curso. Este procedimiento devuelve <u><u><u><u>False</u></u></u></u> si el texto que se está introduciendo contiene caracteres no válidos. <br><br>Este procedimiento tiene el siguiente formato: <br><br><pre><code>PROCEDURE <Procedure name>(<Text> string)</code></pre><br>donde &lt;Texto&gt; corresponde al texto a manipular. <br><br>Si no se especifica esta propiedad, no se realiza ninguna transformación. |


<a name="NOTE0_2"></a>


### Uso
<a name="uso_ELTPARAGRAPHE000183"></a>

- Una variable de tipo **InputMask** puede ser asignada a la propiedad [InputMask](../Proprietes/2510033.md) para controlar de forma personalizada cada evento relacionado con la gestión de una máscara en un control Campo de entrada.

- Las funciones [MaskPhoneNumber](../WDLang1/1000024741.md) y [MaskZipCode](../WDLang1/1000024742.md) manipulan variables de tipo **InputMask**. 





