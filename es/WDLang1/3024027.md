
## Position (Función)



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Busca la posición de una cadena especificada dentro de otra cadena (o Buffer).

También puede buscar la posición de una de las cadenas en un array. 




<a name="Example1"></a>
<a name="sample_code"></a>

## Ejemplo


```wl
n is int
MyString is string = "What a wonderful World"
n = Position(MyString, "won", 1, FromBeginning)
// Returns 8 (position of "w" in "wonderful")
n = Position(MyString, "XXX")
// Returns 0 (no "XXX" found in the string)
n = Position(MyString, "l", 0, FromEnd)
// Returns 21 (position of "l" in "World")
n = Position(MyString, "e", 11)
// Returns 12 (position of "e" in "wonderful")
n = Position(MyString, "o", Length(MyString), FromEnd)
// Returns 19 (position of "o" in "World")
n = Position(MyString, "o", n-1, FromEnd)
// Returns 9 (position of "o" in "wonderful")
n = Position(MyString, "o", n-1, FromEnd)
// Returns 0 (there is no other "o")
```


<a name="Example2"></a>



```wl
n is int
MyString2 is string = "http://Server/File.html"
n = Position(MyString2,["/","//"])	
// Returns 6
n = Position(MyString2,["/","//"], 8)
// Returns 15
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Sintaxis

### Buscar la posición de una cadena especificada dentro de otra cadena

`<Result> = Position(<Initial string> , <Search string> [, <Start position> [, <Option>]])`
---

**`<Result>: (Entero)`**



- Posición del primer carácter de la cadena buscada en la cadena inicial,

- 0 si no se encuentra la cadena buscada. 
	Esta posición se busca desde el inicio de la cadena.




**`<Initial string>: (Cadena de caracteres)`**

Cadena en la que se realizará la búsqueda (tamaño máximo: 2 GB).

**`<Search string>: (Cadena de caracteres)`**

Cadena que se buscará en la cadena inicial.

**`<Start position>: (Entero opcional)`**

Índice del carácter a partir del cual iniciará la búsqueda. De forma predeterminada, la búsqueda comienza desde el primer carácter (carácter 1). 
Para iniciar la búsqueda desde el final de la cadena (constante *FromEnd*), este parámetro debe corresponder a 0 o a la longitud de la cadena. 
Si este parámetro es negativo, la búsqueda comienza desde el primer carácter. 
Si el valor de este parámetro es mayor que el número de caracteres en &lt;Cadena inicial&gt;, &lt;Resultado&gt; es igual a 0.

**`<Option>: (Constante o combinación de constantes opcional)`**

Indica la dirección y las características de la búsqueda:


|   |   |
| --- | --- |
| *FromBeginning*<br>(Valor predeterminado) | Busca del primer al último carácter de la cadena. |
| *FromEnd* | Busca del último al primer carácter de la cadena. |
| *IgnoreCase* | Busca sin distinguir entre mayúsculas, minúsculas o acentos.<br><br>![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png) Esta opción no tiene ningún efecto. |
| *WholeWord* | Busca una palabra completa, es decir, una palabra entre caracteres de puntuación o espacios.<br><br>![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png) Esta opción no tiene ningún efecto. |




<a name="SYNTAX2"></a>

### Buscar una cadena en un array de cadenas

`(<Result>, <Index>) = Position(<Initial string> , <Array of search strings> [, <Start position> [, <Option>]])`
---

**`<Result>: (Entero)`**



- Posición del primer carácter de la primera cadena del array encontrada en la cadena inicial. 
	Esta posición se busca desde el inicio de la cadena.

- 0 si no se encontró ninguna de las cadenas buscadas. 




**`<Index>: (Entero)`**

Índice de la cadena en el array.

**`<Initial string>: (Cadena de caracteres)`**

Cadena en la que se realizará la búsqueda (tamaño máximo: 2 GB).

**`<Array of search strings>: (Array)`**

Array que contiene las cadenas a buscar en la cadena inicial.

**`<Start position>: (Entero opcional)`**

Índice del carácter a partir del cual iniciará la búsqueda. De forma predeterminada, la búsqueda comienza desde el primer carácter (carácter 1). Para iniciar la búsqueda desde el final de la cadena, este parámetro debe corresponder a 0 o a la longitud de la cadena. 
Si este parámetro es negativo, la búsqueda comienza desde el primer carácter. 
Si el valor de este parámetro es mayor que el número de caracteres en &lt;Cadena inicial&gt;, &lt;Resultado&gt; es igual a 0.

**`<Option>: (Constante o combinación de constantes opcional)`**

Indica la dirección y las características de la búsqueda:


|   |   |
| --- | --- |
| *FromBeginning*<br>(Valor predeterminado) | Busca del primer al último carácter de la cadena. |
| *FromEnd* | Busca del último al primer carácter de la cadena. |
| *IgnoreCase* | Busca sin distinguir entre mayúsculas, minúsculas o acentos.<br><br>![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png) Esta opción no tiene ningún efecto. |
| *WholeWord* | Busca una palabra completa, es decir, una palabra entre caracteres de puntuación o espacios.<br><br>![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png) Esta opción no tiene ningún efecto. |





<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Observaciones


### Encontrar una cadena de caracteres
<a name="encontrar_una_cadena_caracteres_ELTPARAGRAPHE000178"></a>

De forma predeterminada, la búsqueda distingue entre mayúsculas y minúsculas: las mayúsculas y minúsculas de la cadena buscada deben ser idénticas a las de la subcadena en la cadena inicial. Para realizar una búsqueda que no distinga entre mayúsculas y minúsculas, utilice la constante *IgnoreCase*.

Las posiciones en una cadena de caracteres siempre se dan desde el inicio de la cadena, independientemente de la dirección de la búsqueda.

Para utilizar la función **Position** con la constante *FromEnd*, se debe iniciar en la posición calculada por Length(&lt;Cadena inicial&gt;). Por ejemplo:


```wl
Position(MyString, "\", Length(MyString), FromEnd)
```


También se puede utilizar la siguiente sintaxis. En este caso, el parámetro 0 permite que la función seleccione la posición inicial más adecuada.


```wl
Position(MyString, "\", 0, FromEnd)
```

<a name="NOTE0_2"></a>




### Unicode/Ansi
<a name="unicodeansi_ELTPARAGRAPHE000198"></a>

La función **Position** permite buscar la posición de una cadena determinada en una cadena Unicode o Ansi.

Se pueden usar cadenas Ansi y/o Unicode en los parámetros &lt;Cadena inicial&gt; y &lt;Cadena buscada&gt; (puede haber un parámetro Unicode y un parámetro Ansi).

La regla de conversión utilizada es la siguiente:

- Si al menos una de las cadenas es Unicode, todas las cadenas se convierten a Unicode y la operación se realiza en este estándar.

- De lo contrario, la operación se realiza en Ansi.  



<a name="NOTE0_3"></a>


### Caracteres que se tienen en cuenta para la puntuación y los espacios
<a name="caracteres_que_tienen_cuenta_para_puntuacion_los_espacios_ELTPARAGRAPHE000363"></a>Los caracteres que se tienen en cuenta para la puntuación y los espacios son proporcionados por el sistema. Para obtener la lista de estos caracteres, escriba el siguiente código WLanguage:

```wl
s is string
FOR i = 0 TO 255
	IF Charact(i) <> StringFormat(Charact(i), ccIgnorePunctuationAndSpace) THEN
		 s += Charact(i)
	END
END
Info(s)
ToClipboard(s)
```

<a name="NOTE0_4"></a>


### Varios
<a name="varios_ELTPARAGRAPHE000219"></a>

La función [PositionOccurrence](../WDLang1/3024029.md) permite obtener la posición de la enésima instancia de una subcadena buscando desde el inicio o desde el final de la cadena.

<a name="XComponent"></a>

## Clasificación Lógica de negocio / UI:
Código neutro
## Componente:
wd280vm.dll
