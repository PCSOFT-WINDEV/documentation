


## Compile( Función)



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Compila dinámicamente una Procedure cuyo código fuente es suministrado. La Procedure creada y compilada es una Procedure global al proyecto.

Observación: [ExecuteCode](../WDLang1/1000019783.md) y [EvaluarExpresión](../WDLang1/1000019782.md) también permiten utilizar un código generado dinámicamente. 


<a name="sample_code"></a>

## Ejemplo

<a name="Example1"></a>

```wl
// Syntax 1: Dynamic compilation of a global procedure
// Source: control in which the procedure code is entered

sResult is string 
sResultCompile is string 
sResultCompile = Compile("Dynamic_Proc", MySourceCode)
SWITCH sResultCompile
	CASE "": sResult = ExecuteProcess("Dynamic_Proc", trtProcedure)
	CASE "ERR": sResult = "Unable to compile. " + CR + ErrorInfo()
	OTHER CASE: sResult = sResultCompile
END
RESULT sResult
```
<a name="Example2"></a>

```wl
// Syntax 2: Dynamic compilation of a global procedure
// Using the Procedure type
// Source: control in which the procedure code is entered

Formula is Procedure = Compile("Dynamic_Proc", MySourceCode)
IF ErrorOccurred = False THEN
	ExecuteProcess("Dynamic_Proc", trtProcedure)
	// Other possibility for runing the procedure: Formula()
ELSE
	Info(ErrorInfo())
END
```
<a name="Example3"></a>

```wl
// Syntax 3: Dynamic compilation of a global procedure
// Direct use of the Procedure type
// Source: control in which the procedure code is entered

Formula is Procedure = Compile(MySourceCode)
IF ErrorOccurred = False THEN
	Formula()
ELSE
	Info(ErrorInfo())
END
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Sintaxis

### Recopilación dinámica de una Procedure global

`<Result> = Compile(<Procedure name> , <Source code>)`
---

**`<Result>: (Cadena de caracteres)`**

Resultado de la compilación:

- Cadena vacía (""") si la compilación se ha realizado correctamente. El procedimiento se puede ejecutar con [ExecuteProcess](../WDLang1/3013031.md) y [Ejecute](../WDLang1/3013041.md) con la constante *trtProcedure*.

- ERR si se ha producido un error fatal: wdxxxcpl.dll no encontrado, incorrecto &lt;Código fuente&gt;, no hay proyecto actual, Procedure global ya creada, etc.. [ErrorInfo](../WDLang1/3013008.md) devuelve los detalles del error.

- Un mensaje de error si se detecta un error de compilación. Este mensaje corresponde al título del error.




**`<Procedure name>: (Cadena de caracteres)`**

Nombre de la dinámica global Procedure a crear.

Para crear una Procedure local, todo lo que tienes que hacer es especificar el nombre del elemento. Por ejemplo: "WindowName.ProcedureName". 

Atención: Si se llama varias veces a **Compile** con el mismo nombre Procedure, el último Procedure creado se sobrescribe automáticamente.

**`<Source code>: (Cadena de caracteres)`**

El código fuente en WLanguage de la Procedure para compilar dinámicamente. 

- Si este código contiene comillas, deben ser duplicadas (ver el ejemplo).

- Este código puede contener el código de llamada y de declaración de un Procedure interno.





<a name="SYNTAX2"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png)![Código de Usuario (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png) 
### Compilación dinámica de una Procedure global asociada a una Procedure Variable

`<Result> = Compile(<Procedure name> , <Source code>)`
---

**`<Result>: (Procedure Variable)`**

Nombre de la variable [Procedure](../Motscles/1000018907.md) que apunta al Procedure compilado. 

La Procedure puede ser dirigida: 

- con la función [ExecuteProcess](../WDLang1/3013031.md).

- con la función [Ejecute](../WDLang1/3013041.md). 

- mediante una llamada a la [Procedure](../Motscles/1000018907.md) variable. 




Si se produce un error de compilación, la variable [ErrorOccurred](../WDLang1/3087001.md) se convierte en <u><u><u><u>True</u></u></u></u> y [ErrorInfo](../WDLang1/3013008.md) devuelve los detalles del error.

**`<Procedure name>: (Cadena de caracteres)`**

Nombre de la dinámica global Procedure a crear.

Para crear una Procedure local, todo lo que tienes que hacer es especificar el nombre del elemento. Por ejemplo: "WindowName.ProcedureName". 

Si se llama varias veces a **Compile** con el mismo nombre Procedure, el último Procedure creado se sobrescribe automáticamente.  

**`<Source code>: (Cadena de caracteres)`**

El código fuente en WLanguage de la Procedure para compilar dinámicamente. 

- Si este código contiene comillas, deben ser duplicadas (ver el ejemplo).

- Este código puede contener el código de llamada y de declaración de un Procedure interno.





<a name="SYNTAX3"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png)![Código de Usuario (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png) 
### Compilación dinámica de Procedure anónima

`<Result> = Compile(<Source code>)`
---

**`<Result>: (Procedure Variable)`**

Nombre de la variable [Procedure](../Motscles/1000018907.md) que apunta al Procedure compilado. 

El Procedure puede ejecutarse mediante una llamada al [Procedure](../Motscles/1000018907.md) Variable. 

Si se produce un error de compilación, la variable [ErrorOccurred](../WDLang1/3087001.md) se convierte en <u><u><u><u>True</u></u></u></u> y [ErrorInfo](../WDLang1/3013008.md) devuelve los detalles del error.

La Procedure está compilada como una Procedure global. Para compilar una Procedure asociada a una ventana, debe nombrar la Procedure (sintaxis 2). 

**`<Source code>: (Cadena de caracteres)`**

El código fuente en WLanguage de la Procedure para compilar dinámicamente. 

- Si este código contiene comillas, deben ser duplicadas.

- Este código puede contener el código de llamada y de declaración de un Procedure interno.






<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Observaciones


### Usando la dinámica Procedure
<a name="usando_dinamica_procedure_ELTPARAGRAPHE000192"></a>

- **Si utiliza la primera sintaxis**, para utilizar sus Procedure s dinámicos, basta con ejecutar el Procedure mediante [ExecuteProcess](../WDLang1/3013031.md) o [Ejecute](../WDLang1/3013041.md).
	Otra solución (no recomendada): antes de llamar a **Compile**, declare el nombre del Procedure al compilador de WLanguage utilizando la palabra clave [EXTERN](../Motscles/1511013.md) y utilice el Procedure directamente.

- **Si está usando la segunda sintaxis**, para usar sus Procedure s dinámicas, simplemente inicie la Procedure: 

	- utilizando [ExecuteProcess](../WDLang1/3013031.md) o [Ejecute](../WDLang1/3013041.md).

	- mediante una llamada directa a la [Procedure](../Motscles/1000018907.md) variable. 




- **Si está utilizando la tercera sintaxis**, para utilizar sus Procedure s dinámicos, todo lo que tiene que hacer es ejecutar el Procedure a través de una llamada directa al [Procedure](../Motscles/1000018907.md) Variable. Una Procedure que se compila dinámicamente puede tomar parámetros. Ejemplo: 





```wl
MySourceCode is string = [
PROCEDURE AddXandY(aa, bb)
RESULT aa + bb
]
MyProc is procedure = Compile(MySourceCode)
Trace(MyProc(2, 3))
```

<a name="NOTE0_2"></a>


### Código dinámico
<a name="codigo_dinamico_ELTPARAGRAPHE000236"></a>

Las constantes **no pueden utilizarse** en el código dinámico (definido por la palabra clave [CONSTANTE](../Motscles/1514012.md)).

Cuando se utilizan constantes en un código, todas las apariciones de las constantes se sustituyen por su valor durante la compilación en el editor, pero la correspondencia entre el nombre de las constantes y su valor no se "incrusta" en la aplicación. Por lo tanto, la compilación dinámica no puede utilizar las constantes.

Veamos dos alternativas:

1ª solución: Usar variables en lugar de constantes. 

El código: 


```wl
CONSTANT
	CST_Name = 1
END
```


se convierte, por ejemplo


```wl
CST_Name is int = 1
```


2ª solución: En la cadena que contiene el código que debe compilarse dinámicamente, sustituya el nombre de la constante por su valor:


```wl
sCode is string
sCode = [
Info(CST_Name)
]

// Replace the name of the constant by its value
sCode = Replace(sCode, "CST_Name", CST_Name, WholeWord + IgnoreCase)

// The code can be compiled now
IF Compile("DynProc", sCode) <> "" THEN
	Error("Error while compiling the dynamic procedure: ", ...
		ErrorInfo())
ELSE
	// Then it can be run
	WHEN EXCEPTION IN 
		ExecuteProcess("DynProc", trtProcedure)
	DO
		Error("Error while running the dynamic procedure: ", ...
			ExceptionInfo())
	END
END
```

<a name="NOTE0_3"></a>


### Parámetros de una consulta
<a name="parametros_una_consulta_ELTPARAGRAPHE000264"></a>

Si el código fuente utilizado para la compilación dinámica ejecuta una consulta con parámetros ([HExecuteQuery](../WDLang4/3044080.md)), los parámetros esperados por la consulta deben especificarse necesariamente en [HExecuteQuery](../WDLang4/3044080.md).

Por ejemplo, debe utilizar:


```wl
HExecuteQuery("MyQuery", hQueryDefault, "Doe")
```
en lugar de:


```wl
MyQuery.CustomerName = "Doe"
HExecuteQuery("MyQuery", hQueryDefault)
```

<a name="NOTE0_4"></a>


### Despliegue de una aplicación que utiliza Compile
<a name="despliegue_una_aplicacion_que_utiliza_docparampagetitleshort_ELTPARAGRAPHE000284"></a>

Cuando se despliega una aplicación que utiliza **Compile** (al crear el ejecutable o desplegar el sitio), debe especificar todas las bibliotecas de WINDEV/WEBDEV/WINDEV Mobile framework utilizadas por el código que se compila dinámicamente. De hecho, este código siendo compilado dinámicamente, WINDEV, WEBDEV y WINDEV Mobile no detectan las librerías framework utilizadas.
<a name="NOTE0_5"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png)![Código de Usuario (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png) 

### Limitaciones
<a name="limitaciones_ELTPARAGRAPHE000296"></a>

- Si una ventana de proyecto (o página) utiliza un Procedure local llamado &lt;Procedure nombre&gt;, las llamadas a &lt;Procedure nombre&gt; desde un proceso perteneciente a la ventana (o página) siempre ejecutarán el Procedure local.

- Si una Procedure global llamada &lt;Procedure nombre&gt; ya existe, la compilación dinámica de una Procedure llamada &lt;Procedure&gt; desencadenará un error. 

- Por Default, la sobrecarga de las funciones de WLanguage es ignorada durante la compilación dinámica. 
	Por ejemplo, si se ha sobrecargado [rastro](../WDLang1/3013050.md), se llamará a la función WLanguage (no a la función sobrecargada) en un código que se compila dinámicamente. Para forzar el uso de la función sobrecargada durante la compilación dinámica, el nombre de la función debe ir precedido de la palabra clave "Extern".

- Las enumeraciones y las combinaciones no están disponibles en la compilación dinámica.






<a name="XComponent"></a>

## Componente:
wd280vm.dll
