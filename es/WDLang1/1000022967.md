


## DocInsertTable (Función)



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Inserta una tabla en un procesador de textos Document ir reemplaza el fragmento especificado con un nuevo tabla. 


<a name="Example1"></a>
<a name="sample_code"></a>

## Ejemplo


```wl
// Inserts a 3x3 table at position 1
DocInsertTable(WP_Table, 1, 3, 3)
```

```wl
// Inserts a table containing 3 columns and 2 rows at cursor position
DocInsertTable(WP_Table, WP_Table.Cursor, 3, 2)
```

<a name="XSYNTAX"></a>

## Sintaxis
<a name="SYNTAX1"></a>

### Insertar un tabla en un procesador de textos Document

`<Result> = DocInsertTable(<Document> , <Position> [, <Number of columns> [, <Number of rows>]])`
---

**`<Result>: (Variable de tipo DocFragment)`**

Variable de tipo [docFragment](../WDLang1/1000022482.md) que contiene el fragmento insertado. 

**`<Document>: (Documento Variable o cadena de caracteres)`**

Document para usar. Este documento corresponde a: 

- ![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) el nombre de un control Procesador de texto.

- a Variable de tipo [Document](../WDLang1/1000022461.md). 




**`<Position>: (Integro)`**

La posición donde se insertará la tabla. Esta posición se expresa en número de caracteres. 

**`<Number of columns>: (Entero opcional)`**

Número de columnas en la tabla. De forma predeterminada, este parámetro corresponde a 1. 

**`<Number of rows>: (Entero opcional)`**

Número de filas en la tabla. Este parámetro corresponde al 1 de Default.


<a name="SYNTAX2"></a>

### Reemplazando el fragmento existente por una nueva tabla

`<Result> = DocInsertTable(<Fragment> [, <Number of columns> [, <Number of rows>]])`
---

**`<Result>: (Variable de tipo DocFragment)`**

Variable de tipo [docFragment](../WDLang1/1000022482.md) que contiene el fragmento insertado. 

**`<Fragment>: (Variable de tipo DocFragment)`**

Nombre de la [docFragment](../WDLang1/1000022482.md) Variable que corresponde al fragmento a manipular. El contenido actual del fragmento será sustituido por el creado por tabla. 

**`<Number of columns>: (Entero opcional)`**

Número de columnas en la tabla. De forma predeterminada, este parámetro corresponde a 1. 

**`<Number of rows>: (Entero opcional)`**

Número de filas en la tabla. Este parámetro corresponde al 1 de Default.



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Observaciones


### Manejar una Mesa mediante programación
<a name="manejar_una_mesa_mediante_programacion_ELTPARAGRAPHE000150"></a>

Una tabla en un procesador de textos Document puede ser manejada por las funciones de WLanguage para la gestión de arrays. 

Por ejemplo: 

- [añadir](../WDLang1/1000017348.md) o [ArrayAdd](../WDLang1/3075003.md) para añadir filas o columnas,

- [suprimir](../WDLang1/1000017354.md) o [ArrayDelete](../WDLang1/3075008.md) para borrar filas, columnas o el propio tabla. 




Ejemplo:


```wl
// Inserts a 3x3 table at position 1
DocInsertTable(WP_ExampleWP, 1, 3, 3)

// Define a fragment corresponding to the table
f is docFragment(WP_ExampleWP.Value, WP_ExampleWP.Cursor, 0)

let para <- f.Paragraph[1]
IF para.Table = Null THEN
	RETURN
END

doc is Document <- WP_ExampleWP.Value

// Adds a row to the table
nIndex is int = Add(para.Table.Rows)

// Input in cell 2,2
para.Table.Cells[2,2].Content.Text = "I am in cell 2,2"

// Deletes row 3
Delete(para.Table.Rows, 3)

// Deletes column 3
Delete(para.Table.Columns, 3)

// Deletes the entire table where the cursor is positioned
Delete(doc.Paragraph, para.ParagraphSubscript)
```


Ejemplo de creación de un tabla en un documento con el contenido de un control Tabla: 

```wl
// A window contains a Table control populated programmatically named TABLE_Demo
// and a Word Processing control named WP_Demo
// The following code adds a table into the Word Processing control with:
// - in first table row, the title of columns found in the Table control,
// - the content of Table control in the other table rows.

MyDoc is Document
cTable is Control <- TABLE_Demo
pCol is Control

FragmentStart is docFragment(MyDoc,1)
FragmentStart.Formatting.FontSize = 24
FragmentStart.Formatting.TextColor = DarkRed
FragmentStart.Text = "Table in WP with " + cTable.Caption + CR + CR

// Insert the table into the document in memory ...
DocInsertTable(MyDoc, 20, TableCount(cTable, toColumn), cTable.Occurrence + 1)

FOR EACH para OF MyDoc.Paragraph
	IF para.Table <> Null THEN
		// For all columns of the table control ...
		FOR nColumn = 1 _TO_ TableCount(cTable, toColumn)
			
			// First tab le line containing the title of columns
			pCol <- TableEnumColumn(cTable, nColumn)
			para.Table.Cells[1, nColumn].Content.Text = pCol.Caption
			
			// Fill all rows of this column
			FOR nRow = 1 _TO_ cTable.Occurrence
				para.Table.Cells[nRow+1, nColumn].Content.Text = pCol[nRow]
			END
		END
		BREAK
	END
END

// Document in memory assigned to the Word Processing control
WP_Demo = MyDoc
```


<a name="XComponent"></a>

## Componente:
wd280mdl.dll
