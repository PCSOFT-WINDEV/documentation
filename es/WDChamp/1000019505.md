


## Manejo de una control Planificador mediante programación
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Presentación
<a name="presentacion_ELTTEXTE000441"></a>
Una control Planificador puede ser: 

- manipular mediante programación. 

- ![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) [vincular a un archivo de datos](../WDChamp/1000019543.md).

- ![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) [enlazar a una variable](../WDChamp/1000019543.md).




Para manipular a control Planificador mediante programación, WINDEV y WEBDEV proponen el [Funciones de SchedulerXXX](../WDLang1/1000019435.md). 

Esta ayuda Page explica cómo manipular a control Planificador a través de la programación. El siguiente ejemplo se utiliza para almacenar las citas en una base de datos HFSQL. 




<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Manejo de la control Planificador mediante programación
<a name="manejo_control_planificador_mediante_programacion_ELTTEXTE000471"></a>


### Añadiendo un Resource
<a name="anadiendo_resource_ELTPARAGRAPHE000053"></a>

La adición de una Resource a una control Planificador se realiza mediante [SchedulerAddResource](../WDLang1/1000019279.md). Este Resource puede corresponder a una persona, una habitación, ...

Ejemplo: 

```wl
// Adds resources into a Scheduler control 
SchedulerAddResource(SCH_Schedule1, "Florence" + gStoredValue("FL"))
SchedulerAddResource(SCH_Schedule1, "Emma" + gStoredValue("EM"))
SchedulerAddResource(SCH_Schedule1, "Fred" + gStoredValue("FR"))
```

En este ejemplo, se utiliza [gStoredValue](../WDLang1/1410088363.md) para proponer un Resource visualizado ("Florencia", por ejemplo) y un Resource gestionado mediante programación ("FL", por ejemplo). 
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) Observación: [SchedulerAddResource](../WDLang1/1000019279.md) se utiliza para insertar una imagen en el título del Resource.


### Añadir una Appointment
<a name="anadir_una_appointment_ELTPARAGRAPHE000082"></a>

La adición de una Appointment a una control Planificador se realiza mediante [SchedulerAddAppointment](../WDLang1/1000019273.md). Esta función acepta dos sintaxis: 

- sintaxis utilizada para especificar las características de la Appointment: título, descripción, etc, ...
	Ejemplo: 
	
	```wl
	// Adds a 1-hour appointment for tonight 
	MyResource is string
	MyTitle is string
	StartAPT is DateTime
	
	MyResource = "Vince"
	MyTitle = "Sales meeting"
	StartAPT = DateSys() + "17000000"
	
	SchedulerAddAppointment(SCH_Schedule, MyResource, MyTitle, StartAPT)
	```


- sintaxis que manipula una variable de tipo [Appointment](../WDLang1/1000019244.md).
	Ejemplo: 
	
	```wl
	// Declares an Appointment variable 
	MyAppointment is Appointment
	
	// Fills the appointment
	MyAppointment.Title = "Sales meeting"
	MyAppointment.Content = "Meeting to discuss the weekly objectives."
	MyAppointment.StartDate = "201003220845"
	MyAppointment.EndDate = "201003221230"
	MyAppointment.Category = "Sales"
	MyAppointment.ID = 1
	
	// Adds the appointment into the control
	SchedulerAddAppointment(SCH_Schedule, MyAppointment)
	```




La propiedad **BackgroundColor** de la variable [Appointment](../WDLang1/1000019244.md) se utiliza para definir un color de visualización para un Appointment. Si no se define un color de fondo, la control Planificador utilizará automáticamente el color asociado a la categoría de la Appointment. 


### Rellenar un control Planificador con los datos de un archivo de datos HFSQL
<a name="rellenar_control_planificador_con_los_datos_archivo_datos_hfsql_ELTPARAGRAPHE000122"></a>

Los registros se almacenan en un archivo HFSQL. El llenado inicial de la control Planificador puede hacerse recorriendo la el archivo de datos mediante la sintaxis FOR EACH y añadiendo cada Appointment mediante [SchedulerAddAppointment](../WDLang1/1000019273.md).

```wl
// Appointment variable 
MyAppointment is Appointment

// Browse the appointments stored in database
FOR EACH APT 
	// Fills the information of the variable
	MyAppointment.Title = APT.Title
	MyAppointment.Content = APT.Content
	MyAppointment.StartDate = APT.StartDate
	MyAppointment.EndDate = APT.EndDate
	MyAppointment.Category = APT.Category
	MyAppointment.ID = APT.APTID
	
	// Adds the appointment into the Scheduler control
	SchedulerAddAppointment(SCH_MySchedule, MyAppointment)
END
```



### Recuperación de una lista de citas
<a name="recuperacion_una_lista_citas_ELTPARAGRAPHE000138"></a>

La función [SchedulerListAppointment](../WDLang1/1000019276.md) permite obtener: 

- la lista de todas las citas encontradas en el control Planificador. 
	Por ejemplo: 
	
	```wl
	// Array containing a list of Appointment 
	arrAppointmentList is array of Appointment
	
	// Lists of appointments 
	arrAppointmentList = SchedulerListAppointment(SCH_MySchedule)
	```


- la lista de citas para un Resource incluido entre dos fechas. 
	Por ejemplo: 
	
	```wl
	// List of appointments for January 2020
	arrAppointmentList is array of Appointment
	
	// Lists of appointments
	arrAppointmentList = SchedulerListAppointment(SCH_MyScheduler, MyResource, ...
				"20200101", "20200131")
	```


- el Appointment actualmente seleccionado o suspendido. 
	Por ejemplo: 
	
	```wl
	// Selected appointment
	arrAppointmentList is array of Appointment
	
	arrAppointmentList = SchedulerListAppointment(SCH_MyScheduler, schAptSelected)
	```






### Mostrando el horario de un Resource específico o de una fecha específica
<a name="mostrando_horario_resource_especifico_una_fecha_especifica_ELTPARAGRAPHE000181"></a>

Para visualizar la control Planificador de: 

- una Resource específica, utilice [SchedulerPositionResource](../WDLang1/1000019969.md). 
	Por ejemplo: 
	
	```wl
	SchedulerAddAppointment(SCH_NoName1, "ABC room", "APT 1", ...
			DateSys() + "14000", DateSys() + "16000")
	SchedulerPositionResource(SCH_NoName1, "ABC room")
	```


- a partir de una fecha determinada, utilice [SchedulerPositionDateTime](../WDLang1/1000019998.md). 
	Por ejemplo: 
	
	```wl
	// Positions the schedule on today's date
	SchedulerPositionDateTime(SCH_Schedule, Today())
	```






### Eliminación de una cita
<a name="eliminacion_una_cita_ELTPARAGRAPHE000215"></a>

[SchedulerDeleteAppointment](../WDLang1/1000019274.md) permite eliminar: 

- el Appointment seleccionado en el control. 

- un Appointment específico.



```wl
// Deletes the first appointment
SchedulerDeleteAppointment(SCH_Schedule, 1)
```



### Borrar un Resource
<a name="borrar_resource_ELTPARAGRAPHE000234"></a>

[SchedulerDeleteResource](../WDLang1/1000019282.md) se utiliza para eliminar un recurso de la control Planificador.

```wl
// Deletes one of the resources
ResDel is boolean
ResDel = SchedulerDeleteResource(SCH_Schedule1, "Flo") 
IF ResDel = True THEN
	Info("Resource deleted")
END
```

[SchedulerDeleteAll](../WDLang1/1000019275.md) borra todas las citas de la control Planificador así como todos sus recursos. 



### Modificando la visualización de la control
<a name="modificando_visualizacion_control_ELTPARAGRAPHE000253"></a>

La visualización actual de una control Planificador se puede modificar a través de las siguientes funciones: 

- [SchedulerDisplayPreviousPeriod](../WDLang1/1000019495.md) y [SchedulerDisplayNextPeriod](../WDLang1/1000019496.md): Estas dos funciones se utilizan para modificar el período de visualización (igual que el botón de giro que se encuentra en la parte superior del control Planificador).. 

- [SchedulerChangeMode](../WDLang1/1000019440.md): Esta función cambia el modo de visualización del control: día, semana, mes.  

- [SchedulerTimeDisplayed](../WDLang1/1000019373.md): Esta función se utiliza para definir el período de tiempo visualizado. 



<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Manejo de una Appointment mediante programación
<a name="manejo_una_appointment_mediante_programacion_ELTTEXTE000537"></a>
Tienes la capacidad de manipular un Appointment programando: 

- utilizando el índice de la Appointment para modificar. 

- señalando por referencia en la Appointment para modificar. 




**1. Utilizar un índice**

Cada vez que se añade una Appointment a un programa, [SchedulerAddAppointment](../WDLang1/1000019273.md) devuelve un índice. Este índice representa el añadido Appointment. Este índice puede utilizarse para manipular la Appointment directamente. 

Ejemplo:

```wl
APTNum is int
APTNum = 5

// Change the title displayed on the appointment #5
SCH_ROOM[APTNum].Title = "Blue room"
```


**2. Usando un reference**

Para manipular una cita, utilice el operador **&lt;-** para asociar la cita del control Planificador con la variable de tipo [Appointment](../WDLang1/1000019244.md). Una modificación realizada en el Variable se aplicará automáticamente al control. 

Ejemplo: 

```wl
A is Appointment
A <- SCH_Room[APTNum]
A.Title = "New title"
```


<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Uso del menú contextual (AAF)
<a name="uso_del_menu_contextual_aaf_ELTTEXTE000561"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png) El control Planificador es associated con un menú contextual (AAF). El menú desplegable de la control Planificador se utiliza para: 

- cambiar el modo de visualización de control,

- añadir, modificar o borrar un Appointment. 




Para grabar las operaciones realizadas, debe utilizar los eventos de la pantalla control Planificador. 

En el caso correspondiente, simplemente recupera el Appointment usado actualmente y realiza el Process correspondiente. 

**Ejemplo**: Para almacenar un Appointment añadido por el usuario a través del menú contextual en un fichero de datos APT, basta con escribir lo siguiente en la "Entrada en edición en un Appointment" Event:


```wl
PROCEDURE Edit(aptEdited is Appointment)

// Store the data
APT.Title = aptEdited.Title
APT.StartDate = aptEdited.StartDate
APT.EndDate = aptEdited.EndDate
...
HAdd(APT)
```


El mismo tipo de código puede ser implementado para los diferentes eventos de la control Planificador. De hecho, por cada evento de control que maneja un Appointment, un Procedure ha sido declarado automáticamente por el control Planificador. 

Estos procedimientos reciben como parámetro una variable de tipo [Appointment](../WDLang1/1000019244.md). 

<a name="NOTE5"></a>
<a name="NOTE5_1"></a>


## Uso avanzado de eventos associated con el control Planificador
<a name="uso_avanzado_eventos_associated_con_control_planificador_ELTTEXTE000585"></a>


### Uso avanzado de eventos con procedimientos
<a name="uso_avanzado_eventos_con_procedimientos_ELTPARAGRAPHE000340"></a>

Puede permitir que el usuario defina con más precisión las características de su Appointment durante una adición o una modificación. Para ello, cree una ventana o una Page con la información a rellenar. 

En el código, basta con abrir la ventana o la página en la "Entrada en edición en un Appointment" Event. Para bloquear la entrada directa, el evento debe devolver <u><u><u><u>False</u></u></u></u>. 
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png) Este principio puede aplicarse a todos los eventos llamados por el menú contextual de control Planificador. Ejemplo: Abrir una ventana para entrar en la Appointment.  

```wl
PROCEDURE Edit(aptEdited is Appointment)

// Opens the window for entering an appointment 
// with the selected appointment (in Creation or Modification mode)
Open(WIN_InputAPT_HFSQL, aptEdited)

// Returns False to lock the direct input in the Scheduler control
RESULT False
```


<a name="NOTE6"></a>
<a name="NOTE6_1"></a>


## Gestión de los días festivos
<a name="gestion_los_dias_festivos_ELTTEXTE000609"></a>
Los días festivos pueden ser programados por set. Varias funciones de WLanguage (empezando por BankHolidayXXX) están disponibles.

Para definir los días festivos que se muestran en los controles Organizador, Programador y Calendar, utilice [BankHolidayAdd](../WDLang1/1000017304.md). Esta función permite definir la lista de días festivos que se utilizarán. Esta función permite personalizar los días festivos según la Country y la normativa local. Esta función debe utilizarse al principio de la aplicación porque tiene un efecto global sobre la aplicación.

Los días festivos aparecerán en verde en el calendario. 

Ejemplo:

```wl
// Delete all public holidays
BankHolidayDeleteAll()
// Initialize the 11 public holidays common to the French regions and territories
BankHolidayAdd("0101")		// 1st of January
BankHolidayAdd(bhEasterMonday)	// Easter Monday
BankHolidayAdd("0501")		// 1st of May
BankHolidayAdd("0508")		// 8th of May
BankHolidayAdd(bhAscensionDay)	// Ascension day
BankHolidayAdd(bhWhitMonday)	// Whit Monday
BankHolidayAdd("0714")		// 14th of July
BankHolidayAdd("0815")		// 15th of August (Assumption)
BankHolidayAdd("1101")		// All Saints' Day
BankHolidayAdd("1111")		// 11th of November
BankHolidayAdd("1225")		// Christmas

// Add 2 additional public holidays for the regions of Moselle and Alsace
BankHolidayAdd("1226" + CR + bhGoodFriday)
```


<a name="NOTE7"></a>
<a name="NOTE7_1"></a>


## Propiedades específicas de los controles del Programador
<a name="propiedades_especificas_los_controles_del_programador_ELTTEXTE000633"></a>
Las siguientes propiedades se utilizan para gestionar un control Planificador a través de la programación.



|   |   |
| --- | --- |
| [DayBreakHeight](../Proprietes/1000020105.md) | El **DayBreakHeight** Property se utiliza para get y cambiar la altura de las pausas entre días en un control Planificador en el que los días están organizados en filas, y los recursos en columnas. |
| [DayHeight](../Proprietes/1000020106.md) | El **DayHeight** Property se utiliza para get y cambiar la altura de los días en un control Planificador donde los días están dispuestos en filas, y los recursos están dispuestos en columnas. |
| [DayWidth](../Proprietes/1000020108.md) | El **DayWidth** Property se utiliza para determinar y modificar la anchura de los días: <br><br>- en una control Planificador donde los días se muestran en columna y los recursos en filas.<br><br>- en una columna del diagrama de Gantt.<br><br><br> |
| [DirectInputAPT](../Proprietes/1000019995.md) | La propiedad **DirectInputAPT** se utiliza para determinar y especificar si el usuario puede cambiar directamente el título de un Appointment en un Programador o control Agenda. |
| [EndDate](../Proprietes/1000017427.md) | El **EndDate** Property se utiliza para get y cambiar la fecha de finalización del período seleccionado: <br><br>- en un control Calendario,<br><br>- en un control Agenda.<br><br>- en un control Planificador.<br><br><br><br><br> |
| [GranularityAppointment](../Proprietes/1000019100.md) | El **GranularityAppointment** obtiene y cambia la precisión de la cuadrícula utilizada para definir las citas en los controles del Organizador o del Programador. <br>Property conservada para compatibilidad con versiones anteriores. |
| [GranularityDuration](../Proprietes/1000019643.md) | El **GranularidadDuración** Property se utiliza para get y cambiar el tamaño de la cuadrícula a redimensionar:<br>Citas de \* en un control Agenda. <br>Citas de \* en un control Planificador.<br>Eventos \* en una control Línea de tiempo.<br>Tareas de \* en una columna del diagrama de Gantt. |
| [GranularityMovement](../Proprietes/1000019642.md) | El **GranularidadMovimiento** Property se utiliza para get y cambiar el tamaño de la cuadrícula para moverse:  <br>Citas de \* en un control Agenda. <br>Citas de \* en un control Planificador.<br>Eventos \* en una control Línea de tiempo.<br>Tareas de \* en una columna del diagrama de Gantt. |
| [MaskTitleDate](../Proprietes/1000019463.md) | La **MaskTitleDate** Property se utiliza para identificar y cambiar la máscara de entrada utilizada para las columnas de título del día en los controles del Organizador o del Programador |
| [MovementAPT](../Proprietes/1000019993.md) | La propiedad **MovementAPT** permite saber y especificar si los usuarios pueden mover citas en un control Planificador o Agenda. |
| [NbDayDisplayed](../Proprietes/1000019005.md) | El **NbDayDisplayed** Property se utiliza para: <br><br>- get y cambiar el número de días mostrados en un Organizador o control Planificador. <br><br>- get el número de días mostrados en un control Diagrama de Gantt en un reporte.<br><br><br> |
| [Num1stDayOfTheWeek](../Proprietes/1000019004.md) | **Num1stDayOfTheWeek** se utiliza para averiguar y modificar el 1er día de la semana mostrado en:<br><br>- a control Calendario.<br><br>- un control Agenda.<br><br>- un control Planificador. <br><br>- y control Campo de entrada en formato Date con Calendar.<br><br><br> |
| [PeriodSelection](../Proprietes/1000019996.md) | El **PeriodSelection** Property se utiliza para determinar y especificar si el usuario puede seleccionar un periodo de tiempo en un Programador o control Agenda. |
| [Resource](../Proprietes/1000019308.md) | El **Resource** Property se utiliza para: <br><br>- averigua los recursos Visible en un control Planificador. <br><br>- averigua el Resource del control Planificador que corresponde al índice especificado. <br><br><br> |
| [ResourceHeight](../Proprietes/1000020109.md) | El **ResourceHeight** Property se utiliza para get y cambiar la altura de los recursos en un control Planificador donde los recursos están dispuestos en filas. |
| [ResourceWidth](../Proprietes/1000020110.md) | El **ResourceWidth** Property se utiliza para get y cambiar la anchura de los recursos en un control Planificador donde los recursos están dispuestos en columnas. |
| [SelectedResource](../Proprietes/1000019445.md) | El **SelectedResource** Property devuelve el nombre del Resource que corresponde a la selección del usuario en un control Planificador. |
| [StartDate](../Proprietes/1000017426.md) | La propiedad **StartDate** permite determinar y cambiar la fecha de inicio del período de tiempo seleccionado:<br><br>- en un control Calendario. <br><br>- en un control Agenda.<br><br>- en un control Planificador.<br><br><br> |
| [WorkingHourEnd](../Proprietes/1000019056.md) | El **WorkingHourEnd** Property se utiliza para identificar y modificar la hora de finalización de las horas de trabajo utilizadas:<br><br>- por un control Agenda.<br><br>- por un control Planificador.<br><br>- mediante una columna de diagrama de Gantt (en una tabla o control Tabla TreeView).<br><br><br> |
| [WorkingHourStart](../Proprietes/1000018958.md) | El **WorkingHourStart** Property se utiliza para get y cambiar la hora de inicio de las horas de trabajo utilizadas: <br><br>- por un control Agenda.<br><br>- por un control Planificador.<br><br>- mediante una columna de diagrama de Gantt (en una tabla o control Tabla TreeView). <br><br><br> |





Para obtener una lista completa de las propiedades de WLanguage que pueden utilizarse con una control Planificador, consulte [Propiedades associated con los controles del Programador](../WDChamp/1000019502.md).


