


## Manejo de una control Procesador de texto mediante programación (sintaxis de prefijo)
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Presentación
<a name="presentacion_ELTTEXTE000087"></a>
Un control Procesador de texto puede ser manejado por programación. 

Para manipular el control Procesador de texto, WLanguage propone: 

- un conjunto de funciones utilizadas para realizar todas las operaciones comunes (programación directa a través de las funciones WLanguage DocXXX. Para obtener más información, consulte [Funciones para la gestión de la control Procesador de texto](../WDLang1/1000024544.md). 

- una programación avanzada a través de los diferentes tipos avanzados de WLanguage utilizados para dirigir manipular el contenido del archivo docx mostrado por el control Procesador de texto. Para obtener más información, consulte [Tipos asociados a la control Procesador de texto](../WDLang1/1000022736.md). 




Esta página de ayuda explica cómo programar manipular a control Procesador de texto. 




## Manejo de la control Procesador de texto mediante programación
<a name="manejo_control_procesador_texto_mediante_programacion_ELTTEXTE000131"></a>


### Abrir y crear un documento docx en un control Procesador de texto
<a name="abrir_crear_documento_docx_control_procesador_texto_ELTPARAGRAPHE000154"></a>

Para abrir un documento docx existente programándolo en un control Procesador de texto, puede: 

- asignar el archivo docx al archivo control Procesador de texto. Por ejemplo:
	
	```wl
	// Select a .docx file in the current directory
	sFileName is string
	sFileName = fSelect("", "presentation.docx", ...
		"Selecting DocX files", ...
		"Docx file (*.docx)" + TAB + "*.docx", "*.docx", fSelOpen)
	
	IF sFileName ~= "" THEN
		// No selected file
		RETURN
	END
	
	// Load the docx file
	WP_MyDocument = sFileName
	```


- usar [&lt;Procesamiento de palabras&gt;.open](../WDLang1/1000024269.md).
	 Por ejemplo:
	
	```wl
	WP_MyDocument = Open("C:\Users\test\Documents\file.docx")
	
	IF ErrorOccurred() THEN
		Error(ErrorInfo())
	RETURN
	```


- utilizar una variable [Documento](../WDLang1/1000022461.md) y asignarla a la variable control Procesador de texto.
	Por ejemplo:
	
	```wl
	MyDocument is Document
	MyDocument = "C:\temp\presentation.docx"
	WP_MyDocument = MyDocument
	```

	o: 
	
	```wl
	MyDocument is Document
	MyDocument = DocOpen("C:\Users\test\Documents\file.docx")
	IF ErrorOccurred() THEN
		Error(ErrorInfo())
	ELSE
		WP_MyDocument = MyDocument
	END
	```



Atención: sólo los archivos en formato docx pueden ser manejados por la control Procesador de texto. 

**Consejo**: Para visualizar un documento vacío en una control Procesador de texto, todo lo que tiene que hacer es asignar un string vacío a la control Procesador de texto. 
Ejemplo: 

```wl
// Create a blank document
WP_MyDocument = ""
```



### Guardar un documento docx
<a name="guardar_documento_docx_ELTPARAGRAPHE000203"></a>

Para guardar un documento docx, todo lo que tienes que hacer es usar [&lt;Procesamiento de palabras&gt;.Save](../WDLang1/1000024393.md). 

Ejemplo: 

```wl
// Choose the directory and the backup name 
sFileName is string = fSelect(CompleteDir(fExeDir()),"presentation.docx", ...
	"Selecting DocX files","Docx file (*.docx)" + TAB + "*.docx" , "*.docx", fselCreate)

// Save the file
WP_NoName1.Save(sFileName)

IF ErrorOccurred THEN
	Error(StringBuild("The %1 file was not saved.", sFileName), ErrorInfo())
	RETURN
END
```
 Notes: 

- El contenido de control Procesador de texto también puede ser: 

	- impreso ([iPrintDoc](../WDLang5/1000022390.md)).

	- guardado en formato PDF ([.ToPDF](../WDLang1/1000024402.md)).




- [&lt;Procesamiento de palabras&gt;.toText](../WDLang1/1000024403.md) se utiliza para recuperar el texto del documento. 

- [&lt;Procesamiento de palabras&gt;.ToImage](../WDLang1/1000024401.md) se utiliza para convertir una página de un documento en una imagen.




### Realizar una búsqueda en un documento y modificar el resultado de la búsqueda
<a name="realizar_una_busqueda_documento_modificar_resultado_busqueda_ELTPARAGRAPHE000258"></a>

Para realizar una búsqueda en un documento de procesamiento de texto, todo lo que tiene que hacer es usar [&lt;Procesador de texto&gt;.Seek](../WDLang1/1000024271.md). 

Ejemplo: 

```wl
// Find "BEAUTIFUL" in the text 
// Selects the first one found to change its color
arrFragments is array of docFragments = WP_NoName1.Find("BEAUTIFUL")

IF arrFragments.Count >= 1 THEN
	// Position the cursor at the beginning of the first word found
	WP_NoName1.Cursor = arrFragments[1].StartPosition
	// Calculate the selection length
	WP_NoName1.SelectionLength = ...
				arrFragments[1].EndPosition - arrFragments[1].StartPosition+1
	// Modify the text color
	arrFragments[1].Formatting.TextColor = PastelRed
	BREAK
END
```


En este código, la variable [docFragment](../WDLang1/1000022482.md) corresponde a la sección de texto que contiene la palabra buscada, así como su posición en todo el texto. Entonces, usted tiene la capacidad de manipular el fragmento encontrado.


### Gestión de la selección
<a name="gestion_seleccion_ELTPARAGRAPHE000296"></a>

Para manipular la selección actualmente realizada en un control Procesador de texto, tiene la capacidad de recuperar esta selección en una variable [docFragment](../WDLang1/1000022482.md). 

El siguiente código se utiliza para cambiar el color del texto seleccionado. 
Ejemplo: 

```wl
// Change the selection color
// Get the current selection
MySelection is docFragment(WP_MyDocument, WP_MyDocument.Cursor, ...
		WP_MyDocument.SelectionLength)
// Change the color
MySelection.Formatting.TextColor = PastelRed
```


La variable [docFragment](../WDLang1/1000022482.md) posee un constructor específico utilizado para recuperar fácilmente la selección.


### Adición de texto a un documento
<a name="adicion_texto_documento_ELTPARAGRAPHE000322"></a>

Se pueden utilizar varios métodos **añadir texto a un documento existente**: 

- Insertar un fragmento en la posición deseada. Ejemplo: 
	
	```wl
	// Insert a text at the end of document 
	frag2 is docFragment(WP_MyDoc, -1 , 0)
	frag..Text += "End text"
	```


- Insertar un texto desde una posición determinada con [&lt;Insertar](../WDLang1/1000024267.md). 
	
	```wl
	WP_Document.Insert(WP_Document..Cursor, "My inserted text")
	```


- Añadiendo un texto al final del documento con [&lt;Procesamiento de palabras&gt;.add](../WDLang1/1000024032.md). 




**Para añadir texto a un documento vacío**se puede: 

- utilizar la asignación directa de control: 
	
	```txt
	<Word Processing control> = "Text to insert"
	```
Ejemplo: 
	
	```wl
	WP_MyDocument = "I am the first sentence of this document."
	```


- añadir el texto con [&lt;Procesamiento de palabras&gt;.add](../WDLang1/1000024032.md). 

- insertar un fragmento en primera posición. Ejemplo: 
	
	```wl
	// sInitialText contains the text to insert 
	frag is docFragment(WP_MyDoc, WP_MyDoc..Cursor, 0)
	frag..Text += sInitialText
	```


- insertar un texto desde la posición 1 con [&lt;Insertar](../WDLang1/1000024267.md). 
	
	```wl
	WP_Document.Insert(1, "My inserted text")
	```






### Añadir una página ruptura a un documento
<a name="anadir_una_pagina_ruptura_documento_ELTPARAGRAPHE000386"></a>

El siguiente código se utiliza para añadir una página ruptura a un documento: 

```wl
// Add a page break 
WP_NoName1.DocAdd(Charact(12))
```
También puede utilizar las siguientes constantes para añadir el tipo deseado de ruptura a un documento o fragmento: 

- *docColumnBreak*: Añade una columna ruptura en una sección multicolumna. Si la sección no es multicolumna, se añade una página ruptura. 

- *docLineBreak*: Añade una línea ruptura. 

- *docPageBreak*: Añade una página ruptura. 

- *docPárrafoBreak*: Añade un párrafo ruptura. 




Ejemplo: 

```wl
// Add a page break 
WP_NoName1.Add(docPageBreak + "Text on next page")
```



### Gestión de marcadores
<a name="gestion_marcadores_ELTPARAGRAPHE000423"></a>

Tiene la capacidad de administrar marcadores en un documento de procesamiento de texto.. Por ejemplo: 

- Añadir un marcador: Agrega un marcador llamado MyBookmark con el texto seleccionado.
	
	```wl
	doc is document
	doc <- WP_MyDocument
	fragSelection is docFragment = WP_MyDocument..Selection..Fragment
	doc..Bookmark["MyBookmark"] = fragSelection
	```


- Eliminar un marcador: Elimina el marcador llamado Marcador1: 
	
	```wl
	doc is document
	doc <- WP_MyDocument
	Delete(doc.Bookmark, "Bookmark1")
	```


- Listado de los marcadores: Enumera los marcadores de documentos: 
	
	```wl
	doc is document 
	doc <- WP_MyDocument
	FOR EACH uABookmark,BookmarkName of doc..Bookmark
		Trace(BookmarkName) // Bookmark name
		Trace(uABookmark..Text) // Bookmark text
	END
	```


- Insertar un texto al final del marcador: 
	
	```wl
	doc is Document <- WP_MyDocument..Value
	// Find the bookmark position 
	fragmentBookmark is docFragment = doc..Bookmark["Bookmark 1"]	
	IF fragmentBookmark <> Null THEN
		// Insert text at the end of bookmark
		let nInsertionPosition = fragmentBookmark..EndPosition
		// Actual insertion
		WP_MyDocument.Insert(nInsertionPosition, "Text to insert at bookmark position")
	ELSE
		Erreur("'Bookmark 1' bookmark not found in the document")
	END
	```






### Secuenciación de informes y documentos de procesamiento de textos
<a name="secuenciacion_informes_documentos_procesamiento_textos_ELTPARAGRAPHE000453"></a>

Ahora tiene la capacidad de secuenciar informes y documentos de procesamiento de textos. 

Para ello, además de las funciones estándar para crear secuencias de informes, se debe utilizar [iSequencingAddDoc](../WDLang5/1000023089.md). Por ejemplo: 

```wl
MyDocument is Document = "c:\temp\generalconditions.docx"

// Print in the report viewer
iDestination(iViewer)
// Add reports into the sequence
iSequencingAdd(RPT_Report1)
iSequencingAdd(RPT_Report2, 3)
// Add the general conditions in document format
iSequencingAddDoc(MyDocument)
iSequencingPrint()
```
 Para más detalles, ver [Secuenciación de informes](../WDChamp/9000128.md).


### Propiedades asociadas a la control Procesador de texto
<a name="propiedades_asociadas_control_procesador_texto_ELTPARAGRAPHE000475"></a>Las siguientes propiedades son específicas de la control Procesador de texto. Se utilizan principalmente para manipular las características del control: 



|   |   |
| --- | --- |
| [DisplayBookmarks](../Proprietes/1000023694.md) | La propiedad **DisplayBookmarks** permite: <br><br>- identificar el modo de visualización de los marcadores en un control Procesador de texto. <br><br>- activar o desactivar los marcadores en un control Procesador de texto.<br><br><br> |
| [DisplayFormattingMarks](../Proprietes/1000022634.md) | La propiedad **DisplayFormattingMarks** permite: <br><br>- Determinar si las marcas de formato están activadas o desactivadas en un control Procesador de texto. <br><br>- Activar o desactivar las marcas de formato en un control Procesador de texto.<br><br><br> |
| [DisplayMode](../Proprietes/1000023378.md) | El **DisplayMode** Property obtiene y cambia: <br><br>- el modo de visualización en un control Procesador de texto, <br><br>- el modo de visualización en un lector PDF control<br><br>- el modo de visualización en un editor HTML control, <br><br>- el modo de visualización en un Kanban control<br><br><br> |
| [FilePath](../Proprietes/1000023104.md) | El **FilePath** Property se utiliza para identificar: <br><br>- el nombre del archivo xlsx associated con un control Hoja de cálculo.<br><br>- el nombre del archivo associated con un editor de imágenes control. <br><br>- el nombre del archivo PDF associated con un lector de PDF control. <br><br>- el nombre del archivo DOCX associated con un control Procesador de texto. <br><br>- el nombre del archivo wddiag asociado a un editor Diagram control. <br><br><br> |
| [FormattingMarksColor](../Proprietes/1000024632.md) | La propiedad **FormattingMarksColor** permite: <br><br>- obtener el color de las marcas de formato en un control Procesador de texto. <br><br>- cambiar el color de las marcas de formato en un control Procesador de texto.<br><br><br> |
| [HTMLEdit](../Proprietes/1000023415.md) | La **HTMLEdit** Property se utiliza para: <br><br>- averigua si un control Procesador de texto se muestra en modo optimizado para la edición HTML, <br><br>- modifica una control Procesador de texto para mostrarla (o no) en modo optimizado para la edición HTML.<br><br><br> |
| [NumberAccessiblePages](../Proprietes/1000023477.md) | El **NúmeroDePáginasAccesibles** Property se utiliza para get el número de páginas cargadas actualmente en un lector de PDF o control Procesador de texto. |
| [NumberDisplayedPage](../Proprietes/1000023479.md) | La propiedad **NumberDisplayedPage** permite identificar y cambiar el número de la página que se muestra actualmente en el control Lector PDF o Procesador de texto. |
| [NumberPage](../Proprietes/2510084.md) | La propiedad **NumberPage** permite obtener: <br><br>- el número de páginas de un archivo "multipágina" Image. Esta Image se muestra en una control Imagen o en el fondo de una control Gráfico.<br><br>- el número de páginas de un archivo PDF que se muestra en un control Imagen.<br><br>- el número de páginas que se encuentran en un archivo PDF que se muestra en un lector de PDF control.<br><br>- el número de páginas que se encuentran en un archivo DOCX mostrado en un control Procesador de texto. <br><br><br> |
| [RuleVisible](../Proprietes/1000023076.md) | La propiedad **RuleVisible** permite: <br><br>- Averigua si las reglas son visibles o Invisible en una control Procesador de texto. <br><br>- Hacer visibles las reglas o Invisible en una control Procesador de texto.<br><br><br> |
| [Selection](../Proprietes/1000023033.md) | El **Selección** Property se utiliza para get las características de la selección (o del cursor): <br><br>- en un control Procesador de texto.<br>	Observación: Esta selección se encuentra en la sección que se está editando en la control (cuerpo, cabecera o pie de página). <br><br>- en un control Hoja de cálculo.<br><br>- en un editor HTML control. <br><br>- en un editor de Diagram control.<br><br><br> |
| [SelectionLength](../Proprietes/1000022522.md) | El **SelectionLength** Property se utiliza para get y cambiar la longitud de la selección realizada en un control Procesador de texto. |
| [ToolbarVisible](../Proprietes/1000021333.md) | La propiedad **ToolbarVisible** permite: <br><br>- saber si la barra de herramientas o la cinta se muestra en un control.  <br><br>- mostrar u ocultar la barra de herramientas o la cinta de opciones en un control.<br><br><br> |





Para conocer la lista completa de las propiedades de WLanguage que se pueden usar con un control Procesador de texto, ver [Propiedades asociadas a la control Procesador de texto](../WDChamp/1410087022.md).


