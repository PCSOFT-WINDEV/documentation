


## Control Tabla rellenado mediante programación: WLanguage
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Presentación
<a name="presentacion_ELTTEXTE000416"></a>
Esta página presenta los principales conceptos para manipular controles Tabla mediante programación en WLanguage. 

WLanguage incluye varias funciones para manipular este tipo de control en el código. Para saber más sobre estas funciones, consulte la documentación. 

Para obtener más información, consulte [Funciones de gestión de controles Tabla](../WDLang1/3074009.md).


<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Inicializar un control Tabla rellenado mediante programación
<a name="inicializar_control_tabla_rellenado_mediante_programacion_ELTTEXTE000440"></a>
Las filas no se crean automáticamente en los controles Tabla. Se debe especificar llamando a las siguientes funciones:

- [TableAdd](../WDLang1/3074017.md) o [TableAddLine](../WDLang1/3074006.md): agrega una fila al final del control Tabla. 

- [TableInsert](../WDLang1/3074025.md) o [TableInsertLine](../WDLang1/3074026.md): agrega una fila en el índice especificado o en el índice actual. 




**Observación**: si la opción "Entrada en cascada (Agregar)" no está seleccionada:

- El control Tabla no contiene filas cuando se crea: 
	TABLE_MyTable.[Count](../WDLang1/3025009.md) = 0. 
	En este caso, no se permite ninguna entrada y el valor de la propiedad [Empty](../Proprietes/2510126.md) se establece en <u><u><u><u>True</u></u></u></u>.

- Para insertar filas automáticamente en un control Tabla, escriba las siguientes líneas de código en el evento "Inicialización" del control: 
	
	```wl
	IF TABLE_MyTable.Empty = True THEN TableAdd(TABLE_MyTable)
	```





<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Agregar filas en un control Tabla rellenado mediante programación
<a name="agregar_filas_control_tabla_rellenado_mediante_programacion_ELTTEXTE000464"></a>
Puede agregar filas a un control Tabla:

- con [TableAddLine](../WDLang1/3074006.md) y [TableInsertLine](../WDLang1/3074026.md): en cada fila, el valor de cada columna está separado por comas.

- con [TableAdd](../WDLang1/3074017.md) y [TableInsert](../WDLang1/3074025.md): en una fila, el valor de cada columna está separado por TAB.


![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Si se agregó una nueva fila en un control Tabla con [TableAdd](../WDLang1/3074017.md), [TableInsert](../WDLang1/3074025.md), [TableAddLine](../WDLang1/3074006.md) o [TableInsertLine](../WDLang1/3074026.md), la propiedad [New](../Proprietes/2510073.md) se establece en <u><u><u><u>True</u></u></u></u> (de lo contrario, se establece en <u><u><u><u>False</u></u></u></u>).

<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Gestión de columnas calculadas
<a name="gestion_columnas_calculadas_ELTTEXTE000488"></a>
La fórmula de cálculo de una columna calculada debe definirse en el evento "Visualización de una fila" de un control Tabla. Por ejemplo: 

```wl
// COL_PBT: calculated column
// COL_UPBT and COL_QTY: columns populated programmatically
COL_PBT = COL_UPBT * COL_QTY
```


<a name="NOTE5"></a>
<a name="NOTE5_1"></a>


## Modificar una fila o columna
<a name="modificar_una_fila_columna_ELTTEXTE000512"></a>
El contenido de las filas y columnas de un control Tabla puede ser modificado:

- por el usuario, introduciendo los datos directamente en las columnas. Los cambios se guardan automáticamente en el control Tabla (no se requiere ningún código adicional). La propiedad [Modified](../Proprietes/2510080.md) se establece en <u><u><u><u>True</u></u></u></u>.

- mediante programación:

	- con la función [TableModify](../WDLang1/3074019.md) para cambiar el contenido de la fila actual o de una fila determinada.
			Por ejemplo:
			
		```wl
		TableModify(TABLE_CUSTOMER, "MOORE" + TAB + "Vince" + TAB + "Miami")
		```

			**Observación**: también es posible utilizar el nombre del control Tabla directamente: 
			
		```wl
		// Modify the current row
		TABLE_CUSTOMER = "MOORE" + TAB +"Vince" + TAB + "Miami"
		// Modify row 3
		TableModify(TABLE_DAY, "Wednesday" + TAB + "Off", 3)
		```


	- especificando directamente el nombre de la columna para cambiar el contenido (como para un control Campo de entrada). Para modificar la columna de una fila determinada, se debe especificar el número de fila (índice).
			Por ejemplo:
			
		```wl
		COL_NAME[Index] = EDT_CustomerName
		```

			Para modificar la columna de la fila actual, no es necesario especificar el índice. Por ejemplo: 
			
		```wl
		COL_NAME = EDT_CustomerName
		```

			La propiedad [Modified](../Proprietes/2510080.md) se establece en <u><u><u><u>False</u></u></u></u> (solo se establece en <u><u><u><u>True</u></u></u></u> cuando se introducen datos en el control Tabla).






<a name="NOTE5_2"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png)![Código de Usuario (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png) 

### Forzar la entrada
<a name="forzar_entrada_ELTPARAGRAPHE000217"></a>

la función [SetFocusAndReturnToUserInput](../WDLang1/1410088107.md) reanuda la entrada en la columna de la fila actual.

Por ejemplo:
```wl
// Entry process in the COL_QTY column
// COL_QTY cannot be entered, 
// if COL_PRODUCT is not entered
IF NoSpace(COL_PRODUCT) = "" THEN
	Error("The Product column must be entered first")
	SetFocusAndReturnToUserInput(COL_PRODUCT)
END
```

<a name="NOTE5_3"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png)![Código de Usuario (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png) 

### Agregar o eliminar una columna
<a name="agregar_eliminar_una_columna_ELTPARAGRAPHE000234"></a>

Es posible:

- Agregar una columna a un control Tabla rellenado mediante programación con la función [ControlClone](../WDLang1/3025017.md).

- Eliminar una columna de un control Tabla rellenado mediante programación con la función [ControlDelete](../WDLang1/3025037.md).




<a name="NOTE6"></a>
<a name="NOTE6_1"></a>


## Operaciones en filas y celdas
<a name="operaciones_filas_celdas_ELTTEXTE000548"></a>


### Eliminar una fila
<a name="eliminar_una_fila_ELTPARAGRAPHE000254"></a>

Puede eliminar filas llamando a la función [TableDelete](../WDLang1/3074032.md). La sintaxis utilizada es la siguiente:

```txt
TableDelete(<Table control>[, <Index>])
```
Si se especifica un índice, la función [TableDelete](../WDLang1/3074032.md) elimina la fila que corresponde al índice. De lo contrario, se elimina la fila actual. Por ejemplo:

```wl
TableDelete(TABLE_CUSTOMER)
```
Al eliminar una fila del control Tabla también se eliminan todos los valores de las columnas de esa fila.
<a name="NOTE6_2"></a>
Una fila se puede seleccionar con la función [TableSelectPlus](../WDLang1/3074031.md).

La sintaxis utilizada es la siguiente:

```txt
TableSelectPlus(<Table control>[, <Index>])
```

<a name="NOTE6_3"></a>


### Obtener el contenido de una fila o celda
<a name="obtener_contenido_una_fila_celda_ELTPARAGRAPHE000301"></a>

El contenido de un control Tabla rellenado mediante programación puede obtenerse:

- para toda la fila. 

- celda por celda. 



<a name="NOTE6_4"></a>


### Valor de una celda en la fila actual
<a name="valor_una_celda_fila_actual_ELTPARAGRAPHE000313"></a>Para obtener el valor de una columna (o celda) de la fila actual, la sintaxis es la misma que la de un control campo de entrada simple.


```txt
<Value> = <Column name>
```
Ejemplo: 


```wl
// COL_QTY is a column of the Table control
IF COL_QTY < 10 THEN
	INFO("Insufficient quantity")
END
```

<a name="NOTE6_5"></a>


### Valor de una celda en una fila específica
<a name="valor_una_celda_una_fila_especifica_ELTPARAGRAPHE000327"></a>Para obtener el valor de una columna que no se encuentra en la fila actual, se debe especificar el índice de la fila.


```txt
<Value> = <Column name>[<Index>]
```
Por ejemplo: 

```wl
// Add the total price before tax (PBT) for all the order lines
TotalPrice = 0
FOR Index = 1 _TO_ TABLE_ORDER.Count
	TotalPrice = TotalPrice + COL_PBT[Index]
END
```

<a name="NOTE6_6"></a>


### Contenido de la fila actual
<a name="contenido_fila_actual_ELTPARAGRAPHE000341"></a>Para obtener el contenido de la fila actual, utilice la siguiente sintaxis:


```txt
<Value> = <Table control>
```

<a name="NOTE6_7"></a>


### Contenido de una fila específica
<a name="contenido_una_fila_especifica_ELTPARAGRAPHE000352"></a>Para obtener el contenido de la fila en el índice &lt;Index&gt;, utilice la siguiente sintaxis:


```txt
<Value> = <Table control>[<Index>]
```

**Observación**: El índice de la fila actual se puede obtener utilizando la función [TableSelect](../WDLang1/3074020.md). 
Por ejemplo:

```wl
// Retrieve row #10 in the TABLE_CUSTOMER control
CurrentRow = TABLE_CUSTOMER[10]
 
// Name of selected customer
CustomerName = COL_NAME[TableSelect(TABLE_CUSTOMER)]
 
// Retrieve the current row in the TABLE_CUSTOMER control
CurrentRow = TABLE_CUSTOMER
```



