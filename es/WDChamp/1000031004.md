


## Manipular controles Cuadro de mando mediante programación
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Presentación
<a name="presentacion_ELTTEXTE000276"></a>
Para manipular controles Cuadro de mando mediante programación, WINDEV, WEBDEV y WINDEV Mobile incluyen [funciones específicas de los Cuadros de mando](../WDLang1/1000021048.md). 

Esta página de ayuda explica cómo manipular controles Cuadro de mando mediante programación. 

![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png) Observación: Los controles Cuadro de mando solo están disponibles en sitios WEBDEV Linux. Los controles Cuadro de mando no están disponibles en aplicaciones WINDEV para Linux.



<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Manipular controles Cuadro de mando mediante programación
<a name="manipular_controles_cuadro_mando_mediante_programacion_ELTTEXTE000300"></a>


### Agregar un widget
<a name="agregar_widget_ELTPARAGRAPHE000042"></a>

Los widget pueden agregarse a los controles Cuadro de mando con la función [DashAddWidget](../WDLang1/1000020953.md). De forma predeterminada, los widgets son invisibles. Se pueden mostrar utilizando la propiedad [Visible](../Proprietes/2510138.md). Si el widget no es visible, el usuario puede hacerlo visible a través del menú contextual del modo de edición.

Ejemplos: 

- Agregar un widget simple (sin parámetros): 
	
	```wl
	// Adds a simple widget
	nIndex = DashAddWidget(DASH_Dashboard, IW_Widget_StockAlert, ...
		"Stock alert")
	```


- Agregar un widget con parámetros: 
	
	```wl
	nIndex is int
	
	// Adds a widget that expects parameters
	nIndex = DashAddWidget(DASH_Dashboard, IW_Widget_Clock, ...
		"Clock" + COMBO_AddWidget[COMBO_AddWidget].DisplayedValue, ...
		COMBO_AddWidget[COMBO_AddWidget].DisplayedValue)
	
	// Displays this widget upon request
	IF YesNo(Yes, "Do you want to display this widget?") = Yes THEN
		DASH_Dashboard[nIndex].Visible = True
	ELSE
	 	ToastDisplay("Widget added to the dashboard as ""hidden"". "+ ...
		"Switch to edit mode to add it", toastShort, vaMiddle, haCenter)
	END
	```



**Caso especial: agregar un widget a la configuración inicial**
De forma predeterminada, el control Cuadro de mando muestra los widgets tal y como se definieron en el editor de ventanas o páginas. 

Para agregar widgets mediante programación cuando se carga el control Cuadro de mando, utilice: 

- la función [DashAddWidget](../WDLang1/1000020953.md) para definir el widget a agregar. 

- la función [DashInitialConfiguration](../WDLang1/1000021043.md) para configurar la posición del widget en la configuración inicial. 




Atención: En este caso, estas funciones deben utilizarse en el evento "Inicialización" del control Cuadro de mando. 

Ejemplos: 

- Agregar un widget simple (sin parámetros)
	
	```wl
	// -- Initialize the DASH_Dashboard control
	// - stock alert
	// Adds the widget
	nIndex = DashAddWidget(MySelf, IW_Widget_StockAlert, "Stock alert")
	// Configures the widget (placed on the left)
	DashInitialConfiguration(MySelf, nIndex, 5, 1)
	```


- Agregar un widget con parámetros
	
	```wl
	// -- Initialize the DASH_Dashboard control
	
	dDate is Date
	nIndex is int
	nX, nY are int
	
	// Adds information to the initial configuration:
	// - key numbers for the last 3 months
	nX = 1 ; nY = 3
	FOR i = 1 TO 3
		// Adds the widget
		nIndex = DashAddWidget(MySelf, IW_Widget_KeyNumber, ...
			StringBuild("Key numbers for %1", ...
			DateToString(dDate, "Mmm YYYY")), dDate)
		// Configures the widget (placed on the left)
		DashInitialConfiguration(Myself, nIndex, nX, nY)
		// The next one will be lower
		nY++
		dDate.Month--
	END
	```



Observación: Para actualizar un widget con parámetros, se recomienda:  

- Agregar un evento opcional para actualizar el widget en los eventos de la ventana o página interna. 

- Introducir el código utilizado para actualizar el widget. Este código puede utilizarse, por ejemplo, para actualizar los elementos que se muestran en la ventana o página interna. 

- Actualizar el widget con la función [DashDisplay](../WDLang1/1000020970.md). 



<a name="NOTE2_2"></a>


### Ejecutar un procedimiento definido para una ventana o página interna (utilizada como widget)
<a name="ejecutar_procedimiento_definido_para_una_ventana_pagina_interna_utilizada_como_widget_ELTPARAGRAPHE000121"></a>

Para ejecutar un procedimiento definido en la ventana o página interna utilizada como widget, utilice las siguientes sintaxis: 

- Sintaxis que utiliza el nombre de la ventana o página interna: 
	
	```txt
	NameInternalWindow.ProcedureName(Param1, Param2, ...)
	```

	Por ejemplo: 
	
	```wl
	IW_Widget_KeyNumber.NewDate(EDT_Date)
	```


- Sintaxis que utiliza una variable de tipo Control (si se utiliza la misma ventana o página interna varias veces en el control Cuadro de mando): 
	
	```txt
	VariableName is Control <- DashboardName[WidgetIndex]
	VariableName.ProcedureName(Param1, Param2, ...)
	```

	Por ejemplo: 
	
	```wl
	X is Control <-DASH_Dashboard[4]
	X.NewDate(EDT_Date)
	```





<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Manipular un widget mediante programación
<a name="manipular_widget_mediante_programacion_ELTTEXTE000330"></a>
Para manipular un widget mediante programación, utilice una de las siguientes sintaxis: 

- Sintaxis 1: Uso directo del control Cuadro de mando:  
	
	```txt
	DashboardName[WidgetIndex]
	```

	Por ejemplo: 
	
	```wl
	Trace(DASH_MyDashboard[1].Caption)
	```


- Sintaxis 2: Uso de una variable de tipo Control : 
	
	```txt
	VariableName is Control <- DashboardName[WidgetIndex]
	```

	Por ejemplo: 
	
	```wl
	X is Control <-DASH_Dashboard[4]
	Trace(X.Caption)
	```





Las propiedades que se pueden utilizar en los widgets son las mismas que se pueden utilizar en las ventanas o páginas internas. Para obtener más información, consulte [Propiedades asociadas a las ventanas internas](../WDChamp/1013256.md) y [Propiedades asociadas a las páginas internas](../WDChamp/1013282.md). 

<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Arrastrar y soltar widgets en un control Cuadro de mando
<a name="arrastrar_soltar_widgets_control_cuadro_mando_ELTTEXTE000354"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) WINDEV permite arrastrar y soltar controles Imagen, List Box y Looper (entre otros) en un control Cuadro de mando. 

Las etapas son las siguientes: 

1. **Declarar que el control Cuadro de mando es el destino del arrastre**.
	Esta declaración se realiza en el código de inicialización del control con la propiedad [DndTarget](../Proprietes/2510016.md). 
	Ejemplo: 
	
	```wl
	// -- Initialize DASH_Dashboard
	// Allows drag-and-drop operations
	MySelf.DndTarget = dndAuto
	```


2. **Declarar que el control Cuadro de mando es el origen del arrastre**.
	En el código de inicialización del control de origen, declare que el control es el origen del arrastre con la propiedad [DndSource](../Proprietes/2510014.md). 
	Ejemplo: 
	
	```wl
	// The image is the drag source
	MySelf.DndSource = dndProgram
	```


3. **Definir el procedimiento para iniciar la operación de arrastre** (por ejemplo, en el código de inicialización del control de origen).
	Ejemplo: 
	
	```wl
	// Defines the drag start procedure
	// This procedure uses DnDCacheDashElement 
	// to define the widget to drop
	DnDEvent(onDndBegin, MySelf, dndBeginDrag)
	```


4. **Escriba el código del procedimiento**. 
	Ejemplo: 
	
	```wl
	PROCEDURE onDndBegin()
	
	// Define the widget to drop according to the source control
	SWITCH _DND.SourceControl
		// Indicates that the drop on the Dashboard control 
		// must add the "Clock" widget 
		// by using the "IW_Widget_Clock" internal window
		CASE IMG_Widget_Calendar.Name
			DnDCacheDashElement(IW_Widget_Calendar, "Clock")
			
		OTHER CASE
			Error("Only the IMG_Widget_Calendar image" + ...
				"is allowed for Drag and Drop")
	END
	```





<a name="NOTE7"></a>
<a name="NOTE7_1"></a>


## Propiedades específicas de los controles Cuadro de mando
<a name="propiedades_especificas_los_controles_cuadro_mando_ELTTEXTE000378"></a>
Utilice las siguientes propiedades para manipular los controles Cuadro de mando mediante programación.



|   |   |
| --- | --- |
| [CompactOption](../Proprietes/1000020608.md) | El **CompactOption** Property se utiliza para:<br><br>- averigua si las opciones que se encuentran en una casilla de verificación, botón de selección o control Cuadro de mando están en modo compactado..<br><br>- cambia las opciones que se encuentran en una casilla de verificación, botón de selección o control Cuadro de mando a modo compactado..<br><br><br> |
| [ElementHeight](../Proprietes/1000020068.md) | La propiedad **ElementHeight** permite:<br><br>- Descubra o modifique la altura de los elementos de un control Organigrama. <br><br>- Descubre o modifica la altura de las celdas de un control Cuadro de mando.<br><br><br> |
| [ElementWidth](../Proprietes/1000020066.md) | La propiedad **ElementWidth** permite:<br><br>- Descubra o modifique la anchura de los elementos de un control Organigrama.<br><br>- Descubre o modifica el ancho de las celdas de un control Cuadro de mando.<br><br><br> |
| [MarginHeight](../Proprietes/1000021029.md) | El **MarginHeight** Property se utiliza para: <br><br>- Averigua el margen vertical entre los widgets que se encuentran en un control Cuadro de mando. <br><br>- Modificar el margen vertical entre los widgets que se encuentran en un control Cuadro de mando.<br><br><br> |
| [MarginWidth](../Proprietes/1000021028.md) | El **MarginWidth** Property se utiliza para: <br><br>- Descubre el margen horizontal entre los widgets que se encuentran en un control Cuadro de mando. <br><br>- Modifica el margen horizontal entre los widgets que se encuentran en un control Cuadro de mando.<br><br><br> |





Para obtener una lista completa de las propiedades WLanguage que pueden utilizarse con controles Cuadro de mando, consulte [Propiedades del control Cuadro de mando](../WDChamp/1000031003.md).


