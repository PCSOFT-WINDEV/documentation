
## Anexo 4. Presentación de las variables disponibles


<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Esta lección abarcará los siguientes temas
<a name="esta_leccion_abarcara_los_siguientes_temas_ELTTEXTE000122"></a>


- Los tipos Date, Time y Duration.

- El tipo Array.

- El tipo Structure. 





|   |   |
| --- | --- |
| <br>![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=dur%E9e.png)<br> | <br>Tiempo estimado: 1 h |

| [Lección anterior](../TutoWB/1410087228.md) | [Tabla de contenido](../TutoWB/1410087510.md) | [Siguiente lección](../TutoWB/1410087231.md) |
| --- | --- | --- |





<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Presentación
<a name="presentacion_ELTTEXTE000179"></a>
Además de los tipos simples (boolean, integer, real, etc.), WLanguage propone varios tipos de variables que permiten manipular diferentes casos específicos. 

Vamos a ver de manera general estos diferentes tipos de variables con un pequeño ejemplo práctico para cada una de ellas. Para obtener más información, consulte la ayuda en línea de cada tipo. 



- Si es necesario, abra el proyecto "WLanguage" de la primera lección (véase [Un proyecto para descubrir WLanguage](../TutoWB/1410087226.md) en la lección Anexo 2).




<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Variables de tipo Date, Time y Duration
<a name="variables_tipo_date_time_duration_ELTTEXTE000226"></a>
WLanguage propone diferentes tipos de variables para manipular fechas, horas y duraciones.


### El tipo Date
<a name="tipo_date_ELTPARAGRAPHE000027"></a>

Para las fechas, WLanguage propone el tipo **Date**. Se pueden utilizar diferentes funciones y propiedades para manipular las fechas.  

- Usaremos un ejemplo de código pequeño para manipular las fechas: 

	1. Elimine el código en el evento "Inicialización" del proyecto (presione Ctrl + A para seleccionar todo y "Supr" para eliminar).

	2. Escriba el siguiente código: 
			
		```wl
		MyDate is Date
		```
Esta línea de código declara una variable de tipo Date. De forma predeterminada, la variable se inicializa con la fecha actual. 

	3. Ahora, veamos cómo asignar un valor a una fecha. 
		```wl
		MyDate = Yesterday()
		MyDate = "20210929"
		```
Este código presenta las diferentes formas de inicializar una variable de tipo Date. La variable se puede inicializar: 

		- mediante una función WLanguage. En este ejemplo, se utiliza la función [Yesterday](../WDLang1/1000023786.md). WLanguage propone un gran número de funciones para determinar el primer o último día de un mes o año, ... 

		- mediante una cadena que contiene la fecha en formato "AAAAMMDD". 




4. También es posible cambiar los parámetros de la fecha usando propiedades WLanguage. Escriba el siguiente código: 
			
		```wl
		// Change date parameters
		MyDate.Year = 1985
		MyDate.Month = 10
		MyDate.Day = 26
		```


5. La función [&lt;Fecha&gt;.ToString](../WDLang1/1000023952.md) permite definir el formato para mostrar las fechas. Por ejemplo, introduzca el siguiente código: 
			
		```wl
		Trace(MyDate.ToString())
		Trace(MyDate.ToString("Dddd Mmmm DD YYYY"))
		Trace(MyDate.ToDayInAlpha())
		```


6. Probemos este código: haga clic en ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=ICO_GO_Projet_WB_GAF.jpg) en los botones de acceso rápido.  

7. El panel "Traza del depurador" contiene: 
			
		```txt
		26/10/1985
		Saturday October 26 1985
		Saturday
		```


	- la fecha en un formato legible.

	- la fecha en un formato legible, utilizando una máscara de visualización.

	- el día de la semana que corresponde a la fecha.





### El tipo Time
<a name="tipo_time_ELTPARAGRAPHE000032"></a>



- Del mismo modo, se pueden manipular las horas utilizando el tipo **Time**. 

	1. Elimine el código en el evento "Inicialización" del proyecto (presione Ctrl + A para seleccionar todo y "Supr" para eliminar).

	2. Escriba el siguiente código: 
			
		```wl
		// Declare a time
		MyTime is Time // By default, the time is set to the current time
		
		// Assign a time
		// - via a WLanguage function
		MyTime = Now()
		// - set in the code (e.g. 3:25 PM)
		MyTime = "1525"
		
		// Change time parameters
		MyTime.Hour = 12
		MyTime.Minute = 0
		MyTime.Second = 0
		
		// Display time in a readable format
		Trace(MyTime.ToString())
		// Display time in a readable format using a display mask
		Trace(MyTime.ToString("HH:mm:SS"))
		
		// Compare 2 times
		IF MyTime < Now() THEN
			Trace("The time is earlier than the current time")
		ELSE
			Trace("The time is later than the current time")
		END
		```
El código toma todas las posibilidades presentadas en las fechas y las aplica a las horas. 

	3. Probemos este código: haga clic en ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=ICO_GO_Projet_WB_GAF.jpg) en los botones de acceso rápido.  

	4. El panel "Traza del depurador" muestra la información deseada. 
			
		```txt
		12:00:00:00
		12:00:00
		The time is earlier than the current time
		```


		- la hora en un formato legible.

		- la hora en un formato legible, utilizando una máscara de visualización.








### El tipo Duration
<a name="tipo_duration_ELTPARAGRAPHE000037"></a>



- Se puede escribir el mismo tipo de código para las duraciones. 

	1. Elimine el código en el evento "Inicialización" del proyecto (presione Ctrl + A para seleccionar todo y "Supr" para eliminar).

	2. Escriba el siguiente código: 
			
		```wl
		// Declare a duration
		MyDuration is Duration
		
		// Assign a duration
		// - with a value in readable format
		MyDuration = 2 min 8 s
		// or 
		MyDuration = 128 s
		// - by specifying the duration in seconds, in minutes, ...
		MyDuration.InSeconds = 128
		
		// Display duration in a readable format
		Trace(MyDuration.ToString("MM:SS"))
		```


	3. Probemos este código: haga clic en ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=ICO_GO_Projet_WB_GAF.jpg) en los botones de acceso rápido.  

	4. El panel "Traza del depurador" muestra la información deseada. 
			
		```txt
		02:08
		```








Para obtener más información sobre las variables de tipo Date, Time y Duration, consulte las páginas de ayuda correspondientes: 

- [Date](../Motscles/1514066.md),

- [Time](../Motscles/1514068.md),

- [Duration](../Motscles/1514069.md).




<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Variables de tipo Array
<a name="variables_tipo_array_ELTTEXTE000407"></a>
Los arrays son uno de los tipos de variable más utilizados.

Un array es un tipo estructurado que permite agrupar una serie de elementos del mismo tipo. Se puede acceder directamente a cada elemento del array por su índice.

Ciertas funciones específicas de WLanguage permiten manipular los arrays y sus elementos.
<a name="NOTE3_2"></a>


### Declaración
<a name="declaracion_ELTPARAGRAPHE000051"></a>

Una variable de tipo Array se declara de la siguiente manera: 

```txt
<Array name> is array of <Type of array elements>
```


Por ejemplo:

```wl
MyArrayOfStrings is array of strings
MyArrayOfIntegers is array of int
```

<a name="NOTE3_3"></a>


### Rellenar un array y acceder a los elementos
<a name="rellenar_array_acceder_los_elementos_ELTPARAGRAPHE000058"></a>

Al declarar el array, este está vacío. Los elementos se agregan con la función [&lt;Array&gt;.Add](../WDLang1/1000024299.md) utilizando la siguiente sintaxis: 

```txt
<Array name>.Add(<Element value>)
```


Para acceder a los elementos de un array, utilice la siguiente sintaxis:

```txt
<Array name> [<Element index>]
```
|   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=avertissement.png) | Importante | Los índices de los elementos del array comienzan en 1. |







- Usaremos un pequeño ejemplo de código para agregar elementos a un array: 

	1. Elimine el código en el evento "Inicialización" del proyecto (presione Ctrl + A para seleccionar todo y "Supr" para eliminar).

	2. Escriba el siguiente código: 
			
		```wl
		// Create an array of strings
		MyArray is array of strings
		
		// Add elements
		MyArray.Add("WINDEV")
		MyArray.Add("WEBDEV")
		MyArray.Add("WINDEV Mobile")
		
		// Display the content of the third element
		Trace("Value of element 3: [%MyArray[3]%]")
		```


	3. Probemos este código: haga clic en ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=ICO_GO_Projet_WB_GAF.jpg) en los botones de acceso rápido.  

	4. Si es necesario, abra el panel "Traza del depurador" para ver el resultado: en la pestaña "Inicio", en el grupo "Entorno", despliegue "Paneles", seleccione "Paneles" y, a continuación, haga clic en "Traza del depurador". 

	5. El panel "Traza del depurador" contiene la siguiente información: 
			
		```txt
		Value of element 3: WINDEV Mobile
		```






|   |   |   |
| --- | --- | --- |
| ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=astuce.png) | Consejo | **Inicializar un array rápidamente** <br><br>También puede utilizar la siguiente sintaxis para inicializar un array: <br><br><pre><code>// Declare an array<br>DayArray is array of strings<br>// Initialization with <br>// the names of the days of the week<br>DayArray = ["Monday", "Tuesday", "Wednesday", "Thursday", <br>"Friday", "Saturday", "Sunday"]</code></pre><br> |




<a name="NOTE3_4"></a>


### Recorrer un array
<a name="recorrer_array_ELTPARAGRAPHE000065"></a>

WLanguage ofrece dos métodos para recorrer los elementos de un array: 

- Un bucle estándar de tipo FOR .. TO. 

- Un bucle FOR EACH.




- En un bucle FOR .. TO .. estándar, deben especificarse los valores del índice inicial (1) y del índice final. 

	1. Por ejemplo, agregue el siguiente código en el evento "Inicialización" del proyecto: 
			
		```wl
		FOR Index = 1 TO MyArray.Count
			Trace("Value of element [%Index%]: [%MyArray[Index]%]")
		END
		```

			En este código, el índice de inicio es 1 y el índice de fin es dado por la propiedad [Count](../Proprietes/2510097.md) del array. 

	2. Probemos este código: haga clic en ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=ICO_GO_Projet_WB_GAF.jpg) en los botones de acceso rápido.  

	3. El panel "Traza del depurador" contiene la siguiente información: 
			
		```txt
		Value of element 1: WINDEV
		Value of element 2: WEBDEV
		Value of element 3: WINDEV Mobile
		```








- WLanguage ofrece un tipo de bucle mucho más simple e igual de efectivo: el bucle FOR EACH. Ya lo hemos visto en las cadenas. Este tipo de bucle también se aplica a los arrays. En este caso, no es necesario especificar el índice de inicio o fin.  

	1. Por ejemplo, reemplace el código del bucle FOR por el siguiente código en el evento "Inicialización" del proyecto: 
			
		```wl
		FOR EACH ELEMENT AString, Index OF MyArray
			Trace("Value of element [%Index%]: [%AString%]")
		END
		```
En este caso, simplemente tuvimos que especificar el nombre de la variable que corresponde al elemento del array y la variable que corresponde al índice. 

	2. Probemos este código: haga clic en ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=ICO_GO_Projet_WB_GAF.jpg) en los botones de acceso rápido.  

	3. El panel "Traza del depurador" contiene la siguiente información: 
			
		```txt
		Value of element 1: WINDEV
		Value of element 2: WEBDEV
		Value of element 3: WINDEV Mobile
		```








Para obtener más información sobre las variables de tipo Array, consulte la página [Array](../Motscles/1514030.md).
<a name="NOTE3_6"></a>


### Array asociativo
<a name="array_asociativo_ELTPARAGRAPHE000072"></a>

Un array asociativo es un tipo "avanzado" de array: permite agrupar una serie de elementos del mismo tipo. Cada elemento del array está indexado en cualquier tipo de información (y no solo en un índice numérico, como en otros tipos de arrays).

Los Arrays asociativos (con o sin duplicados) se pueden manipular en WLanguage.  



- Hagamos una prueba rápida. 

	1. Elimine el código en el evento "Inicialización" del proyecto (presione Ctrl + A para seleccionar todo y "Supr" para eliminar).

	2. Escriba el siguiente código: 
			
		```wl
		// Create an associative array of dates
		MyArray is associative array of Date
		
		// Add elements
		MyArray["Mark"] = "19820201"
		MyArray["Anne"] = "19840604"
		
		// Display contents of the element with key "Mark"
		Trace("Value of element with key ""Mark"": " + MyArray["Mark"])
		
		// Loop through the array
		FOR EACH ELEMENT Value, Key, Index OF MyArray
			Trace("Value of element [%Index%] with key [%Key%]: [%Value%]")
		END
		```


	3. Probemos este código: haga clic en ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=ICO_GO_Projet_WB_GAF.jpg) en los botones de acceso rápido.  

	4. El panel "Traza del depurador" contiene la siguiente información: 
			
		```txt
		Value of element with key "Mark": 19820201
		Value of element 1 with key Mark: 19820201
		Value of element 2 with key Anne: 19840604
		```








Para obtener más información sobre las variables de tipo Associative array, consulte la página [Associative array](../Motscles/1514058.md).

<a name="NOTE7"></a>
<a name="NOTE7_1"></a>


## Variables de tipo Structure
<a name="variables_tipo_structure_ELTTEXTE000665"></a>


### El tipo Structure
<a name="tipo_structure_ELTPARAGRAPHE000081"></a>

Una estructura es un tipo de datos personalizado. Las estructuras contienen diferentes tipos de variables.

- Usaremos un ejemplo de código pequeño para manipular las estructuras: 

	1. Elimine el código en el evento "Inicialización" del proyecto (presione Ctrl + A para seleccionar todo y "Supr" para eliminar).

	2. Escriba el siguiente código: 
			
		```wl
		FullName is Structure
			LastName is string
			FirstName is string
		END
		```
Este código declara una estructura compuesta por dos variables, LastName y FirstName. Las variables que forman parte de una estructura se llaman "Miembros". Para manipular los miembros, debe declarar una variable del tipo de la estructura. 

	3. Ahora vamos a: 

		- Declarar una variable de tipo Structure. Agregue el siguiente código WLanguage: 
						
			```wl
			Contact is FullName
			```


		- Asignar valores a los miembros de la estructura. Agregue el siguiente código WLanguage: 
						
			```wl
			Contact.LastName = "POLO"
			Contact.FirstName = "MARCO"
			```





4. Para leer el contenido de un miembro, utilice la siguiente sintaxis:
			
		```txt
		<Variable name>.<Member name>
		```
En nuestro ejemplo, escriba el siguiente código: 
			
		```wl
		TheLastName is string
		TheLastName = Contact.LastName
		// Displays the content of TheLastName
		Trace(TheLastName)
		// Displays Contact.LastName directly (without using a variable)
		Trace(Contact.LastName)
		```


5. Probemos este código: haga clic en ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=ICO_GO_Projet_WB_GAF.jpg) en los botones de acceso rápido.  

6. El panel "Traza del depurador" muestra: 
			
		```txt
		POLO
		POLO
		```





Al igual que con los otros tipos de variables, también es posible manipular arrays de estructuras. 

- Completemos nuestro ejemplo para probar el uso de arrays de estructuras: 

	1. Escriba el siguiente código después del código ya introducido: 
			
		```wl
		// Declare an array of structures
		MyContacts is array of FullName
		Add(MyContacts, Contact)
		Trace(MyContacts[1].LastName, MyContacts[1].FirstName)
		```
En este código, se crea un array que contiene la estructura FullName, luego se agrega el contacto creado previamente y finalmente se muestran el apellido y el nombre del contacto. 

	2. Probemos este código: haga clic en ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=ICO_GO_Projet_WB_GAF.jpg) en los botones de acceso rápido.  

	3. El panel "Traza del depurador" ahora muestra: 
			
		```txt
		POLO
		POLO
		POLO MARCO
		```








Para obtener más información sobre las variables de tipo Structure y Array of structures, consulte la página [Structure](../Motscles/1514008.md).

<a name="NOTE8"></a>
<a name="NOTE8_1"></a>


## Conclusión
<a name="conclusion_ELTTEXTE000763"></a>
En esta lección, vimos algunos de los tipos de variables disponibles en WLanguage: 

- el tipo String,

- los tipos Date, Time y Duration,

- el tipo Array,

- el tipo Structure. 




En la próxima lección, veremos cómo manipular algunas de estas variables usando declaraciones condicionales disponibles en WLanguage.



| [Lección anterior](../TutoWB/1410087228.md) | [Tabla de contenido](../TutoWB/1410087510.md) | [Siguiente lección](../TutoWB/1410087231.md) |
| --- | --- | --- |




