
## Asociativo array (Tipo de Variable)

*** ***
				



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Un array asociativo es un tipo "avanzado" de array: permite agrupar una serie de elementos del mismo tipo. Cada elemento del array está indexado en cualquier tipo de información (y no solo en un índice numérico, como en otros tipos de arrays).

**Observación**: Este tipo de array le permite acceder rápidamente a cualquier elemento, con algunas opciones en las cuerdas.


<a name="Example1"></a>
<a name="sample_code"></a>

## Ejemplo


```wl
// Fills an associative array with the sizes of files
aaFileSize is associative array of int
aaFileSize["File1.txt"] = fSize("File1.txt")
aaFileSize["File2.txt"] = fSize("File2.txt")

// Retrieves the size of a file
Trace(aaFileSize["File2.txt"])

// Iterates over file sizes
sFile is string
nSize is int
FOR EACH ELEMENT nSize,sFile OF aaFileSize
	Trace(StringBuild("%1: %2", sFile, nSize)) 
END
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Sintaxis

### Declarando una asociación array

`<Array name> is associative array [(<Options> [, <Default value> [, <Key type> [, <Initial size>]]])] of <Type>
`
---

**`<Array name>: ()`**

Nombre de la array Variable a declarar.

**`<Options>: ()`**

Combinación de constantes utilizadas para configurar el modo de funcionamiento de la array: 



|   |   |
| --- | --- |
| *WithDuplicates* | Esta constante se utiliza para permitir que los duplicados. Por Default, una array asociativa no puede contener ningún duplicado. |
| *ccIgnoreAccent* | Si el tipo de clave es una cadena (por Default), la indexación ignora los caracteres acentuados.<br><br> |
| *ccIgnoreCase* | Si el tipo de clave es una cadena (por Default), la indexación no distingue entre mayúsculas y minúsculas. |
| *ccIgnoreSpace* | Si el tipo de clave es una cadena (por Default), la indexación ignora los caracteres espaciales. |
| *ccIgnorePunctuationAndSpace* | Si el tipo de clave es una cadena (por Default), la indexación no es sensible a la puntuación e ignora los caracteres espaciales.<br><br> |
| *WithoutDuplicates* ( Default ) | Por Default, una array asociativa no puede contener ningún duplicado.<br>Puede dejar esta constante sin especificar. |





El valor "\*" permite conservar el valor predeterminado de la opción.

**`<Default value>: ()`**

Valor devuelto al acceder a un elemento inexistente en una array asociativa sin duplicado. Este parámetro es ignorado en una array asociativa con duplicados. Por defecto, se utiliza el valor predeterminado del tipo de array (0, "", False).

El valor "\*" permite conservar el valor predeterminado del tipo.

**`<Type of key>: ()`**

Tipo de clave utilizada para navegar por la array. Por Default, la operación de búsqueda se realiza en una tecla de cadena. Los tipos autorizados son los siguientes:



|   |   |
| --- | --- |
| *wlString* | Cadena de caracteres |
| *wlUnicodeString* | Cadena Unicode |
| *wlDate* | salir con |
| *wlDateTime* | FechaHora |
| *wlInt* | Firmado un entero de 4 bytes |
| *wlInt _8* | Firmado un entero de 8 bytes |
| *wlUnsignedInt_4* | Unsigned 4-byte integer |
| *wlUnsignedInt_8* | Un entero de 8 bytes sin firmar |
| *wlTime* | momento |
| *wlCurrency* | Moneda |
| *wlNumeric* | Numeric |
| *wlReal* | 8 bytes reales |

El valor "\*" permite conservar el tipo de clave predeterminada (string).



**`<Initial size>: ()`**

Integro. Corresponde al tamaño inicial de la zona de memoria para la array asociativa.

La adición en una array asociativa puede ser bastante larga si la array se redimensiona. En caso de adiciones múltiples, le recomendamos que sobredimensione el array para optimizar las adiciones.

El valor "\*" permite conservar el tamaño predeterminado.



**`<Type>: ()`**

Tipo de elementos que se encuentran en la array. 
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png) Observación: Los elementos que componen la array también pueden ser arrays, arrays fijo, arrays asociativo, colas, pilas o listas.. 


<a name="SYNTAX2"></a>

### Declarando un parámetro asociativo de array

`<Parameter name> is associative array of <Type>
`
---

**`<Parameter name>: ()`**

Nombre de la array Variable a declarar.

**`<Type>: ()`**

Tipo de elementos que se encuentran en la array. 
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png) Observación: Los elementos que componen la array también pueden ser arrays, arrays fijo, arrays asociativo, colas, pilas o listas.. 



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Observaciones




### Acceso a la arrays asociativa
<a name="acceso_arrays_asociativa_ELTPARAGRAPHE000206"></a>

Las siguientes propiedades pueden ser utilizadas para manipular una asociación array:


|   |   |
| --- | --- |
| [Conde](../Proprietes/2510097.md) | Devuelve el número de ocurrencias en la array. |
| [Vacío](../Proprietes/2510126.md) | Permite saber si la array está vacía o no. |

Las sintaxis como ***&Array*** y ***Array1 = Array2*** no están permitidas. La asociación arrays no permite a los operadores de la reference ni la copia.
<a name="NOTE0_2"></a>




### Acceder a los elementos de la arrays asociativa
<a name="acceder_los_elementos_arrays_asociativa_ELTPARAGRAPHE000227"></a>

El acceso a los elementos que se encuentran en una array asociativa difiere dependiendo de si la array asociativa acepta duplicados o no.


|   |   |
| --- | --- |
| **Array asociativo sin duplicados** |   |
| Propiedad [Exist](../Proprietes/1000022167.md) | <br><pre><code>MyArray[MyKey]..Exist</code></pre><br><br>Devuelve <u><u><u><u>True</u></u></u></u> si el elemento MyKey existe, o <u><u><u><u>False</u></u></u></u> en el caso contrario.<br><br> |
| Propiedad [Vacío](../Proprietes/2510126.md) | <br><pre><code>MyArray[MyKey]..Empty</code></pre><br><br>Devuelve <u><u><u><u>True</u></u></u></u> si el elemento MyKey no existe, o <u><u><u><u>False</u></u></u></u> en el caso contrario. |
| Propiedad [Conde](../Proprietes/2510097.md) | <br><pre><code>MyArray[MyKey]..Occurence</code></pre><br><br>Devuelve 1 si el elemento MyKey existe, 0 si el elemento no existe. |
| Asignación de un elemento | <br><pre><code>MyArray[MyKey] = <Value></code></pre><br><br>Si el elemento MyKey no existe, se crea un elemento MyKey y se asigna con .<br>Si existe el elemento MyKey, se modifica su valor. |
|   | <br><pre><code>MyArray[MyKey, Index] = Value</code></pre><br><br>Se produce un error de WLanguage si el índice es diferente de 1.<br>Se produce un error de WLanguage si el elemento MyKey no existe..<br>Si existe el elemento MyKey, se modifica su valor. <br><br>Sintaxis compatible con la arrays asociativa con duplicados. |
|   | <br><pre><code>MyArray[MyKey]++</code></pre><br><br>Crea la entrada en la array asociativa , con el valor Default especificado al declarar la array. |
| Acceso al elemento | <br><pre><code>MyArray[MyKey]</code></pre><br><br>Si el elemento MyKey no existe, devuelve el valor Default del array.<br>Si existe el elemento MyKey, devuelve el valor del elemento. |
|   | <br><pre><code>MyArray[MyKey, Index]</code></pre><br><br>Se produce un error de WLanguage si el índice es diferente de 1.<br>Se produce un error de WLanguage si el elemento MyKey no existe..<br>Si el elemento MyKey existe, devuelve el valor del elemento.<br><br>Sintaxis compatible con la arrays asociativa con duplicados. |
| Address del elemento | <br><pre><code>&MyArray[MyKey]</code></pre><br><br>Se produce un error de WLanguage si el elemento MyKey no existe..<br>Si el elemento MyKey existe, devuelve la Address del elemento.<br><br> |
|   | <br><pre><code>&MyArray[MyKey, Index]</code></pre><br><br>Se produce un error de WLanguage si el índice es diferente de 1.<br>Se produce un error de WLanguage si el elemento MyKey no existe..<br>Si el elemento MyKey existe, devuelve la Address del elemento.  <br><br>Sintaxis compatible con la arrays asociativa con duplicados.<br><br> |






|   |   |
| --- | --- |
| **Array asociativo con duplicados** |   |
| Propiedad [Exist](../Proprietes/1000022167.md) | <br><pre><code>MyArray[MyKey]..Exist</code></pre><br><br>Devuelve <u><u><u><u>True</u></u></u></u> si existe al menos un elemento MyKey, <u><u><u><u>False</u></u></u></u> si el elemento MyKey no existe.<br><br> |
| Verificación de la existencia de una clave: Propiedad [Vacío](../Proprietes/2510126.md) | <br><pre><code>MyArray[MyKey]..Empty</code></pre><br><br>Devuelve <u><u><u><u>True</u></u></u></u> si el elemento MyKey no existe, <u><u><u><u>False</u></u></u></u> si existe al menos un elemento MyKey. |
| Número de elementos encontrados en una clave: Propiedad [Conde](../Proprietes/2510097.md) | <br><pre><code>MyArray[MyKey]..Occurence</code></pre><br><br>Devuelve el número de elementos MyKey. |
| Asignación de un elemento | <br><pre><code>MyArray[MyKey] = <Value></code></pre><br><br>Crea un elemento MyKey y asigna el valor especificado a este elemento (incluso si los elementos MyKey ya existen). |
|   | <br><pre><code>MyArray[MyKey, Index] = Value</code></pre><br><br>Se produce un error de WLanguage si el índice es mayor que el número de elementos existentes.<br>Se produce un error de WLanguage si el elemento MyKey no existe.. <br>Si el elemento MyKey existe, se modifica el valor del elemento identificado por su índice. |
| Acceso al elemento | <br><pre><code>MyArray[MyKey]</code></pre><br><br>Sintaxis inválida que desencadena un WLanguage Error( porque varios elementos pueden corresponder a la misma clave). |
|   | <br><pre><code>MyArray[MyKey, Index]</code></pre><br><br>Se produce un error de WLanguage si el índice es mayor que el número de elementos existentes.<br>Se produce un error de WLanguage si el elemento MyKey no existe..<br>Si el elemento MyKey existe, devuelve el valor del elemento identificado por su índice. |
| Address del elemento | <br><pre><code>&MyArray[MyKey]</code></pre><br><br>Sintaxis no permitida |
|   | <br><pre><code>&MyArray[MyKey, Index]</code></pre><br><br>Se produce un error de WLanguage si el índice es mayor que el número de elementos existentes.<br>Se produce un error de WLanguage si el elemento MyKey no existe..<br>Si el elemento MyKey existe, devuelve la Address del elemento identificado por su índice.<br><br> |


Tiene la posibilidad de utilizar [la asignación múltiple](../Motscles/1514031.md) para el asociativo arrays.<a name="NOTE0_2_1"></a>


### ¿Cómo se puede iniciar una array asociativa tan pronto como se declara?
<a name="¿como_puede_iniciar_una_array_asociativa_tan_pronto_como_declara_ELTPARAGRAPHE000456"></a>Para inicializar una array asociativa nada más declararla, basta con añadir las parejas "clave/valor" utilizando
el operador [ ] (corchetes). La sintaxis es la siguiente:


```txt
aaMyArray is associative array of xxx = ...
	[ [<key1>, <value1>], [<key2>, <value2>], ... ]
```

Por ejemplo: 
```wl
// Stores the VAT rates by name
aaVATRate is associative array of real = ...
	[["Normal", 0.2], ["Intermediate", 0.1], ["Low", 0.055]]
```





<a name="NOTE0_3"></a>


### Navegar por la arrays asociativa
<a name="navegar_por_arrays_asociativa_ELTPARAGRAPHE000473"></a>

Los elementos de una array asociativa pueden consultarse mediante [la sintaxis FOR EACH específica de la arrays asociativa](../Motscles/1510019.md).
<a name="NOTE0_4"></a>


### Funciones WLanguage y arrays
<a name="funciones_wlanguage_arrays_ELTPARAGRAPHE000483"></a>

Se pueden utilizar varias funciones de WLanguage para manipular la asociativa arrays. Para obtener más información, consulte [Funciones de gestión de arrays](../WDLang1/3075006.md).

Esto permite eliminar un elemento de una array asociativa utilizando [suprimir](../WDLang1/1000017354.md) (o [ArrayDelete](../WDLang1/3075008.md)) mediante la siguiente sintaxis: 

```txt
Delete(ArrayName, key)
ArrayDelete(ArrayName, Key)
```

<a name="NOTE0_5"></a>




### Parámetros asociativos de Array
<a name="parametros_asociativos_array_ELTPARAGRAPHE000504"></a>

- El tipo de los elementos que se encuentran en la array asociativa pasada como parámetro debe ser el mismo que el tipo de la declaración.

- Una array asociativa no puede ser pasada por valor. Una Variable debe pasar necesariamente como parámetro. 
	Ejemplo:
	
	```wl
	// Adds a customer into the array
	PROCEDURE AddCustomer(aaCustomer is associative array of CCustomer,
		sName is string,
		sInfo is string)
	// Builds the new customer object
	c is CCustomer(sName, sInfo)
	// Adds the new object into the array
	aaCustomer[sName] = c
	```


- No se realiza ninguna verificación durante la compilación: Los controles se realizan en tiempo de ejecución.

- La asociación arrays no permite a los operadores de la reference ni la copia.



<a name="NOTE0_6"></a>




### array asociativa de clases o estructuras
<a name="array_asociativa_clases_estructuras_ELTPARAGRAPHE000519"></a>

Si se utiliza una array asociativa de clases o estructuras, las diferentes instancias se copian automáticamente durante las adiciones. 

Si está utilizando un array asociativo de clases **dinámicas** o estructuras **dinámicas** , las clases o las estructuras deben asignarse durante la adición (las operaciones de liberación se realizan automáticamente).

Ejemplos:


```wl
// Case of an array of non-dynamic objects
// Object
clObj is cLMyClass
// Associative array of objects
aaAssociative is associative array of cLMyClass
// Assign one of the object members
clObj:nMember = 0
// Add into the associative array. 
// It is an array of non-dynamic objects, therefore the object is copied
// The assignment can be quite "long" if there are a lot of members
aaAssociative["Association"] = clObj
// Change in the initial object
clObj:nMember = 2
// The object was copied, therefore 0 is found in the array 
// (value during the assignment)
Info(aaAssociative["Association"]:nMember)
```



```wl
// Case of an array of dynamic objects
// Dynamic object
pclObj is dynamic cLMyClass
// Associative array of dynamic objects
aaAssociative is associative array of dynamic cLMyClass
// Allocate a dynamic object
pclObj = new cLMyClass
// Assign one of the object members
pclObj:nMember = 0
// Add into the associative array. 
// It is an array of dynamic objects. 
// Therefore, it is a new "pointer" to the same object (fast assignment)
aaAssociative["Association"] = pclObj
// Change in the initial object
pclObj:nMember = 2
// The array contains the same object therefore 2 in the array as well
Info(aaAssociative["Association"]:nMember)
```


El acceso a los miembros de un elemento inexistente en una array asociativa de estructuras o clases no crea automáticamente la instancia de la estructura o clase.

La Line del código:


```wl
MyAssociativeArray["Key"]:Member = 5
```


falla si no se ha creado previamente el elemento "Clave".
<a name="NOTE0_7"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Linux](https://doc.pcsoft.fr/ext/images/us/LX.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png) 

### array asociativa de arrays, cola, pila, lista
<a name="array_asociativa_arrays_cola_pila_lista_ELTPARAGRAPHE000546"></a>Se soportan las siguientes sintaxis: 


```txt
<variable> is associative array of arrays of int
<variable> is associative array (withDuplicates) of arrays of int
<variable> is associative array (withDuplicates,wlInt) of arrays of int
<variable> is associative array of arrays of 5 int
<variable> is associative array (withDuplicates) of arrays of 5 int
<variable> is associative array (withDuplicates,wlInt) of arrays of 5 integers

<variable> is associative array of fixed arrays of 5 int
<variable> is associative array (withDuplicates) of  fixed arrays of 5 int
<variable> is associative array (withDuplicates,wlInt) of fixed arrays of 5 int
<variable> is associative array of associative arrays of int
<variable> is associative array (withDuplicates) of associative arrays of int
<variable> is associative array (withDuplicates,wlInt) of associative arrays of int

<variable> is associative array of associative arrays (withDuplicates) of int
<variable> is associative array (withDuplicates) of associative arrays ...
					(withDuplicates) of int
<variable> is associative array (withDuplicates,wlInt) of associative arrays ...
					(withDuplicates) of int
<variable> is associative array of associative arrays ...
					(withDuplicates,wlInt) of int
<variable> is associative array (withDuplicates) of associative arrays ...
					(withDuplicates,wlInt) of int
<variable> is associative array (withDuplicates,wlInt) of associative arrays ...
					(withDuplicates,wlInt) of int

<variable> is associative array of queue of int
<variable> is associative array (withDuplicates) of queue of int
<variable> is associative array (withDuplicates,wlInt) of queue of int

<variable> is associative array of lists of int
<variable> is associative array (withDuplicates) of stacks of integers
<variable> is associative array (withDuplicates,wlInt) of stacks of integers

<variable> is associative array of lists of int
<variable> is associative array (withDuplicates) of lists of int
<variable> is associative array (withDuplicates,wlInt) of lists of int
```


Ejemplo: La asociación array de arrays de cuerdas: 


```wl
// Declaration
arrClasses is associative array of array <growth> of strings

// Inserts an element into the associative array 
Insert(arrClasses, "CM2")

// Fill the CM2 student
arrClasses["CM2"][1] = "ALARD Stephen"
arrClasses["CM2"][2] = "BERTAU Kevin"

// Inserts an element into the associative array 
Insert(arrClasses, "CM1")

// Fill the CM1 student
arrClasses["CM1"][1] = "ALONSO Bernie"
arrClasses["CM1"][2] = "CRISTO Benjie"

FOR EACH AClass, sName of arrClasses
	FOR EACH Student OF AClass
		Trace(sName + ":" + Student)
	END
END
```





