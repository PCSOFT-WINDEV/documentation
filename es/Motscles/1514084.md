
## Lambda: Expresiones y procedimientos
			

<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Presentación
<a name="presentacion_ELTTEXTE000209"></a>
Una expresión Lambda ("función Lambda") es una forma más concisa de escribir procedimientos internos.

Las lambdas son útiles para las funciones que utilizan procedimientos de tipo "callback" como parámetros: esto significa que es posible introducir directamente el código de la función en lugar del parámetro. En ese caso: 

- el procedimiento no tiene nombre,

- el código del procedimiento se encuentra donde debería estar el nombre del procedimiento. 




WLanguage propone varias sintaxis para utilizar funciones Lambda. 

<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Procedimiento Lambda
<a name="procedimiento_lambda_ELTTEXTE000233"></a>
Un procedimiento Lambda es un procedimiento interno anónimo, que no requiere especificar el tipo de parámetros y el valor de retorno. 


### Ejemplo
<a name="ejemplo_ELTPARAGRAPHE000026"></a>


```wl
// Fill in a array with the names of all the files in the data directory
arrFile is array of strings
fListFile(fdataDir() + "\*.*", (Directory,File)=>{arrFile.Add(File)} )
```


En este código, el procedimiento Lambda corresponde al siguiente código: 
```txt
(Directory,File)=>{arrFile.Add(File)}
```





### Sintaxis
<a name="sintaxis_ELTPARAGRAPHE000035"></a>

- Procedimiento Lambda que no espera ningún parámetro: 
	
	```txt
	() => { <WLanguage code of procedure>}
	```


- Procedimiento Lambda que espera un solo parámetro: 

	- sintaxis completa: 
			
		```txt
		(<Parameter 1>) => { <WLanguage code of procedure>}
		```


	- sintaxis simplificada: 
			
		```txt
		<Parameter 1> => { <WLanguage code of procedure>}
		```





- Procedimiento Lambda que espera varios parámetros: 
	
	```txt
	(<Parameter 1>, ..., <Parameter N>) => { <WLanguage code of procedure>}
	```





En estas sintaxis: 

- &lt;Parámetro X&gt; corresponde al nombre del parámetro. 

- &lt;Código WLanguage del procedimiento&gt; corresponde al código WLanguage del procedimiento. Está encerrado entre llaves "{ }". Se pueden utilizar varias instrucciones





### Observaciones
<a name="observaciones_ELTPARAGRAPHE000058"></a>

- Esta sintaxis se puede utilizar en funciones WLanguage ([fListFile](../WDLang1/3036058.md), etc.), y en los procedimientos del proyecto. Para ello, el parámetro formal al que se pasa el procedimiento Lambda debe ser de tipo "Procedure".

- El procedimiento es anónimo, por lo tanto, no tiene nombre y no puede ser llamado en otro lugar o de forma recursiva.

- Un procedimiento Lambda no necesita recuperar todos los parámetros:

	- los parámetros no utilizados no provocan advertencias durante la compilación,

	- los parámetros omitidos al final del prototipo no provocan errores en tiempo de ejecución.







<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Expresión Lambda
<a name="expresion_lambda_ELTTEXTE000275"></a>
Una expresión Lambda es un procedimiento Lambda simplificado cuando el código WLanguage a ejecutar corresponde a: 

```txt
RESULT <Expression>
```



### Ejemplo
<a name="ejemplo_ELTPARAGRAPHE000080"></a>

- Ejemplo de un procedimiento Lambda: 
	
	```wl
	// count odd-numbered elements of an array 
	arrValue is array of int = [1,2,3,4,5]
	Trace(CountValues(arrValue, x=>{RESULT Modulo(x, 2)=1}))
	
	PROCEDURE CountValues(arrValue is array of int,
		Condition is Procedure(x int):boolean)
	let n = 0
	FOR EACH x OF arrValue
		IF Condition(x) THEN
			n++
		END
	END
	RESULT n
	```






- Simplificación a expresión Lambda: 
	
	```wl
	// count odd-numbered elements of an array 
	arrValue is array of int = [1,2,3,4,5]
	Trace(CountValues(arrValue, x=>Modulo(x,2)=1))
	
	PROCEDURE CountValues(arrValue is array of int,
		Condition is Procedure(x int):boolean)
	let n = 0
	FOR EACH x OF arrValue
		IF Condition(x) THEN
			n++
		END
	END
	RESULT n
	```








### Sintaxis
<a name="sintaxis_ELTPARAGRAPHE000094"></a>

- Expresión Lambda que no espera ningún parámetro: 
	
	```txt
	() => <WLanguage expression>
	```


- Expresión Lambda que espera un solo parámetro: 

	- sintaxis completa: 
			
		```txt
		(<Parameter 1>) => <WLanguage expression>
		```


	- sintaxis simplificada: 
			
		```txt
		(<Parameter 1>) => <WLanguage expression>
		```





- Expresión Lambda que espera varios parámetros: 
	
	```txt
	(<Parameter 1>, ..., <Parameter N) => <WLanguage expression>
	```





En estas sintaxis: 

- &lt;Parámetro X&gt; corresponde al nombre del parámetro. 

- &lt;Expresión WLanguage&gt; corresponde a la expresión WLanguage a devolver. 





### Observación
<a name="observacion_ELTPARAGRAPHE000117"></a>

Esta sintaxis se puede utilizar en funciones WLanguage ([fListFile](../WDLang1/3036058.md), etc.), y en los procedimientos del proyecto. Para ello, el parámetro formal al que se pasa la expresión Lambda debe ser de tipo "Procedure".





<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Procedimiento Lambda tipado
<a name="procedimiento_lambda_tipado_ELTTEXTE000317"></a>
Un procedimiento Lambda tipado es un procedimiento interno anónimo, para el que se puede especificar el tipo de parámetros y el valor de retorno.  


### Ejemplo
<a name="ejemplo_ELTPARAGRAPHE000133"></a>


```wl
// Fill in a array with 
// the names of all files in the data directory
arrFile is array of strings
fListFile(fdataDir() + "\*.*", ...
	Procedure(Directory is string, File is string):string {arrFile.Add(File)})
```





### Sintaxis
<a name="sintaxis_ELTPARAGRAPHE000139"></a>


```txt
PROCEDURE (<Parameter 1>, ..., <Parameter N>): <Type of return> { <WLanguage code of procedure>}
```


En esta sintaxis: 

- &lt;Parámetro X&gt; corresponde al nombre del parámetro o de los parámetros del procedimiento Lambda. Este parámetro puede ser tipado o no tipado. Si un parámetro es tipado, la sintaxis es la siguiente 
	
	```txt
	<Identifier> [is] <Type>
	```


- &lt;Tipo de retorno&gt; corresponde al tipo devuelto por el procedimiento Lambda. 

- &lt;Código WLanguage del procedimiento&gt; corresponde al código WLanguage del procedimiento. Está encerrado entre llaves "{ }".  





### Observaciones
<a name="observaciones_ELTPARAGRAPHE000152"></a>

- Esta sintaxis se puede utilizar en funciones WLanguage ([fListFile](../WDLang1/3036058.md), etc.), y en los procedimientos del proyecto. Para ello, el parámetro formal al que se pasa el procedimiento Lambda debe ser de tipo "Procedure".

- El procedimiento es anónimo, por lo tanto, no tiene nombre y no puede ser llamado en otro lugar o de forma recursiva.

- Un procedimiento Lambda no necesita recuperar todos los parámetros:

	- los parámetros no utilizados no provocan advertencias durante la compilación,

	- los parámetros omitidos al final del prototipo no provocan errores en tiempo de ejecución.








