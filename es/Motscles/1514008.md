
## Estructura (Tipo de Variable)

*** ***
				



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Una estructura es un tipo de datos personalizado. Una estructura agrupa diferentes tipos de elementos.

**Observación**: Si está manejando variables personalizadas, le aconsejamos que declare:

- a ***estructura*** si se utilizan varias variables de este tipo en diferentes procesos del proyecto.

- un compuesto ***Variable*** si se utiliza un solo Variable de este tipo en su proyecto (para pasar parámetros a un API por ejemplo).





<a name="Example1"></a>
<a name="sample_code"></a>

## Ejemplo


```wl
// Declare a structure
ProductRef is structure
	SCode is int
	PdtCode is fixed string on 10
END
```
<a name="Example2"></a>

```wl
// Declare a structure variable
ProductRef is structure
	SCode is int
	PdtCode is fixed string on 10
END
Armchair is ProductRef
```
<a name="Example3"></a>

```wl
// Handle a member of a structure variable
ProductRef is structure
	SCode is int
	PdtCode is fixed string on 10
END
Armchair is ProductRef
Armchair:SCode = 7
Armchair:PdtCode = "Furniture"
```
<a name="Example4"></a>

```wl
// Declare and initialize the members of a structure
3Dpoint is structure
	x is int
	y is int
	z is int
END

p1 is 3Dpoint = [0, 0, 50]
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Sintaxis

### Declarar una estructura

`<Structure name> is structure
    <Structure members>
END
`
---

**`<Structure name>: ()`**

Nombre de la estructura a declarar

**`<Structure members>: ()`**

Declaraciones de variables encontradas en la estructura. Estas Variable s se llaman ***miembros*** del compuesto Variable.

Al declarar los miembros de la estructura, tiene la posibilidad de especificar los parámetros de serialización de estos miembros. Para obtener más información, consulte ["Serialización de los miembros de una estructura"](#NOTE0_4). 

**`<END>: ()`**

Fin de la declaración de Variable.  


<a name="SYNTAX2"></a>

### Declarando una estructura Variable

`<Variable name> is <Structure name>
`
---

**`<Variable name>: ()`**

Nombre de la estructura Variable a declarar.

**`<Structure name>: ()`**

Nombre de una estructura que se ha declarado de antemano.  


<a name="SYNTAX3"></a>

### Manejar a un miembro de una estructura Variable

`<Name of structure variable>:<Name of structure member>
`
---

**`<Name of structure variable>: ()`**

Nombre de la estructura Variable a utilizar.

**`<Name of structure member>: ()`**

Nombre del miembro de la estructura a utilizar.

Los miembros de una estructura Variable pueden asignarse a través de [la asignación múltiple](../Motscles/1514031.md).



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Observaciones




### Estructuras de declaración
<a name="estructuras_declaracion_ELTPARAGRAPHE000109"></a>

Si una estructura &lt;Nombre de la estructura&gt; se declara:

- ***en el código del proyecto***, se puede declarar una variable de tipo &lt;Nombre de estructura&gt; en todo el proyecto.

- ***en el código de declaración de las variables globales de una ventana***, se puede declarar una variable de tipo &lt;Nombre de la ventana&gt; en la ventana, en sus controles y en los procedimientos locales asociados.

- ***En el código de apertura de un reporte***, se puede declarar una variable de tipo &lt;Nombre de la estructura&gt; en el reporte, en los controles del reporte y en los procedimientos locales asociados.

- ***en el código de declaración de una clase***, se puede declarar un &lt;nombre de estructura&gt; Variable en la clase y en sus métodos. Atención: "Su estructura" debe situarse necesariamente antes de "su clase".

- ***en un proceso***, se puede declarar una variable de tipo &lt;Nombre de estructura&gt; en dicho proceso.




![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) La estructura debe ser declarada en la declaración Process de variables globales (de una ventana, página o set de procedimientos).


<a name="NOTE0_2"></a>




### Tipo de estructura miembros
<a name="tipo_estructura_miembros_ELTPARAGRAPHE000136"></a>

Los miembros de una estructura pueden tener los siguientes tipos:

- [Integro](../Motscles/1514049.md)(o seleccionado entre los [enteros avanzados](../Motscles/1514049.md)).

- [Real](../Motscles/1514048.md)(o seleccionado entre los [Reales avanzados](../Motscles/1514048.md)).

- [Moneda](../Motscles/1514043.md).

- [Cadena](../Motscles/1514044.md)(o seleccionado entre los [cadenas avanzadas](../Motscles/1514044.md)).

- Array (["simple" array](../Motscles/1514030.md), [array dinámica](../Motscles/1514057.md) o [fijo array](../Motscles/1514025.md)).

- [Automatización](../Motscles/1514014.md) o [Automatización dinámica](../Motscles/1514015.md).

- [Clase](../POO/6010006.md). Esta clase debe tener un constructor sin parámetro (o con parámetros opcionales).

- [Variant](../Motscles/1514019.md).




Cualquier otro tipo de datos (Variable compuesta, constante, ...) está prohibido.


<a name="NOTE0_3"></a>




### Declarar un miembro de array en una estructura
<a name="declarar_miembro_array_una_estructura_ELTPARAGRAPHE000196"></a>

- ***Declarar un miembro de tipo array simple***:
	Las dimensiones de array se fijan cuando se declara array.. Las dimensiones de array pueden ser modificadas por [Dimensión](../WDLang1/3013022.md).

- ***Declarar un miembro dinámico de array***: 
	Las dimensiones de array deben definirse (lo que significa que debe asignarse el array) antes de utilizar el array. Por ejemplo:
	
	```wl
	// Declare a structure
	Struct is structure
		x1 is int
		x2 is dynamic array        // Declare the array
	END
	 
	// Declare a structure variable
	MyStruct is struct
	 
	// Allocate the array
	MyStruct.x2 = new dynamic array of 4,7 int
	// Use the array
	MyStruct:x2[1,1] = 25
	```








<a name="NOTE0_3b"></a>




### copy homónima de una estructura
<a name="copy_homonima_una_estructura_ELTPARAGRAPHE000215"></a>

El operador **=>** permite realizar una copia homónima de una variable de estructura. 

Esta copy homónima también se puede realizar utilizando el operador **=**. 

Atención, la copy se realiza **miembro a miembro**: sólo se copiarán los miembros con el mismo nombre. Los miembros con nombres diferentes o inexistentes serán ignorados.

Ejemplo:

```wl
// Declare a structure
StructImportCustomer is structure
	LastName is string
	FirstName is string
	Company is string
	Address is string
	ZC is string
	City is string
END

StructInternalCustomer is structure
	LastName is string
	FirstName is string
	Company is string
	Address is string
	FullAddress is string
	ZC is string
	City is string
	Country is string
END

 
NewCustomer is StructImportCustomer 

CurrentCustomer is StructInternalCustomer 

CurrentCustomer = NewCustomer // the FullAddress and Country members are not filled
```





<a name="NOTE0_4"></a>


### Serialización de los miembros de una estructura
<a name="serializacion_los_miembros_una_estructura_ELTPARAGRAPHE000235"></a>

Por Default, todos los miembros de una estructura son seriados. 

Tiene la capacidad de gestionar con precisión la serialización de cada miembro de una estructura.: 

- especificando el miembro que será serializado durante la llamada a [Serialize](../WDLang1/3013065.md). 
	Esta operación se puede realizar en todos los tipos de serialización (WXML, JSON, binario).. 

- cambiando el nombre del miembro durante la serialización con [Serialize](../WDLang1/3013065.md).  
	Esta operación sólo se puede realizar durante una serialización binaria.. 




Esta gestión de serialización se realiza utilizando la siguiente sintaxis: 

- Serialización (o no) de un miembro: 
	
	```txt
	<Member name> is <Type of member> [, Serialize = <True/False>]
	```


- Serialización y cambio de nombre de miembro: 
	
	```txt
	<Member name> is <Type of member> [, Serialize = <New name>]
	```





Ejemplo: 


```wl
Cl is structure
	// Serialized member
	SerializedMember is int 
	// Non-serialized member
	NonSerializedMember is string, Serialize = False  
	// Member renamed during the serialization
	RenamedMember is int, Serialize = "NewMemberName" 
END
```

<a name="NOTE0_5"></a>




### ¿Qué tipos "avanzados" pueden ser miembros?
<a name="¿que_tipos_avanzados_pueden_ser_miembros_ELTPARAGRAPHE000271"></a>

Una Variable "avanzada" puede ser miembro de otra Variable"avanzada".. La tabla a continuación presenta las diferentes combinaciones:

| ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=Fleche_1.gif) | Compuesto Variable | Estructura | Clase | Array |
| --- | --- | --- | --- | --- |
| **Compuesto Variable** | No | No | No | No |
| **Estructura** | Sí | Sí | Sí | Sí |
| **Clase** | Sí | Sí | Sí | Sí |
| **Array** | Sí | Sí | Sí | No |


<a name="NOTE0_6"></a>




### Pasar una estructura como parámetro a una Procedure
<a name="pasar_una_estructura_como_parametro_una_procedure_ELTPARAGRAPHE000330"></a>

Una variable de "estructura" puede pasarse como parámetro a una Procedure. Para ello, utilice la siguiente sintaxis: 

```txt
<Procedure name>(<Name of structure variable>)
```


Por ejemplo:


```wl
ProductRef is structure
	SCode is int
	PdtCode is fixed string on 10
END
Armchair is ProductRef
 
// Call the DisplayProduct procedure
DisplayProduct(Armchair)
```

<a name="NOTE0_6_1"></a>


### Agregar el contenido de una estructura a un array de estructuras sin utilizar una variable de la estructura
<a name="agregar_contenido_una_estructura_array_estructuras_sin_utilizar_una_variable_estructura_ELTPARAGRAPHE000345"></a>

Si utiliza un array de estructuras con pocos miembros, puede ser más fácil agregar directamente una estructura utilizando el operador [ ] (corchetes). 
Por ejemplo, para la siguiente estructura:

```wl
// Structure to store a letter and its ASCII code
STKey is Structure
	sKey is string
	nAsciiCode is int
END
// Array of structures
arrKeys is array of STKey
```

En la mayoría de los casos, el contenido se agrega utilizando una variable de un tipo declarado en la estructura:

```wl
// Temporary structure for the addition
stAKey is STKey
// Stores the letter A
stAKey.sKey = "A"
stAKey.nAsciiCode = Asc("A")
arrKeys.Add(stAKey)
```

Con el operador [ ], se obtiene una mejor legibilidad:

```wl
// Stores the letter A
arrKeys.Add(["A", Asc("A")])
```





<a name="NOTE0_7"></a>


### Límites de una estructura
<a name="limites_una_estructura_ELTPARAGRAPHE000380"></a>

- No se permite la herencia de la estructura: una estructura no puede heredar de otra estructura.

- El acceso a un miembro de una estructura no puede ser restringido: todos los miembros de una estructura son públicos.



<a name="NOTE0_8"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) 

### Diferencias entre la versión 8 y la versión 9
<a name="diferencias_entre_version_8_version_9_ELTPARAGRAPHE000390"></a>

Una estructura que contiene un miembro de un personaje no tiene el mismo comportamiento que en las versiones anteriores.. De hecho, los caracteres fueron codificados en 1 byte en la versión 8 y en 2 bytes en la versión 9.

Le recomendamos que utilice el tipo "1 byte entero".


