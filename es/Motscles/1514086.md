
## Procedimiento de extensión
			

<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Presentación
<a name="presentacion_ELTTEXTE000131"></a>
Los **procedimientos de extensión** permiten agregar procedimientos WLanguage personalizados a diferentes tipos de variables (string, date, color etc.). Estos procedimientos pueden llamarse como funciones nativas de WLanguage. Por lo tanto, el tipo básico de WLanguage se "extiende". 

Para indicar que un procedimiento es de extensión, deben cumplirse las siguientes condiciones:

- el primer parámetro es del tipo que debe ser "extendido",

- el prototipo del procedimiento tiene el atributo &lt;extension&gt;.






<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Declaración
<a name="declaracion_ELTTEXTE000155"></a>
Declaración de un procedimiento de extensión del tipo String: 

```txt
PROCEDURE MyCustomProcedure(s is string) <extension>
```

<a name="NOTE2_2"></a>
Observaciones: 

- Los procedimientos de extensión aparecen en las sugerencias de código del tipo extendido.

- Los procedimientos de extensión no pueden utilizarse con tipos como Boolean, Integer, Real, Numeric, etc. (cualquier tipo utilizado para manipular valores numéricos). 

- Solo se pueden extender los tipos WLanguage. 

- Solo los procedimientos globales pueden tener el atributo &lt;extension&gt;. 

- Si el procedimiento de extensión sobrescribe una función WLanguage, también se sobrescribirá la sintaxis estándar de dicha función. Por ejemplo, la creación de un procedimiento de extensión "Format" para el tipo String sobrescribirá las funciones [&lt;Cadena&gt;.Format](../WDLang1/1000024877.md) y [StringFormat](../WDLang1/3024019.md). 

- También es posible encadenar las llamadas a los procedimientos de extensión.




<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Ejemplo
<a name="ejemplo_ELTTEXTE000185"></a>


### Reemplazar tabuladores por espacios
<a name="reemplazar_tabuladores_por_espacios_ELTPARAGRAPHE000063"></a>

Procedimiento para reemplazar tabuladores por espacios: 

```wl
PROCEDURE ReplaceTABWithXSpace(s is string, n is int) <extension>: string
RETURN s.Replace(TAB, RepeatString(" ", n))
```


Llamada al procedimiento de extensión: 

```wl
sText is string = TAB + "XXX"
Trace(sText) // <TAB>XXX
Trace(sText.ReplaceTABWithXSpace(4)) //     XXX
```



### Extensión de tipo Color
<a name="extension_tipo_color_ELTPARAGRAPHE000074"></a>

Procedimiento de extensión de tipo Color, utilizado para obtener un color de texto legible sobre un color de fondo determinado.

```wl
PROCEDURE ReadableTextColor(SourceColor is Color) <extension>: Color

nLightness is int
rLuminance is real
ColorResult is Color


// Calculate the luminance of the color passed as parameter
rLuminance = 1 - (0.299 * SourceColor..Red + ...
		0.587 * SourceColor..Green + 0.114 * SourceColor..Blue)/255

// According to the luminance, the text color must have a low or high lightness
IF rLuminance < 0.5 THEN
	// Low luminosity
	nLightness = 12
ELSE
	// High luminosity
	nLightness = 88
END

// The returned color keeps the shade and the saturation
ColorResult = HSL(ColorHue(SourceColor), ...
	ColorSaturation(SourceColor), nLightness)

RESULT ColorResult
```


Llamada al procedimiento de extensión: 

```wl
// Array of colors
arrHTMLColors is array of strings
arrHTMLColors.Add("#F48FB1")
arrHTMLColors.Add("#C2185B")
arrHTMLColors.Add("#64B5F6")
arrHTMLColors.Add("#1565C0")
arrHTMLColors.Add("#81C784")
arrHTMLColors.Add("#2E7D32")

MyColor is Color

// Gets a random color
MyColor = HTMLToRGB(arrHTMLColors[Random(1, arrHTMLColors..Count)] )

// Sets the initial color as background color
STC_BackgroundColorTest.BackgroundColor = MyColor

// Sets an easily readable color as text color
STC_BackgroundColorTest.Color = MyColor.ReadableTextColor()
```





