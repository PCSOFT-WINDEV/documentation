
## Enumeración (Tipo de Variable)

*** ***
				

<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Una enumeración es una set de valores. Una **Enumeración** Variable o parámetro sólo puede ser asignado con un valor de la enumeración.


<a name="Example1"></a>
<a name="sample_code"></a>

## Ejemplo


```wl
// Declaration code
// Declare an enumeration
RobotType is Enumeration
	Walk
	Stopped
	InMaintenance
	OutOfOrder
END
```


<a name="Example2"></a>



```wl
// Declare an Enumeration variable
Post1 is RobotType
// Assign the variable
Post1 = Walk
// Post1 = 1 // Displays a compilation error
// Post1 = "Walk" // Displays a compilation error
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Sintaxis

### Declarar una enumeración

`<Enumeration name> is Enumeration
    <Enumeration values>
END
`
---

**`<Enumeration name>: ()`**

Nombre de la enumeración a declarar

**`<Enumeration values>: ()`**

Diferentes valores que pueden ser tomados por la enumeración. 

**`<END>: ()`**

Fin de la declaración.  


<a name="SYNTAX2"></a>

### Declarando una Enumeración Variable

`<Variable name> is <Enumeration name>
`
---

**`<Variable name>: ()`**

Nombre de la Enumeración Variable a declarar.

**`<Enumeration name>: ()`**

Nombre de la enumeración que se declaró de antemano. El valor de enumeración de Default corresponde al primer valor de enumeración.  



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Observaciones




### Declaración de las enumeraciones
<a name="declaracion_las_enumeraciones_ELTPARAGRAPHE000078"></a>

Se debe declarar una enumeración en:

- El código de inicialización del proyecto para ser utilizado en todos los procesos del proyecto (código de proyecto, código de ventanas, código de controles, procedimientos, etc.).

- El código de declaración de las variables globales de una ventana para ser utilizadas en todos los procesos de la ventana (código de ventana, código de controles que se encuentran en la ventana, procedimientos locales, etc.).

- El código de apertura de un reporte para ser utilizado en todos los procesos del reporte (código del reporte, código de controles que se encuentra en el reporte, procedimientos locales, etc.).

- El código de declaración de una clase para ser utilizado en la clase.

	- Para acceder a esta enumeración desde un método de clase, utilice la siguiente sintaxis: 
			
		```txt
		"::<Enumeration name>"
		```


	- Para acceder a esta enumeración desde un código externo a la clase, utilice la siguiente sintaxis: 
			
		```txt
		"<Class name>::<Enumeration name>"
		```





- El código de declaración de la set s de procedimientos para ser usado en todos los procedimientos de la set. 




**Observación**: Una enumeración es siempre global:

- a un proyecto,

- a una ventana,

- a un reporte,

- a una clase.



<a name="NOTE0_2"></a>


### Pasar una enumeración como parámetro a una Procedure
<a name="pasar_una_enumeracion_como_parametro_una_procedure_ELTPARAGRAPHE000105"></a>Una variable de Enumeración puede ser pasada como parámetro a una Procedure. Para ello, utilice la siguiente sintaxis: 


```txt
<Procedure name>(<Name of enumeration variable>)
```


Por ejemplo:


```wl
// Procedure with an Enumeration parameter
PROCEDURE OperatingMode(p is RobotType)

// Calls with an Enumeration parameter
OperatingMode(Walk)
OperatingMode(1)		// Displays a compilation error
OperatingMode("A")		// Displays a compilation error
```

<a name="NOTE0_3"></a>


### Casos especiales
<a name="casos_especiales_ELTPARAGRAPHE000118"></a>

- **Entrada asistida**: La entrada asistida para un parámetro de enumeración propone los diferentes valores de enumeración.

- **Orden de los valores**: Los operadores &lt;, &lt;=, &gt;, &gt;= están disponibles para las enumeraciones así como para las operaciones de ordenación automatic ([ArraySort](../WDLang1/3075007.md), [ArraySeek](../WDLang1/3075002.md), etc.). El orden de los valores en la enumeración es el orden en que se declaran los valores. 
	Ejemplo: 
	
	```wl
	// Declaration code
	// Declare an enumeration
	RobotType is Enumeration
		Walk
		Stopped
		InMaintenance
		OutOfOrder
	END
	
	// Declare an Enumeration variable
	Post1 is RobotType
	// Assign the variable
	Post1 = Walk
	
	...
	
	IF Post1>Stopped THEN
		Info("The robot encounters a problem")
	END
	```




<a name="NOTE0_4"></a>


### extension de enumeración
<a name="extension_enumeracion_ELTPARAGRAPHE000136"></a>

Una enumeración puede extender los valores de una o más enumeraciones tomando los valores de éstas y añadiendo nuevos valores.

Se utiliza la siguiente sintaxis: 


```txt
<Enumeration name> is Enumeration
    [Base Enumeration]
    <Additional values of enumeration>
END
```


donde: 

- Nombre de la enumeración: Nombre de la enumeración que se declaró de antemano. 

- Enumeración de la base: Nombre de la enumeración de la base a utilizar.

- Valores adicionales de enumeración: Valores adicionales que se tendrán en cuenta en la enumeración. 




Ejemplo: 


```wl
// Declaration code
// Declare an enumeration
RobotType is Enumeration
	Walk
	Stopped
	InMaintenance
	OutOfOrder
END

AdvancedRobotType is Enumeration
	[RobotType]
	InLoop
END
```


No se permite la asignación de un valor extendido en una enumeración de base.




<a name="NOTE0_5"></a>


### Creación de alias en una enumeración
<a name="creacion_alias_una_enumeracion_ELTPARAGRAPHE000160"></a>

Usted tiene la capacidad de declarar alias para asegurar una compatibilidad temporal, por ejemplo.

Se utiliza la siguiente sintaxis: 


```txt
<Enumeration name> is Enumeration
	<Value 1 of enumeration>
	<Value 2 of enumeration>
	<Value 3 of enumeration> = <Value 1 of enumeration>
END
```


Ejemplo: 


```wl
// Declaration code
// Declare an enumeration
RobotType is Enumeration
	Walk
	Stopped
	InMaintenance
	OutOfOrder
	// Declare the temporary aliases
	Maintenance = InMaintenance
	Breakdown = OutOfOrder
END
```

<a name="NOTE0_6"></a>


### Los valores de associated
<a name="los_valores_associated_ELTPARAGRAPHE000175"></a>

En WLanguage, un valor constante de tipo simple puede ser associated con cada valor de una enumeración. Este valor es associated con el valor de enumeración en la declaración de enumeración.


```wl
// Declare the enumeration with associated values
RobotType is Enumeration
	Walk = "Running the robot"
	Stopped = "Robot stopped"
	InMaintenance = "Robot under maintenance"
	OutOfOrder = "Robot breakdown"
END
```


Entonces, tendrá la posibilidad de leer el valor asociado de una Enumeración Variable a través de [Valor](../Proprietes/2510130.md).


```wl
// Access to the associated value
e is RobotType
RobotMessage is string = e..Value
```


El valor de associated no tiene incidencia en los otros comportamientos de las enumeraciones. Especialmente si el orden de clasificación es idéntico al orden de declaración de los valores de la enumeración.
<a name="NOTE0_7"></a>


### Propiedades de la associated
<a name="propiedades_associated_ELTPARAGRAPHE000193"></a>

Las siguientes propiedades son associated con las variables de Enumeración: 


|   |   |
| --- | --- |
| [denominar](../Proprietes/2510082.md) | Permite conocer el nombre de una Enumeración Variable. |
| [Valor](../Proprietes/2510130.md) | Permite conocer el valor asociado de una Enumeración Variable. |


<a name="NOTE0_8"></a>


### Funciones disponibles de WLanguage
<a name="funciones_disponibles_wlanguage_ELTPARAGRAPHE000213"></a>

Una enumeración puede ser almacenada en un elemento HFSQL o en otro formato de archivo. Dependiendo de sus necesidades, puede almacenar el nombre o el valor de la enumeración (propiedades [denominar](../Proprietes/2510082.md) o [Valor](../Proprietes/2510130.md), respectivamente). 

Las siguientes funciones de WLanguage le permiten averiguar las características de una enumeración almacenada: 



|   |   |
| --- | --- |
| [EnumerationCheckName](../WDLang1/1000020767.md) | Comprueba si un valor de enumeración conocido por su nombre es válido. |
| [EnumerationCheckValue](../WDLang1/1000020866.md) | Comprueba si un valor de enumeración conocido por su valor associated es válido. |
| [EnumerationFromName](../WDLang1/1000020769.md) | Devuelve un valor de enumeración conocido por su nombre. |
| [EnumerationFromValue](../WDLang1/1000020768.md) | Devuelve un valor de enumeración conocido por su valor associated. |




<a name="NOTE0_9"></a>


### Límites
<a name="limites_ELTPARAGRAPHE000229"></a>

- Las enumeraciones no están disponibles en la compilación dinámica. 

- Las enumeraciones no pueden utilizarse fuera del proyecto. 





