
## Operadores indirectos

*** ***
				



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Los operadores **{** y **}** se utilizan para acceder a un control, un Variable, un miembro de la clase o un elemento del archivo construyendo dinámicamente el nombre del control, Variable, miembro de la clase o elemento.

El **{** y **}** presentan varios beneficios:

- Pasando en parámetro el nombre de un control, Variable, miembro de la clase o artículo.

- Construyendo el nombre de un control, Variable, miembro de la clase o artículo por programación.






![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) No se pueden realizar indirecciones a las variables locales. **Las indirecciones sólo pueden realizarse sobre variables globales**. Una indirecta sobre una variable local de un Process desencadenará un error de "elemento desconocido".




<a name="Example1"></a>
<a name="sample_code"></a>

## Ejemplo


```wl
// Simple indirection 
{"NAME"} = CustName               // Is equivalent to NAME=CustName 
{"NAME"} = {"CU.CUSTNAME"}        // Is equivalent to NAME=CU.CUSTNAME 
{"CUSTWIN.NAME"} = CustName        // Is equivalent to CUSTWIN.NAME=CustName 
{"CUSTWIN"+".NAME"} = CustName     // Is equivalent to CUSTWIN.NAME=CustName 

// Typed indirection
{"Name",indControl} = CustName

// Indirection with a file name in a variable
nFileName is string = "Customer"
{nfileName + ".Name"} = "Smith"
HAdd({nFileName})
```




<a name="Example2"></a>





```wl
// Indirection in a procedure 
ControlName is string
ControlName = "EDT_EDIT1"			// EDT_EDIT1 is the name of the control 
// Call to a procedure used to make a control invisible 
Make_Invisible(ControlName)

PROCEDURE Make_Invisible(ControlN)
{ControlN}..State = Invisible
```




<a name="Example3"></a>



```wl
// Indirection with variable 
AliasName is string
AliasName = PreviousWin()
// NAME is the name of the control 
// CustName is the value to assign 
{AliasName+".NAME"} = CustName 
Abbrev is string
ItemName is string
{Abbrev+"."+ItemName} = CustName 
{Abbrev+"."+ItemName} = {ControlName}
```


<a name="Example4"></a>





```wl
// Declare a class with two members 
MyClass is Class
Member1 is string
Member2 is string
// Display the value of a member 
GLOBAL PROCEDURE DisplayMember(Number)
// Retrieve the value of the selected global member
Value is string = {"::Member"+Number}
Info(Value)
```



```wl
c is cWord
var is Variable Description
Def is Definition

Def = GetDefinition(c)
// Retrieves the 2nd variable of the class: version of Word
var = Def..Variable[2]

Trace(var..Name) // Displays the variable name
Trace("Version of Word: ",{c, var}) // Displays the Word version
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Sintaxis

### Indirección simple

`{ <Expression> }
`
---

**`<Expression>: (Cadena de caracteres)`**

Expresión utilizada para identificar el control, la Variable o el elemento a utilizar. Se busca el elemento:

- entre las variables,

- según el número de puntos encontrados en el nombre:

	- 0 punto: buscar entre los controles, luego los ítems HFSQL.

	- 1 punto: buscar entre los elementos HFSQL, los controles y, a continuación, los parámetros de consulta.

	- mayor que 1 punto: buscar entre los controles.




- entre los elementos especiales conservados para la compatibilidad (_Tabx por ejemplo, ....).




Se produce un error de WLanguage si esta expresión corresponde a una cadena vacía (""").


<a name="SYNTAX2"></a>

### Indirección sobre un miembro de la clase

`{ <Expression> }
`
---

**`<Expression>: (Cadena de caracteres)`**

Expresión utilizada para identificar al miembro de la clase.

Si el miembro es:

- global, utilice la siguiente sintaxis: {" **::** Nombre del miembro"}

- no global, utilice la siguiente sintaxis: {" **:** Nombre del miembro"}




Se produce un error de WLanguage si esta expresión corresponde a una cadena vacía (""").  


<a name="SYNTAX3"></a>

### Indirección sobre un miembro de la clase

`{ <Class> , <Member> }`
---

**`<Class>: (Cadena de caracteres)`**

Instancia de clase

**`<Member>: (Cadena de caracteres o Variable description Variable)`**

Corresponde a: 

- el nombre del miembro,

- una variable de tipo [Variable Description](../WDLang1/1000019560.md). 





<a name="SYNTAX4"></a>

### Indirección especificando el tipo de elemento (optimiza la velocidad de ejecución)

`{ <Expression>, <Type> }
`
---

**`<Expression>: (Cadena de caracteres)`**

Expresión utilizada para identificar el control, la Variable o el elemento a utilizar. Se produce un error de WLanguage si esta expresión corresponde a una cadena vacía (""").

**`<Type>: (Constante)`**

Constante utilizada para especificar el tipo de elemento buscado:


|   |   |
| --- | --- |
| *indControl* | Elemento buscado entre los controles y los grupos. |
| *indConnection* | Elemento buscado entre las conexiones. |
| *indReport* | Elemento buscado entre los informes.<br><br> |
| *indWindow* | Elemento buscado entre las ventanas.<br><br> |
| *indFile* | Elemento buscado entre los expedientes. |
| *indGPW* | Elemento buscado entre los elementos de groupware usuario (utilizado para recuperar el estado inicial de los controles).<br><br> |
| *indLink* | Elemento buscado entre los eslabones.<br><br> |
| *indQueryParameter* | Elemento buscado entre los parámetros de las consultas. |
| *indItem* | Elemento buscado entre los ítems. |
| *indVariable* | Elemento buscado entre las variables.<br><br>![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) El elemento sólo se busca dentro de las variables globales. |
| *indPage* | Elemento buscado entre las páginas.<br><br> |





<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Observaciones




### ¿Cuándo se debe utilizar la indirección?
<a name="¿cuando_debe_utilizar_indireccion_ELTPARAGRAPHE000231"></a>

La indirección se puede utilizar, por ejemplo:

- Para acceder al valor de un elemento (control, Variable, elemento, ...):
	
	```wl
	{s_ControlName} = 10
	ControlValue = {s_ControlName}
	```


- Para usar un Property:
	
	```wl
	{s_ControlName}..Height = 10
	```


- Para programar manipular una columna de un control Tabla:
	
	```wl
	{s_ColumnName, indControl}[Row_Number] = 10
	```


- A manipular una columna tabla:
	
	```wl
	{s_TableName + "." +s_ColumnName, indControl}[Row_Number] = 10
	```




<a name="NOTE0_2"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png)![Código de Usuario (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png) 

### Identificación del objeto actual
<a name="identificacion_del_objeto_actual_ELTPARAGRAPHE000253"></a>

Para identificar el objeto actual, utilice las siguientes palabras clave:

- [MySelf](../Motscles/1511007.md): el actual control

- [MiVentana](../Motscles/1511004.md): la ventana actual

- [MyPage](../Motscles/1511017.md): la página actual
	![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Esta palabra clave no está disponible.

- [MiInforme](../Motscles/1511010.md): la reporte actual
	![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Esta palabra clave no está disponible.

- [MiFuente](../Motscles/1511001.md): la fuente de datos actual.

- [MiArchivo](../Motscles/1511019.md): el archivo que liberó el disparador actual.



<a name="NOTE0_3"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png)![Código de Usuario (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png) 

### Indirección sobre una estructura
<a name="indireccion_sobre_una_estructura_ELTPARAGRAPHE000289"></a>

Una indirección no puede contener un acceso a una estructura o a una instancia de clase. Sin embargo, se puede utilizar la siguiente sintaxis:


```txt
{<Structure Variable>}: member
```

<a name="NOTE0_4"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png)![Código de Usuario (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png) 

### Usando la indirección en los caminos en un Document XML
<a name="usando_indireccion_los_caminos_document_xml_ELTPARAGRAPHE000300"></a>

Los operadores indirectos pueden ser utilizados para construir el ruta de un elemento en un Document XML. 

Ejemplo: 


```wl
sXMLDoc is string = [
	<?xml version="1.0" encoding="UTF-8"?>
	<Document>
	<VersionInfo>
	<Version>21</Version>
	<Year>2016</Year>
	</VersionInfo>
	</Document>
]

xmlExample is xmlDocument = XMLOpen(sXMLDoc, fromString)

sParam is string = "WINDEV_Param"

// Direct reading of information ...
Trace(xmlExample.Document.VersionInfo.Version) 

// Reading with part of XML path found in a variable
// (idem if file item, window parameter, method, ...)
sParameter is string = "VersionInfo"
Trace ({"xmlExample.Document." + sParameter + ".Version"})
```

<a name="NOTE0_5"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Windows](https://doc.pcsoft.fr/ext/images/us/WINDOWS.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png)![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png)![Código de Usuario (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png) 

### Limitaciones
<a name="limitaciones_ELTPARAGRAPHE000309"></a>

El operador indirecto permite buscar un elemento por su nombre.

Por lo tanto, las siguientes operaciones no están permitidas en modo indirecto:

- acceso a un miembro de una instancia de clase o de estructura distinta de la instancia de clase actual (: y:: ).

- acceso a un subelemento en un control o array (operador []).

- acceso a una subcadena (operador [[]]).

- acceso a un Property (.. operador).

- indirectamente (operador { }).

- llamada a una Procedure o a un método de objeto de Automatización.

- declarar una variable.




**Observaciones**:

- Los operadores para acceder a un elemento (: y [] y [[]] y .. ) pueden utilizarse en el elemento obtenido por indirección. Por ejemplo:
	
	```txt
	{ <indirection> }: member
	
	{ <indirection> } [subscript]
	
	{ <indirection> } [[ subscript ]]
	
	{ <indirection> }..property
	```


- Para llamar a una Procedure identificada por su nombre, utilice [Ejecute](../WDLang1/3013041.md) o [ExecuteProcess](../WDLang1/3013031.md).

- Para combinar estas operaciones de forma dinámica, tiene la posibilidad de utilizar la compilación dinámica (véase [Compilar](../WDLang1/3013015.md)).



<a name="NOTE0_6"></a>
![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) 

### Indicaciones en Java
<a name="indicaciones_java_ELTPARAGRAPHE000377"></a>

Las indirecciones no son compatibles con las variables locales




