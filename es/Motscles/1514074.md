


## Combinación (Tipo de Variable)

*** ***
				



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Una combinación es una set de opciones. Se puede asignar una combinación Variable o un parámetro de combinación con ninguna, una o varias opciones de combinación. 




<a name="Example1"></a>
<a name="sample_code"></a>

## Ejemplo


```wl
// Declaration code
// Declare a combination
CombinationType is Combination
	CreateFile
	ReadFile
	WriteFile
	DeleteFile
END
```


<a name="Example2"></a>



```wl
// Declare a Combination variable
FileAction is CombinationType
// Assign the variable
FileAction = ReadFile + WriteFile
// FileAction = 0 // Disables all the options
...

// To test the value of the combination:
IF FileAction[WriteFile] THEN
	// The WriteFile option is enabled
ELSE
	// The WriteFile option is not enabled
END
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Sintaxis

### Declarando una combinación

`<Combination name> is Combination
    <Combination options>
END
`
---

**`<Combination name>: ()`**

Nombre de la combinación a declarar

**`<Combination options>: ()`**

Diferentes opciones que puede tomar la combinación. 

**`<END>: ()`**

Fin de la declaración.  


<a name="SYNTAX2"></a>

### Declarar una combinación Variable

`<Variable name> is <Combination name>
`
---

**`<Variable name>: ()`**

Nombre de la combinación Variable a declarar.

**`<Combination name>: ()`**

Nombre de la combinación que se declaró de antemano.   



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Observaciones




### Declaración de las combinaciones
<a name="declaracion_las_combinaciones_ELTPARAGRAPHE000080"></a>

Se debe declarar una combinación en:

- El código de inicialización del proyecto para ser utilizado en todos los procesos del proyecto (código de proyecto, código de ventanas, código de controles, procedimientos, etc.).

- El código de declaración de las variables globales de una ventana para ser utilizadas en todos los procesos de la ventana (código de ventana, código de controles que se encuentran en la ventana, procedimientos locales, etc.).

- El código de apertura de un reporte para ser utilizado en todos los procesos del reporte (código del reporte, código de controles que se encuentra en el reporte, procedimientos locales, etc.).

- El código de declaración de una clase para ser utilizado en la clase.

	- Para acceder a esta combinación desde un método de la clase, utilice la sintaxis "::&lt;Nombre de la combinación&gt;".

	- Para acceder a esta combinación desde un código externo a la clase, utilice la sintaxis "&lt;Nombre de la clase&gt;::&lt;Nombre de la combinación&gt;".




- El código de declaración de set s de procedimientos para ser utilizado en todos los procedimientos de la set.




**Observación**: Una combinación es siempre global:

- a un proyecto,

- a una ventana,

- a un reporte,

- a una clase.



<a name="NOTE0_2"></a>


### Activación o desactivación de las opciones de una combinación
<a name="activacion_desactivacion_las_opciones_una_combinacion_ELTPARAGRAPHE000103"></a>

Se pueden utilizar varias sintaxis para habilitar o deshabilitar las opciones de una combinación. Por ejemplo: 

- Declaración de la combinación utilizada para los ejemplos: 
	
	```wl
	CombinationType is Combination
		CreateFile
		ReadFile
		WriteFile
		DeleteFile
	END
	
	// Declare a Combination variable
	FileAction is CombinationType
	```


- Desactivación de todas las opciones: 
	Sintaxis: 
	
	```txt
	<Combination variable> = 0
	```

	Ejemplo: 
	
	```wl
	FileAction = 0
	```


- Habilitación de una o más opciones: 
	Sintaxis: 
	
	```txt
	<Combination variable> = Option 1
	<Combination variable>[Option 1] = True
	
	<Combination variable> = Option 1 + Option 2
	<Combination variable>[Option 1 + Option 2] = True
	```

	Ejemplos: 
	
	```wl
	FileAction = ReadFile  // Enables an option
	FileAction = ReadFile + WriteFile // Enables 2 options
	
	FileAction[ReadFile] = True  // Enables an option
	FileAction[ReadFile + WriteFile] = True // Enables 2 options
	```


- Desactivar una o más opciones: 
	Sintaxis: 
	
	```txt
	<Combination variable>[Option 1] = False
	
	<Combination variable>[Option 1 + Option 2] = False
	```

	Ejemplos: 
	
	```wl
	FileAction[ReadFile] = False  // Disables an option
	FileAction[ReadFile + WriteFile] = False // Disables 2 options
	```




<a name="NOTE0_3"></a>


### Lectura de las opciones de combinación
<a name="lectura_las_opciones_combinacion_ELTPARAGRAPHE000141"></a>

Se pueden utilizar varias sintaxis para averiguar las opciones de combinación. Por ejemplo: 

- Declarar la combinación
	
	```wl
	CombinationType is Combination
		CreateFile
		ReadFile
		WriteFile
		DeleteFile
	END
	
	// Declare a Combination variable
	FileAction is CombinationType
	```


- Compruebe si todas las opciones están deshabilitadas: 
	Sintaxis: 
	
	```txt
	IF <Combination variable> = 0
	```

	Ejemplos: 
	
	```wl
	IF FileAction = 0 THEN...
	```


- Compruebe si una opción está activada:  
	Sintaxis: 
	
	```txt
	IF <Combination variable> = Option 1 ...
	IF <Combination variable>[Option 1] = True...
	```

	Ejemplos:
	
	```wl
	IF FileAction = ReadFile THEN ...
	IF FileAction[ReadFile] = True THEN...
	```


- Compruebe si hay varias opciones habilitadas: 
	Sintaxis: 
	
	```txt
	IF <Combination variable> = Option 1 + Option 2 THEN ...
	IF <Combination variable>[Option 1 + Option 2] = True ...
	```

	Ejemplos:
	
	```wl
	IF FileAction = ReadFile + WriteFile THEN ...
	IF FileAction[ReadFile + WriteFile] = True THEN...
	```


- Compruebe si al menos una opción está activada: 
	Sintaxis: 
	
	```txt
	IF <Combination variable>[Option 1] OR <Combination variable>[Option 2] THEN...
	```

	Ejemplos:
	
	```wl
	IF FileAction[ReadFile] OR FileAction[WriteFile] THEN...
	```




<a name="NOTE0_4"></a>


### Pasar una combinación como parámetro a una Procedure
<a name="pasar_una_combinacion_como_parametro_una_procedure_ELTPARAGRAPHE000188"></a>

Una variable de combinación puede pasarse como parámetro a un Procedure. Para ello, utilice la siguiente sintaxis:

```txt
<Procedure name>(<Name of combination variable>)
```


Por ejemplo:


```wl
// Procedure with a Combination parameter
PROCEDURE FileType(p is CombinationType)

// Calls with a Combination parameter
FileType(ReadFile + WriteFile)
FileType(FileAction)
```

<a name="NOTE0_5"></a>


### Entrada predictiva
<a name="entrada_predictiva_ELTPARAGRAPHE000201"></a>

La entrada asistida para un parámetro Combinación propone las diferentes opciones de combinación.
<a name="NOTE0_6"></a>


### extension de combinación
<a name="extension_combinacion_ELTPARAGRAPHE000208"></a>

Una combinación puede ampliar los valores de una o más combinaciones tomando las opciones de estas y añadiendo nuevas opciones.

Se utiliza la siguiente sintaxis: 


```txt
<Combination name> is Combination
    [Base Combination]
    <Combination additional options>
END
```


donde: 

- &lt;Nombre de la combinación&gt;&gt;Nombre de la combinación: Nombre de la combinación que se declaró de antemano. 

- Combinación de bases: Nombre de la combinación de base a utilizar.

- &lt;Opciones adicionales de la combinación&gt;: Opciones adicionales que se tendrán en cuenta en la combinación. 




Ejemplo: 


```wl
CombinationType is Combination
	CreateFile
	ReadFile
	WriteFile
	DeleteFile
END


AdvancedCombinationType is Combination
	[CombinationType]
	OtherAttributes
END
```





<a name="NOTE0_7"></a>


### Combinaciones predefinidas
<a name="combinaciones_predefinidas_ELTPARAGRAPHE000230"></a>

Se pueden combinar varias opciones para forzar una combinación preestablecida. Las opciones combinadas deben pertenecer a la combinación (o a combinaciones base)

Se utiliza la siguiente sintaxis: 


```txt
<Combination name> is Combination
	<Option 1 of combination>
	<Option 2 of combination>
	<Option 3 of combination> = <Option 1 of combination> + <Option 2 of combination>
END
```


Ejemplo: 


```wl
CombinationType is Combination
	CreateFile
	ReadFile
	WriteFile
	DeleteFile
	Read_Write_File = ReadFile + WriteFile
END
```

<a name="NOTE0_8"></a>


### Exclusiones mutuas
<a name="exclusiones_mutuas_ELTPARAGRAPHE000245"></a>

Tiene la capacidad de describir exclusiones mutuas entre las opciones de combinación. 

Por ejemplo, una combinación contiene tres opciones: Opción 1, Opción 2 y Opción 3. Las opciones 2 y 3 son exclusivas:

- La opción1 y la opción2 pueden ser Active.

- Las opciones 1 y 3 pueden ser Active. 

- Las opciones 2 y 3 no pueden activarse.

- Las opciones 1, 2 y 3 no pueden ser Active.




Se debe utilizar la siguiente sintaxis: 


```txt
<Combination name> is Combination
	<Option 1 of combination>
	<Option 2 of combination>
	<Option 3 of combination>
	// Exclusions
	<Option 2 of combination> - <Option 3 of combination>
END
```


Observación: Combinando las extensiones y las exclusiones, tiene la capacidad de get para las mismas opciones: 

- un parámetro de una función que impone una exclusión,

- un parámetro de una función que no impone ninguna exclusión o que impone una exclusión diferente.



<a name="NOTE0_9"></a>


### Propiedades de la associated
<a name="propiedades_associated_ELTPARAGRAPHE000268"></a>

Las siguientes propiedades son associated con las variables de combinación: 


|   |   |
| --- | --- |
| [denominar](../Proprietes/2510082.md) | Se utiliza para averiguar el nombre de una combinación Variable. |
| [Valor](../Proprietes/2510130.md) | Sirve para conocer el valor asociado de una Combinación Variable. |






<a name="NOTE0_10"></a>


### Funciones disponibles de WLanguage
<a name="funciones_disponibles_wlanguage_ELTPARAGRAPHE000289"></a>

Una combinación puede almacenarse en un elemento HFSQL o en otro formato de archivo. Según sus necesidades, puede almacenar el nombre de la combinación ([denominar](../Proprietes/2510082.md)) o el valor ([Valor](../Proprietes/2510130.md)). 

Las siguientes funciones de WLanguage permiten get las características de una combinación almacenada: 



|   |   |
| --- | --- |
| [CombinationCheckName](../WDLang1/1000020772.md) | Comprueba si una combinación de una o más opciones conocidas por sus nombres es válida. |
| [CombinationFromName](../WDLang1/1000020771.md) | Devuelve una combinación de una o más opciones conocidas por su nombre.. |
| [CombinationFromValue](../WDLang1/1000020770.md) | Devuelve una combinación de una o más opciones conocidas por sus valores de associated. |




<a name="NOTE0_11"></a>


### Límites
<a name="limites_ELTPARAGRAPHE000305"></a>

- Las combinaciones no están disponibles en la compilación dinámica. 

- Las combinaciones no pueden utilizarse fuera del proyecto. 





