
## ¿Cómo clonar y usar columnas en una control Tabla?
			

<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Presentación
<a name="presentacion_ELTTEXTE000160"></a>
En algunos casos, un control Tabla debe ser llenado con un número desconocido de columnas (al crear el control). Se pueden utilizar dos métodos para construir dinámicamente columnas en una control Tabla: 

- [Método 1](#NOTE2_1): clonación (duplicación de una columna existente): utilizando [ControlClone](../WDLang1/3025017.md). 

- ![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png) [Método 2](#NOTE3_1): creación (se crea la nueva columna): utilizando [ControlCreate](../WDLang1/1000020876.md). 




<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Método 1: Clonación de columnas
<a name="metodo_1_clonacion_columnas_ELTTEXTE000184"></a>
[ControlClone](../WDLang1/3025017.md) se utiliza para Clone( duplicar) un control existente para crear un copy. Esta función también se puede utilizar en una columna que se encuentra en un control Tabla. La nueva columna se añade automáticamente al final de las columnas existentes.

Para crear una nueva columna: 

1. Crear un control Tabla. 

2. En la control Tabla, defina la columna que se utilizará como "modelo para la clonación".. 

3. Borrar el contenido de control Tabla ([TableDeleteAll](../WDLang1/3074034.md)). 

4. Borrar las columnas clonadas anteriores ([ControlDelete](../WDLang1/3025037.md)). 

5. Crear las nuevas columnas ([ControlClone](../WDLang1/3025017.md)). 

6. Llenar el control Tabla con datos. 




Ejemplo de código: 

```wl
NbFixedColumns is int
NbNewColumns is int

NewColumnName is string


// Clear the table
TableDeleteAll(TABLE_YearlyStat)


// Delete the previous cloned columns
NbFixedColumns = 3 // for example, 2 fixed columns + the column to clone

FOR nIndex = TableCount(TABLE_YearlyStat,toColumn) TO NbFixedColumns STEP -1
	ControlDelete(TableEnumColumn(TABLE_YearlyStat, nIndex))
END

// Clone the new columns
NbNewColumns = 5 // we want 5 new columns for example
FOR nIndex = 1 TO NbNewColumns
	// Build the name of the new column
	NewColumnName = "COL_" + nIndex
	// Create the new column by cloning
	ControlClone(COL_Template, NewColumnName)
	
	// Modify the header caption of new column
	{NewColumnName, indControl}..Caption = "Column " + nIndex 
	
END

// Fill the Table control...

// Put your code for filling the table here
```


**Atención**:

- Las columnas deben ser clonadas antes de llenar el control Tabla.  

- Dos columnas no pueden tener el mismo nombre. 








<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Método 2: Creación de las columnas
<a name="metodo_2_creacion_las_columnas_ELTTEXTE000208"></a>
[ControlCreate](../WDLang1/1000020876.md) permite crear un control sin tener que utilizar un modelo. Esta función también se puede utilizar en una columna que se encuentra en un control Tabla. La nueva columna se añade automáticamente al final de las columnas existentes.

Para crear una nueva columna: 

1. Crear un control Tabla. 

2. Borrar el contenido de control Tabla ([TableDeleteAll](../WDLang1/3074034.md)). 

3. Borrar las columnas creadas anteriormente ([ControlDelete](../WDLang1/3025037.md)). 

4. Crear las nuevas columnas ([ControlCreate](../WDLang1/1000020876.md)).

5. Llenar el control Tabla con datos. 




Ejemplo de código: 


```wl
NewColumn is Control

NbFixedColumns is int
NbNewColumns is int

NewColumnName is string


// Clear the table
TableDeleteAll(TABLE_YearlyStat)


// Delete the previous created columns
NbFixedColumns = 3 // for example, 2 fixed columns + the column to create


FOR nIndex = TableCount(TABLE_YearlyStat, toColumn) TO NbFixedColumns STEP -1
	ControlDelete(TableEnumColumn(TABLE_YearlyStat, nIndex))
END

// Create the new columns
NbNewColumns = 5 // we want 5 new columns for example
FOR nIndex = 1 TO NbNewColumns
	// Build the name of the new column
	NewColumnName = TABLE_YearlyStat.Name + ".COL_" + nIndex
	// Create the new column by creation
	NewColumn <- ControlCreate(NewColumnName, typColumn)
	
	// Modify the header caption of new column
	NewColumn.Caption = "Column" + nIndex 
END

// Fill the Table control...
// Put your code for filling the table here
```


**Atención**:

- Las columnas deben crearse antes de rellenar la ficha control Tabla.  

- Dos columnas no pueden tener el mismo nombre. 








<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Uso de la nueva columna
<a name="uso_nueva_columna_ELTTEXTE000232"></a>
Se pueden utilizar dos métodos para manipular la columna creada:

- los operadores indirectos { }. Para obtener más información, consulte [Operadores indirectos](../Motscles/1512005.md).

- el nombre de la nueva columna, método válido sólo si la columna fue creada por [ControlCreate](../WDLang1/1000020876.md). 





