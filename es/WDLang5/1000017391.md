
## Creación de un flujo RSS
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Presentación
<a name="presentacion_ELTTEXTE000142"></a>
WINDEV, WEBDEV y WINDEV Mobile le permiten producir y/o consumir un flujo RSS (Rapid Simple Syndication).

Un flujo RSS se utiliza para producir (hacer disponible) un flujo de datos en formato estándar.. Este flujo de datos puede ser leído (consumido) por una aplicación.

Un lector de flujo RSS se utiliza para mostrar uno o más flujos RSS. 



<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## ¿Cómo proceder?
<a name="¿como_proceder_ELTTEXTE000166"></a>


### Creación de un flujo RSS
<a name="creacion_flujo_rss_ELTPARAGRAPHE000044"></a>

Para crear un flujo RSS:

1. Declarar las variables [rssStream](../WDLang5/1000017795.md), [rssChannel](../WDLang5/1000017769.md) y [rssEntry](../WDLang5/1000017792.md).

2. Describe el canal con las propiedades de [rssChannel](../WDLang5/1000017769.md).

3. Describa las entradas con las propiedades de [rssEntry](../WDLang5/1000017792.md).

4. Añada el canal y luego sus entradas en el flujo.




**Observaciones** : 

- Un arroyo está hecho de canales. Un canal se compone de entradas.

- Le recomendamos que utilice funciones como [añadir](../WDLang1/1000017348.md), [Eliminar](../WDLang1/1000017354.md), ... para manipular el array de las entradas y el array de los canales.



<a name="NOTE2_2"></a>


### Ejemplos
<a name="ejemplos_ELTPARAGRAPHE000082"></a>

Ejemplo de creación de un flujo RSS:


```wl
MyStream is rssStream
MyChannel is rssChannel
MyEntry is rssEntry
MyPath is string

// Characteristics of channel
MyChannel.Title = "My site"
MyChannel.Description = "Example of RSS 2.0 stream"
MyChannel.UpdateDate = DateSys() + TimeSys()
MyChannel.Link = "http://www.example.org"
// Characteristics of entries of channel 1
MyEntry.Author = "pcsoft@pcsoft.fr"
MyEntry.Description = "First news"
MyEntry.Title = "News #1"
// Adds the entry into the channel
Add(MyChannel.Entry, MyEntry)
// Adds the channel into the stream
Add(MyStream.Channel, MyChannel)

// Saves the stream in a file
MyPath = CompleteDir(fCurrentDir()) + "Rss.xml"
rssSave(MyStream, MyPath)

// Displays the XML file of stream
IF NOT ShellExecute(MyPath) THEN
	Error("ShellExecute('" + MyPath + "'). '" + ErrorInfo() + "'")
END
```


**Atención** : En este ejemplo, las adiciones realizadas por [añadir](../WDLang1/1000017348.md) deben realizarse en último lugar para manipular el elemento solicitado. También tiene la posibilidad de utilizar el siguiente código : 


```wl
MyStream is rssStream 
ChannelNum is int
EntryNum is int
MyPath is string

MyPath = CompleteDir(fCurrentDir()) + "Rss.xml"

// Describe and add the channel
ChannelNum = 1
ChannelNum = Add(MyStream.Channel)
MyStream.Channel[ChannelNum].Title = "My site"
MyStream.Channel[ChannelNum].Description = "Example of RSS 2.0 stream"
MyStream.Channel[ChannelNum].PublicationDate = Today()
MyStream.Channel[ChannelNum].Link = "http://www.example.org"

// Describe and add the entry 1
EntryNum = Add(MyStream.Channel[ChannelNum].Entry)
MyStream.Channel[ChannelNum].Entry[EntryNum].Title = "First news"
MyStream.Channel[ChannelNum].Entry[EntryNum].Description = "News #1"
MyStream.Channel[ChannelNum].Entry[EntryNum].Link = "http://www.example.org"
MyStream.Channel[ChannelNum].Entry[EntryNum].PublicationDate = Today()
EntryNum = Add(MyStream.Channel[ChannelNum].Entry)

// Describe and add the next entry
MyEntry is rssEntry dynamic = MyStream.Channel[ChannelNum].Entry[EntryNum]
MyEntry.Title = "Second news"
MyEntry.Description = "News #2"
MyEntry.Link = "http://www.example.org"
MyEntry.PublicationDate = Today()

// Saves the stream in a file
rssSave(MyStream, MyPath)

// Displays the XML file of stream
IF NOT ShellExecute(MyPath) THEN
	Error("ShellExecute('" + MyPath + "'). '" + ErrorInfo() + "'")
END
```





