


## Gestión de los calendarios de Google (sintaxis del prefijo)
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Presentación
<a name="presentacion_ELTTEXTE000111"></a>
El servicio Google Calendar es una aplicación INTERNET proporcionada por Google que se utiliza para gestionar un Calendar en INTERNET.

WINDEV y WEBDEV le permiten crear una aplicación para sincronizar horarios con una aplicación existente, por ejemplo: utilizando una sala de reuniones, un vehículo, ....

Estas funciones WLanguage también le permiten desarrollar interfaces específicas (adaptadas a las necesidades de su empresa, más fáciles de usar, etc.) y añadir procesos específicos (impresiones, entre otros).

Ejemplos de procesos que se pueden implementar de forma nativa en WLanguage:

- Recuperación de la lista detallada de calendarios (calendarios profesionales, personales, etc.).

- Recuperando la lista de citas de un Calendar.

- Realizando una búsqueda en las citas de un Calendar.

- Adición, modificación y eliminación de citas.  




**Advertencia**: Antes de utilizar una función de relacionado para los servicios de Google, le recomendamos encarecidamente que compruebe la licencia para utilizar este servicio.. Se pueden aplicar algunas restricciones. El contenido de las licencias puede cambiar con el tiempo. 

PC SOFT no es en ningún caso responsable de la forma en que se utilizan las funciones de acceso nativas.. Asegúrese de que cumple con la licencia del proveedor de servicios.

<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## ¿Cómo manejar un Google Calendar?
<a name="¿como_manejar_google_calendar_ELTTEXTE000152"></a>


### Gestionando un Google Calendar
<a name="gestionando_google_calendar_ELTPARAGRAPHE000175"></a>

Para manejar un Google Calendar:

1. Crear una cuenta de Google si es necesario. Esta cuenta puede ser creada a través de la siguiente Address:  [https://www.google.com/accounts/NewAccount?hl=fr](https://www.google.com/accounts/NewAccount?hl=fr)
	Atención: el Address de esta página puede haber sido modificado desde la fecha de publicación de esta página.
	La cuenta de Google se identifica con una dirección de correo electrónico y la contraseña de associated. 

2. En el código de su aplicación, cree una Variable de tipo [gglConnection](../WDLang5/1000018009.md). Este Variable contiene las características de la conexión a su cuenta de Google.



<a name="NOTE2_2"></a>


### Creando un Google Calendar
<a name="creando_google_calendar_ELTPARAGRAPHE000197"></a>

Se puede crear un Calendar de Google a través de la interfaz de Google o programando con las funciones de WLanguage.

Para crear un Google Calendar con las funciones de WLanguage:

1. Crear una Variable de tipo [gglCalendar](../WDLang5/1000017418.md).

2. Defina las características del Calendar a través de las propiedades del [gglCalendar](../WDLang5/1000017418.md).

3. Definir (si es necesario) los eventos relacionado al Calendar ([gglEvent](../WDLang5/1000017440.md) Variable).

4. Validar la creación de la Calendar con [&lt;gglConnection Variable&gt;.Escribir](../WDLang5/1000021460.md).



<a name="NOTE2_3"></a>


### Observación
<a name="observacion_ELTPARAGRAPHE000228"></a>

Si utiliza un proxy para acceder a INTERNET, el proxy debe estar configurado ([Proxy](../WDLang3/3043002.md)) para utilizar las funciones de Google.

<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## ¿Cómo recuperar un Google Calendar y sus elementos?
<a name="¿como_recuperar_google_calendar_sus_elementos_ELTTEXTE000236"></a>


### 1er método: recuperación de la lista de calendarios y recuperación de sus eventos
<a name="1er_metodo_recuperacion_lista_calendarios_recuperacion_sus_eventos_ELTPARAGRAPHE000259"></a>

Para recuperar un Calendar de Google de la lista de Calendar s:

1. Declarar una array de variables [gglCalendar](../WDLang5/1000017418.md) (para recuperar varios calendarios).

2. Utilice la función [&lt;Variable gglConnectiongt;.ListCalendar](../WDLang5/1000017747.md). Esta función se utiliza para listar los calendarios disponibles. Los calendarios encontrados se asignan a las variables array de gglCalendar.

3. Utilice [&lt;gglConnection Variable&gt;.FillCalendar](../WDLang5/1000017749.md) para recuperar los eventos. Los eventos pueden ser recuperados de un solo Calendar o de varios Calendar s. Los eventos a recuperar pueden ser filtrados (entre dos fechas, por ejemplo).




Ejemplo:

```wl
Cnt is gglConnection
...
arrCalendars is array of 0 gglCalendar 
arrCalendars = Cnt.ListCalendar() 
// First calendar
Calendar is gglCalendar = ArrCalendars[1]
// Retrieve the events between 01/01/2008 and 01/01/2009 included
Cnt.FillCalendar(Calendar, "20080101", "20090102")
// Browse the events of a calendar
Evt is gglEvent
FOR EACH Evt OF Calendar 
	Trace(Evt.Title)
END
```

<a name="NOTE3_2"></a>


### 2º método: recuperando un Calendar específico
<a name="2º_metodo_recuperando_calendar_especifico_ELTPARAGRAPHE000294"></a>

Para recuperar un Google Calendar específico así como sus eventos: 

1. Declarar una variable de tipo [gglCalendar](../WDLang5/1000017418.md).

2. Utilice la función [&lt;gglConnection Variable&gt;.GetCalendar](../WDLang5/1000017748.md). Esta función se utiliza para recuperar el Google Calendar (y sus eventos) correspondiente al título especificado. 




Ejemplo:

```wl
Cnt is gglConnection
...

// Retrieve the calendar named "Work"
Calendar is gglCalendar = Cnt.GetCalendar("Work")
// Browse the events of the calendar
IF ErrorOccurred = False THEN
	Evt is gglEvent
	FOR EACH Evt OF Calendar
		Trace(Evt.Title)
	END
END
```


<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## ¿Cómo añadir
<a name="¿como_anadir_ELTTEXTE000318"></a>


### Principio
<a name="principio_ELTPARAGRAPHE000343"></a>

El principio para modificar los eventos es sencillo: el Calendar se recupera localmente, las modificaciones se realizan localmente y el Calendar se actualiza en el servidor.

Observación: Para los calendarios compartidos, le aconsejamos que actualice regularmente los calendarios en el servidor.
<a name="NOTE4_2"></a>


### Añadir eventos a un Calendar
<a name="anadir_eventos_calendar_ELTPARAGRAPHE000355"></a>

Para añadir eventos a un Calendar:

1. Recuperar el Calendar solicitado (y sus eventos si es necesario).

2. Declarar una variable de tipo [gglEvent](../WDLang5/1000017440.md).

3. Definir las características de la Event a través de las propiedades de la Variable.

4. Utilice [&lt;gglConnection Variable&gt;.Escribir](../WDLang5/1000021460.md) para actualizar la Calendar en el servidor.




Ejemplo:


```wl
Cnt is gglConnection
...

arrCalendars is array of 0 gglCalendar 
arrCalendars = Cnt.ListCalendar() 

// Retrieve the future events of the first calendar
Cnt.FillCalendar(arrCalendars[1])

// Create an event 
MyEvent is gglEvent
MyEvent.StartDate = "20081201085000"
MyEvent.EndDate = "20081201093000"
MyEvent.Title = "Appointment"
MyEvent.Content = "Appointment to discuss the November status report"

// Add the event into the calendar
Add(arrCalendars[1].Event, MyEvent)

// Update the calendar on the server
Cnt.Write(arrCalendars[1])
```

```wl
Cnt is gglConnection
...

// Retrieve the calendar named "Work"
Calendar is gglCalendar = Cnt.GetCalendar("Work")

// Create an event
MyEvent is gglEvent
MyEvent.StartDate = "20081201085000"
MyEvent.EndDate = "20081201093000"
MyEvent.Title = "Appointment"
MyEvent.Content = "Appointment to discuss the November status report"

// Add the event into the calendar
Add(Calendar.Event, MyEvent)

// Update the calendar on the server
Cnt.Write(Calendar)
```

<a name="NOTE4_3"></a>


### Modificando los eventos en un Calendar:
<a name="modificando_los_eventos_calendar_ELTPARAGRAPHE000393"></a>

Para modificar los eventos en un Calendar:

1. Recuperar el solicitado Calendar y sus eventos.

2. Encuentra el Event para modificarlo.

3. Modificar las características de la Event.

4. Valida las modificaciones con [&lt;gglConnection Variable&gt;.Escribir](../WDLang5/1000021460.md).




**Observación**: Puedes hacer varios cambios antes de usar [&lt;gglConnection Variable&gt;.Escribir](../WDLang5/1000021460.md).

**Ejemplo**:

```wl
Cnt is gglConnection
...

// Retrieve the calendar named "Work"
Calendar is gglCalendar = Cnt.GetCalendar("Work")
// Modify the first event of the calendar
Calendar.Event[1].Title = "Meeting w/ boss"
Calendar.Event[1].EndDate = "200810131530"
// Actually update the changes on the server
Cnt.Write(Calendar)
```

<a name="NOTE4_4"></a>


### Borrar un evento de un Calendar
<a name="borrar_evento_calendar_ELTPARAGRAPHE000428"></a>

Para borrar un evento de un Calendar:

1. Recuperar el solicitado Calendar y sus eventos.

2. Encuentra el Event para eliminarlo.

3. Borre la Event.

4. Valida las modificaciones con [&lt;gglConnection Variable&gt;.Escribir](../WDLang5/1000021460.md).


**Observación**: Se pueden realizar varias supresiones antes de utilizar [&lt;gglConnection Variable&gt;.Escribir](../WDLang5/1000021460.md).

**Ejemplo**:

```wl
Cnt is gglConnection
...

// Retrieve the calendar named "Work"
Calendar is gglCalendar = Cnt.GetCalendar("Work")
// Delete the second event from the calendar
Delete(Calendar.Event, 2)
// Actually update the changes on the server
Cnt.Write(Calendar)
```





