
## Gestión de bases de datos HBase
			

<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Presentación
<a name="presentacion_ELTTEXTE000293"></a>
HBase es una base de datos distribuida no relacional (NoSQL), basada en columnas y que utiliza un almacenamiento no estructurado para las tablas grandes. 

HBase es un componente central de Hadoop.

HBase ha sido concebido para procesar grandes volúmenes de datos (varios millones de filas, varios millones de columnas por fila) y datos no estándar (cada fila puede tener una estructura diferente a la del siguiente fila).

<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## ¿Cómo se organiza una base de datos HBase?
<a name="¿como_organiza_una_base_datos_hbase_ELTTEXTE000317"></a>
En una base de datos HBase, los datos se almacenan en tablas (archivos), de forma desnormalizada. 
Cada tabla se divide en familias de columnas (ítems), que a su vez contienen columnas. 
Cada fila (equivalente a un Record en un DBMS tradicional) se identifica con una única "rowKey". 
Para cada fila, en la misma familia de columnas, el número y el nombre de las columnas son libres.<br>![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=Table-HBase.gif)
"rowKey" es la única clave de la tabla.
Una celda es la intersección entre un fila y una columna.
Se almacena el historial del contenido de la celda, así como su hora (fecha/hora en que se escribió)..
Tiene la capacidad de implementar filtros en una HBase tabla (un poco como un filtro en HFSQL).

<a name="NOTE3"></a>


## ¿Cómo manipular una base de datos HBase en programación?
<a name="¿como_manipular_una_base_datos_hbase_programacion_ELTTEXTE000341"></a>
WLanguage te lo permite:

- [crear tablas](#CREATE_TABLE)

- utilizar las tablas existentes:

	- [recuperar la lista de tablas](#GET_LIST)

	- [recuperar la description de una tabla](#GET_TABLE_DESC)

	- [borrar un tabla](#DEL_TABLE)




- utilice los datos de tabla:

	- [lectura de datos](#READ)

	- [escritura de datos](#WRITE)

	- [eliminar datos](#DEL)

	- [navegar por los datos según un filtro](#FILTER)







Atención: Las funciones de HBase están disponibles en tiempo de edición y ejecución desde Windows Vista o Windows Server 2008.

<a name="CREATE_TABLE"></a>


### ¿Cómo crear un HBase tabla?
<a name="¿como_crear_hbase_tabla_ELTPARAGRAPHE000068"></a>

Para crear una HBase tabla, usted debe: 

1. Definir e inicializar una [hbConnection](../WDLang4/1000021685.md) Variable.

2. Definir e inicializar una [hbTableDescription](../WDLang4/1000021688.md) Variable. Este Variable contiene un array de [hbColumnDescription](../WDLang4/1000021694.md) Variable s que describen las columnas.

3. Utilice la función [hbCreateTable](../WDLang4/1000021696.md).Ejemplo:



```wl
// Connection
Connection is hbConnection
Connection.Server = "MyServer"
// Describe the table
TableDesc is hbTableDescription
TableDesc.Name = "blog"
// Describe the "Post" column
ColDesc is hbColumnDescription
ColDesc.Name = "Post"
Add(TableDesc.Column, ColDesc)
// Create the table
hbCreateTable(Connection, TableDesc)
```

<a name="GET_LIST"></a>


### ¿Cómo recuperar la lista de tablas que se encuentran en una base de datos HBase?
<a name="¿como_recuperar_lista_tablas_que_encuentran_una_base_datos_hbase_ELTPARAGRAPHE000093"></a>

Para recuperar la lista de tablas encontradas en una base de datos de HBase, debe: 

1. Definir e inicializar una [hbConnection](../WDLang4/1000021685.md) Variable.

2. Utilice la función [hbListTable](../WDLang4/1000021687.md).Ejemplo:



```wl
// Connection
Connection is hbConnection
Connection.Server = "MyServer"
// Display the list of tables
Trace(hbListTable(Connection))
```

<a name="GET_TABLE_DESC"></a>


### ¿Cómo recuperar la description de una base de datos HBase?
<a name="¿como_recuperar_description_una_base_datos_hbase_ELTPARAGRAPHE000111"></a>

Para recuperar la description de una base de datos HBase, debe: 

1. Definir e inicializar una [hbConnection](../WDLang4/1000021685.md) Variable.

2. Utilice la función [hbGetTableDescription](../WDLang4/1000021695.md).

3. Recuperar el tabla description en un [hbTableDescription](../WDLang4/1000021688.md) Variable. Este Variable contiene un array de [hbColumnDescription](../WDLang4/1000021694.md) Variable s que describen las columnas.Ejemplo:



```wl
// Connection
Connection is hbConnection
Connection.Server = "MyServer"
// Retrieve the description of the table
TableDesc is hbTableDescription = hbGetTableDescription(Connection, "blog")
// Display the name of the table
Trace("table: " + TableDesc.Name)
// Display the description of columns
FOR EACH ColDesc OF TableDesc.Column
	Trace(TAB + "column: " + ColDesc.Name)
	Trace(TAB + TAB + "attribute: ")
	FOR EACH AttributeVal, AttributeName OF ColDesc.Attribute
		Trace(TAB + TAB + AttributeName + " = " + AttributeVal)
	END
END
```

<a name="DEL_TABLE"></a>


### ¿Cómo eliminar un HBase tabla?
<a name="¿como_eliminar_hbase_tabla_ELTPARAGRAPHE000136"></a>

Para borrar una HBase tabla, debe: 

1. Definir e inicializar una [hbConnection](../WDLang4/1000021685.md) Variable.

2. Utilice [hbDeleteTable](../WDLang4/1000021713.md) y especifique el nombre de la tabla a eliminar.Ejemplo:



```wl
// Connection
Connection is hbConnection
Connection.Server = "MyServer"
// Delete the table
hbDeleteTable(Connection, "blog")
```

<a name="READ"></a>


### ¿Cómo leer los datos que se encuentran en una base de datos HBase?
<a name="¿como_leer_los_datos_que_encuentran_una_base_datos_hbase_ELTPARAGRAPHE000154"></a>

Para leer los datos encontrados en una base de datos de HBase, debe: 

1. Definir e inicializar una [hbConnection](../WDLang4/1000021685.md) Variable.

2. Definir e inicializar una [hbReading](../WDLang4/1000021701.md) Variable. Este Variable contiene un array de [hbColumn](../WDLang4/1000021698.md) Variable s que describe las columnas a recuperar.

3. Utilice la función [hbLeer](../WDLang4/1000021706.md).

4. Leer los datos en el resultado de [hbCell](../WDLang4/1000021707.md)Ejemplo:



```wl
// Connection
Connection is hbConnection
Connection.Server = "MyServer"
// Description of data to read
Reading is hbReading
Reading.Row = "post1"
Reading.Version = 2
// Column description
Col is hbColumn
Col.Family = "Post"
Add(Reading.Column, Col)
// Read data
MyResult is array of hbCells = hbRead(Connection, "blog", Reading)
// Display the data read
FOR EACH Cell OF MyResult DO
	Trace("Column: " + Cell.Column.Qualifier + "; Value: " + ...
	Cell.Value + "; timestamp: " + Cell.Timestamp)
END
```

<a name="WRITE"></a>


### ¿Cómo escribir datos en una base de datos HBase?
<a name="¿como_escribir_datos_una_base_datos_hbase_ELTPARAGRAPHE000183"></a>

Para escribir datos en una base de datos de HBase, debe: 

1. Definir e inicializar una [hbConnection](../WDLang4/1000021685.md) Variable.

2. Definir e inicializar una [hbWriting](../WDLang4/1000021701.md) Variable. Este Variable contiene un array de [hbCell](../WDLang4/1000021707.md) Variable s que describen los datos a escribir.

3. Utilice la función [hbWrite](../WDLang4/1000021712.md).Ejemplo:



```wl
// Connection
Connection is hbConnection
Connection.Server = "MyServer"
// Description of data to write
Writing is hbWriting
Writing.Row = "post1"
Cell is hbCell 
Cell.Column.Family = "post"
Cell.Column.Qualifier = "title"
Cell.Value = "This is my blog post."
Add(Writing.Cell, Cell)
// Write data
hbWrite(Connection, "blog", Writing)
```

<a name="DEL"></a>


### ¿Cómo eliminar datos de un HBase tabla?
<a name="¿como_eliminar_datos_hbase_tabla_ELTPARAGRAPHE000208"></a>

Para borrar datos de una HBase tabla, debe: 

1. Definir e inicializar una [hbConnection](../WDLang4/1000021685.md) Variable.

2. Utilice [hbDelete](../WDLang4/1000021714.md) y especifique el identificador de la fila a eliminar.Ejemplo:



```wl
// Connection
Connection is hbConnection
Connection.Server = "MyServer"
// Delete the entire row
hbDelete(Connection, "blog", "post1")
```

<a name="FILTER"></a>


### ¿Cómo realizar una navegación con filtro en un HBase tabla?
<a name="¿como_realizar_una_navegacion_con_filtro_hbase_tabla_ELTPARAGRAPHE000226"></a>

Para realizar una búsqueda con filtro en una base de datos de HBase, debe: 

1. Definir e inicializar una [hbConnection](../WDLang4/1000021685.md) Variable.

2. Definir e inicializar una [hbScanParameter](../WDLang4/1000021715.md) Variable. Este Variable define el filtro a implementar. 

3. Examinar los datos filtrados (correspondientes a la [hbScanParameter](../WDLang4/1000021715.md) Variable) mediante la sentencia FOR EACH. El resultado de la búsqueda se lee en un [hbScanResult](../WDLang4/1000021724.md) Variable. Ejemplo:



```wl
// Connection
Connection is hbConnection
Connection.Server = "MyServer"
// Filter description
MyScan is hbScanParameter
MyScan.Connection = Connection
MyScan.Table = "blog"
// Filters, for all the rows, the values of columns whose name starts with "Title"
MyScan.Filter = "{ ""type"": ""ColumnPrefixFilter"", ""value"": """ + ...
	Encode("Title", encodeBASE64) + """ }"
// Loops through and displays the filtered data
// MyResult is an hbScanResult variable
FOR EACH MyResult OF MyScan
	Trace("The column " + MyResult.Column.Family + ":" + ...
		MyResult.Column.Qualifier + ...
		" of the row " + MyResult.Row + " is set to " + MyResult.Value)
END
```



