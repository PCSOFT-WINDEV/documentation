


## HFilterStartsWith (Función)



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Define y habilita un filtro "Empezar con" en un archivo de datos, una vista o una consulta. El filtro "Empieza con" se utiliza para seleccionar todos los registros que empiezan con un determinado set de caracteres.

Después de su ejecución, los casos más comunes son los siguientes:

- no se puede crear el filtro: **HFilterStartsWith** devuelve una cadena vacía (""). [HError](../WDLang4/3044088.md) devuelve el identificador del error.

- se crea el filtro: la función **HFilterStartsWith** devuelve la clave de búsqueda adecuada que se utilizará para navegar por el archivo de datos, la vista o la consulta.



<a name="Example1"></a>
<a name="sample_code"></a>

## Ejemplo


```wl
MyKey is string
// Selects all the customers living in a city whose name starts with "Mon"
MyKey = HFilterStartsWith(Customer, City, "Mon")
HReadFirst(Customer, MyKey)
WHILE NOT HOut()
	Trace(Customer.CustomerName, Customer.City) 
	HReadNext(Customer, MyKey)
END
...
// Cancels the filter
HDeactivateFilter(Customer)
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Sintaxis

`<Result> = HFilterStartsWith(<Data file> , <Key item> , <Filter for key component 1> [, <Filter for key component 2> [... [, <Filter for key component N>]]])`
---

**`<Result>: (Cadena de caracteres)`**

Campo de navegación. Corresponde a:

- la clave de búsqueda del archivo de datos si el filtro está activado

- una cadena vacía si el filtro no se puede implementar




**`<Data file>: (Cadena de caracteres)`**

Nombre del archivo de datos HFSQL, vista o consulta utilizada.

**`<Key item>: (Cadena de caracteres)`**

Nombre del elemento clave sobre el que se aplicará el filtro. Este elemento puede ser una llave simple (cadena) o una llave compuesta. 
Atención: este parámetro sólo debe contener letras, caracteres subrayados o dígitos. De lo contrario, el nombre debe estar entre comillas.

**`<Filter for key component 1>: (Tipo del primer componente de la clave de búsqueda)`**

Valor buscado para el primer componente (el segundo, respectivamente) de la clave compuesta. 
El tipo de este parámetro debe corresponderse con el tipo de componente del elemento clave sobre el que se define el filtro.

**`<Filter for key component 2>: (Tipo del segundo componente de la clave de búsqueda)`**

Valor buscado para el segundo componente de la clave compuesta. 
El tipo de este parámetro debe corresponderse con el tipo de componente del elemento clave sobre el que se define el filtro.



**`<Filter for key component N>: (Tipo del componente N de la clave de búsqueda)`**

Valor buscado para el componente N de la clave compuesta. 
El tipo de este parámetro debe corresponderse con el tipo de componente del elemento clave sobre el que se define el filtro.



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Observaciones


### automatic gestión de claves compuestas
<a name="automatic_gestion_claves_compuestas_ELTPARAGRAPHE000119"></a>

La función **HFilterStartsWith** gestiona automáticamente los filtros tanto para las claves simples como compuestas.

**Ejemplo 1:** Uso de la función **HFilterStartsWith** con una clave simple:

```wl
Res = HFilterStartsWith(Customer, Name, "A")
// Select all the customers whose name starts with A
```


**Ejemplo 2:** Uso de la función **HFilterStartsWith** con una clave compuesta de dos elementos

```wl
// Example where LastNameFirstname is a key made of: LastName + FirstName
// Select all customers whose last name is SMITH 
// and whose first name starts with a C
Res = HFilterStartsWith(Customer, LastNameFirstName, "SMITH", "C")
```
 Este Line de código equivale a:

```wl
HFilter(Customer, LastNameFirstName, ...
	HBuildKeyValue(Customer, LastNameFirstName, "SMITH", "C") + hMinVal, ...
	HBuildKeyValue(Customer, LastNameFirstName, "SMITH", "C") + hMaxVal)
```


**Ejemplo 3:** Uso de la función **HFilterStartsWith** con una clave compuesta de tres elementos:

```wl
// Example with CUSTINV a key made of: CustomerNumber+Date+Time
// All the invoices of a customer (sorted by date)
Res = HFilterStartsWith(Invoice, CUSTINV, Customer.CustNum)
```
 Este Line de código equivale a:

```wl
HFilter(Invoice, CustInv, ...
	HBuildKeyValue(Invoice, CustInv, Customer.CustNum) + hMinVal, ...
	HBuildKeyValue(invoice, CustInv, Customer.CustNum) + hMaxVal)
```


**Ejemplo 4:** Uso de la función **HFilterStartsWith** con una clave compuesta de tres elementos:

```wl
// Example with CUSTINV a key made of: CustomerNumber+Date+Time
// All the invoices of a customer for today (sorted by date)
Res = HFilterStartsWith(Invoice, CustInv, Customer.CustNum, Today())
```
 Este Line de código equivale a:

```wl
HFilter(Invoice, CustInv, ...
	HBuildKeyValue(Invoice, CustInv, Customer.CustNum, Today()) + hMinVal, ...
	HBuildKeyValue(invoice, CustInv, Customer.CustNum, Today()) + hMaxVal)
```

<a name="NOTE0_2"></a>


### Filtro e iteración filtrada:
<a name="filtro_iteracion_filtrada_ELTPARAGRAPHE000191"></a>

Después de ejecutar la función **HFilterStartsWith**, la navegación del archivo de datos debe realizarse en el elemento devuelto por la función **HFilterStartsWith**. El filtro será ignorado si se utiliza otro elemento para recorrer el archivo de datos.

Cuando se define y habilita un filtro en un archivo de datos (vista o consulta), todos los registros leídos corresponden al filtro. Si ningún otro Record corresponde al filtro durante la iteración: 

- [HOut](../WDLang4/3044094.md) devuelve <u><u><u><u>True</u></u></u></u>

- el actual Record corresponde al último Record leído con el filtro. 




Por ejemplo:

| En un archivo de datos filtrado, después de la función: | el actual Record es: |
| --- | --- |
| **HReadFirst** | el primer archivo Record correspondiente al filtro. |
| **HReadLast** | el último archivo Record correspondiente al filtro. |
| **HReadNext** (or **HForward**) | el siguiente Record (o el siguiente N-ésimo Record) correspondiente al filtro. |
| **HReadPrevious** (o **HBackward**) | el anterior Record (o el anterior N-ésimo Record) correspondiente al filtro. |


<a name="NOTE0_3"></a>


### Habilitar/Deshabilitar un filtro
<a name="habilitardeshabilitar_filtro_ELTPARAGRAPHE000237"></a>

- [HDeactivateFilter](../WDLang4/3044085.md) se utiliza para desactivar un filtro.

- [HActivateFilter](../WDLang4/3044158.md) se utiliza para volver a habilitar un filtro.

- El filtro se borra cuando se cierra el archivo de datos (consulta o vista) ([HClose](../WDLang4/3044073.md) por ejemplo).

- Un único filtro puede existir en un momento dado en un archivo de datos (consulta o vista). Si la función **HFilterStartsWith** se utiliza varias veces, solo se tendrá en cuenta el último filtro: se borrarán los filtros anteriores. Del mismo modo, si se utilizan varios filtros (funciones HFilter\*), sólo se tendrá en cuenta el último filtro.



<a name="NOTE0_4"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Código de Usuario (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png)![Procedimientos almacenados](https://doc.pcsoft.fr/ext/images/us/PS.png)![OLE DB](https://doc.pcsoft.fr/ext/images/us/OLEDB.png)![Conectores Nativos](https://doc.pcsoft.fr/ext/images/us/AN.png) 

### Archivos de datos no HFSQL
<a name="archivos_datos_hfsql_ELTPARAGRAPHE000260"></a>

- El filtro se puede crear a partir de cualquier elemento (clave o no clave).

- El filtro se activa automáticamente cuando se ejecuta **HFilterStartsWith**.

- Los cambios realizados con **HFilterStartsWith** y [HDeactivateFilter](../WDLang4/3044085.md) sólo se tienen en cuenta cuando se utilizan las siguientes funciones [HReadFirst](../WDLang4/3044051.md)/[HFirst](../WDLang4/3044113.md), [HReadLast](../WDLang4/3044140.md)/[HLast](../WDLang4/3044101.md) o [HReadSeekFirst](../WDLang4/3044036.md)/[HReadSeekLast](../WDLang4/3044034.md).




<a name="XComponent"></a>

## Componente:
wd280hf.dll
