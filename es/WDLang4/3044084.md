


## HExecuteSQLQuery (Función)



<a name="XUse"></a>
<a name="Use"></a>
<a name="description"></a>
Inicializa una consulta escrita en lenguaje SQL y declara esta consulta al motor HFSQL. Esta consulta puede corresponder a: 

- el código SQL introducido directamente en la función **HExecuteSQLQuery**. 

- una variable de tipo SQL Query.


Se podrá navegar por el resultado de la consulta. Si ya existe una consulta con el mismo nombre, se reemplaza la consulta. El resultado se vuelve a calcular.

**Característica específica de HFSQL**: Las consultas SQL ejecutadas en una base de datos HFSQL Clásico, Móvil o Cliente/Servidor pueden contener funciones WLanguage. Para obtener más información, consulte [Uso de una función WLanguage en una consulta SQL](../Motscles/1513004.md).

**Atención**: Para establecer la posición en el primer Record del resultado de la consulta, utilice [HReadFirst](../WDLang4/3044051.md), por ejemplo.

Para liberar recursos de una consulta introducida en la función **HExecuteSQLQuery**, utilice: 

- [HCancelDeclaration](../WDLang4/3044174.md).

- [HFreeQuery](../WDLang4/1000019374.md).




**Observación**: no se podrá iniciar una consulta si:

- el nombre de la consulta ya se utiliza en el análisis en curso (nombre de archivo de datos o nombre definido por una de las siguientes funciones: [HDeclare](../WDLang4/3044203.md), [HDeclareExternal](../WDLang4/3044204.md), [HDescribeFile](../WDLang4/3044206.md)).

- se encuentra un error de sintaxis en la consulta.




![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) No se pueden ejecutar consultas con código SQL que contengan funciones de WLanguage. 
<a name="Example1"></a>
<a name="sample_code"></a>

## Ejemplo


```wl
QryCustomer is Data Source
// Initialize the "Customer" query
HExecuteSQLQuery(CustomerQry, "SELECT NAME FROM CUSTOMER")
```


<a name="Example2"></a>

```wl
TaskQry is Data Source
// Initialize the "TaskQry" query
// This query uses a WLanguage function
// This query selects the tasks exceeding 5 days
HExecuteSQLQuery(TaskQry, ...
	"SELECT Task.Caption FROM Task" +...
	"WHERE WL.DateDifference(Task.StartTime, Task.EndDate) > 5")
```


<a name="Example3"></a>

```wl
// Dynamic modification of the LIMIT condition of an SQL query

QRY is Data Source

sMyQry is string = [
SELECT * FROM
ZIPCODES
%1
]

sMyQry = StringBuild(sMyQry, "LIMIT 1, 10")
IF NOT HExecuteSQLQuery(QRY, hQueryDefault, sMyQry) THEN
	Error(HErrorInfo())
ELSE
	Info("The query contains " + HNbRec(QRY) + " records.")
END
```

<a name="XSYNTAX"></a>
<a name="SYNTAX1"></a>

## Sintaxis

### Ejecución de una consulta SQL (sin conexión)

`<Result> = HExecuteSQLQuery(<Query> [, <Mode>] , <Text of SQL query>)`
---

**`<Result>: (booleano)`**



- <u><u><u><u>True</u></u></u></u> si se inicializó la consulta SQL,

- <u><u><u><u>False</u></u></u></u> en caso contrario. La función [HErrorInfo](../WDLang4/3044071.md) permite obtener más detalles sobre el problema.




**`<Query>: (Cadena de caracteres (con o sin citas) o fuente de datos)`**



- Nombre de la consulta que será ejecutada. Este nombre se utiliza para manipular el resultado de &lt;Text of SQL query&gt; por programación. Si ya se ha declarado una consulta con el mismo nombre, se reemplaza por la nueva consulta.

- Nombre de la [consulta SQL](../Motscles/1514082.md) Variable que contiene el código SQL de la consulta. En este caso, no se debe especificar el parámetro &lt;Text of SQL query&gt;.. 
	




**`<Mode>: (Constante opcional de tipo Integer)`**

Opción para iniciar la consulta:


|   |   |
| --- | --- |
| *hWithFilter*<br>(constante que se puede combinar con las otras constantes) | Si se especifica este parámetro:<br><br>- el resultado de la consulta corresponde a una selección de registros de tipo filtro si el motor HFSQL lo permite. De lo contrario, el resultado de la consulta corresponde a una vista HFSQL.<br><br>- las siguientes operaciones no están disponibles: buscar en el resultado de la consulta, consulta a partir de una consulta, vista a partir de una consulta, consulta a partir de una vista.<br><br>- se ignoran los elementos de navegación especificados al examinar el resultado.<br><br>- la lupa no está disponible en las tablas relacionadas con una consulta.<br><br>- no se puede utilizar la función [HNbRec](../WDLang4/3044006.md) en las consultas. <br>	Atención: si se especifica esta opción, la constante *hModifyFile* se utiliza automáticamente.<br><br><br>Si no se especifica este parámetro (por Default):<br><br>- el resultado de la consulta corresponde a una vista HFSQL.<br><br>- se pueden realizar las siguientes operaciones en las consultas: buscar en el resultado de la consulta, consulta a partir de una consulta, vista a partir de una consulta, consulta a partir de una vista.<br><br>- se tienen en cuenta los elementos de navegación especificados al examinar el resultado.<br><br>- la lupa está disponible en las mesas relacionado para una consulta.<br><br>- se puede utilizar la función [HNbRec](../WDLang4/3044006.md) en las consultas.<br><br><br>![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Acceso con JDBC: Esta constante no está disponible. |
| *hWithTransaction* | En una transacción sólo se ejecutan las peticiones de escritura (INSERT, UPDATE y DELETE).<br><br>Se inicia una transacción, los registros se bloquean durante la ejecución de la consulta y se desbloquean al final de su ejecución, independientemente del resultado de la consulta. La transacción siempre se completa al final de la ejecución de **HExecuteSQLQuery**. No hay fuente de datos para las solicitudes de escritura: no es necesario llamar a [HFreeQuery](../WDLang4/1000019374.md).<br><br>Si la consulta no puede finalizar correctamente (registros bloqueados, corte de energía, etc.), se cancela la transacción y los archivos de datos se restauran al estado en el que se encontraban antes de ejecutar la consulta).<br><br>Observación: la ejecución de la consulta puede ralentizarse.<br>![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Esta constante no está disponible. |
| *hModifyFile*<br>(constante que se puede combinar con las otras constantes) | <br><br>- **En archivos de datos HFSQL**: Cuando se modifique el resultado de la consulta ([HAdd](../WDLang4/3044147.md), [HWrite](../WDLang4/3044092.md), [HModify](../WDLang4/3044042.md), [HCross](../WDLang4/3044121.md), [HDelete](../WDLang4/3044018.md)), estas modificaciones se propagarán en los ficheros de datos incluidos en la consulta. <br>	Si no se especifica esta opción, solo se modifica el resultado de la consulta. <br>	Para más detalles sobre la modificación del resultado de una consulta, véase [Modificar el contenido de una consulta](../WDLang4/3044234.md).<br><br>- **En archivos de datos con un formato distinto a HFSQL**, no es necesario utilizar la función *hModifyFile*: los archivos de datos utilizados en la consulta se modifican automáticamente al modificar el resultado de la consulta.<br><br><br>![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Acceso con JDBC: Esta constante no está disponible. |
| *hQueryBreakable* | El inicio de la consulta se puede interrumpir con la tecla ESC.<br>![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png) Esta constante no está disponible.![Procedimientos almacenados](https://doc.pcsoft.fr/ext/images/us/PS.png) Esta constante no se tiene en cuenta.![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Acceso con JDBC: Esta constante no está disponible.![HFSQL Client/Server](https://doc.pcsoft.fr/ext/images/us/HFCS.png) La tecla Esc debe pulsarse en el equipo cliente. |
| *hQueryDefault*<br>(valor predeterminado) | La consulta se inicia sin interrupción. |
| *hQueryWithoutCorrection* | ![OLE DB](https://doc.pcsoft.fr/ext/images/us/OLEDB.png)![Conectores Nativos](https://doc.pcsoft.fr/ext/images/us/AN.png) El motor HFSQL no verifica la consulta. Esta opción debe utilizarse si la consulta contiene comandos específicos de un tipo de conexión (Oracle, SQL Server, etc.).<br><br>Atención: si se utiliza esta constante:<br><br>- se debe especificar el nombre de la conexión (parámetro &lt;Conexión&gt;).<br><br>- no se puede utilizar la siguiente función en la consulta: [HFilter](../WDLang4/3044100.md).<br><br>- no se puede cancelar una condición asignándola a NULL. <br><br>- no se debe especificar ningún elemento de navegación en las siguientes funciones: [HLast](../WDLang4/3044101.md), [HReadLast](../WDLang4/3044140.md), [HReadFirst](../WDLang4/3044051.md), [HFirst](../WDLang4/3044113.md), [HSeekLast](../WDLang4/3044129.md), [HSeekFirst](../WDLang4/3044125.md).<br><br><br> |
| *hQueryWithoutHFCorrection* | ![HFSQL Classic](https://doc.pcsoft.fr/ext/images/us/HF.png) El motor HFSQL no verifica el formato del archivo de datos (con o sin espacios). Se debe utilizar si la consulta manipula archivos de datos HFSQL con un formato que se completa con espacios y con un formato que no se completa con espacios.![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png) Esta constante no está disponible. |
| *hNoBind* | ![Conectores Nativos](https://doc.pcsoft.fr/ext/images/us/AN.png) SQL Server, Oracle, Sybase: Se utiliza para ejecutar una consulta sin activar la opción bind. |
| *hNoHourglass* | Al leer el resultado de la consulta, esta operación de lectura puede bloquearse y aparece un reloj de arena de Default. Esta constante se utiliza para que no se muestre el reloj de arena. <br><br> |
| *hSQLUnicode* |   |
| *hYield* | En las consultas UPDATE y DELETE, que se utilizan en un gran número de registros y suelen tomar mucho tiempo, esta constante devuelve el control a las demás aplicaciones que utilizan los archivos de datos.<br>En este caso:<br><br>- la consulta se ejecuta un poco más lento,<br><br>- el servidor de archivos no se satura,<br><br>- los demás usuarios de los archivos de datos pueden continuar accediendo a ellos sin ningún tipo de bloqueo.<br><br><br>![HFSQL Client/Server](https://doc.pcsoft.fr/ext/images/us/HFCS.png)![OLE DB](https://doc.pcsoft.fr/ext/images/us/OLEDB.png)![Conectores Nativos](https://doc.pcsoft.fr/ext/images/us/AN.png) Esta constante no se tiene en cuenta.<br><br>![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Acceso con JDBC: Esta constante no está disponible. |
| *hCheckDuplicates* | Activa la gestión de duplicados durante la ejecución de la consulta. Si se detecta un error de duplicados, no se ejecuta la consulta.<br>Esta constante fuerza la constante *hWithTransaction* . Se inicia una transacción, los registros se bloquean durante la ejecución de la consulta y se desbloquean al final de su ejecución, independientemente del resultado de la consulta. Si la aplicación se detiene durante la ejecución de la consulta (corte de energía, por ejemplo), la transacción se cancelará la próxima vez que se utilicen los archivos de datos de la consulta.<br>Observación: la ejecución de la consulta puede ralentizarse.<br>![HFSQL Client/Server](https://doc.pcsoft.fr/ext/images/us/HFCS.png) Si no se especifica la constante *hCheckDuplicates*, se desactiva la gestión de duplicados. Si el usuario no tiene permisos para desactivar la gestión de duplicados (función [HModifyDatabaseRights](../WDLang4/3044326.md)) y se debe realizar la gestión de duplicados, se muestra un error de permisos.![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Acceso con JDBC: Esta constante no está disponible. |
| *hCheckIntegrity* | Activa la gestión de integridad durante la ejecución de la consulta. Si se detecta un error de integridad, no se ejecuta la consulta.<br>Esta constante obliga a la constante **hWithTransaction**. Se inicia una transacción, los registros se bloquean durante la ejecución de la consulta y se desbloquean al final de su ejecución, independientemente del resultado de la consulta. Si la aplicación se detiene durante la ejecución de la consulta (corte de energía, por ejemplo), la transacción se cancelará la próxima vez que se utilicen los archivos de datos de la consulta.<br>Observación: la ejecución de la consulta puede ralentizarse.<br>![HFSQL Client/Server](https://doc.pcsoft.fr/ext/images/us/HFCS.png) Si no se especifica la constante *hCheckIntegrity*, se desactiva la gestión de la integridad. Sin embargo, se produce un error si se debe realizar una operación de gestión de la integridad, pero el usuario no tiene derechos para desactivar la gestión de la integridad ([HModifyDatabaseRights](../WDLang4/3044326.md)).![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Acceso con JDBC: Esta constante no está disponible. |

![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Acceso con JDBC: Este parámetro solo puede tomar el valor *hQueryDefault*. Si se utiliza otra constante, se genera un error.

**`<Text of SQL query>: (Cadena de caracteres (con comillas))`**

Texto de la consulta SQL a ejecutar. Este texto puede corresponder a una cadena de caracteres en formato ANSI o en formato Unicode..  
![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) No se pueden ejecutar consultas con código SQL que contengan funciones de WLanguage. Este parámetro no está disponible si &lt;Nombre de consulta&gt; corresponde a una variable de tipo SQL Query.


<a name="SYNTAX2"></a>

### Ejecución de una consulta SQL a través de una conexión

`<Result> = HExecuteSQLQuery(<Query> [, <Connection> [, <Mode>]] , <Text of SQL query>)`
---

**`<Result>: (booleano)`**



- <u><u><u><u>True</u></u></u></u> si se inicializó la consulta SQL,

- <u><u><u><u>False</u></u></u></u> en caso contrario. La función [HErrorInfo](../WDLang4/3044071.md) permite obtener más detalles sobre el problema.




**`<Query>: (Cadena de caracteres (con o sin citas) o fuente de datos)`**



- Nombre de la consulta que será ejecutada. Este nombre se utiliza para manipular el resultado de &lt;Text of SQL query&gt; por programación. Si ya se ha declarado una consulta con el mismo nombre, se reemplaza por la nueva consulta.

- Nombre de la [consulta SQL](../Motscles/1514082.md) Variable que contiene el código SQL de la consulta. En este caso, no se debe especificar el parámetro &lt;Text of SQL query&gt;.. 
	




**`<Connection>: (Cadena de caracteres opcional o Conexión Variable)`**

Conexión utilizada para ejecutar la consulta. Esta conexión corresponde a: 

- una conexión definida en el editor de análisis o mediante programación con [HDescribeConnection](../WDLang4/3044205.md) o [HOpenConnection](../WDLang4/3044107.md). 

- a [Conexión](../WDLang4/1514073.md) Variable. 


&lt;Resultado&gt; es set a False si este parámetro no corresponde a una conexión existente.

**`<Mode>: (Constante opcional de tipo Integer)`**

Opción para iniciar la consulta:


|   |   |
| --- | --- |
| *hWithFilter*<br>(constante que se puede combinar con las otras constantes) | Si se especifica este parámetro:<br><br>- el resultado de la consulta corresponde a una selección de registros de tipo filtro si el motor HFSQL lo permite. De lo contrario, el resultado de la consulta corresponde a una vista HFSQL.<br><br>- las siguientes operaciones no están disponibles: buscar en el resultado de la consulta, consulta a partir de una consulta, vista a partir de una consulta, consulta a partir de una vista.<br><br>- se ignoran los elementos de navegación especificados al examinar el resultado.<br><br>- la lupa no está disponible en las tablas relacionadas con una consulta.<br><br>- no se puede utilizar la función [HNbRec](../WDLang4/3044006.md) en las consultas. <br>	Atención: si se especifica esta opción, la constante *hModifyFile* se utiliza automáticamente.<br><br><br>Si no se especifica este parámetro (por Default):<br><br>- el resultado de la consulta corresponde a una vista HFSQL.<br><br>- se pueden realizar las siguientes operaciones en las consultas: buscar en el resultado de la consulta, consulta a partir de una consulta, vista a partir de una consulta, consulta a partir de una vista.<br><br>- se tienen en cuenta los elementos de navegación especificados al examinar el resultado.<br><br>- la lupa está disponible en las mesas relacionado para una consulta.<br><br>- se puede utilizar la función [HNbRec](../WDLang4/3044006.md) en las consultas.<br><br><br>![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Acceso con JDBC: Esta constante no está disponible. |
| *hWithTransaction* | En una transacción sólo se ejecutan las peticiones de escritura (INSERT, UPDATE y DELETE).<br><br>Se inicia una transacción, los registros se bloquean durante la ejecución de la consulta y se desbloquean al final de su ejecución, independientemente del resultado de la consulta. La transacción siempre se completa al final de la ejecución de **HExecuteSQLQuery**. No hay fuente de datos para las solicitudes de escritura: no es necesario llamar a [HFreeQuery](../WDLang4/1000019374.md).<br><br>Si la consulta no puede finalizar correctamente (registros bloqueados, corte de energía, etc.), se cancela la transacción y los archivos de datos se restauran al estado en el que se encontraban antes de ejecutar la consulta).<br><br>Observación: la ejecución de la consulta puede ralentizarse.<br>![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Esta constante no está disponible. |
| *hModifyFile*<br>(constante que se puede combinar con las otras constantes) | <br><br>- **En archivos de datos HFSQL**: Cuando se modifique el resultado de la consulta ([HAdd](../WDLang4/3044147.md), [HWrite](../WDLang4/3044092.md), [HModify](../WDLang4/3044042.md), [HCross](../WDLang4/3044121.md), [HDelete](../WDLang4/3044018.md)), estas modificaciones se propagarán en los ficheros de datos incluidos en la consulta. <br>	Si no se especifica esta opción, solo se modifica el resultado de la consulta. <br>	Para más detalles sobre la modificación del resultado de una consulta, véase [Modificar el contenido de una consulta](../WDLang4/3044234.md).<br><br>- **En archivos de datos con un formato distinto a HFSQL**, no es necesario utilizar la función *hModifyFile*: los archivos de datos utilizados en la consulta se modifican automáticamente al modificar el resultado de la consulta.<br><br><br>![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Acceso con JDBC: Esta constante no está disponible. |
| *hQueryBreakable* | El inicio de la consulta se puede interrumpir con la tecla ESC.<br>![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png) Esta constante no está disponible.![Procedimientos almacenados](https://doc.pcsoft.fr/ext/images/us/PS.png) Esta constante no se tiene en cuenta.![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Acceso con JDBC: Esta constante no está disponible.![HFSQL Client/Server](https://doc.pcsoft.fr/ext/images/us/HFCS.png) La tecla Esc debe pulsarse en el equipo cliente. |
| *hQueryDefault*<br>(valor predeterminado) | La consulta se inicia sin interrupción. |
| *hQueryWithoutCorrection* | ![OLE DB](https://doc.pcsoft.fr/ext/images/us/OLEDB.png)![Conectores Nativos](https://doc.pcsoft.fr/ext/images/us/AN.png) El motor HFSQL no verifica la consulta. Esta opción debe utilizarse si la consulta contiene comandos específicos de un tipo de conexión (Oracle, SQL Server, etc.).<br><br>Atención: si se utiliza esta constante:<br><br>- se debe especificar el nombre de la conexión (parámetro &lt;Conexión&gt;).<br><br>- no se puede utilizar la siguiente función en la consulta: [HFilter](../WDLang4/3044100.md).<br><br>- no se puede cancelar una condición asignándola a NULL. <br><br>- no se debe especificar ningún elemento de navegación en las siguientes funciones: [HLast](../WDLang4/3044101.md), [HReadLast](../WDLang4/3044140.md), [HReadFirst](../WDLang4/3044051.md), [HFirst](../WDLang4/3044113.md), [HSeekLast](../WDLang4/3044129.md), [HSeekFirst](../WDLang4/3044125.md).<br><br><br> |
| *hQueryWithoutHFCorrection* | ![HFSQL Classic](https://doc.pcsoft.fr/ext/images/us/HF.png) El motor HFSQL no verifica el formato del archivo de datos (con o sin espacios). Se debe utilizar si la consulta manipula archivos de datos HFSQL con un formato que se completa con espacios y con un formato que no se completa con espacios.![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png) Esta constante no está disponible. |
| *hNoBind* | ![Conectores Nativos](https://doc.pcsoft.fr/ext/images/us/AN.png) SQL Server, Oracle, Sybase: Se utiliza para ejecutar una consulta sin activar la opción bind. |
| *hNoHourglass* | Al leer el resultado de la consulta, esta operación de lectura puede bloquearse y aparece un reloj de arena de Default. Esta constante se utiliza para que no se muestre el reloj de arena. <br><br> |
| *hSQLUnicode* |   |
| *hYield* | En las consultas UPDATE y DELETE, que se utilizan en un gran número de registros y suelen tomar mucho tiempo, esta constante devuelve el control a las demás aplicaciones que utilizan los archivos de datos.<br>En este caso:<br><br>- la consulta se ejecuta un poco más lento,<br><br>- el servidor de archivos no se satura,<br><br>- los demás usuarios de los archivos de datos pueden continuar accediendo a ellos sin ningún tipo de bloqueo.<br><br><br>![HFSQL Client/Server](https://doc.pcsoft.fr/ext/images/us/HFCS.png)![OLE DB](https://doc.pcsoft.fr/ext/images/us/OLEDB.png)![Conectores Nativos](https://doc.pcsoft.fr/ext/images/us/AN.png) Esta constante no se tiene en cuenta.<br><br>![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Acceso con JDBC: Esta constante no está disponible. |
| *hCheckDuplicates* | Activa la gestión de duplicados durante la ejecución de la consulta. Si se detecta un error de duplicados, no se ejecuta la consulta.<br>Esta constante fuerza la constante *hWithTransaction* . Se inicia una transacción, los registros se bloquean durante la ejecución de la consulta y se desbloquean al final de su ejecución, independientemente del resultado de la consulta. Si la aplicación se detiene durante la ejecución de la consulta (corte de energía, por ejemplo), la transacción se cancelará la próxima vez que se utilicen los archivos de datos de la consulta.<br>Observación: la ejecución de la consulta puede ralentizarse.<br>![HFSQL Client/Server](https://doc.pcsoft.fr/ext/images/us/HFCS.png) Si no se especifica la constante *hCheckDuplicates*, se desactiva la gestión de duplicados. Si el usuario no tiene permisos para desactivar la gestión de duplicados (función [HModifyDatabaseRights](../WDLang4/3044326.md)) y se debe realizar la gestión de duplicados, se muestra un error de permisos.![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Acceso con JDBC: Esta constante no está disponible. |
| *hCheckIntegrity* | Activa la gestión de integridad durante la ejecución de la consulta. Si se detecta un error de integridad, no se ejecuta la consulta.<br>Esta constante obliga a la constante **hWithTransaction**. Se inicia una transacción, los registros se bloquean durante la ejecución de la consulta y se desbloquean al final de su ejecución, independientemente del resultado de la consulta. Si la aplicación se detiene durante la ejecución de la consulta (corte de energía, por ejemplo), la transacción se cancelará la próxima vez que se utilicen los archivos de datos de la consulta.<br>Observación: la ejecución de la consulta puede ralentizarse.<br>![HFSQL Client/Server](https://doc.pcsoft.fr/ext/images/us/HFCS.png) Si no se especifica la constante *hCheckIntegrity*, se desactiva la gestión de la integridad. Sin embargo, se produce un error si se debe realizar una operación de gestión de la integridad, pero el usuario no tiene derechos para desactivar la gestión de la integridad ([HModifyDatabaseRights](../WDLang4/3044326.md)).![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Acceso con JDBC: Esta constante no está disponible. |

![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) Acceso con JDBC: Este parámetro solo puede tomar el valor *hQueryDefault*. Si se utiliza otra constante, se genera un error.

**`<Text of SQL query>: (Cadena de caracteres (con comillas))`**

Texto de la consulta SQL a ejecutar. Este texto puede corresponder a una cadena de caracteres en formato ANSI o en formato Unicode..  
![Java](https://doc.pcsoft.fr/ext/images/us/JAVA.png) No se pueden ejecutar consultas con código SQL que contengan funciones de WLanguage. Este parámetro no está disponible si &lt;Nombre de consulta&gt; corresponde a una variable de tipo SQL Query.



<a name="NOTE0"></a>
<a name="NOTE0_1"></a>

## Observaciones


### Lectura de datos durante la inicialización
<a name="lectura_datos_durante_inicializacion_ELTPARAGRAPHE000504"></a>

Dependiendo de la consulta (clasificaciones, grupos,...), los datos pueden ser leídos (o no) cuando se inicializa la consulta.

Observación: La propiedad [ExecutionCompleted](../Proprietes/2512117.md) permite saber si se ha completado la consulta (y, por lo tanto, si se pueden realizar lecturas sin bloqueos).. 

Para acceder a los elementos de consulta, hay que declarar una [Fuente de datos](../WDLang4/1514053.md) Variable (véase la sección Ejemplo). La consulta se libera automáticamente cuando se destruye el Variable. Para forzar la liberación de los recursos de esta consulta, utilice: 

- [HCancelDeclaration](../WDLang4/3044174.md).

- [HFreeQuery](../WDLang4/1000019374.md).



<a name="NOTE0_2"></a>


### Texto de la consulta SQL
<a name="texto_consulta_sql_ELTPARAGRAPHE000532"></a>

Si un nombre de un archivo de datos o un nombre de ítem contiene caracteres de espacio, estos nombres deben ser encerrados entre corchetes en el texto de la consulta. Por ejemplo:

```sql
SELECT [My File1].MyItem, [My File1].[My item1], MyFile2.[My key1]
From [My File1], MyFile2
WHERE [My File1].[My key1] = MyFile2.[My key1]
```

<a name="NOTE0_2A"></a>


### Ejecutar una consulta SQL con parámetros
<a name="ejecutar_una_consulta_sql_con_parametros_ELTPARAGRAPHE000543"></a>

Una consulta SQL puede contener parámetros. Para ejecutar este tipo de consulta SQL con **HExecuteSQLQuery**, debe: 

1. En el texto de la consulta SQL, defina los diferentes parámetros utilizando la notación {Nombre de parámetro}.. Por ejemplo: 
	```txt
	"SELECT * FROM client WHERE name={p_name}"
	```


2. Definir a **Data source** Variable. El nombre de esta variable debe corresponder al parámetro &lt;Nombre de la consulta&gt; de la función **HExecuteSQLQuery**. 
	Ejemplo: 
	
	```wl
	MyQuery is Data Source
	```


3. Especifique el valor de los parámetros mediante la siguiente sintaxis: 
	
	```txt
	<Data source variable >.<Name of Parameter1> = xxx
	<Data source variable >.<Name of Parameter2> = xxx
	<Data source variable >.<Name of Parameter3> = xxx
	```

	Ejemplo: 
	
	```wl
	MyQuery.p_name = "Doe"
	```


4. Ejecutar la consulta SQL con **HExecuteSQLQuery**. Ejemplo: 
	
	```wl
	HExecuteSQLQuery(MyQuery, "SELECT * FROM customer WHERE name={p_name}")
	```





**Observación**: La estructura de los parámetros de consulta se restablece cada vez que se ejecuta la consulta.


<a name="NOTE0_2B"></a>


### inyección SQL: ¿Cómo prevenirlo?
<a name="inyeccion_sql_¿como_prevenirlo_ELTPARAGRAPHE000590"></a>

La inyección SQL es una técnica de hacking. Consiste en inyectar código SQL en los parámetros de las consultas, forzando la ejecución de código SQL no deseado. 

Para evitar la inyección SQL al ejecutar consultas a través de **HExecuteSQLQuery**, debe utilizar consultas con parámetros y no debe concatenar cadenas para construir la consulta. 

Ejemplo: 


```wl
// Build the query by concatenation
// -> security failure
sdQuery is Data Source
HExecuteSQLQuery(sdQuery, "SELECT * FROM customer WHERE name = '" + EDT_Name + "'")
```

```wl
// Using parameters in the query
// -> secure code
sdQuery is Data Source
sdQuery.p_name = EDT_Name
HExecuteSQLQuery(sdQuery, "SELECT * FROM customer WHERE name={p_name}")
```


La utilización de parámetros no permite interpretar el contenido como código SQL. Esta técnica evita la inyección de código SQL y elimina varios riesgos de hacking.


<a name="NOTE0_2C"></a>


### Inserción de datos binarios
<a name="insercion_datos_binarios_ELTPARAGRAPHE000625"></a>

Al insertar datos a través de INSERT o UPDATE, si un elemento memo debe asignarse con datos binarios que puedan contener el carácter \\0 (código ASCII para el cero), los datos deben colocarse en un búfer hexadecimal. 

Ejemplo: 

```wl
SQLQuery is string
DataSource is Data Source

b is Buffer = "Binary data including "+Charact(0)+" (binary zero)"
// or: b is Buffer = fLoadBuffer("<path>\photo.jpg")

SQLQuery = "INSERT INTO Product (Photo) " + ... 
		"VALUES (UNHEX('" + BufferToHexa(b, NoGrouping, NoLine)+"'))" 
HExecuteSQLQuery(DataSource, hQueryDefault, SQLQuery)
```





<a name="NOTE0_3"></a>
![Conectores Nativos](https://doc.pcsoft.fr/ext/images/us/AN.png) 

### Combinaciones
<a name="combinaciones_ELTPARAGRAPHE000648"></a>

Las uniones como (A unir B en x=y) unir C en y=z ... no son automáticamente soportados por los Accesos Nativos: En este caso, se debe utilizar la constante *hQueryWithoutCorrection* para gestionar este tipo de combinación.

**Observación**: Estas uniones son gestionadas por el motor HFSQL.
<a name="NOTE0_4"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Código de Usuario (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png)![OLE DB](https://doc.pcsoft.fr/ext/images/us/OLEDB.png)![Conectores Nativos](https://doc.pcsoft.fr/ext/images/us/AN.png) 

### Uso de la constante hQueryWithoutCorrection?
<a name="uso_constante_hquerywithoutcorrection_ELTPARAGRAPHE000661"></a>

Por Default, WINDEV y WEBDEV interpretan las consultas SQL:

- construido a través de un acceso nativo,

- construido sobre OLEDB y sobre ODBC a través del proveedor OLE DB.




**Para no interpretar la consulta, utilice la constante hQueryWithoutCorrection.**

| Si no se especifica hQueryWithoutCorrection | Si se especifica hQueryWithoutCorrection |
| --- | --- |
| La conexión associated con los archivos de datos de la consulta se define automáticamente. | La conexión que se utilizará debe especificarse en la función **HExecuteSQLQuery**. |
| Todos los letreros de propiedad de PC SOFT son reemplazados (por ejemplo.: ']=' comienza con) con su equivalente en SQL estándar. | No se realiza ningún reemplazo. Se deben utilizar los símbolos de SQL estándar. |
| Formatear las fechas y horas de acuerdo con el formato utilizado por la base de datos..<br>Por ejemplo, las fechas están en formato 'AAAAMMDD' en WINDEV y WEBDEV mientras que en Access, las fechas están en formato #AAAADDMM# o #AAAAMMDD# dependiendo del idioma del sistema. | No se define ningún formato. Se debe utilizar el formato reconocido por las bases de datos. |
| Los flotadores están formateados (el separador decimal puede ser '.' o ',') | No se define ningún formato para las comas flotantes. |
| Dependiendo de la base de datos utilizada, los alias se reemplazan por los nombres completos de los elementos en las cláusulas Where, Order by y Group by<br>Por ejemplo, el motor JET (Access, dBase, etc.) no acepta ningún alias en la cláusula Where de una consulta | No se realiza ningún reemplazo. Se deben utilizar los nombres completos de los elementos directamente en el código de la consulta, en las cláusulas Where, Order by y Group by. |

![Conectores Nativos](https://doc.pcsoft.fr/ext/images/us/AN.png) **Caso especial: Oracle y SQL Server**: Si la consulta a ejecutar contiene un script con ":param" (Oracle) o "@param" (SQL Server), debe utilizar las constantes *hQueryWithoutCorrection* + *hNoBind* para que la consulta no sea interpretada.
<a name="NOTE0_5"></a>


### Condición de una clave compuesta en una consulta SQL
<a name="condicion_una_clave_compuesta_una_consulta_sql_ELTPARAGRAPHE000713"></a>

Para definir una condición en una clave compuesta en una consulta SQL, **las condiciones deben ser especificadas para cada componente de la clave**.

No intente asignar directamente un valor a la clave compuesta (de hecho, las claves compuestas se almacenan como valores binarios).

**Ejemplo**: La clave compuesta se compone de los elementos LASTNAME y FIRSTNAME (Last Name First Name):


```sql
SELECT MyFile.MyItem, MyFile.MyItem1
FROM MyFile
WHERE FileName.LastName = "Smith" and FileName.FirstName = "Fred"
```

<a name="NOTE0_6"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png)![Código de Usuario (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png)![Lenguaje Externo](https://doc.pcsoft.fr/ext/images/us/LE.png)![HFSQL Classic](https://doc.pcsoft.fr/ext/images/us/HF.png)![HFSQL Client/Server](https://doc.pcsoft.fr/ext/images/us/HFCS.png)![Hyper File 5.5](https://doc.pcsoft.fr/ext/images/us/HF55.png) 

### Consultas sobre archivos de datos HFSQL
<a name="consultas_sobre_archivos_datos_hfsql_ELTPARAGRAPHE000730"></a>

- Para optimizar el tiempo de ejecución de la consulta, utilice [HOptimize](../WDLang4/3044005.md) o [HOptimizeQuery](../WDLang4/3044003.md) en los diferentes archivos de datos incluidos en la consulta.

- Para optimizar el tiempo de procesamiento de la consulta, utilice [HStatCalculate](../WDLang4/3044134.md) o [HIndex](../WDLang4/3044133.md) en todos los archivos de datos incluidos en la consulta.

- **Al ejecutar una consulta INSERT en archivos de datos HFSQL**, se puede conocer el identificador automático asignado al nuevo registro mediante la siguiente sintaxis: &lt;Nombre archivo&gt;.&lt;Nombre elemento identificador automático&gt;.

- Si los archivos de datos utilizados por la consulta no se encuentran en el análisis, estos archivos deben ser declarados dinámicamente por [HDeclareExternal](../WDLang4/3044204.md). De lo contrario, un error como **No se puede inicializar la consulta. Desconocido FILENAME** ocurrirá. Ejemplo:
	
	```wl
	// Connection parameters
	cntDatabase is Connection
	cntDatabase.Provider = hAccessHFClientServer
	cntDatabase.Server = "HYPERFILESQLSERVER:4900"
	cntDatabase.Database = "Database"
	cntDatabase.User = "admin"
	cntDatabase.Password = ""
	
	// Connect to the database
	IF HOpenConnection(cntDatabase) = False THEN
		Error(HErrorInfo())
		RETURN
	END
	
	// Dynamic declaration of the files used in the query
	IF HDeclareExternal("CUSTOMER.FIC", "CUSTOMER", cntDatabase) = False THEN
		Error(HErrorInfo())
		RETURN	
	END
	
	// Run the query
	dsSQLQuery is Data Source
	IF NOT HExecuteSQLQuery(dsSQLQuery, cntDatabase, hQueryDefault,[
	SELECT * 
	FROM CUSTOMER
	]) THEN 
		Error("Runtime problem")
	ELSE
		Info("ok")
	END
	
	// Cancels the dynamic file declaration
	HCancelDeclaration("CUSTOMER")
	
	// Closes the connection to the database
	HCloseConnection(cntDatabase)
	```





- **Al ejecutar una consulta INSERT/UPDATE/DELETE en archivos de datos HFSQL**, el número de registros añadidos, modificados o eliminados es devuelto por el [H.NbRecModificationQuery](../WDLang4/3087002.md) Variable.



<a name="NOTE0_7"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Código de Usuario (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png)![Conectores Nativos](https://doc.pcsoft.fr/ext/images/us/AN.png) 

### Optimización para ejecutar la misma consulta varias veces (sólo accesos nativos de Oracle y SQL Server)
<a name="optimizacion_para_ejecutar_misma_consulta_varias_veces_solo_accesos_nativos_oracle_sql_server_ELTPARAGRAPHE000773"></a>

Para optimizar la ejecución de una consulta varias veces, puede hacerlo:

1. Declarar una fuente de datos. Esta fuente de datos contendrá el resultado de la consulta SQL.

2. Declarar los diferentes parámetros o variables de la consulta. 
	Los parámetros son parámetros de cadena de Default. Se puede especificar su tipo con la [escribir a máquina](../Proprietes/2510131.md) Property. 
	**Atención**: Para especificar el tipo de una consulta Variable, también puede asignar un Variable existente del tipo deseado a este Variable.

3. Prepare la consulta con [HPrepareSQLQuery](../WDLang4/3044119.md).

4. Indique el valor del o de los diferentes parámetros que se deben tener en cuenta y ejecute la consulta con la función **HExecuteSQLQuery**. Sólo se debe especificar el nombre de la fuente de datos que corresponde a la consulta.




Este último paso debe repetirse tantas veces como sea necesario.

**Ejemplo**:


```wl
// Run the same query in Oracle
// Declare a data source
// This data source corresponds to the query. 
Insert1 is Data Source
// Declare one of the query parameters
// This parameter is an integer
Insert1.age = 0
i is int
// Prepare the query for multiple executions
HPrepareSQLQuery(Insert1, MyConnection, ...
	hQueryWithoutCorrection, ...
	"INSERT INTO PERSONE VALUES (:lastname,:firstname,:age )")
// Loop for running the query
// Only some parameters are modified
FOR I = 1 TO 10
	Insert1.lastname = "LastName" + i
	Insert1.firstname = "FirstName" + i
	Insert1.age = i
	HExecuteSQLQuery(Insert1)
END
```

<a name="NOTE0_8"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Código de Usuario (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png)![Conectores Nativos](https://doc.pcsoft.fr/ext/images/us/AN.png) 

### Especificación y recuperación de valores al ejecutar una consulta (procedimientos almacenados)
<a name="especificacion_recuperacion_valores_ejecutar_una_consulta_procedimientos_almacenados_ELTPARAGRAPHE000814"></a>

**Observación**: Esta nota sólo se aplica a los conectores nativos de Oracle y SQL Server.

Para especificar y recuperar valores al ejecutar una consulta, debe hacerlo:

1. Declarar una fuente de datos. Esta fuente de datos contendrá el resultado de la consulta SQL.

2. Declarar las diferentes variables de consulta (variables de entrada y salida). 
	Las variables son variables de cadena de Default. Se puede especificar su tipo con la [escribir a máquina](../Proprietes/2510131.md) Property. 
	**Atención**: Para especificar el tipo de una consulta Variable, también puede asignar un Variable existente del tipo deseado a este Variable.

3. Ejecutar la consulta con la función **HExecuteSQLQuery**.



```wl
// Example for Oracle
// Declare the data source associated with the query
MyProc is Data Source
// Declare the variables
MyProc.n = 3
MyProc.str = "Example"
// Run the query and retrieve the result
HExecuteSQLQuery(MyProc, MyConnection, hQueryWithoutCorrection, ...
	"begin:Res:=sp_cut(:n,:str);end;")
// Display the result 
Info(MyProc.Res)
```


**Observaciones**

- La función **HExecuteSQLQuery** debe utilizarse con:

	- el nombre de la conexión,

	- la constante *hQueryWithoutCorrection*.




- Las variables declaradas en la consulta deben ser idénticas a las utilizadas en el código de Procedure almacenado. De lo contrario, se produce un error WLanguage.

- En la llamada a la Procedure almacenada, se debe utilizar la sintaxis específica de la base de datos utilizada, incluso para los parámetros.
	Por lo tanto, para Oracle, los parámetros se especifican con la sintaxis **:ParamName**.. Atención: El carácter ":" debe ir seguido de al menos una letra (la sintaxis:1 no está permitida). 
	Para SQL Server, los parámetros se especifican mediante la siguiente notación: @ParamName..
	El mismo parámetro se puede utilizar varias veces. En este caso, la Variable correspondiente será reutilizada.

- Para ejecutar una consulta sin activar la opción bind, utilice la constante ***hNoBind*** en **HExecuteSQLQuery**. 


<a name="NOTE0_9"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png)![Código de Usuario (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png)![HFSQL Classic](https://doc.pcsoft.fr/ext/images/us/HF.png)![HFSQL Client/Server](https://doc.pcsoft.fr/ext/images/us/HFCS.png)![Hyper File 5.5](https://doc.pcsoft.fr/ext/images/us/HF55.png)![OLE DB](https://doc.pcsoft.fr/ext/images/us/OLEDB.png)![Conectores Nativos](https://doc.pcsoft.fr/ext/images/us/AN.png) 

### Consultas SQL (HExecuteSQLQuery o ejecución de consultas SQL creadas en el editor de consultas)
<a name="consultas_sql_docparampagetitleshort_ejecucion_consultas_sql_creadas_editor_consultas_ELTPARAGRAPHE000871"></a>

**Cuando se utilizan las sentencias SQL DELETE, INSERT o UPDATE en consultas SQL, Default no realiza ninguna comprobación de integridad ni de duplicación en una base de datos HFSQL.**

**To realizar un chequeo de integridad automático**, simplemente especifica la constante *hCheckIntegrity*. Esta constante permite activar la gestión de la integridad al ejecutar la consulta. Si se detecta un error de integridad, no se ejecuta la consulta. Se inicia una transacción, los registros se bloquean en escritura durante la ejecución de la consulta y se desbloquean al final de su ejecución, independientemente del resultado de la consulta. Si la aplicación se detiene durante la ejecución de la consulta (corte de energía, por ejemplo), la transacción se cancelará la próxima vez que se utilicen los archivos de datos de la consulta.
![HFSQL Client/Server](https://doc.pcsoft.fr/ext/images/us/HFCS.png) Si no se especifica la constante *hCheckIntegrity*, se desactiva la gestión de la integridad. Sin embargo, se produce un error si se debe realizar una operación de gestión de la integridad, pero el usuario no tiene derechos para desactivar la gestión de la integridad ([HModifyDatabaseRights](../WDLang4/3044326.md)).**Para realizar una comprobación automática de duplicados**, basta con especificar la constante *hCheckDuplicates*. Esta constante permite activar la gestión de duplicados al ejecutar la consulta. Si se detecta un error de duplicados, no se ejecuta la consulta. Se inicia una transacción, los registros se bloquean en escritura durante la ejecución de la consulta y se desbloquean al final de su ejecución, independientemente del resultado de la consulta. Si la aplicación se detiene durante la ejecución de la consulta (corte de energía, por ejemplo), la transacción se cancelará la próxima vez que se utilicen los archivos de datos de la consulta.

Observación: Si se produce un error de duplicados al ejecutar una consulta UPDATE, la función **HExecuteQuery** devuelve False y el proceso continúa. El error correspondiente puede conocerse con la función [HErrorInfo](../WDLang4/3044071.md).

**Observación**: La asistencia automática predeterminada no se llama para los errores de duplicados. Se genera un error simple de duplicados. Si se definió una asistencia con la función [HOnError](../WDLang4/3044017.md), se llama a esta asistencia personalizada.
![HFSQL Client/Server](https://doc.pcsoft.fr/ext/images/us/HFCS.png) Si no se especifica la constante *hCheckDuplicates*, se desactiva la gestión de duplicados. Se produce un error si se debe realizar una operación de gestión de duplicados, pero el usuario no tiene derechos para desactivar la gestión de duplicados ([HModifyDatabaseRights](../WDLang4/3044326.md)).**Las consultas UPDATE, DELETE e INSERT sólo existen durante su ejecución**. No se puede utilizar una función WLanguage en este tipo de consulta después de que ha sido ejecutada.
<a name="NOTE0_10"></a>
![WINDEV](https://doc.pcsoft.fr/ext/images/us/WD.png)![Universal Windows 10 App](https://doc.pcsoft.fr/ext/images/us/UNIVERSALAPP.png)![Código de Usuario (UMC)](https://doc.pcsoft.fr/ext/images/us/MCU.png)![Lenguaje Externo](https://doc.pcsoft.fr/ext/images/us/LE.png)![HFSQL Classic](https://doc.pcsoft.fr/ext/images/us/HF.png)![OLE DB](https://doc.pcsoft.fr/ext/images/us/OLEDB.png)![Conectores Nativos](https://doc.pcsoft.fr/ext/images/us/AN.png) 

### Consultas de navegación ([HReadFirst](../WDLang4/3044051.md))
<a name="consultas_navegacion_hreadfirstwdlang43044051md_ELTPARAGRAPHE000910"></a>

- Mediante Default, [HReadFirst](../WDLang4/3044051.md) vuelve a realizar la consulta a actualización el resultado de la misma. Se recomienda utilizar la constante *hNoRefresh* para evitar que se repita la consulta. 

- **Browsing a query run with the hQueryWithoutCorrection option**:
	Para examinar los registros en el orden en que fueron devueltos por la base de datos, no es necesario especificar un elemento de búsqueda. Ejemplo:
	
	```wl
	HReadFirst(MyQuery, hNoRefresh)
	```

	Si se especifica un elemento de navegación, el resultado de la consulta se recupera e indexa por completo.. La búsqueda se realiza en el elemento especificado. Se ignora el orden inicial de la consulta (especificado por ORDER BY).. El índice creado (en formato HFSQL) es sensible al caso, a la puntuación, a los caracteres acentuados y en orden ascendente..
	Ejemplo:
	
	```wl
	HReadFirst("MyQuery", "MyItem", hNoRefresh)
	```

	El índice creado se utiliza para realizar búsquedas en el resultado de la consulta.




<a name="XComponent"></a>

## Clasificación Lógica de negocio / UI:
Lógica de negocio
## Componente:
wd280hf.dll
