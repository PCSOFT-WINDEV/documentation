


## Claves compuestas
			



<a name="NOTE1"></a>
<a name="NOTE1_1"></a>


## Presentación
<a name="presentacion_ELTTEXTE000409"></a>
Una clave compuesta es un elemento clave que contiene varios otros elementos. Estos elementos pueden ser de texto o numéricos.

Una clave compuesta se utiliza para simplificar las búsquedas realizadas sobre varios criterios.

Las claves compuestas son elementos binarios. Su valor no puede ser visualizado directamente (en un rastro o en una control).

Esta Page presenta los siguientes temas:

- [Creación de una clave compuesta](#NOTE2_1)

- [Valor de la clave compuesta](#NOTE3_1)

- [Uso de una clave compuesta para realizar búsquedas de coincidencias exactas](#NOTE4_1)

- [Uso de una clave compuesta para realizar búsquedas genéricas](#NOTE5_1)

- [Uso de una clave compuesta para crear filtros](#NOTE6_1)






<a name="NOTE2"></a>
<a name="NOTE2_1"></a>


## Creación de una clave compuesta
<a name="creacion_una_clave_compuesta_ELTTEXTE000433"></a>
Se crea directamente una clave compuesta en la editor de análisis.

Para crear una clave compuesta en la description de un archivo de datos:

1. Abre los artículos de description del archivo de datos: 

	- Seleccione el archivo de datos en el editor. 

	- Abra el menú contextual (clic derecho) y seleccione "description de elementos".




2. Haga clic en ![](https://doc.pcsoft.fr/en-US/images/image.awp?langid=7&name=Manipulations_rubriques%20-%20HC%20N%B0001%207.gif). Aparece una pantalla que permite construir la clave compuesta.

3. La lista de artículos encontrados en el archivo de datos se muestra en el tabla a la izquierda. Haga doble clic en los elementos que deben incluirse en la clave compuesta. Estas posiciones se visualizan en la tabla de la derecha.

4. Reorganizar (si es necesario) las posiciones que pertenecen a la clave compuesta.. 
	**Atención**: el orden de las posiciones es importante porque define el orden de clasificación. Por ejemplo, la tecla compuesta "Nombre + Estado" se ordenará en el nombre y luego en el estado.

5. Especifique la dirección de búsqueda y los parámetros de búsqueda para cada tecla Component.

6. Validar. La clave compuesta se muestra en la lista de elementos del archivo de datos.



<a name="NOTE2_2"></a>


### Clave y enlace compuesto
<a name="clave_enlace_compuesto_ELTPARAGRAPHE000071"></a>

Las claves compuestas pueden utilizarse en los enlaces entre los archivos de datos. Al describir el análisis, la clave compuesta que se encuentra en el archivo de datos de relacionado aparece no como una clave compuesta sino como una clave ***binaria***. No podrá acceder a los diferentes componentes de la clave compuesta en el archivo de datos de relacionado.

**Observaciones**: 

- La gestión de la integridad referencial es compatible con una clave compuesta utilizada en un enlace.

- La gestión de las modificaciones en cascada no es compatible con una clave compuesta utilizada en un enlace.. 




<a name="NOTE3"></a>
<a name="NOTE3_1"></a>


## Valor de la clave compuesta
<a name="valor_clave_compuesta_ELTTEXTE000463"></a>


### Presentación
<a name="presentacion_ELTPARAGRAPHE000085"></a>

Una clave compuesta se almacena como una cadena binaria. Su valor no puede ser visualizado (ni en un control ni en el depurador, ...).
<a name="NOTE3_2"></a>


### Añadiendo una Record que contiene una clave compuesta
<a name="anadiendo_una_record_que_contiene_una_clave_compuesta_ELTPARAGRAPHE000092"></a>

Cuando se añade o modifica una Record que contiene una clave compuesta, el valor de la clave compuesta se define automáticamente según los valores de los diferentes componentes de la clave. No se requiere programación específica.
<a name="NOTE3_3"></a>


### Añadir un Record que contiene una clave compuesta en un archivo de datos relacionado
<a name="anadir_record_que_contiene_una_clave_compuesta_archivo_datos_relacionado_ELTPARAGRAPHE000099"></a>

Cuando un Record que contiene una clave compuesta se añade a un archivo de datos de relacionado, el valor de la clave debe ser construido. De hecho, en el archivo de datos de relacionado, la clave compuesta no se considera como una clave compuesta sino como una clave binaria. Por lo tanto, se le debe asignar un valor.

Este valor puede ser asignado:

- directamente. Por ejemplo, se ha añadido un Record en el archivo de datos del cliente. Para añadir el valor de la clave al fichero de datos relacionado, basta con realizar una asignación directa: 
	
	```wl
	LinkFile.NameDate = Customer.NameDate
	```


- mediante la función [HBuildKeyValue](../WDLang4/3044258.md). Esta función se utiliza para construir el valor de la clave compuesta a partir de sus componentes.



<a name="NOTE3_4"></a>


### Construir el valor de una clave compuesta para implementar una búsqueda o un filtro
<a name="construir_valor_una_clave_compuesta_para_implementar_una_busqueda_filtro_ELTPARAGRAPHE000116"></a>

Cuando se implementa un filtro o una búsqueda en una clave compuesta, se debe definir el valor de la clave compuesta (para definir el límite inferior y el límite superior del filtro, por ejemplo).

Se pueden utilizar varios métodos para construir el valor de una clave compuesta:

- Método 1: Utilizar una lista de valores en [HFilter](../WDLang4/3044100.md).

- Método 2: Utilizando la declaración [FOR EACH](../Motscles/1510013.md).

- Método 3: Utilizando la función [HBuildKeyValue](../WDLang4/3044258.md).

- Método 4: Uso de la función [HConvertX](../WDLang4/3044260.md) (método conservado por motivos de compatibilidad con WINDEV 5.5)




**Método 1: Lista de valores**

Basta con especificar la lista de valores que debe tomar cada Component de clave compuesta para el filtro o para la búsqueda. La clave compuesta se construye directamente en la sintaxis de la función.

**Ejemplo**: Encuentra todos los registros en el archivo de datos del cliente cuyo apellido está entre "AA" y "Barnaby" y cuyo nombre está entre "Philomene" y "Tartuffe"..


```wl
HFilter(Customer, LastNameFirstName, ["AA", "Philomene"], ["Barnaby", "Tartuffe"])
```
En este ejemplo, "AA","Zorro" es devuelto por el filtro mientras que "Philomene","Zorro" no lo es.

**Ejemplo**: Encuentra todos los registros en el archivo de datos de Tareas cuyas tareas se incluyen entre el 15/03/2011 00:00 y el 25/03/2011 00:00.


```wl
HFilter(Tasks, TaskStarDateTaskStartTime, [20110315,0000], [20110325,0000])
```


**Método 2: Uso de la sentencia FOR EACH**

Especifique la lista de valores que debe tomar cada clave Component para el filtro o para la búsqueda. La clave compuesta se construye directamente en la sintaxis de la función.

**Ejemplo**: Encontrar a los clientes potenciales que viven en el estado 69.


```wl
FOR EACH Contacts WHERE CCState = ["Prospect", 69]
...
END
```


**Método 3: Utilizar la función HBuildKeyValue**

El valor de una clave compuesta puede ser construido por [HBuildKeyValue](../WDLang4/3044258.md). Para ello, en esta función se debe especificar el archivo de datos correspondiente, el nombre de la clave y los valores de los componentes.

Por ejemplo, la siguiente Line de código se utiliza para construir el valor de la clave compuesta ("NAMEDATE") correspondiente a "CUSTOMERNAME+ORDER_DATE" del fichero de datos del cliente:


```wl
HBuildKeyValue(Customer, NAMEDATE, "SMITH","03/11/99")
```


**Método 4: Uso de HConvertX (método conservado para la compatibilidad con WINDEV 5.5)**

Debes hacerlo:

- completar los componentes de texto con la constante *hMinVal*.

- convertir los componentes numéricos con la función [HConvert](../WDLang4/3044260.md).




Ejemplo:


```wl
MyCompositeKey = Complete(Customer.CustomerLastName, Dimension(Customer.LastName), hMinval)) + ...
	Complete(Customer.FirstName, Dimension (Customer.FirstName), hMinVal)
```

<a name="NOTE3_5"></a>


### Propiedades del lenguaje associated con claves compuestas
<a name="propiedades_del_lenguaje_associated_con_claves_compuestas_ELTPARAGRAPHE000188"></a>Las siguientes propiedades se utilizan para gestionar las llaves compuestas mediante programación:



|   |   |
| --- | --- |
| [Binary](../Proprietes/2512001.md) | El **Binario** Property se utiliza para determinar si un elemento es binario. |
| [Component](../Proprietes/2512020.md) | La propiedad **Component** configura los diferentes componentes de una clave compuesta. |
| [CompositeKey](../Proprietes/2512004.md) | El **CompositeKey** Property se utiliza para determinar si un elemento es una clave compuesta. |
| [KeyExpression](../Proprietes/2512035.md) | La **KeyExpression** Property se utiliza para set los diferentes componentes de una clave compuesta. |
| [NbComponent](../Proprietes/2512058.md) | El **NbComponent** Property se utiliza para get el número de elementos de una clave compuesta. |





<a name="NOTE4"></a>
<a name="NOTE4_1"></a>


## Uso de una clave compuesta para realizar búsquedas de coincidencias exactas
<a name="uso_una_clave_compuesta_para_realizar_busquedas_coincidencias_exactas_ELTTEXTE000538"></a>


### Búsqueda por coincidencia exacta realizada por HReadSeekFirst
<a name="busqueda_por_coincidencia_exacta_realizada_por_hreadseekfirst_ELTPARAGRAPHE000198"></a>

Para realizar una búsqueda por coincidencia exacta del valor de una clave compuesta, debe hacerlo:

1. Construir el valor de la clave de búsqueda con [HBuildKeyValue](../WDLang4/3044258.md). Se deben especificar todos los valores de los componentes clave.

2. Utiliza la función [HReadSeekFirst](../WDLang4/3044036.md).




**Observación**: Mediante Default, se utiliza [HReadSeekFirst](../WDLang4/3044036.md) para realizar una búsqueda de coincidencia exacta.


```wl
ValCompKey is Buffer
ValCompKey = HBuildKeyValue(Customer, Name_ZC, "Smith", "34000")
HReadSeekFirst(Customer, Name_ZC, ValCompKey)
WHILE HFound() = True
	...
	HReadNext(Customer, Name_ZC)
END
```

<a name="NOTE4_2"></a>


### Búsqueda por coincidencia exacta y HReadSeek
<a name="busqueda_por_coincidencia_exacta_hreadseek_ELTPARAGRAPHE000221"></a>

Para realizar una búsqueda por coincidencia exacta del valor de una clave compuesta, debe hacerlo:

1. Construir el valor de la clave de búsqueda con [HBuildKeyValue](../WDLang4/3044258.md). Se deben especificar todos los valores de los componentes clave.

2. Llama a [HReadSeek](../WDLang4/3044050.md) con la constante ***hIdentical***.




Observación: Por Default, [HReadSeek](../WDLang4/3044050.md) se utiliza para realizar una búsqueda genérica. Para realizar una búsqueda exacta, se debe especificar la constante *hIdentical*.


```wl
ValCompKey is Buffer
ValCompKey = HBuildKeyValue(Customer, Name_ZC, "Smith", "34000")
HReadSeek(Customer, Name_ZC, ValCompKey, hIdentical)
WHILE HFound() = True
	...
	HReadNext(Customer, Name_ZC)
END
```


<a name="NOTE5"></a>
<a name="NOTE5_1"></a>


## Uso de una clave compuesta para realizar búsquedas genéricas
<a name="uso_una_clave_compuesta_para_realizar_busquedas_genericas_ELTTEXTE000568"></a>


### Búsqueda genérica realizada por HReadSeekFirst
<a name="busqueda_generica_realizada_por_hreadseekfirst_ELTPARAGRAPHE000246"></a>

Para realizar una búsqueda genérica sobre el valor de una clave compuesta, debe hacerlo:

1. Construir el valor de la clave de búsqueda con [HBuildKeyValue](../WDLang4/3044258.md). Sólo se pueden especificar los valores de los primeros componentes clave.

2. Llama a [HReadSeekFirst](../WDLang4/3044036.md) con la constante ***hGeneric***.




**Observación**: Mediante Default, se utiliza [HReadSeekFirst](../WDLang4/3044036.md) para realizar una búsqueda de coincidencia exacta. Para realizar una búsqueda genérica, especifique la constante ***hGeneric***.


```wl
ValCompKey is Buffer
ValCompKey = HBuildKeyValue(Customer, Name_ZC, "Moore")
HReadSeekFirst(Customer, Name_ZC, ValCompKey, hGeneric)
WHILE HFound() = True
	...
	HReadNext(Customer, Name_ZC)
END
```

<a name="NOTE5_2"></a>


### Búsqueda genérica realizada por HReadSeek
<a name="busqueda_generica_realizada_por_hreadseek_ELTPARAGRAPHE000269"></a>

Para realizar una búsqueda genérica sobre el valor de una clave compuesta, debe hacerlo:

1. Construir el valor de la clave de búsqueda con [HBuildKeyValue](../WDLang4/3044258.md). Sólo se pueden especificar los valores de los primeros componentes clave.

2. Utiliza la función [HReadSeek](../WDLang4/3044050.md).




Observación: Por Default, [HReadSeek](../WDLang4/3044050.md) se utiliza para realizar una búsqueda genérica. Para realizar una búsqueda exacta, se debe especificar la constante *hIdentical*.


```wl
ValCompKey is Buffer
ValCompKey = HBuildKeyValue(Customer, Name_ZC, "Moore")
HReadSeek(Customer, Name_ZC, ValCompKey)
WHILE HFound() = True
	...
	HReadNext(Customer, Name_ZC)
END
```


<a name="NOTE6"></a>
<a name="NOTE6_1"></a>


## Uso de una clave compuesta para crear filtros
<a name="uso_una_clave_compuesta_para_crear_filtros_ELTTEXTE000598"></a>


### Funciones para crear filtros
<a name="funciones_para_crear_filtros_ELTPARAGRAPHE000294"></a>

Además de [HFilter](../WDLang4/3044100.md), se pueden utilizar varias funciones de WLanguage para crear filtros específicos. Estas funciones permiten manipular las teclas compuestas:



|   |   |
| --- | --- |
| [Filtro HF](../WDLang4/3044100.md) | Define y activa un filtro en un archivo de datos, vista o consulta. |
| [HFilterIdentical](../WDLang4/3044105.md) | Define y habilita un filtro utilizado para encontrar el valor exacto de un elemento de cadena. |
| [HFilterIncludedBetween](../WDLang4/3044097.md) | Define y activa un filtro de tipo "Comprendido entre" en un archivo de datos, vista o consulta. |
| [HFilterStartsWith](../WDLang4/3044098.md) | Define y activa un filtro de tipo "Comienza por" en un archivo, vista o consulta. |




<a name="NOTE6_2"></a>


### Filtro entre dos valores (límites)
<a name="filtro_entre_dos_valores_limites_ELTPARAGRAPHE000305"></a>

Para filtrar registros entre dos valores específicos de una clave compuesta, debe hacerlo:

1. Cree los valores de cada límite con [HBuildKeyValue](../WDLang4/3044258.md).

2. Cree el filtro con [HFilter](../WDLang4/3044100.md) y especifique los dos límites.

3. Examinar los registros seleccionados.



```wl
sFilterKey1 = HBuildKeyValue(CUSTOMER, NAME_ZC, "MOORE", "34000")
sFilterKey2 = HBuildKeyValue(CUSTOMER, NAME_ZC, "MOORE", "34999")
sSearchKey= HFilter(Customer, NAME_ZC,  sFilterKey1,  sFilterKey2)
HReadFirst(Customer, sSearchKey)
WHILE NOT HOut()
	...
	HReadNext(Customer, sSearchKey)
End
```

<a name="NOTE6_3"></a>


### Filtrar a partir de un valor dado
<a name="filtrar_partir_valor_dado_ELTPARAGRAPHE000324"></a>

Para filtrar los registros de un valor dado (sin especificar ningún límite superior), debe: 

1. Crear el valor del límite inferior con [HBuildKeyValue](../WDLang4/3044258.md).

2. Crear el filtro con [HFilter](../WDLang4/3044100.md) especificando los límites inferior y superior. En este caso, el límite máximo es igual a la constante *hMaxVal*.

3. Examinar los registros seleccionados.



```wl
sFilterKey = HBuildKeyValue(CUSTOMER, NAME_ZC, "MOORE", "34000")
sSearchKey = HFilter(Customer, NAME_ZC, sFilterKey, hMaxVal)
HReadFirst(Customer, sSearchKey)
WHILE NOT HOut()
	...
	HReadNext(Customer, sSearchKey)
END
```

<a name="NOTE6_4"></a>


### Filtro en los primeros componentes clave
<a name="filtro_los_primeros_componentes_clave_ELTPARAGRAPHE000343"></a>

Para filtrar los registros de los primeros componentes de una clave compuesta y realizar una clasificación de los siguientes componentes, debe hacerlo:

1. Crear los valores mínimos y máximos de la clave compuesta con [HBuildKeyValue](../WDLang4/3044258.md). Sólo deben especificarse los primeros componentes.

2. Crear el filtro con [HFilter](../WDLang4/3044100.md) especificando los límites inferior y superior. En este caso, la constante *hMinVal* debe añadirse al límite inferior y *hMaxVal* debe añadirse al límite superior. Observaciones: *hMinVal* es equivalente a Char(0) y *hMaxVal* es equivalente a Char(255).

3. Examinar los registros seleccionados.



<a name="NOTE6_5"></a>


### Observación: Filtro con condición de selección
<a name="observacion_filtro_con_condicion_seleccion_ELTPARAGRAPHE000360"></a>

Si la condición del filtro afecta a varios elementos correspondientes a una clave compuesta del archivo de datos, la clave de búsqueda seleccionada automáticamente por el filtro será esta clave compuesta.


